#maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

_extramodules=extramodules-4.5-CHAKRA
_kver="$(cat /usr/lib/modules/${_extramodules}/version)"
_kernelver=4.5.1

pkgname=broadcom-wl
pkgver=6.30.223.271
pkgrel=3
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('x86_64')
license=('custom')
depends=("linux=${_kernelver}")
makedepends=("linux-headers=${_kernelver}")
source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz"
         #"http://ftp.de.debian.org/debian/pool/non-free/b/broadcom-sta/broadcom-sta_5.100.82.112.orig.tar.gz" "http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz" we need the debian version, because upstream URL doesn't exist anymore
        'modprobe.d'
        'license.patch'
        'wl_linux.c.patch')
sha1sums=('1f568bb989d175813c5631c6629d9479eae6f3be'
          'a37c5dd9ab279372f0c68595bb2a8f1fe694cd13'
          '81c05d48b234d56ad2e18eaee5ce89b79550ef20'
          '94e12ef9e64fde04a5bfb04858dd5be86a70e902')


install=install

prepare() {
    cd "${srcdir}"

    patch -p1 -i license.patch
    patch -p1 -i wl_linux.c.patch

    sed -e "/BRCM_WLAN_IFNAME/s:eth:wlan:" \
        -i src/wl/sys/wl_linux.c
}

build() {
    cd "${srcdir}"

    make -C /usr/lib/modules/"${_kver}"/build M=`pwd`
}

package() {
    cd "${srcdir}"

    install -Dm644 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"

    install -Dm644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 modprobe.d "${pkgdir}/usr/lib/modprobe.d/broadcom-wl.conf"
}
