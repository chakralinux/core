#maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

_extramodules=extramodules-3.15-CHAKRA
_kernver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=broadcom-wl
pkgver=6.30.223.141
pkgrel=8
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('x86_64')
license=('custom')
depends=('linux')
makedepends=('linux-headers')
source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz"
         #"http://ftp.de.debian.org/debian/pool/non-free/b/broadcom-sta/broadcom-sta_5.100.82.112.orig.tar.gz" "http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz" we need the debian version, because upstream URL doesn't exist anymore
        'modprobe.d'
        'license.patch'
        'gcc.patch'
        'linux-recent.patch')
sha1sums=('3a2453c71a07030f41a8fc613c87badf8452848c'
          '8f5234da6769784b3331d2fefac4a38b20b5723f'
          '81c05d48b234d56ad2e18eaee5ce89b79550ef20'
          '9ba530076d0b45eb96b2134e6b5d9f3a61adf4e0'
          'c6a935a08480ca36f9e4b10b745ed46467fccc00')

backup=('etc/modprobe.d/broadcom-wl.conf')
install=install


prepare() {
	cd "${srcdir}"

	patch -p1 -i linux-recent.patch
	patch -p1 -i license.patch
        patch -p1 -i gcc.patch

        sed -e "/BRCM_WLAN_IFNAME/s:eth:wlan:" \
            -i src/wl/sys/wl_linux.c
}

build() {
	cd "${srcdir}"
	make -C /lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 ${srcdir}/modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl.conf
}
