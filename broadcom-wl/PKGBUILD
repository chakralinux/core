#maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

_extramodules=extramodules-3.16-CHAKRA
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
_kernelver=3.16.4

pkgname=broadcom-wl
pkgver=6.30.223.248
pkgrel=3
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('x86_64')
license=('custom')
depends=("linux=${_kernelver}")
makedepends=("linux-headers=${_kernelver}")
source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz"
         #"http://ftp.de.debian.org/debian/pool/non-free/b/broadcom-sta/broadcom-sta_5.100.82.112.orig.tar.gz" "http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz" we need the debian version, because upstream URL doesn't exist anymore
        'modprobe.d'
        'license.patch'
        'gcc.patch'
        'linux-recent.patch')
sha1sums=('bd55f1fdc6b0a4d0a70dc4ba4bb4169a824e238c'
          '8f5234da6769784b3331d2fefac4a38b20b5723f'
          '81c05d48b234d56ad2e18eaee5ce89b79550ef20'
          '8a940675e8c0baa65494c378e6155543b26a9a58'
          '4f07fffec5994bad1a857e3be8bd1e3b85576721')

backup=('etc/modprobe.d/broadcom-wl.conf')
install=install


prepare() {
	cd "${srcdir}"

	patch -p1 -i linux-recent.patch
	patch -p1 -i license.patch
        patch -p1 -i gcc.patch

        sed -e "/BRCM_WLAN_IFNAME/s:eth:wlan:" \
            -i src/wl/sys/wl_linux.c
}

build() {
	cd "${srcdir}"

	make -C /usr/lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -Dm644 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"

	install -Dm644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 modprobe.d "${pkgdir}/usr/lib/modprobe.d/broadcom-wl.conf"
}
