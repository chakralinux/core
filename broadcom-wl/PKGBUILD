# Source global configuration
source ../linux.conf

pkgname=broadcom-wl
pkgver=6.30.223.271
pkgrel=18
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('x86_64')
license=('custom')
depends=("linux=${_kernelver}")
makedepends=("linux-headers=${_kernelver}")
source=("https://docs.broadcom.com/docs-and-downloads/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz"
         #"http://ftp.de.debian.org/debian/pool/non-free/b/broadcom-sta/broadcom-sta_5.100.82.112.orig.tar.gz" "http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz" we need the debian version, because upstream URL doesn't exist anymore
        'modprobe.d'
        '001-null-pointer-fix.patch'
        '002-rdtscl.patch'
        '003-linux47.patch'
        '004-linux48.patch'
        '005-debian-fix-kernel-warnings.patch'
        '006-linux411.patch'
        '007-linux412.patch'
        '008-linux415.patch')
sha1sums=('1f568bb989d175813c5631c6629d9479eae6f3be'
          'a37c5dd9ab279372f0c68595bb2a8f1fe694cd13'
          '75300b4ad9fa0f7b8ad98dfa6fbd0159a3a4c222'
          '80dc4aba85043dc56041040e423cfb3548c867bf'
          '6429084dc8476d1256f3735272815d94a2b9dc09'
          '7ce912f606476b61d200e1051a27c9db66fcff61'
          'faca186c7c1f894240c46ffa53bece501fe58773'
          '4263937f6ec075b7c5461806771dd17261d56d54'
          '1d3fb939df5ba3b04e2949df6c5ed24f4a3d1f48'
          '0b14384c82fe2a511d4f0210a682871b8906ae6a')

install=install

prepare() {
    cd "${srcdir}"

    patch -p1 -i 001-null-pointer-fix.patch
    patch -p1 -i 002-rdtscl.patch
    patch -p1 -i 003-linux47.patch
    patch -p1 -i 004-linux48.patch
    patch -p1 -i 005-debian-fix-kernel-warnings.patch
    patch -p1 -i 006-linux411.patch
    patch -p1 -i 007-linux412.patch
    patch -p1 -i 008-linux415.patch

    sed -e "/BRCM_WLAN_IFNAME/s:eth:wlan:" \
        -i src/wl/sys/wl_linux.c
}

build() {
    cd "${srcdir}"

    make -C /usr/lib/modules/"${_kver}"/build M=`pwd`
}

package() {
    cd "${srcdir}"

    install -Dm644 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"

    install -Dm644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 modprobe.d "${pkgdir}/usr/lib/modprobe.d/broadcom-wl.conf"
}
