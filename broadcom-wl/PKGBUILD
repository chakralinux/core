#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# Find the kernel name inside the chroot
_kernver=`pacman -Q linux | cut -c7-9 | sed 's/linux //g'`-CHAKRA

pkgname=broadcom-wl
pkgver=5.100.82.112
pkgrel=2
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('i686' 'x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux>=3.1' 'linux<3.2')
makedepends=('linux-headers')

[ "$CARCH" = 'x86_64' ] && ARCH=x86_64 || ARCH=x86_32
source=("http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz"
        'license.patch'
        'semaphore.patch')

sha1sums=('01aa32f9e85621253a3f15cf4361bb80d41da3e8'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
          '105f8e1d48ebe1f25d53859e5ab9326a27435c66')
[ "$CARCH" = 'x86_64' ] && sha1sums[0]='5bd78c20324e6a4aa9f3fafdc6f0155e884d5131'

install=install

build() {
	cd "$srcdir"
	patch -p1 < license.patch
	patch -p1 < semaphore.patch
	make -C /lib/modules/$_kernver/build M=`pwd`
}


package() {
	cd "$srcdir"
	install -D -m 755 wl.ko "$pkgdir/lib/modules/$_kernver/kernel/drivers/net/wireless/wl.ko"
	install -D -m 644 lib/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}