pkgname=pypy
pkgver=4.0.1
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
arch=('x86_64')
url="http://www.pypy.org/"
license=('custom:MIT')
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('python2' 'tk' 'sqlite3')
optdepends=('sqlite3: sqlite module'
            'tk: tk module')
source=("https://bitbucket.org/${pkgname}/${pkgname}/downloads/$pkgname-$pkgver-src.tar.bz2")
options=(!buildflags)
md5sums=('f5b35ebedee2fa4fdfee82733be59996')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src/pypy/tool/release

  python2 package.py --archive-name pypy --targetdir .
  cd ../..
  mkdir unpacked
  tar xf pypy.tar.bz2 -C unpacked

  cd unpacked
  install -Dm755 pypy/bin/pypy "${pkgdir}"/opt/pypy/bin/pypy
  install -Dm755 pypy/bin/libpypy-c.so "${pkgdir}"/usr/lib/libpypy-c.so
  cp -r pypy/include pypy/lib_pypy pypy/lib-python pypy/site-packages "${pkgdir}"/opt/pypy/
  cd ..

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy/bin/pypy "${pkgdir}"/usr/bin/pypy

  install -Dm644 LICENSE "${pkgdir}"/opt/pypy/LICENSE
  install -Dm644 README.rst "${pkgdir}"/opt/pypy/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy/LICENSE
}
