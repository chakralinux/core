# Maintainer: Daniele Cocca <jmc@chakra-project.org>

pkgname=pypy
pkgver=1.9
_shasum=('341e1e3821ff')
pkgrel=1
pkgdesc="PyPy is a fast, compliant alternative implementation of the Python language (2.7.1)."
arch=('i686' 'x86_64')
url="http://www.pypy.org/"
license=('MIT')
depends=('expat' 'zlib' 'bzip2' 'openssl' 'python-greenlet')
makedepends=('pypy')
source=("https://bitbucket.org/${pkgname}/${pkgname}/get/release-${pkgver}.tar.bz2")
md5sums=('f92c0171a9578a3e4a0f74947ec596ab')

build() {
  cd "${srcdir}/${pkgname}-${pkgname}-${_shasum}"
  cd "pypy/translator/goal"

  pypy translate.py -Ojit

  # Switch to this one instead, if the default one saturates your memory:
  # PYPY_GC_MAX_DELTA=200MB pypy --jit loop_longevity=300 ./translate.py -Ojit
}

package() {
  cd "${srcdir}/${pkgname}-${pkgname}-${_shasum}"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licences/${pkgname}/LICENSE"

  cd "pypy/tool/release"
  # Clean up the /tmp directory from previous packaging attempts
  rm -r "/tmp/usession-release-${pkgver}"-*
  # Generate the PyPy release tarball
  pypy package.py ../../.. "${pkgname}"
  # Then, install it under "${pkgdir}/opt"
  install -d -m755 "${pkgdir}/opt"
  tar -jxvf "/tmp/usession-release-${pkgver}-0/build/${pkgname}.tar.bz2" -C "${pkgdir}/opt"

  # Install binary symlink
  install -d -m755 "${pkgdir}/usr/bin"
  ln -sf "/opt/${pkgname}/bin/pypy" "${pkgdir}/usr/bin/pypy"
}

# vim:set ts=2 sw=2 et:
