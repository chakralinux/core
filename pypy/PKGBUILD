# Maintainer: Fabian Kosmale <inkane@chakra-project.org>
# Contributor: Daniele Cocca <jmc@chakra-project.org>

pkgname=pypy
pkgver=2.2.1
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
arch=('x86_64')
url="http://www.pypy.org/"
license=('custom:MIT')
depends=('libffi')
provides=('python2')
makedepends=('sqlite3' 'python2' 'pypy' 'tk')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module'
            'tk: tk module')
source=("https://bitbucket.org/${pkgname}/${pkgname}/downloads/$pkgname-$pkgver-src.tar.bz2")
options=(!buildflags)
md5sums=('7ec9f48702323f9e93654ba73dd46720')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src/pypy/goal

  # here you can boostrap with python if needed
  python2 ../../rpython/bin/rpython -Ojit targetpypystandalone
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src/pypy/tool/release

  python2 package.py ../../../ pypy pypy-c "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/opt
  tar x -C "${pkgdir}"/opt -f "${srcdir}"/${pkgname}.tar.bz2

  # Install binary symlink
  install -d -m755 "${pkgdir}/usr/bin"
  ln -sf "/opt/${pkgname}/bin/pypy-c" "${pkgdir}/usr/bin/pypy"
  
  install -Dm644 "${pkgdir}"/opt/pypy/LICENSE "${pkgdir}"/usr/share/licenses/pypy/LICENSE
}
