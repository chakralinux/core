# Arch Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-incremental
pkgname=('python3-incremental' 'python2-incremental')
pkgver=17.5.0
pkgrel=2
pkgdesc='A small library that versions your Python projects'
arch=('any')
license=('MIT')
url='https://github.com/hawkowl/incremental'
makedepends=('python3-setuptools' 'python2-setuptools' 'python3-click' 'python2-click'
             'python3-twisted' 'python2-twisted')
checkdepends=('python3-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/hawkowl/incremental/archive/incremental-$pkgver.tar.gz")
sha1sums=('c141a5108b90d11764635d649614dca7e672ae46')

prepare() {
  cp -a incremental-incremental-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/incremental-incremental-$pkgver
  python3 setup.py build

  cd "$srcdir"/incremental-incremental-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/incremental-incremental-$pkgver
  touch build/__init__.py
  touch build/lib/__init__.py
  LC_CTYPE=en_US.UTF-8 python setup.py ptr

  cd "$srcdir"/incremental-incremental-$pkgver-py2
  touch build/__init__.py
  touch build/lib/__init__.py
  python2 setup.py ptr
}

package_python3-incremental() {
  depends=('python3-click' 'python3-twisted')

  cd incremental-incremental-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-incremental() {
  depends=('python2-click' 'python2-twisted')

  cd incremental-incremental-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

