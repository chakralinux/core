pkgbase=vigra
pkgname=('vigra' 'vigra-doc')
pkgver=1.11.1
pkgrel=5
arch=('x86_64')
url="http://hci.iwr.uni-heidelberg.de/vigra/"
license=('custom:MIT')
makedepends=(# runtime deps
             'libpng' 'libtiff' 'openexr' 'gcc-libs' 'sh' 'hdf5' 'fftw'
              # buildtime deps
             'cmake' 'python3-nose' 'doxygen' 'python3-sphinx' 'boost' 'python3-numpy')
source=("https://github.com/ukoethe/vigra/releases/download/Version-${pkgver//./-}/${pkgname}-${pkgver}-src.tar.gz")
md5sums=('e72faf9fb82fa71fa63ba064bb63b703')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DWITH_OPENEXR=true \
      -DWITH_VIGRANUMPY=1 \
      -DDOCDIR=../build/doc \
      -DDOCINSTALL=share/doc
  make
}

check() {
  cd build
  make -j1 -k check || warning "Tests failed" # https://github.com/ukoethe/vigra/issues/409
}

package_vigra() {
  pkgdesc="Computer vision library"
  depends=('libpng' 'libtiff' 'openexr' 'gcc-libs' 'sh' 'hdf5' 'fftw')
  optdepends=('python3: for python bindings'
              'boost-libs: for python bindings')

  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  
  # Remove doc
  rm -rf "${pkgdir}"/usr/share/doc
}

package_vigra-doc() {
  pkgdesc="Computer vision library - documentation and examples"
  #arch=('any') # Not supported for now, maybe later

  cd build
  make DESTDIR="${pkgdir}" install

  # Remove vigra package content
  rm -rf "${pkgdir}"/usr/{bin,include,lib}

  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
