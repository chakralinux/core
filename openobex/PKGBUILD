#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: Fabian Kosmale <inkane@chakra-devel.org>
# contributor (i686): Phil Miller <philm[at]chakra-project[dog]org>
# contributor (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=openobex
pkgver=1.7.1
pkgrel=1
pkgdesc="Implementation of the OBject EXchange (OBEX) protocol"
url="http://dev.zuckschwerdt.org/openobex/"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('libusbx' 'bluez-libs')
makedepends=('cmake' 'libxslt' 'docbook-xsl')
options=('staticlibs' '!makeflags')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-Source.tar.gz)
md5sums=('3181bfed9cb7db591605391068cb0085')

prepare() {
  sed -i 's|MODE="660", GROUP="plugdev"|TAG+="uaccess"|' ${pkgname}-${pkgver}-Source/udev/openobex.rules.in
}

build() {
  [ -d build ] && rm -r build
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver}-Source \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib 
  make all openobex-apps
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  cp ../${pkgname}-${pkgver}-Source/apps/lib/*.h  "${pkgdir}/usr/include/openobex/"
  install -m644 apps/lib/libopenobex-apps-common.a "${pkgdir}/usr/lib/libopenobex-apps-common.a"
}
