#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: abveritas[at]chakra-project[dot]org>

pkgbase=libsigc++
pkgname=('libsigc++' 'libsigc++-docs')
pkgver=2.2.10
pkgrel=2
arch=('i686' 'x86_64')
license=('LGPL')
url="http://libsigc.sourceforge.net/"
source=("http://ftp.gnome.org/pub/GNOME/sources/libsigc++/2.2/libsigc++-${pkgver}.tar.xz")
makedepends=('gcc-libs>=4.5.0')
options=(!libtool !emptydirs)
sha256sums=('8ceb6f2732f5399ef50d5b70f433d49945a12e0900b8f9f43c135866a2e5bf47')

build() {
  cd "${srcdir}/libsigc++-${pkgver}"
  ./configure --prefix=/usr 
  make 
}

package_libsigc++() {
  pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2."
  depends=('gcc-libs')
  provides=("libsigc++2.0=${pkgver}")
  replaces=('libsigc++2.0')
  conflicts=('libsigc++2.0')

  cd "${srcdir}/libsigc++-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile 
  make DESTDIR="${pkgdir}" install 
}

package_libsigc++-docs() {
  pkgdesc="Developer documentation for libsigc++"
  provides=("libsigc++2.0-docs=${pkgver}")
  replaces=('libsigc++2.0-docs')
  conflicts=('libsigc++2.0-docs')

  cd "${srcdir}/libsigc++-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install 
}

