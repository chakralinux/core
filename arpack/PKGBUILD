#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# contributor Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=arpack
pkgver=3.1.5
pkgrel=1
arch=('x86_64')
pkgdesc="Fortran77 subroutines designed to solve large scale eigenvalue problems"
url="http://forge.scilab.org/index.php/p/arpack-ng/"
license=('BSD')
depends=('lapack' 'openmpi')
makedepends=('openmpi' 'gcc-fortran')
provides=('arpack-ng')
source=("http://forge.scilab.org/upload/$pkgname-ng/files/$pkgname-ng_$pkgver.tar.gz")
md5sums=('f773f34079a9c24807da6bc2e72fe6df')

build() {
  cd "$pkgname-ng-$pkgver"

  ./configure --prefix=/usr --enable-mpi
  make \
    F77="mpif77" \
    CFLAGS+=" `pkg-config --cflags ompi` " \
    LIBS+=" `pkg-config --libs ompi` "
}

package() {
  cd "$srcdir/$pkgname-ng-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
