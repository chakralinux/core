#
# Platform Packages for Chakra, part of chakra-project.org
#

pkgname=arpack
pkgver=3.1.5
pkgrel=2
arch=('x86_64')
pkgdesc="Fortran77 subroutines designed to solve large scale eigenvalue problems"
url="http://forge.scilab.org/index.php/p/arpack-ng/"
license=('BSD')
depends=('lapack' 'openmpi' 'openblas')
makedepends=('openmpi' 'gcc-fortran')
provides=('arpack-ng')
source=("https://github.com/opencollab/arpack-ng/archive/${pkgver}.tar.gz")
sha256sums=('f609d001a247195c019626cb0f2144db7b08c83f53d875dcdeeee4cdb0609098')


build() {
  cd "$pkgname-ng-$pkgver"

  ./configure --prefix=/usr --enable-mpi --with-blas="openblas" 
  make \
    F77="mpif77" \
    CFLAGS+=" $(pkg-config --cflags ompi) " \
    LIBS+=" $(pkg-config --libs ompi) "
}

package() {
  cd "$srcdir/$pkgname-ng-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
