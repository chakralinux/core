# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/chromaprint

pkgname=chromaprint
pkgver=1.4.2
pkgrel=1
pkgdesc='Client-side library that implements a custom algorithm for extracting fingerprints from any audio source'
url='http://acoustid.org/chromaprint/'
arch=('x86_64')
license=('LGPL')
depends=('ffmpeg')
makedepends=('cmake')
source=("https://bitbucket.org/acoustid/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
sha1sums=('3d90530e643a351f49460f08fcbc163179e9c036')

build() {
	cd "${pkgname}-${pkgver}"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_EXAMPLES=ON \
		.
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
