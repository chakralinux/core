# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/chromaprint

pkgname=chromaprint
pkgver=1.3.1
pkgrel=1
pkgdesc='Client-side library that implements a custom algorithm for extracting fingerprints from any audio source'
url='http://acoustid.org/chromaprint/'
arch=('x86_64')
license=('LGPL')
depends=('ffmpeg')
makedepends=('cmake')
source=("https://bitbucket.org/acoustid/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
sha1sums=('439c5d34a3ff46bcdb54cbcb613da175eb4d83c5')

build() {
	cd "${pkgname}-${pkgver}"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_EXAMPLES=ON \
		.
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
