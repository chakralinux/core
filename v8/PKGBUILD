#
# QT Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <jmc@chakra-project.org>

pkgbase=v8
pkgname=('v8' 'v8-src')
pkgver=3.9
pkgrel=3
arch=('x86_64')
url="http://code.google.com/p/v8/"
license=('BSD')
makedepends=('python2' 'subversion' 'readline')
source=("http://chakra.sourceforge.net/sources/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('e8823d3c9ffe5408a52ff0d869b69c70')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make dependencies
  make x64.release library=shared console=readline
}

package_v8() {
pkgdesc="V8 is Google's open source JavaScript engine."
depends=('glibc' 'gcc-libs' 'readline')

  cd "${srcdir}/${pkgname}-${pkgver}/out/x64.release"

  install -D -m755 lib.target/libv8.so \
                   "${pkgdir}/usr/lib/libv8.so"

  install -D -m755 d8 \
                   "${pkgdir}/usr/bin/d8"
}

package_v8-src() {
pkgdesc="V8 is Google's open source JavaScript engine. (Source package)"

  install -d "${pkgdir}/usr/src/"
  tar -Jxf "${srcdir}/${pkgbase}-${pkgver}.tar.xz" \
        -C "${pkgdir}/usr/src/" \
      --exclude-vcs

  # Remove the version number from the source folder name
  mv "${pkgdir}/usr/src/${pkgbase}-${pkgver}" \
     "${pkgdir}/usr/src/${pkgbase}"
}

# vim:set ts=2 sw=2 et:
