#
# Chakra Packages for Chakra, part of chakraos.org
#
# maintainer: Fabian Kosmale <inkane@chakra-project.org>
pkgname=gnokii
pkgver=0.6.31
pkgrel=1
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('x86_64')
url="http://www.gnokii.org/"
license=('GPL')
depends=('libusb-compat' 'libxpm' 'bluez' 'libical' 'sqlite3')
makedepends=('libmariadbclient' 'postgresql-libs' 'intltool')
optdepends=('dialog: sendsms tool'
            'libmariadbclient: smsd mysql backend'
	    'postgresql-libs: smsd postgresql backend')
backup=('etc/gnokiirc')
install=$pkgname.install
source=("http://www.gnokii.org/download/$pkgname/$pkgname-$pkgver.tar.bz2"
        'gnokii-config.patch'
        'gnokii-lock.patch')
md5sums=('d9627f4a1152d3ea7806df4532850d5f'
         'b2961b52ac1f770c4704ccc50b64fde2'
         '17b629db85a6bb2b98ca59941aa95295')

prepare() {
  cd $pkgname-$pkgver

  # Set bindir location
  patch -Np1 -i "$srcdir/gnokii-config.patch"

  # Set lock path
  patch -Np1 -i "$srcdir/gnokii-lock.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#13249
  sed -i 's|cellphone|phone|' xgnokii/xgnokii.desktop.in 

  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin \
              --disable-static --enable-security --disable-gtk2 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install

  install -Dm644 Docs/sample/gnokiirc "${pkgdir}/etc/gnokiirc"
  
  # Fix file permission
  chmod 755 "$pkgdir/usr/bin/sendsms"
}
