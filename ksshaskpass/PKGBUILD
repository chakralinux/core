# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/ksshaskpass


pkgname=ksshaskpass
pkgver=5.14.5
pkgrel=1
pkgdesc="ssh-add helper that uses kwallet and kpassworddialog"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/ksshaskpass'
license=('LGPL')
depends=(kwallet)
makedepends=(extra-cmake-modules kdoctools python3)
provides=(x11-ssh-askpass)
groups=('plasma')
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        "$pkgname.sh")
sha256sums=('b29987093aea1fae79f7d724daaf5137c83ab2cdb352ded6a5c74f320f11796a'
            'SKIP'
            'cba50feec8b0d058d0967407e79573b225c53dc1fe5461b3b2ddfbfbb2aff809')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
    install -Dm755 "$srcdir"/ksshaskpass.sh \
    "${pkgdir}"/etc/profile.d/ksshaskpass.sh
}
