# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@seblu.net>

pkgname=wayland-protocols
pkgver=1.5
pkgrel=1
pkgdesc='Specifications of extended Wayland protocols'
arch=('any')
url='http://wayland.freedesktop.org'
license=('MIT')
makedepends=('wayland')
source=("https://wayland.freedesktop.org/releases/$pkgname-$pkgver.tar.xz"{,.sig})
sha1sums=('fb1df343635407b4082ff73cca7005f2b780b518'
          'SKIP')
validpgpkeys=('8307C0A224BABDA1BABD0EB9A6EEEC9E0136164A')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
