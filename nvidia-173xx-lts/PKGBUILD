#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

# Find the kernel name inside the chroot
_extramodules=extramodules-3.0-lts
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=nvidia-173xx-lts
pkgver=173.14.35
pkgrel=3
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=('linux-lts>=3.0' 'linux-lts<3.1' 'nvidia-173xx-utils')
makedepends=('linux-lts-headers')
conflicts=('nvidia-96xx-lts' 'nvidia-lts')
license=('custom')
install=nvidia.install
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run"
	"ftp://download.nvidia.com/XFree86/patches/security/2012-08-01/nvidia-blacklist-vga-pmu-registers-195.diff")
md5sums=('0fa45e6fc23a2dc69faa8a66ae0e0cc5')
[ "$CARCH" = "x86_64" ] && md5sums=('6934b522d2cf313ae6e94e25763fe1a8')
md5sums+=('4d9eeb020da469b6c75cab6e05f4cf77')

build() {
    sh "NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run" --extract-only
    cd "NVIDIA-Linux-$ARCH-${pkgver}-pkg0"

    # apply security patch "http://nvidia.custhelp.com/app/answers/detail/a_id/3140"
    patch -p0 < "${srcdir}/nvidia-blacklist-vga-pmu-registers-195.diff"
    cd "usr/src/nv/"
    ln -s Makefile.kbuild Makefile
    make SYSSRC=/lib/modules/${_kver}/build module 
}

package() {
    cd "NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/"
    mkdir -p $pkgdir//lib/modules/${_extramodules}
    install -m644 nvidia.ko $pkgdir/lib/modules/${_extramodules}
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
