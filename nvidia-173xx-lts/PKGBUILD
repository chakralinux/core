#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# Find the kernel name inside the chroot
_extramodules=extramodules-`pacman -Q linux-lts | cut -c11-13 | sed 's/linux-lts //g'`-CHAKRA-LTS
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=nvidia-173xx-lts
pkgver=173.14.31
pkgrel=2
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=('linux-lts>=3.0' 'linux-lts<3.1' 'nvidia-173xx-utils')
makedepends=('linux-lts-headers')
conflicts=('nvidia-96xx-lts' 'nvidia-lts')
license=('custom')
install=nvidia.install
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run")
md5sums=('01cd3b91196cd4495f56c34494f310b5')
[ "$CARCH" = "x86_64" ] && md5sums=('d53e8faf8ac90ad98b8998f25f121535')

build() {
    sh "NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run" --extract-only
    cd "NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/"
    ln -s Makefile.kbuild Makefile
    make SYSSRC=/lib/modules/${_kver}/build module || return 1
}

package() {
    cd "NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/"
    mkdir -p $pkgdir//lib/modules/${_extramodules}
    install -m644 nvidia.ko $pkgdir/lib/modules/${_extramodules}
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
