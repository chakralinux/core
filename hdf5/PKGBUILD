#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=hdf5
pkgver=1.8.5_patch1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="General purpose library and file format for storing scientific data"
url="http://www.hdfgroup.org/HDF5/"
license=('custom')
depends=('zlib' 'sh')
source=(ftp://ftp.hdfgroup.org/HDF5/current/src/${pkgname}-${pkgver/_/-}.tar.bz2)
options=('!libtool')
md5sums=('27308a145e6c208d57db3f6f2634a0f6')

build() {
  cd $srcdir/${pkgname}-${pkgver/_/-}
  export CFLAGS="${CFLAGS/O2/O0}"
  export CXXFLAGS="${CFLAGS}"
  ./configure --prefix=/usr --disable-static \
    --enable-hl \
    --enable-threadsafe \
    --enable-linux-lfs \
    --enable-production \
    --with-pic \
    --docdir=/usr/share/doc/hdf5/ \
    --with-pthread=/usr/lib/ \
    --disable-sharedlib-rpath
  make || return 1
}

package() {
  cd $srcdir/${pkgname}-${pkgver/_/-}

  make -j1 prefix=$pkgdir/usr install

  install -d -m755 $pkgdir/usr/share/licenses/${pkgname}
  install -m644 $srcdir/${pkgname}-${pkgver/_/-}/COPYING \
          $pkgdir/usr/share/licenses/${pkgname}/LICENSE 
}

