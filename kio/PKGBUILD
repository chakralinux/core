# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kio

pkgname=kio
pkgver=5.54.0
pkgrel=1
pkgdesc='Resource and network access abstraction'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kio'
license=('LGPL')
depends=('solid' 'karchive' 'kjobwidgets' 'kbookmarks' 'libxslt' 'kwallet'
        'desktop-file-utils' 'krb5' 'acl')
makedepends=("extra-cmake-modules>=5.54.0" 'kdoctools')
checkdepends=('xorg-server-xvfb' 'openbox')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('4b24d6fd8a636b2e8d9e2f3d2cf552474ef77955f8857abb2fbda8bfe321009e'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd ${srcdir}/${pkgname}-${pkgver}
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
