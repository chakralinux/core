# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kio
source ../frameworks.conf

pkgname=kio
pkgver=${KFVersion}
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kio'
license=('LGPL')
depends=('solid' 'karchive' 'kjobwidgets' 'kbookmarks' 'libxslt' 'kwallet'
        'desktop-file-utils' 'krb5' 'acl' 'python3')
makedepends=("extra-cmake-modules>=${KFECMVersion}" 'kdoctools')
checkdepends=('xorg-server-xvfb' 'openbox')
groups=('kf5')
install=kio.install
options=("debug")
source=("${KFServer}/${pkgname}-${pkgver}.tar.xz"
        'kurlnavigator.diff')
sha256sums=( $(getSum ${pkgname}) 
            '0f8175caaa4d27e8ab0bb8c6c00c06969e0673c23e72cede1b8dabb654e6a2b5')

prepare() {
  mkdir -p build
  
  cd ${pkgname}-${pkgver}
  # patch from demm
  patch -p1 -R -i ${srcdir}/kurlnavigator.diff
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
