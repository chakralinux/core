#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=python-imaging
pkgver=1.1.7
pkgrel=2
pkgdesc="PIL. Provides image processing capabilities for python"
arch=('i686' 'x86_64')
url="http://www.pythonware.com/products/pil/index.htm"
license=('custom:"pil"')
depends=('python2' 'libjpeg' 'freetype2') 
makedepends=('tk')
optdepends=('tk')
provides=("pil=$pkgver")
conflicts=('pil')
replaces=('pil')
source=(http://effbot.org/downloads/Imaging-$pkgver.tar.gz)
md5sums=('fc14a54e1ce02a0225be8854bfba478e')

build() {
  cd $srcdir/Imaging-$pkgver
  python2 setup.py build_ext
}

package() {
  cd $srcdir/Imaging-$pkgver
  python2 setup.py install --root=$pkgdir
  install -dm755 $pkgdir/usr/include/python2.7/
  install -m644 -t $pkgdir/usr/include/python2.7/ libImaging/*.h 

  # do not have files ending in .py in /usr/bin
  for f in pildriver pilprint pilconvert pilfile pilfont; do
    mv $pkgdir/usr/bin/$f{.py,}
  done

  # Install license
  install -Dm644 $srcdir/Imaging-$pkgver/README \
                 $pkgdir/usr/share/licenses/$pkgname/README
}
