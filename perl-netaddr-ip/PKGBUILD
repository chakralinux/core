# Platform packages for Chakra, part of chakra-project.org

pkgname=perl-netaddr-ip
_realname=NetAddr-IP
pkgver=4.058
pkgrel=1
pkgdesc="Perl module to manage IP addresses and subnets"
arch=(i686 x86_64)
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
url="http://search.cpan.org/dist/${_realname}/"
source=(http://search.cpan.org/CPAN/authors/id/M/MI/MIKER/${_realname}-${pkgver}.tar.gz)
md5sums=('8829958dd84a8b36e3cd10347564739d')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
