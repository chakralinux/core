# Maintainer: Drake Justice <djustice@chakraos.org>

source ../xorg.conf

pkgname=xf86-input-evdev
pkgver=2.10.5
pkgrel=1
pkgdesc="X.org evdev input driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc' 'systemd' 'mtdev' 'libevdev')
makedepends=('xorg-server-devel' 'resourceproto' 'scrnsaverproto')
provides=('xf86-input-driver')
conflicts=('xorg-server<1.16.0' "X-ABI-XINPUT_VERSION<${X_ABI_XINPUT_MAJOR}" "X-ABI-XINPUT_VERSION>=$((X_ABI_XINPUT_MAJOR+1))")
options=('!makeflags')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('9edaa6205baf6d2922cc4db3d8e54a7e7773b5f733b0ae90f6be7725f983b70d')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

