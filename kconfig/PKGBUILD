# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kconfig
pkgver=5.48.0
pkgrel=1
pkgdesc='Offers functionality around reading and writing configuration'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kconfig'
license=('LGPL')
depends=("qt5-base")
makedepends=("extra-cmake-modules>=5.48.0" "qt5-tools")
checkdepends=("xorg-server-xvfb")
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('43a51e90092dd4d986f3200f709374e3ada9bb93ef97b5cab852c8d42c03c4bc'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
