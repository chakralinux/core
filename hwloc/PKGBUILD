
pkgname=hwloc
pkgver=1.11.3
pkgmin="${pkgver%.*}"
pkgrel=2
pkgdesc="Portable Hardware Locality is a portable abstraction of hierarchical architectures"
arch=('x86_64')
url="http://www.open-mpi.org/projects/hwloc/"
license=('BSD')
depends=('sh' 'pciutils' 'libxml2')
makedepends=('pkg-config')
options=('!docs')
source=(http://www.open-mpi.org/software/hwloc/v${pkgmin}/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('66e71c50dbbd483c0c1cdac4dbced6ce19fcbcb3')

build() {
   cd ${pkgname}-${pkgver}
   ./configure --prefix=/usr --disable-cairo
   make
}

check() {
   cd ${pkgname}-${pkgver}
   make check
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR="${pkgdir}" install

   install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

