#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Drake Justice <djustice[at]chakra-project[dot]org>

pkgname=hwloc
pkgver=1.11.3
pkgmin="${pkgver%.*}"
pkgrel=1
pkgdesc="Portable Hardware Locality is a portable abstraction of hierarchical architectures"
arch=('i686' 'x86_64')
url="http://www.open-mpi.org/projects/hwloc/"
license=('BSD')
depends=('sh' 'pciutils' 'libxml2')
makedepends=('pkg-config')
options=('!libtool' '!docs')
source=(http://www.open-mpi.org/software/hwloc/v${pkgmin}/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('66e71c50dbbd483c0c1cdac4dbced6ce19fcbcb3')

build() {
   cd ${pkgname}-${pkgver}
   ./configure --prefix=/usr --disable-cairo
   make
}

check() {
   cd ${pkgname}-${pkgver}
   make check
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR="${pkgdir}" install

   install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

