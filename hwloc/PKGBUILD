
pkgname=hwloc
pkgver=1.11.8
pkgmin="${pkgver%.*}"
pkgrel=1
pkgdesc="Portable Hardware Locality is a portable abstraction of hierarchical architectures"
arch=('x86_64')
url="http://www.open-mpi.org/projects/hwloc/"
license=('BSD')
depends=('numactl' 'libtool')
optdepends=('cairo' 'libxml2' 'pciutils')
makedepends=('cairo' 'libxml2' 'pciutils')
options=('!docs')
source=(http://www.open-mpi.org/software/hwloc/v${pkgmin}/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('5d6164400a49e2c77a7e1e98dd0247a1f7e6d4cf')

build() {
   cd ${pkgname}-${pkgver}
   ./configure --prefix=/usr --disable-cairo --enable-plugins
   make
}

check() {
   cd ${pkgname}-${pkgver}
   make check
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR="${pkgdir}" install

   install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

