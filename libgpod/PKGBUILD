# maintainer abveritas@chakra-project.org

pkgname=libgpod
pkgver=0.8.3
pkgrel=1
pkgdesc="libgpod is a shared library to access the contents of an iPod"
arch=('x86_64')
license=('LGPL')
depends=('mutagen' 'sg3_utils' 'libplist' 'libimobiledevice' 'gdk-pixbuf2')
makedepends=('intltool' 'swig' 'docbook-xsl' 'python2-gobject')
url="http://www.gtkpod.org/libgpod.html"
source=("http://downloads.sourceforge.net/sourceforge/gtkpod/${pkgname}-${pkgver}.tar.bz2")
md5sums=('f8a0b7a34e768e33a708e8dd172bd6f8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-udev-dir=/usr/lib/udev \
              --enable-udev \
              --with-python=/usr/bin/python2 \
              --disable-fast-install \
              --disable-static \
              --without-hal
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install

  #copy headers
  cp -v ${srcdir}/${pkgname}-${pkgver}/src/*.h ${pkgdir}/usr/include/gpod-1.0/gpod/
}
