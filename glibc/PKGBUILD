# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>


pkgname=glibc
pkgver=2.20
pkgrel=8
pkgdesc="GNU C Library"
arch=('x86_64')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
groups=('base')
depends=('linux-api-headers>=3.16.2' 'tzdata' 'filesystem>=2014.07')
makedepends=('gcc>=4.9')
backup=(etc/gai.conf
        etc/locale.gen
        etc/nscd.conf)
options=('!strip' 'staticlibs')
install=glibc.install
source=(http://ftp.gnu.org/gnu/libc/${pkgname}-${pkgver}.tar.xz{,.sig}
	glibc-2.20-getifaddrs_internal-segfault.patch
	glibc-2.20-linux-3.16-additions.patch
	glibc-2.20-do_ftell_wide-memleak.patch
        glibc-2.20-disable-tsx.patch
        dtv_surplus.patch
        locale.gen.txt
        locale-gen)
md5sums=('948a6e06419a01bd51e97206861595b0'
         'SKIP'
         '1c5d5c2017445c75dbc5c6d0c1e45ddb'
         '8f1059f431b842e54b12bde689620df8'
         'b50feeab78fa6ce0a8cfb41ee8dc1fd8'
         'd7fd951fd0b7d891eefb6b14e5dd2d28'
         '6469cba12e2252e16e32db0353ed3f21'
         '07ac979b6ab5eeb778d55f041529d623'
         '476e9113489f93b348b21e144b6a8fcf')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix segfault in getifaddrs_internal
  # https://sourceware.org/ml/libc-alpha/2014-09/msg00312.html
  patch -p1 -i $srcdir/glibc-2.20-getifaddrs_internal-segfault.patch
  
  # linux 3.16 additions - commit 0bd72468
  patch -p1 -i $srcdir/glibc-2.20-linux-3.16-additions.patch
  
  # plug memory leak - commit 984c0ea9
  patch -p1 -i $srcdir/glibc-2.20-do_ftell_wide-memleak.patch

  # disable tsx to avoid crashes on fw update (see https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=762195#20, https://bbs.archlinux.org/viewtopic.php?id=188879)
  patch -p1 -i $srcdir/glibc-2.20-disable-tsx.patch

  # http://chakraos.org/bugtracker/index.php?do=details&task_id=1060&project=8
  patch -p1 -i $srcdir/dtv_surplus.patch

  mkdir ${srcdir}/glibc-build
}

build() {
  cd ${srcdir}/glibc-build

  echo "slibdir=/usr/lib" >> configparms
  echo "rtlddir=/usr/lib" >> configparms
  echo "sbindir=/usr/sbin" >> configparms
  echo "rootsbindir=/usr/sbin" >> configparms

  # remove hardening options for building libraries
  CFLAGS=${CFLAGS/-fstack-protector-strong/}
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

  ${srcdir}/${pkgname}-${pkgver}/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --with-headers=/usr/include \
      --with-bugurl=https://chakraos.org/bugtracker \
      --enable-add-ons \
      --enable-obsolete-rpc \
      --enable-kernel=2.6.32 \
      --enable-bind-now --disable-profile \
      --enable-stackguard-randomization \
      --enable-multi-arch
#     --enable-lock-elision \

  # build libraries with hardening disabled
  echo "build-programs=no" >> configparms
  make

  # re-enable hardening for programs
  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CC += -fstack-protector-strong -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -fstack-protector-strong -D_FORTIFY_SOURCE=2" >> configparms
  make

  # remove harding in preparation to run test-suite
  sed -i '5,7d' configparms
}

check() {
  # the linker commands need to be reordered - fixed in 2.19
  LDFLAGS=${LDFLAGS/--as-needed,/}

  cd ${srcdir}/glibc-build

  make check || true
}

package() {
  cd ${srcdir}/glibc-build

  install -dm755 ${pkgdir}/etc
  touch ${pkgdir}/etc/ld.so.conf

  make install_root=${pkgdir} install

  rm -f ${pkgdir}/etc/ld.so.{cache,conf}

  install -dm755 ${pkgdir}/usr/lib/{locale,systemd/system,tmpfiles.d}

  install -m644 ${srcdir}/${pkgname}-${pkgver}/nscd/nscd.conf ${pkgdir}/etc/nscd.conf
  install -m644 ${srcdir}/${pkgname}-${pkgver}/nscd/nscd.service ${pkgdir}/usr/lib/systemd/system
  install -m644 ${srcdir}/${pkgname}-${pkgver}/nscd/nscd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/nscd.conf

  install -m644 ${srcdir}/${pkgname}-${pkgver}/posix/gai.conf ${pkgdir}/etc/gai.conf

  install -m755 ${srcdir}/locale-gen ${pkgdir}/usr/bin

  # temporary symlink
  ln -s ../sbin/ldconfig ${pkgdir}/usr/bin/ldconfig

  # create /etc/locale.gen
  install -m644 ${srcdir}/locale.gen.txt ${pkgdir}/etc/locale.gen
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    ${srcdir}/glibc-${pkgver}/localedata/SUPPORTED >> ${pkgdir}/etc/locale.gen

  # remove the static libraries that have a shared counterpart
  # libc, libdl, libm and libpthread are required for toolchain testsuites
  # in addition libcrypt appears widely required
  rm $pkgdir/usr/lib/lib{anl,BrokenLocale,nsl,resolv,rt,util}.a

  # Do not strip the following files for improved debugging support
  # ("improved" as in not breaking gdb and valgrind...):
  #   ld-${pkgver}.so
  #   libc-${pkgver}.so
  #   libpthread-${pkgver}.so
  #   libthread_db-1.0.so

  cd $pkgdir
  strip $STRIP_BINARIES usr/bin/{gencat,getconf,getent,iconv,makedb,locale,localedef} \
                        usr/sbin/{iconvconfig,nscd} \
                        usr/sbin/{ldconfig,sln} \
                        usr/bin/{pcprofiledump,pldd,rpcgen,sprof} \
                        usr/lib/getconf/*

  strip $STRIP_STATIC usr/lib/*.a

  strip $STRIP_SHARED usr/lib/{libanl,libBrokenLocale,libcidn,libcrypt}-*.so \
                      usr/lib/libnss_{compat,db,dns,files,hesiod,nis,nisplus}-*.so \
                      usr/lib/{libdl,libm,libnsl,libresolv,librt,libutil}-*.so \
                      usr/lib/{libmemusage,libpcprofile,libSegFault}.so \
                      usr/lib/{audit,gconv}/*.so

  #
  # add lib symlinks
  #
  ln -s usr/lib ${pkgdir}/lib
  ln -s usr/lib ${pkgdir}/lib64
  ln -s lib ${pkgdir}/usr/lib64               

}
