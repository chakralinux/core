# maintainer: AlmAck <almack@chakralinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# NOTE: valgrind requires rebuilt with each major glibc version

pkgname=glibc
pkgver=2.26
pkgrel=1
_commit=633e2f7f3d88df6427aa3a7a984d3a6b796d9611  # release/2.26/master
pkgdesc="GNU C Library"
arch=('x86_64')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
groups=('base')
depends=('linux-api-headers>=4.14' 'tzdata' 'filesystem')
makedepends=('gcc>=5.2' 'git' 'gd')
backup=(etc/gai.conf
        etc/locale.gen
        etc/nscd.conf)
options=('!strip' 'staticlibs')
install=glibc.install
source=(git+https://sourceware.org/git/glibc.git#commit=${_commit}
        locale.gen.txt
        locale-gen
        0001-manual-Update-to-mention-ENODEV-for-ttyname-and-ttyn.patch
        0002-linux-ttyname-Update-a-reference-to-kernel-docs-for-.patch
        0003-linux-ttyname-Change-return-type-of-is_pty-from-int-.patch
        0004-linux-ttyname-_r-Make-tty-checks-consistent.patch
        0005-linux-ttyname-_r-Don-t-bail-prematurely-BZ-22145.patch
        0006-linux-ttyname-_r-Add-tests.patch
        bz20338.patch)
md5sums=('SKIP'
         '07ac979b6ab5eeb778d55f041529d623'
         '476e9113489f93b348b21e144b6a8fcf'
         '2bb2dfa660fbb4b349f844459f79d5a1'
         '299316b1a35426eadf3301a3fad170be'
         '41d212e3f8d838bff341102679b2af09'
         '8a886e84f950caf2ad1c37cc3a456339'
         'fc5af427803b139d9753230849053458'
         'eb4e1836f4f3721f1215450a2cca3523'
         'dc0d3ad59aeaaf591b085a77de6e03e9')

prepare() {
  mkdir -p glibc-build

  cd glibc

  local i; for i in ${source[@]}; do
    case ${i%::*} in
      *.patch)
        msg2 "Applying ${i}"
        patch -p1 -i "$srcdir/${i}"
        ;;
    esac
  done
}

build() {
  cd ${srcdir}/glibc-build

  echo "slibdir=/usr/lib" >> configparms
  echo "rtlddir=/usr/lib" >> configparms
  echo "sbindir=/usr/sbin" >> configparms
  echo "rootsbindir=/usr/sbin" >> configparms

  # remove fortify for building libraries
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

  ../${pkgname}/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --libexecdir=/usr/lib \
      --with-headers=/usr/include \
      --with-bugurl=https://code.chakralinux.org/ \
      --enable-add-ons \
      --enable-bind-now \
      --enable-lock-elision \
      --enable-multi-arch \
      --enable-obsolete-nsl \
      --enable-obsolete-rpc \
      --enable-stack-protector=strong \
      --enable-stackguard-randomization \
      --disable-profile \
      --disable-werror

  # build libraries with fortify disabled
  echo "build-programs=no" >> configparms
  make

  # re-enable fortify for programs
  sed -i "/build-programs=/s#no#yes#" configparms

  echo "CC += -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -D_FORTIFY_SOURCE=2" >> configparms
  make
}

check() {
  cd glibc-build

  # remove fortify in preparation to run test-suite
  sed -i '/FORTIFY/d' configparms

  # some failures are "expected"
  make check || true
}

package() {
  cd glibc-build

  install -dm755 ${pkgdir}/etc
  touch ${pkgdir}/etc/ld.so.conf

  make install_root=${pkgdir} install
  rm -f ${pkgdir}/etc/ld.so.{cache,conf}

  cd ../glibc
  
  install -dm755 ${pkgdir}/usr/lib/{locale,systemd/system,tmpfiles.d}

  install -m644 nscd/nscd.conf ${pkgdir}/etc/nscd.conf
  install -m644 nscd/nscd.service ${pkgdir}/usr/lib/systemd/system
  install -m644 nscd/nscd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/nscd.conf

  install -m644 posix/gai.conf ${pkgdir}/etc/gai.conf

  install -m755 ${srcdir}/locale-gen ${pkgdir}/usr/bin

  # temporary symlink
  ln -s ../sbin/ldconfig ${pkgdir}/usr/bin/ldconfig

  # create /etc/locale.gen
  install -m644 ${srcdir}/locale.gen.txt ${pkgdir}/etc/locale.gen
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    ${srcdir}/glibc/localedata/SUPPORTED >> ${pkgdir}/etc/locale.gen

  # Do not strip the following files for improved debugging support
  # ("improved" as in not breaking gdb and valgrind...):
  #   ld-${pkgver}.so
  #   libc-${pkgver}.so
  #   libpthread-${pkgver}.so
  #   libthread_db-1.0.so

  cd "$pkgdir"
  strip $STRIP_BINARIES usr/bin/{gencat,getconf,getent,iconv,makedb,locale,localedef} \
                        usr/sbin/{iconvconfig,nscd} \
                        usr/sbin/{ldconfig,sln} \
                        usr/bin/{pcprofiledump,pldd,rpcgen,sprof} \
                        usr/lib/getconf/*

  strip $STRIP_STATIC usr/lib/lib{anl,BrokenLocale,c{,_nonshared},crypt}.a \
                      usr/lib/lib{dl,g,ieee,mcheck,nsl,pthread{,_nonshared}}.a \
                      usr/lib/lib{resolv,rpcsvc,rt,util}.a \
                      usr/lib/lib{m-${pkgver},mvec{,_nonshared}}.a

  strip $STRIP_SHARED usr/lib/lib{anl,BrokenLocale,cidn,crypt}-${pkgver}.so \
                      usr/lib/libnss_{compat,db,dns,files,hesiod,nis,nisplus}-*.so \
                      usr/lib/lib{dl,m,nsl,resolv,rt,util}-${pkgver}.so \
                      usr/lib/lib{memusage,pcprofile,SegFault}.so \
                      usr/lib/{audit,gconv}/*.so usr/lib/libmvec-*.so || true

  #
  # add lib symlinks
  #
  ln -s usr/lib ${pkgdir}/lib
  ln -s usr/lib ${pkgdir}/lib64
  ln -s lib ${pkgdir}/usr/lib64               

}
