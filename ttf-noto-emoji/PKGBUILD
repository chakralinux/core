# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=ttf-noto-emoji
_pkgver=2015-12-24
_commit=941712
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Google Noto emoji fonts"
arch=(any)
url="https://www.google.com/get/noto/"
license=(Apache)
depends=(fontconfig)
makedepends=(cairo optipng python2-fonttools nototools imagemagick zopfli python2-setuptools)
install=$pkgname.install
source=("https://github.com/googlei18n/noto-emoji/archive/$_commit.zip")
md5sums=('ee5c7ba995682b98a9f9b55e5eb7c7d5')

prepare() {
  cd noto-emoji-*
  sed -e 's|ttx "$<"|ttx2 "$<"|g' -i Makefile
  ln -rs add_emoji_gsub.py third_party/color_emoji/
}

build() {
  cd noto-emoji-*
  make
}

package() {
  cd noto-emoji-*
  install -Dm644 NotoColorEmoji.ttf "$pkgdir"/usr/share/fonts/TTF/NotoColorEmoji.ttf 
}
