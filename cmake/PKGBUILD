# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=cmake
pkgver=2.8.10.2
pkgrel=3
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
license=('custom')
url="http://www.cmake.org"
depends=('curl' 'libarchive>=2.3.1' 'shared-mime-info')
makedepends=('qt')
install=${pkgname}.install
source=("http://www.cmake.org/files/v2.8/${pkgname}-${pkgver}.tar.gz"
        "add_extra_prefix_to_find_paths.patch")
md5sums=('097278785da7182ec0aea8769d06860c'
         '1e6575c3c54ecbbbccd64dd94a4192df')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  
  # add /extra prefix to find paths
  patch -Np1 -i ../add_extra_prefix_to_find_paths.patch

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --qt-gui \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  vimpath="$pkgdir/usr/share/vim/vimfiles"
  install -Dm644 Docs/cmake-indent.vim ${vimpath}/indent/cmake-indent.vim
  install -Dm644 Docs/cmake-syntax.vim ${vimpath}/syntax/cmake-syntax.vim
  install -Dm644 Docs/cmake-mode.el ${pkgdir}/usr/share/emacs/site-lisp/cmake-mode.el

  install -Dm644 Copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
