# maintainer: Neophytos Kolokotronis <tetris4ATgmailDOTcom>

pkgname=cmake
pkgver=3.0.2
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
license=('custom')
url="http://www.cmake.org"
depends=('curl' 'libarchive>=2.3.1' 'shared-mime-info')
makedepends=('qt' 'cmake')
install=${pkgname}.install
source=("http://www.cmake.org/files/v3.0/${pkgname}-${pkgver}.tar.gz")
sha1sums=('379472e3578902a1d6f8b68a9987773151d6f21a')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  # patch -Np1 < ${srcdir}/findfreetype.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  
 # cmake -DBUILD_QtDialog=ON \
 #       -DCMAKE_INSTALL_PREFIX=/usr \
#	-DCMAKE_USE_SYSTEM_LIBRARIES=ON \
 #       . 

  # below version is for boostrapping cmake;
  # one can use it to break the recursive makedependency
  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --qt-gui \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${vimpath}"/{help,indent,syntax}
  ln -s /usr/share/cmake-3.0/editors/vim/cmake-help.vim "${vimpath}"/help/
  ln -s /usr/share/cmake-3.0/editors/vim/cmake-indent.vim "${vimpath}"/indent/
  ln -s /usr/share/cmake-3.0/editors/vim/cmake-syntax.vim "${vimpath}"/syntax/

  install -d "${pkgdir}"/usr/share/emacs/site-lisp/
  ln -s /usr/share/cmake-3.0/editors/emacs/cmake-mode.el "${pkgdir}"/usr/share/emacs/site-lisp/

  install -Dm644 Copyright.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
