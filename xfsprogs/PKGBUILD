#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=xfsprogs
pkgver=3.1.8
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
license=('LGPL')
url="http://oss.sgi.com/projects/xfs/"
groups=('base')
depends=('util-linux')
options=('!makeflags' '!libtool')
source=("http://chakra-linux.org/sources/xfsprogs/${pkgname}-${pkgver}.tar.gz")
md5sums=('f70b2e7200d4c29f0af1cf70e7be1db6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DIST_ROOT="${pkgdir}" install install-dev
  chown -R root $pkgdir
  chgrp -R root $pkgdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}

