#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=xfsprogs
pkgver=3.2.1
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
license=('LGPL')
url="http://oss.sgi.com/projects/xfs/"
groups=('base')
depends=('util-linux')
options=('!makeflags' '!libtool')
# We mirror the sources as upstream tends to move them once a new release is out
source=("ftp://ftp.archlinux.org/other/xfsprogs/${pkgname}-${pkgver}.tar.gz")
md5sums=('5c6905932029c8f9207fe5a0a8aac24b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/sbin" install install-dev
  chown -R root $pkgdir
  chgrp -R root $pkgdir
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
