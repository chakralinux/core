# $Id: PKGBUILD 59900 2009-11-29 16:03:59Z jgc $
# Maintainer: damir <damir@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio
pkgver=0.83
pkgrel=1
pkgdesc="GNU Compact Disc Input and Control Library"
arch=("i686" "x86_64")
license=('GPL3')
url="http://www.gnu.org/software/libcdio/"
depends=('gcc-libs>=4.4.2' 'libcddb' 'ncurses')
options=('!libtool')
install=libcdio.install
source=(http://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.gz)
source=("http://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.gz"
        "libcdio-0.83-linking.patch")
md5sums=('b9e0f1bccb142e697cd834fe56b6e6fb'
         '8ff23299effd635725fd070d03aac9cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -Np1 -i "${srcdir}/libcdio-0.83-linking.patch"
  
  ./configure --prefix=/usr \
              --disable-vcd-info \
              --enable-cpp-progs \
              --disable-static
              
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make -j1 DESTDIR="${pkgdir}" install
  sed -i "/LIBCDIO_SOURCE_PATH/s|.*|/* #undef LIBCDIO_SOURCE_PATH */|" \
    "${pkgdir}/usr/include/cdio/cdio_config.h"
}
