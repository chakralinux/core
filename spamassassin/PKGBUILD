#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=spamassassin
pkgver=3.3.2
pkgrel=2
pkgdesc="A mail filter to identify spam."
arch=('x86_64')
license=('APACHE')
url="http://spamassassin.apache.org"
depends=('perl>=5.14.2' 'perl-net-dns' 'perl-uri' 'perl-html-parser' \
		'perl-digest-sha1' 'perl-io-socket-ssl' 'openssl' \
		'perl-libwww' 'perl-io-zlib' 'perl-mail-spf' 'perl-mail-dkim' \
		'perl-archive-tar' 'perl-io-socket-inet6' 'zlib' 're2c')
makedepends=('razor' 'perl-dbi') 
optdepends=('razor: to identify collaborately-flagged spam')
backup=('etc/mail/spamassassin/local.cf'
        'etc/mail/spamassassin/init.pre'
        'etc/mail/spamassassin/v310.pre'
        'etc/mail/spamassassin/v312.pre'
        'etc/mail/spamassassin/v320.pre'
        'etc/mail/spamassassin/v330.pre')
install="${pkgname}.install"
options=('!emptydirs')
source=("http://mirrors.devlib.org/apache/${pkgname}/source/Mail-SpamAssassin-${pkgver}.tar.gz"
        'spamd.conf.d' 'spamassassin.service')

md5sums=('d1d62cc5c6eac57e88c4006d9633b81e'
         'af501c6b0bb937a2775b747cc4b6d150'
         'ded3e012a4d502df001f2d97c495cc4e')

build() {
  cd "${srcdir}/Mail-SpamAssassin-${pkgver}"

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor \
      CONTACT_ADDRESS=root@localhost ENABLE_SSL=yes PERL_TAINT=no
  make
}

check() {
  cd "${srcdir}/Mail-SpamAssassin-${pkgver}"

  # SSLv2 is disabled by upstream so this test would fail
  rm -rf t/spamd_ssl_v2.t

  # parallel tests cause lots of failures; disable for now
  #export HARNESS_OPTIONS="j$(echo $MAKEFLAGS | sed 's/.*-j\([0-9][0-9]*\).*/\1/')"

  make test
}

package() {
  cd "${srcdir}/Mail-SpamAssassin-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/spamd.conf.d" "${pkgdir}/etc/conf.d/spamd"
  install -D -m644 "${srcdir}/spamassassin.service" "${pkgdir}/usr/lib/systemd/system/spamassassin.service"
}
