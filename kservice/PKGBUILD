# Contributor : Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kservice
pkgver=${KFVersion}
pkgrel=2
pkgdesc='Advanced plugin and service introspection'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kservice'
license=('LGPL')
depends=('ki18n' 'kconfig' 'kcrash' 'kdbusaddons')
makedepends=("extra-cmake-modules>=${KFECMVersion}" 'kdoctools' 'python3')
checkdepends=('xorg-server-xvfb' 'kded')
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgname}-${pkgver}.tar.xz"
        '03_chakra_menu.patch')
sha256sums=( $(getSum ${pkgname}) 
            '505e9e5e969be5094b3f713b865d5927cc3de1f4693915174732b20793228c4f')

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}/src
  patch -Np2 -i ${srcdir}/03_chakra_menu.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver} \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

# check() {
#   cd build
#   eval `dbus-launch --sh-syntax `
#   xvfb-run make test
#   kill $DBUS_SESSION_BUS_PID
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
