# Contributor : Andrea Scarpino <andrea@archlinux.org>


pkgname=kservice
pkgver=5.50.0
pkgrel=1
pkgdesc='Advanced plugin and service introspection'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kservice'
license=('LGPL')
depends=('ki18n' 'kconfig' 'kcrash' 'kdbusaddons')
makedepends=("extra-cmake-modules>=5.50.0" 'kdoctools' 'python3')
checkdepends=('xorg-server-xvfb' 'kded')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        '03_chakra_menu.patch')
sha256sums=('29850fb51f6f4eb757e89ca3ad2baf740aff62f07ecd4de6d6a0d9935f981e78'
            SKIP
            '505e9e5e969be5094b3f713b865d5927cc3de1f4693915174732b20793228c4f')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}/src
  patch -Np2 -i ${srcdir}/03_chakra_menu.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

# check() {
#   cd build
#   eval `dbus-launch --sh-syntax `
#   xvfb-run make test
#   kill $DBUS_SESSION_BUS_PID
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
