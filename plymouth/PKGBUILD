pkgname=plymouth
pkgver=0.9.3
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support"
url="http://www.freedesktop.org/wiki/Software/Plymouth/"
arch=('x86_64')
license=('GPL')
depends=('libdrm' 'pango' 'systemd')
makedepends=('git' 'docbook-xsl')
optdepends=('ttf-dejavu')
options=('!libtool' '!emptydirs')
conflicts=('plymouth-git')
backup=('etc/plymouth/plymouthd.conf')
source=("http://www.freedesktop.org/software/${pkgname}/releases/${pkgname}-${pkgver}.tar.xz"
        'chakra-logo.png'
        'plymouth.encrypt_hook'
        'plymouth.encrypt_install'
        'sddm-plymouth.service'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'sd-plymouth.initcpio_install'
        'plymouth-set-default-theme.in.patch'
        'plymouth-quit.service.in.patch'
        'plymouth-update-initrd.patch'
        'plymouthd.conf')

sha256sums=('9f8dd08a90ceaf6228dcd8c27759adf18fc9482f15b6c56dcbcced268b4e4a74'
            '17ba688027bad20e888f703fef33f2460be4cd4426dd2d5291f9126c71cfceb2'
            'cbfb5bcb6897703e088eefdee9d578f75d987116becafecbb2018b283ba3865e'
            '544399d4f3454644f24ad11e504eaa7d622c73942728f52fb55bea054c756fa1'
            '62be126971068eb6138674f797dc9cfce4dc59371d31ff5388cd8de2b1cb397e'
            'd349b30832ff84ea7e4b86265efd5d08f8ff00e9153f05f41c8e8b49addf321c'
            'b0467b7c490539470f726358e5d0c72951efd58d268a2d16411dafacba68041a'
            'f434dd9cc96c3cec470aedb7734663cd3e04ffa34eff2bea92708f966132a2da'
            'b7ce99519c4ecbd582ac4b2ee873c56424c6eee4d32ba8b0074798f4c23a12df'
            'fc900a0c330b9bcbf84afaa6575d9df392c4e8c2ea699b72dc9a137f4972de49'
            'bb597e2ced7c3beca8c9e9f212bc001750746d9b41e00071697a0352ec2a2e27'
            '4e28ed8a22dd722f939f788283edde6a6310985c2060c1fd9562ed5b72f6b5ae')

prepare() {
        cd "$srcdir"/${pkgname}-${pkgver}

        patch -p1 -i $srcdir/plymouth-set-default-theme.in.patch
        patch -p1 -i "$srcdir"/plymouth-update-initrd.patch
        patch -p1 -i "$srcdir"/plymouth-quit.service.in.patch
}

build() {
	cd "$srcdir"/${pkgname}-${pkgver}

	LDFLAGS="$LDFLAGS -ludev" ./configure \
		--prefix=/usr \
		--exec-prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--sbindir=/usr/bin \
		--enable-systemd-integration \
		--enable-drm \
		--enable-tracing \
		--enable-pango \
		--enable-gtk=no \
		--with-release-file=/etc/os-release \
		--with-logo=/usr/share/plymouth/chakra-logo.png \
		--with-background-color=0x000000 \
		--with-background-start-color-stop=0x000000 \
		--with-background-end-color-stop=0x4D4D4D \
		--without-rhgb-compat-link \
		--without-system-root-install

	make
}

package() {
	cd "$srcdir"/${pkgname}-${pkgver}

	make DESTDIR="$pkgdir" install

	install -Dm644 "$srcdir"/chakra-logo.png "$pkgdir"/usr/share/plymouth/arch-logo.png

	install -Dm644 "$srcdir"/plymouth.encrypt_hook "$pkgdir"/usr/lib/initcpio/hooks/plymouth-encrypt
	install -Dm644 "$srcdir"/plymouth.initcpio_hook "$pkgdir"/usr/lib/initcpio/hooks/plymouth
	install -Dm644 "$srcdir"/plymouth.encrypt_install "$pkgdir"/usr/lib/initcpio/install/plymouth-encrypt
	install -Dm644 "$srcdir"/plymouth.initcpio_install "$pkgdir"/usr/lib/initcpio/install/plymouth
	install -Dm644 "$srcdir"/sd-plymouth.initcpio_install "$pkgdir"/usr/lib/initcpio/install/sd-plymouth

	install -dm755 "$pkgdir"/usr/lib/systemd/system
	install -m644 "$srcdir"/sddm-plymouth.service "$pkgdir"/usr/lib/systemd/system

	ln -s '../systemd-ask-password-plymouth.path' "$pkgdir"/usr/lib/systemd/system/sysinit.target.wants/systemd-ask-password-plymouth.path

	install -Dm644 "$srcdir"/plymouthd.conf "$pkgdir"/etc/plymouth/plymouthd.conf
}
