#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=alsa-utils
pkgver=1.0.24.2
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib" 'dialog' 'pciutils' 'ncurses')
makedepends=('xmlto' 'docbook-xsl')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$pkgname-${pkgver}.tar.bz2
#source=("http://chakra.sourceforge.net/sources/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        alsa
        alsaconf.patch
        alsa.conf.d
        90alsa)
backup=(etc/conf.d/alsa)

# create tarball: source PKGBUILD && mksource

mksource() {
	  rm -vRf ${pkgname}-${pkgver}
	  git clone git://git.alsa-project.org/${pkgname}.git ${pkgname}-${pkgver}
	  rm -vRf ${pkgname}-${pkgver}/.git
	  pushd ${pkgname}-${pkgver}
	  popd
	  tar -cvJf ${pkgname}-${pkgver}.tar.xz ${pkgname}-${pkgver}/*
	  md5sum ${pkgname}-${pkgver}.tar.xz
}

build() {
  cd $startdir/src/$pkgname-${pkgver}

  patch -Np1 -i ../alsaconf.patch
  ./configure --prefix=/usr
  make 
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m755 ../alsa ${pkgdir}/etc/rc.d/alsa
  install -D -m644 ../alsa.conf.d ${pkgdir}/etc/conf.d/alsa
  install -D -m755 ../90alsa ${pkgdir}/etc/pm/sleep.d/90alsa
  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}
md5sums=('8238cd57cb301d1c36bcf0ecb59ce6b2'
         '3ef812cae83053d01cd560284efad71f'
         '62ebef77d5617d0036012cc541dcf23c'
         '93089f0d8872078939f76dd2acaebc2b'
         '529216f6a46e61a546640e08ea7a0879')
