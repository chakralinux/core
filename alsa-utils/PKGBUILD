#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=alsa-utils
pkgver=1.0.25
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib" 'dialog' 'pciutils' 'ncurses' 'psmisc' 'libsamplerate')
makedepends=('xmlto' 'docbook-xsl')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$pkgname-${pkgver}.tar.bz2
        alsa
        alsaconf.patch
        alsa.conf.d
        90alsa)
backup=(etc/conf.d/alsa)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ../alsaconf.patch
  ./configure --prefix=/usr --with-systemdsystemunitdir=/lib/systemd/system
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m755 ../alsa ${pkgdir}/etc/rc.d/alsa
  install -D -m644 ../alsa.conf.d ${pkgdir}/etc/conf.d/alsa
  install -D -m755 ../90alsa ${pkgdir}/etc/pm/sleep.d/90alsa
  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}
md5sums=('f81f9dcb9a014fd32cb3a70066a5b9a9'
         '2ba1a1cbf653bf1a69f8a720fd6549da'
         '7521bf20f5b3501a510ec4462ae4a934'
         '4c8ae9f976c7df5e271a4cb91ccc7767'
         '529216f6a46e61a546640e08ea7a0879')
