# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=libxt
pkgver=1.1.5
pkgrel=1
pkgdesc="X11 toolkit intrinsics library"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libsm' 'libx11')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXt-${pkgver}.tar.bz2)
sha1sums=('c79e2c4f7de5259a2ade458817a139b66a043d59')

build() {
  cd "${srcdir}/libXt-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXt-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
