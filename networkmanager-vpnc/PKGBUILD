pkgname=networkmanager-vpnc
pkgver=1.2.4
pkgrel=1
pkgdesc="NetworkManager VPN plugin for VPNC"
url="https://wiki.gnome.org/Projects/NetworkManager"
arch=('x86_64')
license=('GPL')
depends=('networkmanager' 'vpnc')
makedepends=('intltool')
_commit=f6969f0038438d4ef9320e275eb22df98592fe53  # tags/1.2.4^0
source=("git://git.gnome.org/network-manager-vpnc#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd network-manager-vpnc
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd network-manager-vpnc
  # NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd network-manager-vpnc
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --enable-more-warnings=yes \
    --without-gnome \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd network-manager-vpnc
  make DESTDIR="$pkgdir" install
}
