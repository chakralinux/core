#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=raptor
pkgver=2.0.3
pkgrel=1
pkgdesc="A C library that parses RDF/XML/N-Triples into RDF triples"
arch=('i686' 'x86_64')
url="http://librdf.org/raptor"
depends=('libxml2>=2.7.8' 'curl>=7.21.6' 'zlib>=1.2.5' 'libxslt>=1.1.26')
license=('LGPL')
options=('!libtool')
source=("http://librdf.org/dist/source/raptor2-$pkgver.tar.gz"
        'fix-soprano-bug.patch')
md5sums=('46eff4b20f8752d1146a3e0c8b2168dd'
         'fd63150d1e9f83bb59af0d86c373c8fa')

build() {
  cd ${srcdir}/raptor2-${pkgver}

  # see http://bugs.librdf.org/mantis/view.php?id=451
  patch -p1 -i "${srcdir}"/fix-soprano-bug.patch

  # fix build with curl >= 7.21.2
  sed -i '/#include <curl\/types.h>/d' src/raptor_internal.h

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${srcdir}/raptor2-${pkgver}
  make prefix=${pkgdir}/usr install
}