# Platform Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributions from AUR: https://aur.archlinux.org/packages/nvidia-bumblebee/

pkgname=nvidia-bumblebee
pkgver=313.30
pkgrel=1
_kernver='3.9.0-1-CHAKRA'
_extramodules='extramodules-3.8-CHAKRA' 
pkgdesc="NVIDIA drivers for linux. Packaged for Bumblebee"
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('linux>3.9' 'linux<3.10' "nvidia-utils-bumblebee")
provides=("nvidia=${pkgver}")
makedepends=('linux-headers>3.9' 'linux-headers<3.10')
conflicts=('nvidia' 'nvidia-96xx' 'nvidia-173xx' 'nvidia-304xx' 'dkms-nvidia')
license=('custom')
install=nvidia.install
options=(!strip)
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run")
md5sums=('e5f147fbcdcad71472b4ddeccf259bd7')

build() {
    cd "${srcdir}"
    sh "NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run" --extract-only

    cd "NVIDIA-Linux-x86_64-${pkgver}-no-compat32/kernel"
    make SYSSRC=/lib/modules/"${_kernver}/build" module
}
 
package() {
    install -D -m644 "${srcdir}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/lib/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
