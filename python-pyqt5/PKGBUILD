pkgbase=pyqt5
pkgname=('python-pyqt5-common' 'python3-pyqt5' 'python2-pyqt5')
pkgver=5.12
pkgrel=1
arch=('x86_64')
url="https://riverbankcomputing.com/software/pyqt/intro"
license=('GPL')
makedepends=('sip'
             'python3-sip'
             'python2-sip'
             'python3-sip-pyqt5'
             'python2-sip-pyqt5'
             'python3-opengl'
             'python2-opengl'
             'python2-dbus'
             'python3-dbus'
             'python2-enum34'
             'qt5-connectivity'
             'qt5-multimedia'
             'qt5-tools'
             'qt5-serialport'
             'qt5-svg'
             'qt5-webkit'
             'qt5-websockets'
             'qt5-x11extras'
             'qt5-networkauth'
             'qt5-xmlpatterns'
             'qt5-remoteobjects')
source=("https://www.riverbankcomputing.com/static/Downloads/PyQt5/PyQt5_gpl-$pkgver.tar.gz")
sha256sums=('d9e70065b5980afde5f2b9bc900910050331604e02c70666c45fcfc66b0d4f34')

prepare() {
  # Support new versions of Qt - don't remove, needs to be reapplied after every new Qt release
  pushd PyQt5_gpl-$pkgver
  #patch -p1 -i ../pyqt-support-new-qt.patch
  popd

  cp -a PyQt5_gpl-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PyQt5_gpl-$pkgver
  python3 configure.py \
    --confirm-license \
    --no-sip-files \
    --no-dist-info \
    --qsci-api \
    -q /usr/bin/qmake-qt5

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
  make

  cd "$srcdir"/PyQt5_gpl-$pkgver-py2
  python2 configure.py \
    --confirm-license \
    --no-sip-files \
    --no-dist-info \
    --qsci-api \
    -q /usr/bin/qmake-qt5

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
  make
}

package_python-pyqt5-common(){
  pkgdesc="Common PyQt files shared between python3-pyqt5 and python2-pyqt5"
  depends=('qt5-base')

  cd PyQt5_gpl-$pkgver
  install -Dm644 PyQt5.api "$pkgdir"/usr/share/qt5/qsci/api/python/PyQt5.api

  install -d "$pkgdir"/usr/share/sip/PyQt5
  cp -a sip/* "$pkgdir"/usr/share/sip/PyQt5
}

package_python3-pyqt5(){
  pkgdesc="A set of Python 3.x bindings for the Qt5 toolkit"
  depends=('python3-sip-pyqt5' 'python-pyqt5-common')
  optdepends=('python3-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python3-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-x11extras: QtX11Extras'
              'qt5-networkauth: QtNetworkAuth'
              'qt5-remoteobjects: QtRemoteObjects')

  cd PyQt5_gpl-$pkgver

  msg "Packaging python3-pyqt5"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Remove unused py2 version of uic modules:
  rm -r "$pkgdir"/usr/lib/python*/site-packages/PyQt5/uic/port_v2

  # compile Python bytecode
  python3 -m compileall -d / "$pkgdir"/usr/lib

  # Provided by pyqt-common
  rm "$pkgdir"/usr/share/qt5/qsci/api/python/PyQt5.api
}

package_python2-pyqt5(){
  pkgdesc="A set of Python 2.x bindings for the Qt5 toolkit"
  depends=('python2-sip-pyqt5' 'python-pyqt5-common')
  optdepends=('python2-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python2-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-x11extras: QtX11Extras'
              'qt5-networkauth: QtNetworkAuth'
              'qt5-remoteobjects: QtRemoteObjects')

  cd PyQt5_gpl-$pkgver-py2
  msg "Packaging pyqt5-python2"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Remove unused py3 version of uic modules:
  rm -r "$pkgdir"/usr/lib/python*/site-packages/PyQt5/uic/port_v3

  # compile Python bytecode
  python2 -m compileall -d / "$pkgdir"/usr/lib

  # Fix conflicts with python3-pyqt5
  mv "$pkgdir"/usr/bin/{,python2-}pyuic5
  mv "$pkgdir"/usr/bin/{,python2-}pylupdate5
  mv "$pkgdir"/usr/bin/{,python2-}pyrcc5

  rm "$pkgdir"/usr/lib/qt5/plugins/designer/libpyqt5.so
  rm "$pkgdir"/usr/lib/qt5/plugins/PyQt5/libpyqt5qmlplugin.so
  rm "$pkgdir"/usr/share/qt5/qsci/api/python/PyQt5.api
}
