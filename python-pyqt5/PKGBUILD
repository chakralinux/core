pkgbase=pyqt5
pkgname=('python-pyqt5-common' 'python3-pyqt5' 'python2-pyqt5')
pkgver=5.10.1
pkgrel=1
arch=('x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
makedepends=('python3-sip'
             'python2-sip'
             'python3-opengl'
             'python2-opengl'
             'python2-dbus'
             'python3-dbus'
             'qt5-connectivity'
             'qt5-multimedia'
             'qt5-tools'
             'qt5-serialport'
             'qt5-svg'
             'qt5-webengine'
             'qt5-webkit'
             'qt5-websockets'
             'qt5-x11extras'
             'qt5-networkauth')
source=("http://sourceforge.net/projects/pyqt/files/PyQt5/PyQt-$pkgver/PyQt5_gpl-$pkgver.tar.gz"
        pyqt-support-new-qt.patch
        pyqt5-cura-crash.patch
        pyqt-qt5.11.patch)
sha512sums=('04cd0292f7a088f9c0d088d7c73ec96cfc40253f771dfdf194eb92be7635e6fcaf04863fa24215e9c34a096658549bb2feeb8d8fdca1e39ed87f0256ef0f790b'
            'a9c45534d540cd4eff732a92e66f98eb313b0734e64a2289fbc703fdb208265a610ccc3c4f54f43d0813afa3a7003ec7956e6029904cc56992a5e8cc2efb280c'
            '3f6e1c0dcd95d3ff39c03f59d7c09cbf2e42431bf9cb6dc7a63da10b7a387e5977992e57ef0ff1dea495f29dfbf4db2568b4588e801cca966a0136fe98d1ed45'
            'bae60dd592701929acf403514d8be87d53330dccff74267efb615005d82f5710ce13636a99b2a75beb04fd93a4ade34486c3c2aa190efd334d01c78b34f1bcc0')

prepare() {
  # Support new versions of Qt - don't remove, needs to be reapplied after every new Qt release
  pushd PyQt5_gpl-$pkgver
  patch -p1 -i ../pyqt-support-new-qt.patch
  patch -p1 -i ../pyqt5-cura-crash.patch # Fix crash in Cura
  patch -p1 -i ../pyqt-qt5.11.patch # Fix build with Qt 5.11 (opensuse)
  popd

  # The additional include path was removed due to this line, I don't really know why they are doing this...
  sed -i '/target_config.dbus_inc_dirs = \[\]/d' PyQt5_gpl-$pkgver/configure.py

  cp -a PyQt5_gpl-$pkgver{,-py2}
}

build() {
  msg "Configuring the Python 3 version"
  cd "$srcdir"/PyQt5_gpl-$pkgver
  python3 configure.py \
    --confirm-license \
    --no-sip-files \
    --qsci-api \
    -q /usr/bin/qmake-qt5

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
  msg "Making the Python 3 version"
  make

  cd "$srcdir"/PyQt5_gpl-$pkgver-py2
  msg "Configuring the Python 2 version"
  python2 configure.py \
    --confirm-license \
    --no-sip-files \
    --qsci-api \
    -q /usr/bin/qmake-qt5

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
  msg "Making the Python 2 version"
  make
}

package_python-pyqt5-common(){
  pkgdesc="Common PyQt files shared between python3-pyqt5 and python2-pyqt5"
  depends=('qt5-base')

  cd PyQt5_gpl-$pkgver
  install -Dm644 PyQt5.api "$pkgdir"/usr/share/qt5/qsci/api/python/PyQt5.api

  install -d "$pkgdir"/usr/share/sip/PyQt5
  cp -a sip/* "$pkgdir"/usr/share/sip/PyQt5
}

package_python3-pyqt5(){
  pkgdesc="A set of Python 3.x bindings for the Qt5 toolkit"
  depends=('python3-sip' 'python-pyqt5-common')
  optdepends=('python3-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python3-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-webengine: QtWebEngine, QtWebEngineCore, QtWebEngineWidgets'
              'qt5-x11extras: QtX11Extras')

  cd PyQt5_gpl-$pkgver

  msg "Packaging python3-pyqt5"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Fix conflicts with python2-pyqt5
  mv "$pkgdir"/usr/bin/{,python3-}pyuic5
  mv "$pkgdir"/usr/bin/{,python3-}pylupdate5
  mv "$pkgdir"/usr/bin/{,python3-}pyrcc5

  # Provided by pyqt-common
  rm "$pkgdir"/usr/share/qt5/qsci/api/python/PyQt5.api
}

package_python2-pyqt5(){
  pkgdesc="A set of Python 2.x bindings for the Qt5 toolkit"
  depends=('python2-sip' 'python-pyqt5-common')
  optdepends=('python2-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-webengine: QtWebEngine, QtWebEngineCore, QtWebEngineWidgets'
              'qt5-x11extras: QtX11Extras')

  cd PyQt5_gpl-$pkgver-py2
  msg "Packaging pyqt5-python2"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Fix conflicts with python3-pyqt5
  mv "$pkgdir"/usr/bin/{,python2-}pyuic5
  mv "$pkgdir"/usr/bin/{,python2-}pylupdate5
  mv "$pkgdir"/usr/bin/{,python2-}pyrcc5

  # Default to python2 version
  ln -s python2-pyuic5     "$pkgdir"/usr/bin/pyuic5
  ln -s python2-pylupdate5 "$pkgdir"/usr/bin/pylupdate5
  ln -s python2-pyrcc5     "$pkgdir"/usr/bin/pyrcc5

  rm "$pkgdir"/usr/lib/qt5/plugins/designer/libpyqt5.so
  rm "$pkgdir"/usr/lib/qt5/plugins/PyQt5/libpyqt5qmlplugin.so
  rm "$pkgdir"/usr/share/qt5/qsci/api/python/PyQt5.api
}
