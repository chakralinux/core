pkgbase=pyqt5
pkgname=('python-pyqt5-common' 'python3-pyqt5' 'python2-pyqt5')
pkgver=5.11.2
pkgrel=1
arch=('x86_64')
url="https://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
makedepends=('python3-sip'
             'python2-sip'
             'python3-sip-pyqt5'
             'python2-sip-pyqt5'
             'python3-opengl'
             'python2-opengl'
             'python2-dbus'
             'python3-dbus'
             'python2-enum34'
             'qt5-connectivity'
             'qt5-multimedia'
             'qt5-tools'
             'qt5-serialport'
             'qt5-svg'
             'qt5-webengine'
             'qt5-webkit'
             'qt5-websockets'
             'qt5-x11extras'
             'qt5-networkauth')
source=("https://sourceforge.net/projects/pyqt/files/PyQt5/PyQt-$pkgver/PyQt5_gpl-$pkgver.tar.gz")
sha512sums=('fc60246b5e1ca0d8aeef829fe20de9dd28b77e3c151532f9dbcd92c0deec12d8308cc799901aeec3f84af745e735053d2aaad2866ef64a2426cba343e08bc2c0')

prepare() {
  # Support new versions of Qt - don't remove, needs to be reapplied after every new Qt release
  pushd PyQt5_gpl-$pkgver
  #patch -p1 -i ../pyqt-support-new-qt.patch
  popd

  cp -a PyQt5_gpl-$pkgver{,-py2}

  # Remove check for sip from py2, it doesn't work since sip doesn't install __init__.py
  sed -e 's|from PyQt5 import sip|pass|g' -i PyQt5_gpl-$pkgver-py2/configure.py
}

build() {
  msg "Configuring the Python 3 version"
  cd "$srcdir"/PyQt5_gpl-$pkgver
  python3 configure.py \
    --confirm-license \
    --no-sip-files \
    --no-dist-info \
    --qsci-api \
    -q /usr/bin/qmake-qt5

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
  msg "Making the Python 3 version"
  make

  cd "$srcdir"/PyQt5_gpl-$pkgver-py2
  msg "Configuring the Python 2 version"
  python2 configure.py \
    --confirm-license \
    --no-sip-files \
    --no-dist-info \
    --qsci-api \
    -q /usr/bin/qmake-qt5

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
  msg "Making the Python 2 version"
  make
}

package_python-pyqt5-common(){
  pkgdesc="Common PyQt files shared between python3-pyqt5 and python2-pyqt5"
  depends=('qt5-base')

  cd PyQt5_gpl-$pkgver
  install -Dm644 PyQt5.api "$pkgdir"/usr/share/qt5/qsci/api/python/PyQt5.api

  install -d "$pkgdir"/usr/share/sip/PyQt5
  cp -a sip/* "$pkgdir"/usr/share/sip/PyQt5
}

package_python3-pyqt5(){
  pkgdesc="A set of Python 3.x bindings for the Qt5 toolkit"
  depends=('python3-sip' 'python-pyqt5-common')
  optdepends=('python3-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python3-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-webengine: QtWebEngine, QtWebEngineCore, QtWebEngineWidgets'
              'qt5-x11extras: QtX11Extras')

  cd PyQt5_gpl-$pkgver

  msg "Packaging python3-pyqt5"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Fix conflicts with python2-pyqt5
  mv "$pkgdir"/usr/bin/{,python3-}pyuic5
  mv "$pkgdir"/usr/bin/{,python3-}pylupdate5
  mv "$pkgdir"/usr/bin/{,python3-}pyrcc5

  # Provided by pyqt-common
  rm "$pkgdir"/usr/share/qt5/qsci/api/python/PyQt5.api
}

package_python2-pyqt5(){
  pkgdesc="A set of Python 2.x bindings for the Qt5 toolkit"
  depends=('python2-sip' 'python-pyqt5-common')
  optdepends=('python2-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-webengine: QtWebEngine, QtWebEngineCore, QtWebEngineWidgets'
              'qt5-x11extras: QtX11Extras')

  cd PyQt5_gpl-$pkgver-py2
  msg "Packaging pyqt5-python2"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Fix conflicts with python3-pyqt5
  mv "$pkgdir"/usr/bin/{,python2-}pyuic5
  mv "$pkgdir"/usr/bin/{,python2-}pylupdate5
  mv "$pkgdir"/usr/bin/{,python2-}pyrcc5

  # Default to python2 version
  ln -s python2-pyuic5     "$pkgdir"/usr/bin/pyuic5
  ln -s python2-pylupdate5 "$pkgdir"/usr/bin/pylupdate5
  ln -s python2-pyrcc5     "$pkgdir"/usr/bin/pyrcc5

  rm "$pkgdir"/usr/lib/qt5/plugins/designer/libpyqt5.so
  rm "$pkgdir"/usr/lib/qt5/plugins/PyQt5/libpyqt5qmlplugin.so
  rm "$pkgdir"/usr/share/qt5/qsci/api/python/PyQt5.api
}
