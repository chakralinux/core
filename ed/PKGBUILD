#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=ed
pkgver=1.10
pkgrel=2
pkgdesc="A POSIX-compliant line-oriented text editor"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/ed/ed.html"
depends=('glibc' 'sh')
groups=('base-devel')
options=('!emptydirs')
source=("ftp://ftp.gnu.org/gnu/ed/$pkgname-$pkgver.tar.lz"{,.sig})
install='ed.install'
md5sums=('d1e51bb6e78417af8fb12684c31fd9eb'
         'SKIP')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/ \
              --bindir=/usr/bin \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              "CFLAGS=$CFLAGS" \
              "LDFLAGS=$LDFLAGS"

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" "DESTDIR=$pkgdir" install
}
