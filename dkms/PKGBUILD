# Platform Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor: Balwinder S "bsd" Dheeman (bdheeman AT gmail.com)
#

pkgname=dkms
pkgver=2.2.0.3
pkgrel=1
pkgdesc='Dynamic Kernel Modules System'
arch=('any')
url='http://linux.dell.com/dkms/'
license=('GPL2')
depends=('linux-headers')
backup=('etc/dkms/framework.conf')
source=("http://linux.dell.com/$pkgname/permalink/$pkgname-$pkgver.tar.gz"
        'dkms.rc')
md5sums=('11a8aaade2ebec2803653837c7593030'
         'ae8f9f58554ce5c8194642494ea0ccf4')

package() {
  # add initscripts
  install -D -m 755 $pkgname.rc "${pkgdir}/etc/rc.d/$pkgname"

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # fix hardcoded paths
  sed -i "s|/sbin/depmod|depmod|" $pkgdir/usr/sbin/dkms

  # remove debian specific
  rm -r "$pkgdir/etc/kernel"

  # move bash completion in right place
  install -d -m 755 "$pkgdir/usr/share/bash-completion"
  mv "$pkgdir/etc/bash_completion.d" "$pkgdir/usr/share/bash-completion/completions"
}
