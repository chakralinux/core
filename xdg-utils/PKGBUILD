pkgname=xdg-utils
pkgver=1.1.3
pkgrel=2
pkgdesc='Command line tools that assist applications with a variety of desktop integration tasks.'
arch=('any')
url='https://www.freedesktop.org/wiki/Software/xdg-utils/'
depends=('sh' 'xorg-xset') # xset needed inside xdg-screensaver
makedepends=('docbook-xsl' 'lynx' 'xmlto' 'git')
optdepends=('kde-cli-tools: for KDE Plasma5 support in xdg-open')
license=('MIT')
source=("https://portland.freedesktop.org/download/${pkgname}-${pkgver}.tar.gz")
md5sums=('902042508b626027a3709d105f0b63ff')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	./configure --prefix=/usr --mandir=/usr/share/man
	make 
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	
	# install empty directory to prevent xdg-desktop-menu install *.desktop to fail, see Arch's FS#33316
	install -dm755 $pkgdir/usr/share/desktop-directories
}
