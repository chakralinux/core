#
# Chakra Packages for Chakra, part of chakra-project.org
#

_extramodules=extramodules-3.12.15-CHAKRA
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=vhba-module
pkgver=20130607
pkgrel=1
pkgdesc="Kernel module that emulates SCSI devices"
arch=('x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL2')
depends=('linux>=3.12' 'linux<3.13')
makedepends=('linux-headers')
install=vhba-module.install
source=("http://sourceforge.net/projects/cdemu/files/vhba-module/vhba-module-$pkgver.tar.bz2"
        '60-vhba.rules')
md5sums=('660df41d08b1fd86baad1f16a0264438'
         '4dc37dc348b5a2c83585829bde790dcc')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 KDIR=/usr/src/linux-${_kver}
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -D vhba.ko "$pkgdir/lib/modules/${_extramodules}/vhba.ko"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" \
          "${startdir}/vhba-module.install"

  install -Dm644 "$srcdir/60-vhba.rules" \
	  "$pkgdir/usr/lib/udev/rules.d/60-vhba.rules"
}
