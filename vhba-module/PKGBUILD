#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

_extramodules=extramodules-3.4-CHAKRA
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=vhba-module
pkgver=20120422
pkgrel=2
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL2')
depends=('linux>=3.4' 'linux<3.5')
makedepends=('linux-headers')
install=vhba-module.install
source=(http://sourceforge.net/projects/cdemu/files/vhba-module/vhba-module-$pkgver.tar.bz2
        60-vhba.rules)
md5sums=('d97372da1d270d1605742b2995fb6678'
         '549bd2d9696bd1884c8eed7193c00e21')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 KDIR=/usr/src/linux-${_kver}
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -D vhba.ko "$pkgdir/lib/modules/${_extramodules}/vhba.ko"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" \
          "${startdir}/vhba-module.install"

  install -Dm644 "$srcdir/60-vhba.rules" \
	  "$pkgdir/usr/lib/udev/rules.d/60-vhba.rules"
}
