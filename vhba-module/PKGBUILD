#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# Find the kernel name inside the chroot
_extramodules=extramodules-`pacman -Q linux | cut -c7-9 | sed 's/linux //g'`-CHAKRA
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=vhba-module
pkgver=20110915
pkgrel=3
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL2')
depends=('linux>=3.2' 'linux<3.3')
makedepends=('linux-headers')
install=vhba-module.install
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.gz)
md5sums=('fb90cd8fc4cd74d08c92e8235f99f1f4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 KDIR=/usr/src/linux-${_kver}
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D vhba.ko "$pkgdir/lib/modules/${_extramodules}/vhba.ko"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/vhba-module.install"
}
