# Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/zeromq

pkgname=zeromq
pkgver=4.1.3
pkgrel=1
pkgdesc="Fast messaging system built on sockets.  C and C++ bindings. aka 0MQ, ZMQ."
arch=('x86_64')
url="http://www.zeromq.org"
license=('LGPL')
depends=('gcc-libs' 'util-linux' 'libsodium' 'libpgm')
makedepends=('python2')
options=('staticlibs')
source=(http://download.zeromq.org/$pkgname-$pkgver.tar.gz
        https://raw.githubusercontent.com/zeromq/cppzmq/a88bf3e0b/zmq.hpp)
md5sums=('d0824317348cfb44b8692e19cc73dc3a'
         '3b61c38fca2967e3c7ef252516da5dd1')

prepare() {
  # Needed for new libsodium
  sed -i 's/libzmq_werror="yes"/libzmq_werror="no"/' $pkgname-$pkgver/configure
}
      
build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure prefix=/usr \
    --with-pgm \
    --with-libsodium \
    --with-documentation \
    --enable-static

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/zmq.hpp" "$pkgdir/usr/include/zmq.hpp"
}
