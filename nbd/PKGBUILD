# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# contributions from Arch: https://www.archlinux.org/packages/community/x86_64/nbd/

pkgname=nbd
pkgver=3.7
pkgrel=1
pkgdesc="Tools for network block devices, allowing you to use remote block devices over TCP/IP."
arch=('x86_64')
url="http://nbd.sourceforge.net"
license=('GPL')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz"
	'config' 'nbd.service')
backup=('etc/nbd-server/config')
depends=('glib2')
install=nbd.install
md5sums=('09a17fd19596b6908bff8b7db74141fc'
         '2c43761cd261c9b002601bb78f7500a3'
         '59a2a7c119919d8f9b384b5a783af62a')
         
build(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --enable-syslog

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m644 "${srcdir}"/nbd.service "${pkgdir}"/usr/lib/systemd/system/nbd.service

  install -D -m644 doc/README "${pkgdir}"/usr/share/doc/nbd/README
}
