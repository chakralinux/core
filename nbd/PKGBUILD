# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# contributions from Arch: https://www.archlinux.org/packages/community/x86_64/nbd/

pkgname=nbd
pkgver=3.16.2
pkgrel=1
pkgdesc="Tools for network block devices, allowing you to use remote block devices over TCP/IP."
arch=('x86_64')
url="http://nbd.sourceforge.net"
license=('GPL')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz"
        'config' 'nbd.service')
depends=('glib2' 'gnutls')
backup=('etc/nbd-server/config')
install=nbd.install
md5sums=('68f24143b873d72c94dad3b2d4ff8eb2'
         '2c43761cd261c9b002601bb78f7500a3'
         '9dc8577a5b80b05b1209f4b6ed8887d5')
         
build(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --enable-syslog --sbindir=/usr/bin

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m644 "${srcdir}"/nbd.service "${pkgdir}"/usr/lib/systemd/system/nbd.service

  install -D -m644 doc/README "${pkgdir}"/usr/share/doc/nbd/README
}
