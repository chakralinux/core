# Maintainer: abveritas@chakra-project.org

pkgname=jack2
pkgver=1.9.9.5
pkgrel=1
pkgdesc="The next-generation JACK with SMP support"
arch=('x86_64')
url="http://jackaudio.org/"
backup=(etc/security/limits.d/99-audio.conf)
license=('GPL')
depends=('libsamplerate' 'celt' 'libffado' 'dbus-core')
makedepends=('python2' 'doxygen' 'libsamplerate' 'celt')
optdepends=('libffado: FireWire support'
              'dbus-core: jackdbus'
              'python2: jack_control')
conflicts=('jack')
provides=('jack' 'jackmp' 'jackdmp' 'jackdbus' 'jack-audio-connection-kit')
# Build for doxygen fix, commit 95a1162d6aecc91882e4d8b01ba7fb12f6d29d1c Dec 11 2012, remove with 1.9.9.10 or above
source=("http://chakra.sourceforge.net/sources/jack2/jack2.tar.xz"
        #"https://dl.dropbox.com/u/28869550/${pkgname}-${pkgver}.tgz"
        '99-audio.conf'
        '40-hpet-permissions.rules')
md5sums=('dea04856e7483f257da8ef3f1bea01ce'
         '4d928a76e3f3d77d037c42ab75ac0f0b'
         '23eba69d2dbbb800f68176e5d38b9af5')

build() {
  cd "${srcdir}/${pkgname}-95a1162d6aecc91882e4d8b01ba7fb12f6d29d1c"
  
  LDFLAGS="$LDFLAGS,--no-as-needed -lm" python2 ./waf configure \
    --prefix=/usr \
    --alsa

  python2 ./waf build


}

package() {
  cd "${srcdir}/${pkgname}-95a1162d6aecc91882e4d8b01ba7fb12f6d29d1c"

  python2 waf install --destdir="${pkgdir}"

  # configure realtime access/scheduling
  install -Dm644 "${srcdir}/99-audio.conf" \
    "${pkgdir}/etc/security/limits.d/99-audio.conf"

  install -Dm644 "${srcdir}/40-hpet-permissions.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/40-hpet-permissions.rules"
}

