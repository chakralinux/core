pkgbase=jack2
pkgname=('jack2' 'jack2-dbus')
_tarname=jack
pkgver=1.9.12
pkgrel=1
arch=('x86_64')
url="http://jackaudio.org/"
backup=(etc/security/limits.d/99-audio.conf)
license=('GPL')
makedepends=('python3' 'python2-dbus' 'libffado' 'celt' 'opus' 'libsamplerate')
source=("${pkgbase}-${pkgver}::https://github.com/jackaudio/${pkgbase}/archive/v${pkgver}.tar.gz"
        '99-audio.conf'
        '40-hpet-permissions.rules')
md5sums=('6cb5dfea0586bcf009c733c4e4b04a03'
         '4d928a76e3f3d77d037c42ab75ac0f0b'
         '23eba69d2dbbb800f68176e5d38b9af5')

prepare() {
  cd "${pkgbase}-${pkgver}"

  # https://github.com/jackaudio/jack2/issues/308
  sed -i 's:bin/env python:bin/env python2:' \
    "example-clients/jack_control"

  # copy of the source for jack2-dbus
  cp -r "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/${pkgname[1]}-${pkgver}"

  # configure jack2
  python3 waf configure --prefix=/usr \
                       --htmldir="/usr/share/doc/${pkgbase}/" \
                       --freebob=no \
                       --classic \
                       --dbus

  # configure jack2-dbus
  cd "${srcdir}/${pkgname[1]}-${pkgver}"
  python3 waf configure --prefix=/usr \
                       --htmldir="/usr/share/doc/${pkgbase}/" \
                       --freebob=no \
                       --dbus
}


build() {
  # build jack2
  cd "${pkgname[0]}-${pkgver}"
  python3 waf build
  # build jack2-dbus
  cd "${srcdir}/${pkgname[1]}-${pkgver}"
  python3 waf build
}

package_jack2() {
  pkgdesc="The next-generation JACK with SMP support"
  depends=('celt' 'opus' 'libffado' 'python2-dbus')
  conflicts=('jack')
  provides=('jack' 'jackmp' 'jackdmp' 'jackdbus')
  backup=(etc/security/limits.d/99-audio.conf)

  cd "${pkgname[0]}-${pkgver}"
  python3 waf install --destdir="${pkgdir}"

  # configure realtime access/scheduling
  # see https://bugs.archlinux.org/task/26343
  install -Dm644 "${srcdir}/99-audio.conf" \
    "${pkgdir}/etc/security/limits.d/99-audio.conf"

  install -Dm644 "${srcdir}/40-hpet-permissions.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/40-hpet-permissions.rules"
}

package_jack2-dbus() {
  pkgdesc="The next-generation JACK with SMP support (for D-BUS interaction only)"
  depends=('celt' 'opus' 'libffado' 'python2-dbus')
  conflicts=('jack' 'jack2')
  provides=('jack' 'jack2' 'jackmp' 'jackdmp' 'jackdbus')
  backup=(etc/security/limits.d/99-audio.conf)

  cd "${pkgname[1]}-${pkgver}"
  python3 waf install --destdir="${pkgdir}"

  # configure realtime access/scheduling
  # see https://bugs.archlinux.org/task/26343
  install -Dm644 "${srcdir}/99-audio.conf" \
    "${pkgdir}/etc/security/limits.d/99-audio.conf"

  install -Dm644 "${srcdir}/40-hpet-permissions.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/40-hpet-permissions.rules"
}
