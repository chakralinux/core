# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/texlive-pictures

pkgname=texlive-pictures
pkgver=2016.41299
_revnr=${pkgver#*.}
pkgrel=1
pkgdesc="TeX Live - Packages for drawings graphics"
license=('GPL')
arch=(x86_64)
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps")
options=('!emptydirs')
install=texlive.install
md5sums=('eb472d7206189f1318c1d2a0bd22f6f2'
         '7cdcbb7f8c85e587169f98bcae17f22f')

build() {
  cd "$srcdir"
  for p in *.tar.xz; do
    bsdtar -xf $p
  done
  rm -rf {tlpkg,doc,source} || true
}

package() {
  cd "$srcdir"
  install -m755 -d $pkgdir/var/lib/texmf/chakra/installedpkgs
  sed -i '/^#/d' CONTENTS
  install -m644 CONTENTS $pkgdir/var/lib/texmf/chakra/installedpkgs/${pkgname}_${_revnr}.pkgs
  install -m644 $pkgname.maps $pkgdir/var/lib/texmf/chakra/installedpkgs/
  install -m755 -d $pkgdir/usr/share
  wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
  for dir in $wanteddirs; do
    find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
    find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
  done
  if [[ -d texmf-dist ]]; then
    find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
    find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
  fi
  if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
    find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
  fi

  _linked_scripts="
cachepic/cachepic.tlu
epspdf/epspdftk.tcl
epspdf/epspdf.tlu
fig4latex/fig4latex
getmap/getmapdl.lua
mathspic/mathspic.pl
mkpic/mkpic
petri-nets/pn2pdf
"
  install -m755 -d $pkgdir/usr/bin
  for _script in ${_linked_scripts}; do
    _scriptbase=$(basename $_script)
    _scriptbase=${_scriptbase%.*}
    ln -s /usr/share/texmf-dist/scripts/${_script} ${pkgdir}/usr/bin/${_scriptbase}
  done
}
