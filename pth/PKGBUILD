pkgname=pth
pkgver=2.0.7
pkgrel=5
pkgdesc="The GNU Portable Threads."
arch=('x86_64')
url="http://www.gnu.org/software/pth/"
license=('LGPL')
depends=('glibc')
options=('!makeflags')
source=(ftp://ftp.gnu.org/gnu/pth/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('9cb4a25331a4c4db866a31cbe507c793'
         'SKIP')
validpgpkeys=('4E23E878D41A0A88EDFCFA5A6E744ACBA9C09E30') # Ralf S. Engelschall A9C09E30

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --mandir=/usr/share/man
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
