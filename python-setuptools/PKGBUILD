#
# Software packages for Chakra, part of www.chakra-project.org
#
# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Velasquez <angvp@archlinux.org>

pkgbase=python-setuptools
pkgname=('python3-setuptools' 'python2-setuptools')
pkgver=5.7
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="http://pypi.python.org/pypi/setuptools"
makedepends=('python3' 'python2')
source=(http://pypi.python.org/packages/source/s/setuptools/setuptools-${pkgver}.tar.gz)
md5sums=('81f980854a239d60d074d6ba052e21ed')

prepare() {
   cp -a setuptools-${pkgver}{,-python2}
 
   cd "${srcdir}"/setuptools-${pkgver}
   sed -i -e "s|^#\!.*/usr/bin/python|#!/usr/bin/python3|" setuptools/tests/test_resources.py
   sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python3|" setuptools/command/easy_install.py
 
   cd ../setuptools-${pkgver}-python2
   sed -i -e "s|^#\!.*/usr/bin/python|#!/usr/bin/python2|" setuptools/tests/test_resources.py
   sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python2|" setuptools/command/easy_install.py
}

build() {
   # Build python 3 module
   cd "${srcdir}"/setuptools-${pkgver}
   python3 setup.py build
 
   # Build python 2 module
   cd "${srcdir}"/setuptools-${pkgver}-python2
   python2 setup.py build
}

#check() {
#  # Workaround UTF-8 tests by setting LC_CTYPE
#
#  # Check python3 module
#  cd "${srcdir}"/setuptools-${pkgver}
#  LC_CTYPE=en_US.utf8 python3 setup.py test
#
#  # Check python2 module
#  cd "${srcdir}"/setuptools-${pkgver}-python2
#  LC_CTYPE=en_US.utf8 python2 setup.py test
#}

package_python3-setuptools() {
   depends=('python3>=3.4')
   provides=('python3-distribute')
   conflicts=('python3-distribute')
   replaces=('python3-distribute')
 
   cd "${srcdir}/setuptools-${pkgver}"
   python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}
 
package_python2-setuptools() {
   depends=('python2>=2.7')
   provides=('python-distribute' 'python2-distribute')
   conflicts=('python-distribute' 'python2-distribute')
   replaces=('python-distribute' 'python2-distribute')
 
   cd "${srcdir}/setuptools-${pkgver}-python2"
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
   rm "${pkgdir}/usr/bin/easy_install"
}
