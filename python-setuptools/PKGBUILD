#
# Software packages for Chakra, part of www.chakraos.org
#
# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Velasquez <angvp@archlinux.org>

pkgbase=python-setuptools
pkgname=('python3-setuptools' 'python2-setuptools')
pkgver=20.2.2
pkgrel=2
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="http://pypi.python.org/pypi/setuptools"
makedepends=('python3-six' 'python2-six')
#checkdepends=('python3-mock' 'python2-mock')

source=(http://pypi.python.org/packages/source/s/setuptools/setuptools-${pkgver}.tar.gz)
md5sums=('bf37191cb4c1472fb61e6f933d2006b1')

prepare() {  
   cp -a setuptools-${pkgver}{,-python2}
 
   cd "${srcdir}"/setuptools-${pkgver}
   sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python3|" setuptools/command/easy_install.py

   cd "${srcdir}"/setuptools-${pkgver}-python2
   sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python2|" setuptools/command/easy_install.py
}

build() {
   # Build python 3 module
   cd "${srcdir}"/setuptools-${pkgver}
   python3 setup.py build
 
   # Build python 2 module
   cd "${srcdir}"/setuptools-${pkgver}-python2
   python2 setup.py build
}

#check() {
#  # Workaround UTF-8 tests by setting LC_CTYPE
#
#  # Check python3 module
#  cd "${srcdir}"/setuptools-${pkgver}
#  LC_CTYPE=en_US.utf8 python3 setup.py ptr
#
#  # Check python2 module
#  cd "${srcdir}"/setuptools-${pkgver}-python2
#  LC_CTYPE=en_US.utf8 python2 setup.py ptr
#}

package_python3-setuptools() {
   provides=('python3-distribute')
   conflicts=('python3-distribute')
   replaces=('python3-distribute')
 
   cd "${srcdir}/setuptools-${pkgver}"
   python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}
 
package_python2-setuptools() {
   provides=('python-distribute' 'python2-distribute')
   conflicts=('python-distribute' 'python2-distribute')
   replaces=('python-distribute' 'python2-distribute')
 
   cd "${srcdir}/setuptools-${pkgver}-python2"
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
   rm "${pkgdir}/usr/bin/easy_install"
}
