pkgname=rp-pppoe
pkgver=3.12
pkgrel=1
pkgdesc="Roaring Penguin's Point-to-Point Protocol over Ethernet client"
arch=('x86_64')
url="http://www.roaringpenguin.com/pppoe/"
license=('GPL')
groups=('base')
depends=('ppp>=2.4.5' 'sh')
options=('!makeflags')
backup=(etc/ppp/{pppoe.conf,firewall-standalone,firewall-masq,pppoe-server-options})
source=("http://www.roaringpenguin.com/files/download/rp-pppoe-${pkgver}.tar.gz"
        'adsl.service')
install=${pkgname}.install
sha256sums=('00794e04031546b0e9b8cf286f2a6d1ccfc4a621b2a3abb2d7ef2a7ab7cc86c2'
            '0247cd38b933da5033cf5229e695b29514dfc13f4c65ebd8ba03da65f4b2cff0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  ./configure --prefix=/usr \
              --enable-plugin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make PLUGIN_DIR="/usr/lib/rp-pppoe" DESTDIR="${pkgdir}" install
  
  install -Dm644 "${srcdir}/adsl.service" "${pkgdir}/usr/lib/systemd/system/adsl.service"
}
