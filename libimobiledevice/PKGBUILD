pkgname=libimobiledevice
pkgver=1.2.0+66+g5a85432
pkgrel=1
pkgdesc="Library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('x86_64')
license=('GPL2' 'LGPL2.1')
depends=('libusbmuxd' 'usbmuxd' 'gnutls')
makedepends=('python2' 'cython2' 'python3' 'cython3' 'libplist' 'autoconf-archive' git python2-setuptools python3-setuptools)
_commit=5a85432719fb3d18027d528f87d2a44b76fd3e12  # master
source=("git+https://github.com/libimobiledevice/libimobiledevice/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

check() {
  cd build-py2
  make check
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir build-py2
  pushd build-py2
  PYTHON=/usr/bin/python2 CYTHON=/usr/bin/cython2 ../$pkgname/configure --prefix=/usr --disable-openssl
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
  popd

  mkdir build-py3
  pushd build-py3
  PYTHON=/usr/bin/python3 CYTHON=/usr/bin/cython3 ../$pkgname/configure --prefix=/usr --disable-openssl
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  pushd build-py2
  make DESTDIR="$pkgdir" install
  popd
  pushd build-py3/cython
  make DESTDIR="$pkgdir" install
  popd
}
