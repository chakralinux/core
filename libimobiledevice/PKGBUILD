#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=libimobiledevice
pkgver=1.1.1
pkgrel=1
pkgdesc="Is a software library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('gnutls'  'glib2' 'libplist' 'usbmuxd' 'libgcrypt')
makedepends=('swig' 'python2')
options=('!libtool')
source=(http://libimobiledevice.org/downloads/${pkgname}-${pkgver}.tar.bz2
        swig-version-check.patch)
md5sums=('cdc13037e822d9ac2e109536701d153a'
         '8bb215e36fe0e08a72332c108c7842cc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # SWIG version check is wrong (still not fixed upstream)
  patch -p1 -i "$srcdir"/swig-version-check.patch

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --libexecdir=/usr/lib/usbmuxd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
