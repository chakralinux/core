# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>
# Contributor: Jack Lloyd <jack@randombit.net>

pkgname=botan
pkgver=2.8.0
pkgrel=1
pkgdesc='Crypto library written in C++'
arch=('x86_64')
url='https://botan.randombit.net/'
license=('BSD')
makedepends=('python')
optdepends=('python: for using botan2.py')
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')
source=("https://botan.randombit.net/releases/Botan-${pkgver}.tgz"{,.asc})
sha256sums=('e7159b127e91e0c158245d61c638c50d443ec7b440b6b0161328c47b3aba3960'
            'SKIP')

build() {
  cd "${pkgname^}-$pkgver"

  # botan benefits from -O3. Upstream is using it.
  CXXFLAGS="$CXXFLAGS -O3" ./configure.py \
    --prefix=/usr \
    --with-bzip \
    --with-lzma \
    --with-zlib
  make
}

check() {
  cd "${pkgname^}-$pkgver"

  ./botan-test
}

package() {
  DESTDIR="$pkgdir" make -C "${pkgname^}-$pkgver" install
  install -Dm644 "${pkgname^}-$pkgver/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/randombit/botan
# vim: ts=2 sw=2 et:
