pkgname=jack
pkgver=0.124.1
pkgrel=2
pkgdesc="A low-latency audio server"
arch=('x86_64')
license=('GPL2' 'custom:LGPL2')
depends=('readline' 'libsamplerate' 'libffado')
makedepends=('doxygen' 'libffado' 'celt')
optdepends=('libffado: FireWire support'
            'celt: NetJACK driver')
url="http://jackit.sourceforge.net/"
backup=(etc/security/limits.d/99-audio.conf)
provides=("jack-audio-connection-kit=${pkgver}")
replaces=('jack-audio-connection-kit')
install=$pkgname.install
source=("http://jackaudio.org/downloads/jack-audio-connection-kit-$pkgver.tar.gz"
        "99-audio.conf"
        "40-hpet-permissions.rules")
md5sums=('d64e90121be8a54860b870a726fb5b5d'
         '4d928a76e3f3d77d037c42ab75ac0f0b'
         '23eba69d2dbbb800f68176e5d38b9af5')

build() {
  cd "${srcdir}/jack-audio-connection-kit-${pkgver}"

  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/jack-audio-connection-kit-${pkgver}"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/99-audio.conf" \
    "$pkgdir/etc/security/limits.d/99-audio.conf"

  install -Dm644 "$srcdir/40-hpet-permissions.rules" \
    "$srcdir/lib/udev/rules.d/40-hpet-permissions.rules"
}

