# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=wayland
pkgver=1.11.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('libffi' 'expat')
makedepends=('doxygen' 'xmlto' 'graphviz')
source=("https://wayland.freedesktop.org/releases/$pkgname-$pkgver.tar.xz"{,.sig})
sha1sums=('ba0494a3ab811251f7ad72710bc751cb668d8848'
          'SKIP')
validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --disable-static
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
