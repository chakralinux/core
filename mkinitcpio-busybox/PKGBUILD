
pkgname=mkinitcpio-busybox
pkgver=1.23.2
pkgrel=1
pkgdesc="base initramfs tools"
arch=('x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
options=('!buildflags')
source=("http://busybox.net/downloads/busybox-$pkgver.tar.bz2"
        'config')
sha256sums=('05a6f9e21aad8c098e388ae77de7b2361941afa7157ef74216703395b14e319a'
            '240c9ab805fbf5eb3347b2a42f62d840f160c4999d0f172b28ba50dadad09ada')

prepare() {
  cd "busybox-$pkgver"

  local safeflags="-march=${CARCH/_/-} -mtune=generic -Os -pipe -fno-strict-aliasing"

  sed 's|^\(CONFIG_EXTRA_CFLAGS\)=.*|\1="'"$safeflags"'"|' "$srcdir/config" >.config
}

build() {
  make -C "busybox-$pkgver"
  
  # save configuration for later reuse
  cat busybox-$pkgver/.config > "${startdir}/config.last"
}

package() {
  install -Dm755 "$srcdir/busybox-$pkgver/busybox" "$pkgdir/usr/lib/initcpio/busybox"
}
