#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# Find the kernel name inside the chroot
_extramodules=extramodules-`pacman -Q linux | cut -c7-9 | sed 's/linux //g'`-CHAKRA
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=acpi_call
pkgver=20120115
pkgrel=3
pkgdesc="A kernel module that enables you to call parameterless ACPI methods by writing the method name to /proc/acpi/call, e.g. to turn off discrete graphics card in a dual graphics environment (like NVIDIA Optimus)."
arch=('i686' 'x86_64')
url=("http://github.com/mkottman/acpi_call")
license=('GPL')
depends=('linux>=3.2' 'linux<3.3' 'linux-headers')
makedepends=('git')
install=acpi_call.install
source=(http://chakra.sourceforge.net/sources/${pkgname}/${pkgname}-${pkgver}.tar.xz)
md5sums=('129841dd806887492609214462792413')


# create tarball: source PKGBUILD && mksource

mksource() {
  git clone http://github.com/mkottman/acpi_call.git
  pushd acpi_call
  popd
  tar -cvJf ${pkgname}-${pkgver}.tar.xz acpi_call/*
  md5sum ${pkgname}-${pkgver}.tar.xz
}

build() {
  cd ${srcdir}

 ## Build
  rm -rf ${srcdir}/$pkgname-build
  cp -r ${srcdir}/$pkgname ${srcdir}/$pkgname-build
  cd ${srcdir}/$pkgname-build

  sed \
    -e  's/$(shell uname -r)/3.2-CHAKRA/g' \
    -i Makefile
  make
}
package() {
  cd ${srcdir}/$pkgname-build
  install -d ${pkgdir}/usr/share/$pkgname || return 1
  install -d ${pkgdir}/usr/bin || return 1
  install -d ${pkgdir}/usr/share/$pkgname/doc || return 1
  install -Dm755  ${srcdir}/$pkgname-build/test_off.sh \
    ${pkgdir}/usr/share/$pkgname || return 1
  ln -s /usr/share/$pkgname/test_off.sh \
    ${pkgdir}/usr/bin/test_off.sh || return 1
  install -Dm644 README \
    ${pkgdir}/usr/share/$pkgname/README

  msg2 "Building module for $_kver..."

  # KDIR is necessary even when cleaning
  make KDIR=/usr/src/linux-${_kver} clean
  make KDIR=/usr/src/linux-${_kver}

  install -D -m644 acpi_call.ko $pkgdir/lib/modules/$_extramodules/acpi_call.ko
}
