# Software packages for Chakra, part of www.chakra-project.org
#
# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Josh Taylor <joshtaylor.mail@gmail.com>
#               simo <simo@archlinux.org>
#               Douglas Soares de Andrade <douglas@archlinux.org>
#               Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=python-opengl
pkgname=('python3-opengl' 'python2-opengl')
pkgver=3.0.2
pkgrel=1
pkgdesc="The cross platform Python binding to OpenGL and related APIs"
arch=('any')
license=('BSD')
url="http://pyopengl.sourceforge.net"
makedepends=('freeglut' 'python2-setuptools' 'python3-setuptools')
source=("http://pypi.python.org/packages/source/P/PyOpenGL/PyOpenGL-${pkgver}.tar.gz"
        'LICENSE')
sha512sums=('e5663682eee5aa7276590ae7547d8345141e2e8e34ddfcbb3867c10a91e9959ebe8ec82606e257826ba6dd21d731ad76f38134849f095a01760d007d752fe9a9'
            '84e8b6e984aa53ffdce33df2f4ee8993887bdb8752617d7f66b2ff5641ca16f9c3a13b383a9bb3236192b3af9518a3f3de1e7ac9f1496c8704911bb4238d091d')

prepare() {
    cp -r PyOpenGL-${pkgver} python3-PyOpenGL-${pkgver}
}

package_python2-opengl() {
    depends=('python2' 'freeglut')
    conflicts=('pyopengl' 'python-pyopengl' 'python-opengl')
    replaces=('pyopengl' 'python-pyopengl' 'python-opengl')
    provides=('pyopengl' 'python-pyopengl' 'python-opengl')

    cd PyOpenGL-${pkgver}
    python2 setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 "${srcdir}/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Fix the shebang
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/python2|' \
      "${pkgdir}"/usr/lib/python2.7/site-packages/OpenGL/Tk/__init__.py
    sed -i 's|#! /usr/bin/env python|#!/usr/bin/python2|' \
      "${pkgdir}"/usr/lib/python2.7/site-packages/OpenGL/arrays/{_,}buffers.py
}

package_python3-opengl() {
    depends=('python3' 'freeglut')
 
    cd python3-PyOpenGL-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 "${srcdir}/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Fix the shebang
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/python3|' \
      "${pkgdir}"/usr/lib/python3.3/site-packages/OpenGL/Tk/__init__.py
    sed -i 's|#! /usr/bin/env python|#!/usr/bin/python3|' \
      "${pkgdir}"/usr/lib/python3.3/site-packages/OpenGL/arrays/{_,}buffers.py
}
