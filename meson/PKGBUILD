pkgname=meson
pkgver=0.48.2
pkgrel=1
pkgdesc='High productivity build system'
url='https://mesonbuild.com/'
arch=('x86_64')
license=('Apache')
depends=('python3-setuptools' 'ninja')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc}
        chakra-meson)
sha256sums=('39ead8bfd0dc9c7b0af15e23ea975c864600bf871fba279c9918625bb9a85506'
            'SKIP'
            '52066130b9938b38bd75a11a5f3aee8c7cdacdd719af8e3b5ab2770e05176049')
validpgpkeys=('95181F4EED14FDF4E41B518D3BF4693BFEEB9428') # Jussi Pakkanen <jpakkane@gmail.com>

build() {
  cd ${pkgname}-${pkgver}
  python3 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -d "${pkgdir}/usr/share/vim/vimfiles"
  cp -rt "${pkgdir}/usr/share/vim/vimfiles" data/syntax-highlighting/vim/*/

  install -Dt "${pkgdir}/usr/share/emacs/site-lisp" -m644 data/syntax-highlighting/emacs/*
  install -Dt "${pkgdir}/usr/share/zsh/site-functions" -m644 data/shell-completions/zsh/*

  # Chakra packaging helper
  install -D ../chakra-meson -t "${pkgdir}/usr/bin"
}
