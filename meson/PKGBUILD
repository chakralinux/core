pkgname=meson
pkgver=0.45.1
pkgrel=1
pkgdesc='High productivity build system'
url='http://mesonbuild.com/'
arch=('any')
license=('Apache')
depends=('python3' 'ninja')
makedepends=('python3-setuptools')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt5-base' 'git'
              'python3-cython' 'gtest' 'gmock' 'protobuf' 'python3-gobject' 'gobject-introspection'
              'itstool' 'valgrind' 'sdl2' 'doxygen' 'llvm' 'clang' 'gnustep-base' 'gtk-doc' 'gtk3' 'vulkan-validation-layers'
              'openmpi' 'openssh' 'mercurial')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc}
        chakra-meson)
sha512sums=('50bcc299d69a3649a3aa3582322c548af44c2771d65e57c79eae9d67ccb256a17e534649ee5080f50f6f9d4e5d717a1b1a28b3ef4684468094b16bbed8864846'
            'SKIP'
            'a4398dcd70bf8ad983f80f666b07c2122029a69d385ea10e411a6f31fb5ac77b7f6f3910ca35baebf6f577e82baab3cc38fb53afbe0be6fe4da74cedfb8eaecd')
validpgpkeys=('95181F4EED14FDF4E41B518D3BF4693BFEEB9428') # Jussi Pakkanen <jpakkane@gmail.com>


build() {
  cd ${pkgname}-${pkgver}
  python3 setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  LDFLAGS="${LDFLAGS/,--as-needed/}"
  # export MESON_PRINT_TEST_OUTPUT=1 # set this for debug output
  
  export LC_CTYPE=en_US.UTF-8
  ./run_tests.py
}

package() {
  cd ${pkgname}-${pkgver}
  python3 setup.py install --root="${pkgdir}" -O1

  install -d "${pkgdir}/usr/share/vim/vimfiles"
  cp -rt "${pkgdir}/usr/share/vim/vimfiles" data/syntax-highlighting/vim/*
  install -Dt "${pkgdir}/usr/share/emacs/site-lisp" -m644 data/syntax-highlighting/emacs/*
  install -Dt "${pkgdir}/usr/share/zsh/site-functions" -m644 data/shell-completions/zsh/*

  # Chakra packaging helper
  install -D ../chakra-meson -t "${pkgdir}/usr/bin"
}

