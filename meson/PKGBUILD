pkgname=meson
pkgver=0.43.0
pkgrel=2
pkgdesc='High productivity build system'
url='http://mesonbuild.com/'
arch=('any')
license=('Apache')
depends=('python3' 'ninja')
makedepends=('python3-setuptools')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt5-base' 'git'
              'python3-cython' 'gtest' 'gmock' 'protobuf' 'python3-gobject' 'gobject-introspection'
              'itstool' 'valgrind' 'sdl2' 'doxygen' 'llvm' 'clang' 'gnustep-base' 'gtk-doc' 'gtk3' 'vulkan-validation-layers'
              'openmpi' 'openssh' 'mercurial')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc}
        fix-tests.diff
        chakra-meson)
sha512sums=('e149758fa6ad4b2d3643bcda176577633f2e50e58f38074c1a17f1712d554bac0fbabf26fe7eef15954dc51630b1f923b15cfe9040a025cf61b5666098197d16'
            'SKIP'
            '7ea1f02adf79e4fde5d002956852d9b976b36386be8d78a3845292e8195a36d462db20a9c708e332e0d3b261a7abf9d05f5d034aab96ede58229639f3620ec91'
            'a4398dcd70bf8ad983f80f666b07c2122029a69d385ea10e411a6f31fb5ac77b7f6f3910ca35baebf6f577e82baab3cc38fb53afbe0be6fe4da74cedfb8eaecd')
validpgpkeys=('95181F4EED14FDF4E41B518D3BF4693BFEEB9428') # Jussi Pakkanen <jpakkane@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../fix-tests.diff
}

build() {
  cd ${pkgname}-${pkgver}
  python3 setup.py build
}

#check() {
#  cd ${pkgname}-${pkgver}
#  unset CLASSPATH  # GNUstep breaks java tests
#  # export MESON_PRINT_TEST_OUTPUT=1 # set this for debug output
#  # Installing graphviz breaks doxygen tests
#  LC_CTYPE=en_US.UTF-8 DC=ldc ./run_tests.py
#}

package() {
  cd ${pkgname}-${pkgver}
  python3 setup.py install --root="${pkgdir}" -O1
  install -Dm 644 syntax-highlighting/vim/ftdetect/meson.vim -t "${pkgdir}/usr/share/vim/vimfiles/ftdetect"
  install -Dm 644 syntax-highlighting/vim/indent/meson.vim -t "${pkgdir}/usr/share/vim/vimfiles/indent"
  install -Dm 644 syntax-highlighting/vim/syntax/meson.vim -t "${pkgdir}/usr/share/vim/vimfiles/syntax"

  # Chakra packaging helper
  install -D ../chakra-meson -t "${pkgdir}/usr/bin"
}

