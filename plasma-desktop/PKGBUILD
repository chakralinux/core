#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/plasma-desktop

pkgname=plasma-desktop
pkgver=5.14.5
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=(polkit-kde-agent libcanberra libxkbfile kmenuedit systemsettings ksysguard kpeople baloo qt5-graphicaleffects) #appstream-qt 
makedepends=(extra-cmake-modules kdoctools boost xf86-input-evdev xf86-input-libinput xf86-input-synaptics xorg-server-devel python3 kdesignerplugin ) #scim libibus
optdepends=('plasma-nm: Network manager applet'
            'xf86-input-synaptics: Manage touchpad')
checkdepends=("cmake")
replaces=('kde-workspace')
conflicts=('kde-workspace')
groups=('plasma')
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d4da2bcd9752a3b88e93e9b55ded2b925b1b2b598e84ebc89cfb6669c63d205f'
            'SKIP'
            'af2856ae7904726e6e8cfb3616a51f82fbfee10dc7edc85b579be684601a554c')
source+=( chakra_defaultPanel.patch::https://bugsfiles.kde.org/attachment.cgi?id=102600 )
sha256sums+=( 'af2856ae7904726e6e8cfb3616a51f82fbfee10dc7edc85b579be684601a554c' )
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
 
  cd ${pkgname}-${pkgver} 
  # apply custom patch to change from taskmanager to icontasks
  patch -Np1 -i "$srcdir/chakra_defaultPanel.patch"
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
