#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/plasma-desktop
source ../plasma.conf

pkgname=plasma-desktop
pkgver=${PVersion}
pkgrel=2
pkgdesc='KDE Plasma Desktop'
arch=('x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('qt5-graphicaleffects' 'powerdevil' 'polkit-kde-agent-frameworks' 'libcanberra'
         'kwin' 'kpeople' 'libusb-compat' 'libxkbfile' 'kmenuedit' 'systemsettings' 'ksysguard' 'breeze' 'krunner' 'baloo' 'kfilemetadata'
         #sddm can be removed as a dependency in the future, only added here to automate the switch to Plasma 5
         'sddm-kcm')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'xf86-input-synaptics' 'xorg-server-devel')
optdepends=('plasma-nm: Network manager applet'
            'xf86-input-synaptics: Manage touchpad')
checkdepends=("cmake")
provides=("kcm-touchpad-kf5=${pkgver}")
replaces=('kde-workspace' 'kcm-touchpad-kf5<=5.2.0')
conflicts=('kde-workspace' 'kcm-touchpad-kf5<=5.2.0')
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PSubVersion}.tar.xz"
        'kickoffrc.patch')
sha256sums=( $(getSum ${pkgname}) 
            '265013591ef26870057f1eee6bdc67d0a4c3bef370005f4b64e61b2c1f4c0525')
install=${pkgname}.install

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  # patch to do a smoth migration of the favorites #356398
  # http://quickgit.kde.org/?p=plasma-desktop.git&a=commitdiff&h=97ced66da372737ae5d36d86c9bc0c6fe684d842
  patch -p1 -i $srcdir/kickoffrc.patch

}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
