#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/plasma-desktop
source ../plasma.conf

pkgname=plasma-desktop
pkgver=${PVersion}
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=(polkit-kde-agent libcanberra libxkbfile kmenuedit systemsettings ksysguard kpeople baloo qt5-graphicaleffects) #appstream-qt 
makedepends=(extra-cmake-modules kdoctools boost xf86-input-evdev xf86-input-libinput xorg-server-devel python3 kdesignerplugin ) #scim libibus
optdepends=('plasma-nm: Network manager applet'
            'xf86-input-synaptics: Manage touchpad')
checkdepends=("cmake")
replaces=('kde-workspace')
conflicts=('kde-workspace')
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PVersion}.tar.xz"{,.sig})
sha256sums=( $(getSum ${pkgname}) 
             'SKIP' )
source+=( chakra_defaultPanel.patch::https://bugsfiles.kde.org/attachment.cgi?id=102600 )
sha256sums+=( 'af2856ae7904726e6e8cfb3616a51f82fbfee10dc7edc85b579be684601a554c' )
validpgpkeys=(${Pvalidpgpkeys[@]})
install=${pkgname}.install

prepare() {
  mkdir -p build
 
  cd ${pkgname}-${pkgver} 
  # apply custom patch to change from taskmanager to icontasks
  patch -Np1 -i "$srcdir/chakra_defaultPanel.patch"
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
