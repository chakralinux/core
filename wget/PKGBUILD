#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer : <inkane@chakra-project.org>
# contributor: <abveritas[at]chakra-project[dot]org>

pkgname=wget
pkgver=1.14
pkgrel=2
pkgdesc="A network utility to retrieve files from the Web"
arch=('x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
groups=('base')
depends=('glibc' 'openssl' 'libidn' 'util-linux')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        "0001-Fix-error-in-texi2pod-intriduced-with-Perl-5.18.patch" )
md5sums=('316f6f59292c9098ad81fd54f658c579'
         '3e121933d69f32fa58776bff76bcebd0'
         '93de527d1ed556400c0a7ffe82910af4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 <  "${srcdir}"/0001-Fix-error-in-texi2pod-intriduced-with-Perl-5.18.patch

  ./configure -with-ssl=openssl --prefix=/usr --sysconfdir=/etc --enable-nls
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  echo '# default root certs location' >> "$pkgdir/etc/wgetrc"
  echo 'ca_certificate=/etc/ssl/certs/ca-certificates.crt' >> "$pkgdir/etc/wgetrc"

}
