# Maintainer: <abveritas[at]chakra-project[dot]org>

pkgname=speech-dispatcher
pkgver=0.8
pkgrel=4
arch=('x86_64')
url="http://www.freebsoft.org/speechd"
pkgdesc="High-level device independent layer for speech synthesis interface"
license=('GPL2' 'FDL')
depends=('glib2' 'libltdl' 'dotconf' 'texinfo' 'pulseaudio' 'python3-xdg')
makedepends=('intltool' 'espeak')
optdepends=('espeak: Speech output using ESpeak.')
provides=("speechd=$pkgver")
replaces=('opentts')
source=("http://www.freebsoft.org/pub/projects/speechd/$pkgname-$pkgver.tar.gz"
        'speech-dispatcherd.service')
install="speech-dispatcher.install"
md5sums=('d88691a64c676122f996230c107c392f'
         'db4690d8f8aaab7be415bdf27d838768')
backup=(etc/speech-dispatcher/clients/emacs.conf
	etc/speech-dispatcher/modules/ibmtts.conf
	etc/speech-dispatcher/modules/espeak-generic.conf
	etc/speech-dispatcher/modules/espeak.conf
	etc/speech-dispatcher/modules/swift-generic.conf
	etc/speech-dispatcher/modules/cicero.conf
	etc/speech-dispatcher/modules/espeak-mbrola-generic.conf
	etc/speech-dispatcher/modules/dtk-generic.conf
	etc/speech-dispatcher/modules/llia_phon-generic.conf
	etc/speech-dispatcher/modules/ivona.conf
	etc/speech-dispatcher/modules/epos-generic.conf
	etc/speech-dispatcher/modules/flite.conf
	etc/speech-dispatcher/speechd.conf)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --without-flite
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/speech-dispatcherd.service" "${pkgdir}/usr/lib/systemd/system/speech-dispatcherd.service"
  install -d "${pkgdir}/var/log/speech-dispatcher"
}
