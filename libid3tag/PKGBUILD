# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/libid3tag

pkgname=libid3tag
pkgver=0.15.1b
pkgrel=5
pkgdesc="library for id3 tagging"
arch=('x86_64')
url="http://www.underbit.com/products/mad/"
license=('GPL')
depends=('zlib')
source=("ftp://ftp.mars.org/pub/mpeg/${pkgname}-${pkgver}.tar.gz"
	'id3tag.pc'
	'10_utf16.diff' 
	'11_unknown_encoding.diff' 
	'CVE-2008-2109.patch')
md5sums=('e5808ad997ba32c498803822078748c3'
         '8bb41fd814fafcc37ec8bc88f5545a4a'
         '1ac8efdf9a6c0f86d552fff9270839f6'
         '040dbfc90cbb268991273c2fef8aa50b'
         '072325b44d1c6e5c9fcf4e92106a1432')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/10_utf16.diff
  patch -p1 -i ${srcdir}/11_unknown_encoding.diff
  patch -Np0 -i ${srcdir}/CVE-2008-2109.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/id3tag.pc" "${pkgdir}/usr/lib/pkgconfig/id3tag.pc"
}