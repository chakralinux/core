#Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/libinput

pkgname=libinput
pkgver=1.12.1
pkgrel=2
pkgdesc="Input device management and event handling library"
arch=(x86_64)
url="http://www.freedesktop.org/wiki/Software/libinput/"
license=(custom:X11)
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
makedepends=('gtk3' 'meson') # To build docs (not recommended by upstream) add these deps: 'doxygen' 'graphviz' 'python-sphinx' 'python-recommonmark'
optdepends=('gtk3: libinput debug-gui'
            'python-pyudev: libinput measure'
            'python-evdev: libinput measure')
install=libinput.install
options=('!libtool')
source=(http://freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('570c48cc7b744b9b4da711ed0c5e8c3de132484684b0c4f0309a9a83df5e8692'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>


build() {
  meson $pkgname-$pkgver build \
-Dudev-dir=/usr/lib/udev \
    -Dtests=false \
    -Ddocumentation=false
    ninja -C build
}

package() {
  DESTDIR=$pkgdir ninja -C build install

  install -Dvm644 $pkgname-$pkgver/COPYING \
    $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

