# Part of X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=libfontenc
pkgver=1.1.2
pkgrel=1
pkgdesc="X11 font encoding library"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('zlib')
makedepends=('pkgconfig' 'xproto')
options=('!libtool')
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('2beffa0e9260c479b49f90f789318c7f9db2d41d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-encodingsdir=/usr/share/fonts/encodings
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
