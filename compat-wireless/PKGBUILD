#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=compat-wireless
pkgver=3.2_rc6_3
_upver="${pkgver//_/-}"
pkgrel=2
pkgdesc='Snapshots of the wireless-testing tree ported to older kernels'
url='http://wireless.kernel.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('linux>=3.1.6-1')
makedepends=('linux-api-headers' 'linux-headers')
source=("http://www.orbit-lab.org/kernel/compat-wireless-3.0-stable/v3.2/compat-wireless-${_upver}.tar.bz2"
        'eeprom_93xx46.h::http://git.kernel.org/?p=linux/kernel/git/stable/linux-stable.git;a=blob_plain;f=include/linux/eeprom_93xx46.h;h=06791811'
        'eeprom_93xx46.c::http://git.kernel.org/?p=linux/kernel/git/stable/linux-stable.git;a=blob_plain;f=drivers/misc/eeprom/eeprom_93xx46.c;h=0c7ebb1e'
        http://chakra.sourceforge.net/sources/linux/patches/3.1/features/fedora/compat-wireless-change-CONFIG_IWLAGN-CONFIG_IWLWIFI.patch
        http://chakra.sourceforge.net/sources/linux/patches/3.1/features/fedora/compat-wireless-config-fixups.patch
        http://chakra.sourceforge.net/sources/linux/patches/3.1/features/fedora/bcma-brcmsmac-compat.patch
        http://chakra.sourceforge.net/sources/linux/patches/3.1/features/fedora/iwlwifi-do-not-set-the-sequence-control-bit-is-not-n.patch
        http://chakra.sourceforge.net/sources/linux/patches/3.1/features/fedora/ath9k-fix-max-phy-rate-at-rate-control-init.patch
        http://chakra.sourceforge.net/sources/linux/patches/3.1/features/fedora/mwifiex-avoid-double-list_del-in-command-cancel-path.patch
        http://chakra.sourceforge.net/sources/linux/patches/3.1/features/fedora/iwlwifi-tx_sync-only-on-PAN-context.patch
        http://chakra.sourceforge.net/sources/linux/patches/3.1/features/fedora/iwlwifi-allow-to-switch-to-HT40-if-not-associated.patch
        http://chakra.sourceforge.net/sources/linux/patches/3.1/features/fedora/iwlwifi-update-SCD-BC-table-for-all-SCD-queues.patch
)
sha1sums=('7f00885ff224f4b7ddaaeee74043c508c4e9be50'
          '7631b5a883c605b5c352fa69e004872796f6e11b'
          'a50247dacd10b4e744014b35b5930e0db5aa7b21'
          '44be50c11dac1f0019110731c2768466e26d5bc4'
          '7cb4b0de6cea1a3acb3ae81fbdea6dcfc9d8c99e'
          '7114abd956e5f45aa9bb46458563f81d20b0b35b'
          '468ca053b25119d4b9a385e9b9adf8feccd86253'
          '7fc61ad711614eb0fba519da2da470dd5b97561d'
          '456273728b68d409d0a0cb538868ee15ea11e2a0'
          'c63f5c7c858cf4a267b1b45cb690a7114b71f2f7'
          '222a892ed269060feeef353ee1b034c9b1469eb4'
          'fede888a5f3b66e114d31b3125ab935f30318bce')

install=install

build() {
	cd "${srcdir}/compat-wireless-${_upver}"

	echo '#define br_port_exists(dev) (dev->priv_flags & IFF_BRIDGE_PORT)' >> net/wireless/core.h # rfkill.h does not use compat-3.1.h
	mv ../eeprom_93xx46.c drivers/misc/eeprom/eeprom_93xx46.c # missing file from compat-wireless tarball
	mv ../eeprom_93xx46.h include/linux/eeprom_93xx46.h # missing file from compat-wireless tarball

	# apply patches
	patch -Np1 -i ${srcdir}/compat-wireless-change-CONFIG_IWLAGN-CONFIG_IWLWIFI.patch
	patch -Np1 -i ${srcdir}/compat-wireless-config-fixups.patch
	patch -Np1 -i ${srcdir}/bcma-brcmsmac-compat.patch
	patch -Np1 -i ${srcdir}/iwlwifi-do-not-set-the-sequence-control-bit-is-not-n.patch
	patch -Np1 -i ${srcdir}/ath9k-fix-max-phy-rate-at-rate-control-init.patch
	patch -Np1 -i ${srcdir}/mwifiex-avoid-double-list_del-in-command-cancel-path.patch
	patch -Np1 -i ${srcdir}/iwlwifi-tx_sync-only-on-PAN-context.patch
	patch -Np1 -i ${srcdir}/iwlwifi-allow-to-switch-to-HT40-if-not-associated.patch
	patch -Np1 -i ${srcdir}/iwlwifi-update-SCD-BC-table-for-all-SCD-queues.patch
	
	# Uncomment the line below if you encounter problems with rt2870.
	#sed -i 's/^\# CONFIG_RT2800USB_RT30XX/CONFIG_RT2800USB_RT30XX/' config.mk

	make
}

package() {
	cd "${srcdir}/compat-wireless-${_upver}"

	make INSTALL_MOD_PATH="${pkgdir}" install-modules
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;

	install -d "${pkgdir}"/usr/sbin
	install scripts/{athenable,athload,b43enable,b43load,iwl-enable,iwl-load,madwifi-unload} "${pkgdir}"/usr/sbin/

	install -d "${pkgdir}"/usr/lib/compat-wireless
	install scripts/{check_depmod,modlib.sh} "${pkgdir}"/usr/lib/compat-wireless/

	install -d "${pkgdir}"/lib/udev/rules.d
	install udev/50-compat_firmware.rules "${pkgdir}"/lib/udev/rules.d/
	install udev/compat_firmware.sh	"${pkgdir}"/lib/udev/
}