#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=mjpegtools
pkgver=2.0.0
pkgrel=3
pkgdesc="Set of tools that can do recording of videos and playback, simple cut-and-paste editing and the MPEG compression of audio and video"
arch=(i686 x86_64)
license=('GPL')
url="http://mjpeg.sourceforge.net/"
depends=('libjpeg-turbo' 'libpng>=1.4.0' 'sdl' 'gcc-libs' 'libdv')
makedeends=('v4l-utils')
options=('!makeflags' '!libtool')
source=("http://downloads.sourceforge.net/sourceforge/mjpeg/${pkgname}-${pkgver}.tar.gz"
        "mjpegtools-v41-2.6.38.patch")
md5sums=('903e1e3b967eebcc5fe5626d7517dc46'
         '529dc1735c4f1e5b7b61c8e61c716811')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/mjpegtools-v41-2.6.38.patch"
  sed -i -e '/ARCHFLAGS=/s:=.*:=:' configure
  # compilation fix, gcc 4.7 related
  sed -i -e '2199s/DeleteRegion (a_pRegion);/this->DeleteRegion (a_pRegion);/' y4mdenoise/MotionSearcher.hh
  ./configure --prefix=/usr --enable-largefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 mpeg2enc/mpeg2syntaxcodes.h "${pkgdir}/usr/include/mjpegtools/mpeg2enc/"
}
