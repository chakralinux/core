# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/mjpegtools

pkgname=mjpegtools
pkgver=2.1.0
pkgrel=3
pkgdesc="Set of tools that can do recording of videos and playback, simple cut-and-paste editing and the MPEG compression of audio and video"
arch=('x86_64')
license=('GPL')
url="http://mjpeg.sourceforge.net/"
depends=('libjpeg-turbo' 'libpng' 'sdl' 'gcc-libs' 'libdv')
makedepends=('v4l-utils')
options=('!makeflags')
source=("http://downloads.sourceforge.net/sourceforge/mjpeg/${pkgname}-${pkgver}.tar.gz"
                mjpegtools-2.1.0-no_format.patch
                mjpegtools-2.1.0-pic.patch
                mjpegtools-2.1.0-sdl-cflags.patch)
md5sums=('57bf5dd78976ca9bac972a6511b236f3'
         '1ada83185885e2b9f952214fc010fece'
         '00e2790ca6a0331d23cd1a504e02b782'
         '6f9604d0afa88e6fbae20975971cb1d7')

prepare() {
  cd ${pkgname}-${pkgver}
  

  patch -p1 -i ../mjpegtools-2.1.0-no_format.patch
  patch -p1 -i ../mjpegtools-2.1.0-pic.patch
  patch -p1 -i ../mjpegtools-2.1.0-sdl-cflags.patch
  
  sed -i -e '/ARCHFLAGS=/s:=.*:=:' configure

}

build() {
  cd ${pkgname}-${pkgver}
 
  ./configure --prefix=/usr --enable-largefile
  make # CFLAGS="${CFLAGS} -fPIC"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -m0644 mpeg2enc/mpeg2syntaxcodes.h "${pkgdir}/usr/include/mjpegtools/mpeg2enc/"
}
