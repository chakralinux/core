# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/mjpegtools

pkgname=mjpegtools
pkgver=2.1.0
pkgrel=2
pkgdesc="Set of tools that can do recording of videos and playback, simple cut-and-paste editing and the MPEG compression of audio and video"
arch=('x86_64')
license=('GPL')
url="http://mjpeg.sourceforge.net/"
depends=('libjpeg-turbo' 'libpng' 'sdl' 'gcc-libs' 'libdv')
makedepends=('v4l-utils')
options=('!makeflags')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/sourceforge/mjpeg/${pkgname}-${pkgver}.tar.gz")
md5sums=('57bf5dd78976ca9bac972a6511b236f3')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i -e '/ARCHFLAGS=/s:=.*:=:' configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-largefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -m0644 mpeg2enc/mpeg2syntaxcodes.h "${pkgdir}/usr/include/mjpegtools/mpeg2enc/"
}
