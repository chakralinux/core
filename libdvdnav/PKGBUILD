#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=libdvdnav
pkgver=5.0.2
pkgrel=1
pkgdesc="The library for xine-dvdnav plugin."
arch=(i686 x86_64)
license=('GPL')
url="https://dvdnav.mplayerhq.hu/"
depends=('libdvdread')
options=(!libtool)
source=("http://download.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'Fix-reading-DVD-label.patch')
md5sums=('d8d6c01a8273d842965bd907ebddcc5d'
         'a92e0638a7cbe5f0e11458a05cf5fb8a')


prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../Fix-reading-DVD-label.patch
  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sharedstatedir=/var \
              --localstatedir=/var \
              --disable-fast-install \
              --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
