pkgbase=python-cairo
pkgname=(python3-cairo python2-cairo)
pkgver=1.18.0
pkgrel=1
pkgdesc="Python bindings for the cairo graphics library"
arch=('x86_64')
url="https://pycairo.readthedocs.io/en/latest/"
license=('LGPL2.1' 'MPL')
makedepends=(python3 python2 cairo git)
_commit=c666979e51c7b42404eadb5a118476cae757eea9  # tags/v1.18.0
source=("git+https://github.com/pygobject/pycairo/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pycairo
  git describe --tags | sed 's/^v//;s/-/+/g'
}


build() {
  cd pycairo
  python2 setup.py build
  python3 setup.py build
}

package_python2-cairo() {
  depends=('cairo' 'python2')
  pkgdesc="Python2 bindings for the cairo graphics library"

  cd pycairo
  python2 setup.py install --skip-build --root="${pkgdir}" --optimize='1'
}

package_python3-cairo() {
  depends=('cairo' 'python3')

  cd pycairo
  python3 setup.py install --skip-build --root="${pkgdir}" --optimize='1'
}
