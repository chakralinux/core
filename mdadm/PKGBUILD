#
# Core Packages for Chakra, part of chakrao.org
#

pkgname=mdadm
pkgver=3.3.2
pkgrel=1
pkgdesc="A tool for managing/monitoring Linux md device arrays, also known as Software RAID"
arch=(x86_64)
license=('GPL')
url="http://neil.brown.name/blog/mdadm"
groups=('base')
conflicts=('mkinitcpio<0.7')
depends=('glibc')
backup=('etc/mdadm.conf')
install=$pkgname.install
source=(ftp://ftp.kernel.org/pub/linux/utils/raid/mdadm/mdadm-$pkgver.tar.xz
        mdadm.conf 
        mdadm_install
        mdadm_hook
        mdadm_udev_install
        disable-werror.patch)
replaces=('raidtools')
md5sums=('44698d351501cac6a89072dc877eb220'
         '5a37c112aa07dccdde62f9fa5b888607'
         '1c13620a503cad264b869ff27f00e5c6'
         'e7bb7410a435045b238c2fa4ae38021e'
         'f2fc02195288251b42b4ccacacf42307'
         '599745ed2bec4489e83991cff89c46ee')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np0 -i ../disable-werror.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make CXFLAGS="$CFLAGS" BINDIR=/usr/sbin UDEVDIR=/usr/lib/udev
  # build static mdassemble for initramfs
  make MDASSEMBLE_AUTO=1 mdassemble

}

package() {
  cd $srcdir/$pkgname-$pkgver
  make INSTALL=/usr/bin/install BINDIR=/usr/sbin DESTDIR=$pkgdir UDEVDIR=/usr/lib/udev install
  make BINDIR=/usr/sbin DESTDIR=$pkgdir \
       SYSTEMD_DIR=/usr/lib/systemd/system install-systemd
  install -D -m755 mdassemble $pkgdir/usr/sbin/mdassemble
  install -D -m644 ../mdadm.conf $pkgdir/etc/mdadm.conf
  install -D -m644 ../mdadm_install $pkgdir/usr/lib/initcpio/install/mdadm
  install -D -m644 ../mdadm_hook $pkgdir/usr/lib/initcpio/hooks/mdadm
  install -D -m644 ../mdadm_udev_install $pkgdir/usr/lib/initcpio/install/mdadm_udev
  # systemd service file
  ln -s "/usr/lib/systemd/system/mdmonitor.service" "$pkgdir/usr/lib/systemd/system/mdadm.service"
}
