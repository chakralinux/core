# Maintainer: Neophytos Kolokotronis <tetris4ATgmailDOTcom>

pkgname=cups-filters
pkgver=1.0.37
pkgrel=3
pkgdesc="OpenPrinting CUPS Filters"
arch=('x86_64')
url="http://www.linuxfoundation.org/collaborate/workgroups/openprinting"
license=('GPL')
depends=('lcms2' 'poppler' 'qpdf')
makedepends=('ghostscript' 'ttf-dejavu') 
optdepends=('ghostscript: for non-PostScript printers to print with CUPS to convert PostScript to raster images'
	    'foomatic-db: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-engine: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-nonfree: drivers use Ghostscript to convert PostScript to a printable form directly')
backup=(etc/fonts/conf.d/99pdftoopvp.conf)
source=("http://www.openprinting.org/download/cups-filters/$pkgname-$pkgver.tar.gz"
        'cups-browsed.service')
md5sums=('42d2dd9745d4d5d626c7b871155cb430'
         '9ef68d7c2a84713fd421f4e87dec0a6e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  ./configure --prefix=/usr  \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --with-rcdir=no \
    --enable-avahi \
    --with-test-font-path=/usr/share/fonts/TTF/DejaVuSans.ttf \
    --with-browseremoteprotocols=DNSSD,CUPS
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # drop static lib
  rm -f ${pkgdir}/usr/lib/*.a
  # systemd service
  install -Dm644 ${srcdir}/cups-browsed.service ${pkgdir}/usr/lib/systemd/system/cups-browsed.service
}
