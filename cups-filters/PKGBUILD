# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakraproject.org

pkgname=cups-filters
pkgver=1.0.20
pkgrel=1
pkgdesc="OpenPrinting CUPS Filters"
arch=('i686' 'x86_64')
url="http://www.linuxfoundation.org/collaborate/workgroups/openprinting"
license=('GPL')
depends=('lcms2' 'poppler>=0.20.2')
makedepends=('ghostscript' 'ttf-dejavu') # ttf-dejavu for make check
optdepends=('ghostscript: for non-PostScript printers to print with CUPS to convert PostScript to raster images'
	    'foomatic-db: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-engine: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-nonfree: drivers use Ghostscript to convert PostScript to a printable form directly')
backup=(etc/fonts/conf.d/99pdftoopvp.conf)
options=(!libtool)
source=(http://www.openprinting.org/download/cups-filters/$pkgname-$pkgver.tar.gz)
md5sums=('fc1d2b8cb5b0513224d7e2b01f900704')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr  \
    --sysconfdir=/etc \
    --with-test-font-path=/usr/share/fonts/TTF/DejaVuSans.ttf
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # drop static lib
  rm -f ${pkgdir}/usr/lib/*.a
}
