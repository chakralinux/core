#
# Platform Packages for Chakra, part of chakraos.org
#
# Maintainer: Weng Xuetian <wengxt@gmail.com>

pkgname=dnsmasq
pkgver=2.71
pkgrel=1
pkgdesc="Lightweight, easy to configure DNS forwarder and DHCP server"
url="http://www.thekelleys.org.uk/dnsmasq/doc.html"
arch=('x86_64')
license=('GPL')
depends=('glibc' 'dbus-core')
install=$pkgname.install
backup=('etc/dnsmasq.conf')
source=("http://www.thekelleys.org.uk/$pkgname/$pkgname-$pkgver.tar.xz"
        'dnsmasq.service')
md5sums=('9e2e4d59c75e71ee3ca817ff0f9be69e'
         '7ac45726cabef4145db40d758cc7fedf')

build() {
  cd "$pkgname-$pkgver"

  make \
    CFLAGS="$CPPFLAGS $CFLAGS" \
    LDFLAGS="$LDFLAGS" \
    COPTS="-DHAVE_DNSSEC -DHAVE_DBUS"
}

package() {
  cd "$pkgname-$pkgver"

  make BINDIR=/usr/bin PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm644 "dbus/dnsmasq.conf" "$pkgdir"/etc/dbus-1/system.d/dnsmasq.conf
  install -Dm644 "dnsmasq.conf.example" "$pkgdir"/etc/dnsmasq.conf
  install -Dm644 "$srcdir/dnsmasq.service" "$pkgdir"/usr/lib/systemd/system/dnsmasq.service

  # DNSSEC setup
  sed -i 's,%%PREFIX%%,/usr,' "$pkgdir"/etc/dnsmasq.conf
  install -Dm644 "trust-anchors.conf" "$pkgdir"/usr/share/dnsmasq/trust-anchors.conf
}

# vim: ts=2 sw=2 et ft=sh
