# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgbase=python-xcffib
pkgname=(python3-xcffib python2-xcffib)
pkgver=0.2.2
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('i686' 'x86_64')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
makedepends=('python3-setuptools' 'python2-setuptools' 'python3-cffi' 'python2-cffi'
             'python3-six' 'python2-six' 'libxcb')
source=("https://pypi.python.org/packages/source/x/xcffib/xcffib-$pkgver.tar.gz")
md5sums=('b398d5c6d8b190848c45287715da1383')

prepare() {
  cp -a xcffib-$pkgver{,-py2}
}

package_python3-xcffib() {
  depends=('python3-six' 'python3-cffi' 'libxcb')

  cd "$srcdir/xcffib-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-xcffib() {
  depends=('python2-six' 'python2-cffi' 'libxcb')

  cd "$srcdir/xcffib-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
