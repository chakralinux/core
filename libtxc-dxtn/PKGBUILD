#
# Part of chakra-project.org
#
# Maintainer: Francesco Marinucci <franzmari[at]chakra-project[dot]it>

pkgname=libtxc-dxtn
_pkgname=libtxc_dxtn
pkgver=1.0.1
pkgrel=3
arch=('x86_64')
pkgdesc="Texture compression library for Mesa"
url="http://dri.freedesktop.org/wiki/S3TC"
license=(custom:BSD)
depends=(mesa)
options=(!libtool)
source=(http://people.freedesktop.org/~cbrill/$_pkgname/$_pkgname-$pkgver.tar.bz2)
md5sums=('7105107f07ac49753f4b61ba9d0c79c5')
 
build() {
  cd "$srcdir/$_pkgname-$pkgver"
 
  ./configure --prefix=/usr
  make
}
 
package() {
  cd "$srcdir/$_pkgname-$pkgver"
 
  make DESTDIR="$pkgdir" install
 
  # License
  mkdir -p "$pkgdir/usr/share/licenses/$_pkgname"
  sed -n '5,22{s|^ \* \?||;p}' txc_dxtn.h \
    > "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
