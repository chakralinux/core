#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=hugin-tools
_pkgname=hugin
pkgver=2011.4.0
pkgrel=1
pkgdesc="Hugin libs and tools to be used by Kipi-plugins"
arch=('x86_64')
url="http://hugin.sourceforge.net/"
license=('LGPL')
depends=('boost-libs' 'enblend-enfuse' 'exiv2' 'openexr' 'autopano-sift-c'
         'lapack' 'desktop-file-utils' 'make' 'perl-exiftool')
makedepends=('zip' 'cmake' 'boost' 'mesa' 'tclap')
conflict=('hugin')
provides=('hugin')
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"
        'gcc47.patch')
sha1sums=('21ec7331d5e196aca2a8771d3c9101dd181ad93b'
          'b2d377579943fca6abd9d1f5bec621f099cb93b5')

build() {
  cd "${srcdir}"
  patch -p1 -i "${srcdir}/gcc47.patch"
  mkdir build 
  cd build

  # this makes possible to compile all the hugin tools without GUis
  sed -i -e "s~FIND_PACKAGE(wxWidgets REQUIRED)~~g" ${srcdir}/${_pkgname}-${pkgver}/CMakeLists.txt
  mkdir ${srcdir}/${_pkgname}-${pkgver}/src/hugin_base/PT
  cp ${srcdir}/${_pkgname}-${pkgver}/src/hugin1/PT/*.h ${srcdir}/${_pkgname}-${pkgver}/src/hugin_base/PT

  export CXXFLAGS+=" -fpermissive"
  cmake "${srcdir}/${_pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS="-lpthread" \
    -DENABLE_LAPACK=yes 
  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
