#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# Find the kernel name inside the chroot
_extramodules=extramodules-`pacman -Q linux | cut -c7-9 | sed 's/linux //g'`-CHAKRA
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=(nvidia-96xx chd-nvidia-96xx)
pkgver=96.43.20
pkgrel=9
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
makedepends=('linux-headers')
license=('custom')
install=nvidia.install
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run"
	"nvidia_supported")
md5sums=('875cc011385ea26ab5edd54a9b65a003')
[ "$CARCH" = "x86_64" ] && md5sums=('df3c87005487bdf54a6c5b783ff373db')
md5sums+=('62d0cdb856cfae64753c0f3acf729d47')

build() {
    sh "NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run" --extract-only
    cd "NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/"
    ln -s Makefile.kbuild Makefile
    make SYSSRC=/lib/modules/${_kver}/build module 
}

package_nvidia-96xx() {
pkgdesc="NVIDIA drivers for kernel26, 96xx branch."
depends=('linux>=3.2' 'linux<3.3' 'nvidia-96xx-utils')
conflicts=('nvidia' 'nvidia-173xx')

    cd "NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/"
    mkdir -p $pkgdir//lib/modules/${_extramodules}
    install -m644 nvidia.ko $pkgdir/lib/modules/${_extramodules}
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

package_chd-nvidia-96xx() {
    pkgdesc="CHD ids for nvidia $pkgver"
    arch=('any')

    cd "${srcdir}/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/"

    install -dm755 "${pkgdir}/opt/chakra/hwdb"
    sh -e "${srcdir}/nvidia_supported" ../../share/doc/README.txt nv-kernel.o \
        > "${pkgdir}/opt/chakra/hwdb/hw_nvidia173xx_hwdb"
}
