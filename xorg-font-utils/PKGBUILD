# Part of X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=xorg-font-utils
pkgver=7.6
pkgrel=3
pkgdesc="X.Org font utilities"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfont' 'libfontenc' 'xproto')
makedepends=('pkgconfig' 'bigreqsproto')
install=xorg-font-utils.install
source=(${url}/releases/individual/app/bdftopcf-1.0.4.tar.bz2
        ${url}/releases/individual/app/mkfontdir-1.0.7.tar.bz2
        ${url}/releases/individual/app/mkfontscale-1.1.1.tar.bz2
        ${url}/releases/individual/font/font-util-1.3.0.tar.bz2)
md5sums=('96a648a332160a7482885800f7a506fa'
         '18c429148c96c2079edda922a2b67632'
         '03de3f15db678e277f5ef9c013aca1ad'
         'ddfc8a89d597651408369d940d03d06b')


build() {
  cd "${srcdir}"

  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
        ./configure --prefix=/usr --mandir=/usr/share/man \
        --with-mapdir=/usr/share/fonts/util --with-fontrootdir=/usr/share/fonts
        make
      popd
     fi
   done
}

package() {
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"   
        make DESTDIR="${pkgdir}" install
        install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done
}
