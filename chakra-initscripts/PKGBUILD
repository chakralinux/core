#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_origname=initscripts
pkgname=chakra-initscripts
pkgver=2011.02
pkgrel=2
pkgdesc="System initialization/bootup scripts"
arch=('i686' 'x86_64')
url="http://www.chakra-project.org"
license=('GPL2')
groups=('livecd')
provides=("initscripts=${pkgver}")
conflicts=('initscripts')
backup=(etc/inittab etc/rc.conf etc/rc.local etc/rc.local.shutdown etc/conf.d/wireless)
depends=('bash' 'awk' 'grep' 'coreutils' 'sed' 'udev'
         'net-tools' 'ncurses' 'kbd' 'findutils' 'sysvinit')
optdepends=('bridge-utils: Network bridging support'
            'dhcpcd: DHCP network configuration'
            'wireless_tools: Wireless networking')
install=initscripts.install
source=("http://chakra-project.org/sources/initscripts/initscripts-${pkgver}.tar.xz"
        'http://chakra-project.org/sources/initscripts/wireless.conf.d'
        'http://chakra-project.org/sources/initscripts/chakra-initscripts-2011.02-2.patch')
sha256sums=('0734166a2fd170a8b144a51d8be5ac3773f8c1381240a744cfd05005464aabda'
            '0eba4250b07e0a727ef64772f5ee9027b123cbd4bb0894ba2842f545035c827f'
            '941ff3f1c886ae82b3e1b89ede2ad12b29caf66eb4a013de985a4a9e00a41877')

package() {
  cd ${srcdir}/${_origname}-${pkgver}
  DESTDIR=${pkgdir} ./install.sh
  install -D -m644 ${srcdir}/wireless.conf.d ${pkgdir}/etc/conf.d/wireless
  cd ${pkgdir}
  patch -Np1 -i ${srcdir}/chakra-initscripts-2011.02-2.patch || return 1
}
