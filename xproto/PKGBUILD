# Part of X.org group
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=xproto
pkgver=7.0.31
pkgrel=1
pkgdesc="X11 core wire protocol and auxiliary headers"
arch=(x86_64)
license=('custom')
url="https://xorg.freedesktop.org"
source=("${url}/releases/individual/proto/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha1sums=('f496ce16048c0b6b854fd6aefd6efb183eb61822'
          'SKIP')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # Keith Packard

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
