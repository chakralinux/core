# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Benjamin Andresen <benny(at)klapmuetz(dot)org>
# Contributor: scj <scj archlinux us>

pkgname=ocaml-findlib
pkgver=1.7.1
pkgrel=1
license=('MIT')
arch=('x86_64')
pkgdesc='Objective Caml (OCaml) package manager'
url='http://projects.camlcity.org/projects/findlib.html'
depends=('ocaml')
source=("http://download.camlcity.org/download/findlib-$pkgver.tar.gz")
md5sums=('108717618e724295d8a01c21ba3f7311')
options=('staticlibs' '!strip' '!makeflags') # otherwise the bytecode gets broken

build() {
  cd "$srcdir/findlib-$pkgver"

  ./configure -config /etc/findlib.conf -sitelib /usr/lib/ocaml -mandir /usr/share/man
  make all opt
}

package () {
  cd "$srcdir/findlib-$pkgver"

  make prefix="$pkgdir" install 

  install -m755 src/findlib/ocamlfind_opt "$pkgdir/usr/bin/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
