#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=file
pkgver=5.07
pkgrel=1
pkgdesc="File type identification utility"
arch=('i686' 'x86_64')
license=('custom')
groups=('base')
url="http://www.darwinsys.com/file/"
depends=('glibc' 'zlib')
options=('!libtool')
source=(ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz 
        file-5.05-zip64.patch
        file-5.07-zip-detect.patch)
md5sums=('b8d1f9a8a644067bd0a703cebf3f4858'
         '385f020467debd98bd2d8df6143f93d0'
         'bcbf2e152f38003a2736298bbd0f37f8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#\$(datadir)/misc#\$(datadir)#' configure 

  # identify zip64 files
  patch -Np1 -i ${srcdir}/file-5.05-zip64.patch
  patch -Np1 -i ${srcdir}/file-5.07-zip-detect.patch
  ./configure --prefix=/usr --datadir=/usr/share/file
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install
  install -dm755 ${pkgdir}/usr/share/misc
  ln -s ../file/magic.mgc ${pkgdir}/usr/share/misc
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
