#
# Core Packages for Chakra, part of chakraos.org
#

pkgname=libpcap
pkgver=1.7.4
pkgrel=1
pkgdesc="A system-independent interface for user-level packet capture"
arch=('x86_64')
url="http://www.tcpdump.org/"
license=('BSD')
depends=('glibc' 'libnl' 'sh' 'libusbx')
makedepends=('flex' 'bluez-libs')
source=("http://www.tcpdump.org/release/libpcap-${pkgver}.tar.gz"
        'mgmt.h')
md5sums=('b2e13142bbaba857ab1c6894aedaf547'
         'aa0697d54248b0b728211d19d38c4b9a')
validpgpkeys=('1F166A5742ABB9E0249A8D30E089DEF1D9C15D0D')  # The Tcpdump Group

prepare() {
  mkdir -p ${srcdir}/include/bluetooth
  cp ${srcdir}/mgmt.h ${srcdir}/include/bluetooth/
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  CFLAGS="$CFLAGS -I${srcdir}/include $(pkg-config libnl-genl-3.0 --cflags)" \
  ./configure --prefix=/usr \
    --enable-ipv6 \
    --enable-bluetooth \
    --with-libnl
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -d -m755 ${pkgdir}/usr/bin
  make DESTDIR=${pkgdir} install
  
  # backwards compatibility, programs often look for net/bpf.h
  mkdir -p ${pkgdir}/usr/include/net
  cd ${pkgdir}/usr/include/net
  ln -s ../pcap-bpf.h bpf.h

  #install the license
  install -D -m644 ${srcdir}/$pkgname-$pkgver/LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

