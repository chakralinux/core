#
# Chakra Packages, part of chakra-project.org
#
# maintainer: abveritas@chakra-project.org>


pkgname=make
pkgver=3.82
pkgrel=1
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'sh')
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        bug30612.patch
        bug30723.patch
        make-3.82-sort-blank.patch
        make-3.82-makeflags.patch)
md5sums=('1a11100f3c63fcf5753818e59d63088f'
         'c8f496b22191f9fb9420ab14c1a19a47'
         '662e6450e19a5acdaa5c9fcb8ad78dea'
         '7d01a99f389d8f08dec93ed479071ee4'
         'c62acc8a2925362bd780cb5d4206d5c3')


build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  # http://savannah.gnu.org/bugs/?30612
  patch -Np1 -i $srcdir/bug30612.patch
  
  # http://savannah.gnu.org/bugs/?30723
  patch -Np1 -i $srcdir/bug30723.patch
  
  # fix from Fedora
  patch -Np1 -i $srcdir/make-3.82-sort-blank.patch
  
  # https://savannah.gnu.org/support/index.php?107487
  # https://savannah.gnu.org/bugs/?33873
  patch -Np0 -i $srcdir/make-3.82-makeflags.patch

  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
