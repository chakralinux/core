# Maintainer: abveritas@chakra-project.org>

pkgname=make
pkgver=3.82
pkgrel=2
pkgdesc="GNU make utility to maintain groups of programs"
arch=('x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'sh')
install=$pkgname.install
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'bug30612.patch'
        'bug30723.patch'
        'make-3.82-sort-blank.patch'
        'make-3.82-makeflags.patch'
        'make-3.82-expensive_glob.patch'
        'make-3.82-dont-prune-intermediate.patch')
md5sums=('1a11100f3c63fcf5753818e59d63088f'
         'c8f496b22191f9fb9420ab14c1a19a47'
         '662e6450e19a5acdaa5c9fcb8ad78dea'
         '7d01a99f389d8f08dec93ed479071ee4'
         'c62acc8a2925362bd780cb5d4206d5c3'
         'e58d4211c5b29d7d927a304d082849e7'
         '45ac9e987f4bf16c0d7e1685db943917')


build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  # http://savannah.gnu.org/bugs/?30612
  patch -p1 -i $srcdir/bug30612.patch
  
  # http://savannah.gnu.org/bugs/?30723
  patch -p1 -i $srcdir/bug30723.patch
  
  # fix from Fedora
  patch -p1 -i $srcdir/make-3.82-sort-blank.patch
  
  # https://savannah.gnu.org/support/index.php?107487
  # https://savannah.gnu.org/bugs/?33873
  patch -p0 -i $srcdir/make-3.82-makeflags.patch
  
  # http://lists.gnu.org/archive/html/bug-make/2011-04/msg00002.html
  patch -p0 -i $srcdir/make-3.82-expensive_glob.patch
 
  # https://savannah.gnu.org/bugs/?30653
  patch -p0 -i $srcdir/make-3.82-dont-prune-intermediate.patch

  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
