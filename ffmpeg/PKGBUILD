# Contributor: abveritas@chakra-project.org
# Maintainer: Bruce Liu <brli@users.sourceforge.net>

pkgname=ffmpeg
pkgver=2.1.4
pkgrel=2
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('x86_64')
url="http://ffmpeg.org"
license=('GPL')
depends=('bzip2' 'lame' 'sdl' 'libvorbis' 'xvidcore' 'zlib' 'x264' 'libtheora' 'celt'
         'opencore-amr' 'alsa-lib' 'libvdpau' 'libxfixes' 'schroedinger' 'libvpx>=1.1.0'
         'libva' 'openjpeg' 'rtmpdump' 'speex' 'gsm' 'pulseaudio' 'v4l-utils' 'libass'
         'fontconfig' 'freetype2' 'openal' 'libdc1394' 'libavc1394' 'libcaca' 'libxv')
makedepends=('yasm' 'libbluray')
optdepends=('libbluray: for Blueray disc playback support')
source=("${url}/releases/${pkgname}-${pkgver}.tar.gz"
        "${url}/releases/${pkgname}-${pkgver}.tar.gz.asc")
md5sums=('bf8414a2c4c092330041cfdfe110cd2f'
         'SKIP')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --disable-debug \
              --disable-static \
              --enable-avresample \
              --enable-bzlib \
              --enable-dxva2 \
              --enable-fontconfig \
              --enable-gpl \
              --enable-libass \
              --enable-libbluray \
              --enable-libcaca \
              --enable-libcelt \
              --enable-libdc1394 \
              --enable-libgsm \
              --enable-libmp3lame \
              --enable-libopencore_amrnb \
              --enable-libopencore_amrwb \
              --enable-libopenjpeg \
              --enable-libpulse \
              --enable-librtmp \
              --enable-libschroedinger \
              --enable-libspeex \
              --enable-libtheora \
              --enable-libv4l2 \
              --enable-libvorbis \
              --enable-libvpx \
              --enable-libx264 \
              --enable-libxvid \
              --enable-openal \
              --enable-postproc \
              --enable-runtime-cpudetect \
              --enable-shared \
              --enable-vaapi \
              --enable-vda \
              --enable-vdpau \
              --enable-version3 \
              --enable-x11grab

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play,server}.1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install install-man
  install -D -m755 tools/qt-faststart "${pkgdir}/usr/bin/qt-faststart"
}
