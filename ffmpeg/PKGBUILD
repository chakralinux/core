#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ffmpeg
pkgver=20110330
_head=HEAD-da3534b
pkgrel=4
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('i686' 'x86_64')
url="http://ffmpeg.org/"
license=('GPL3')
depends=('bzip2' 'lame' 'sdl' 'libvorbis' 'xvidcore' 'zlib' 'x264' 'libtheora' 'opencore-amr' 'alsa-lib' 'libvdpau' 'libxfixes' 'schroedinger' 'libva>=1.0.7' 'openjpeg' 'libvpx')
makedepends=('yasm')
#remake snapshot with: http://git.videolan.org/?p=ffmpeg.git;a=snapshot;h=HEAD;sf=tgz
source=(http://chakra-project.org/sources/ffmpeg/ffmpeg-${_head}.tar.gz)
options=('force')
#source=(http://ffmpeg.org/releases//releases/ffmpeg-${pkgver}.tar.bz2)
md5sums=('fcd523f02061aa03a3a81dd022bac640')

build() {
  cd "$srcdir/$pkgname-$_head"

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-libmp3lame \
    --enable-libvorbis \
    --enable-libxvid \
    --enable-libx264 \
    --enable-libtheora \
    --enable-postproc \
    --enable-shared \
    --enable-pthreads \
    --enable-x11grab \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-libschroedinger \
    --enable-libopenjpeg \
    --enable-version3 \
    --enable-runtime-cpudetect \
    --enable-libvpx \
    --disable-debug

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play,server}.1
}

package(){
  cd "$srcdir/$pkgname-$_head"
  make DESTDIR="$pkgdir" install install-man
  install -D -m755 tools/qt-faststart "$pkgdir/usr/bin/qt-faststart"
}

# vim:set ts=2 sw=2 et:
