#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ffmpeg
pkgver=0.6.3
pkgrel=2
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix."
arch=('i686' 'x86_64')
url="http://ffmpeg.org/"
license=('GPL3')
depends=('bzip2' 'lame' 'sdl' 'libvorbis' 'xvidcore' 'zlib' 'x264' 'libtheora' 'opencore-amr' 'alsa-lib' 'libvdpau' 'libxfixes' 'schroedinger' 'libva>=1.0.7' 'openjpeg' 'libvpx' 'gsm')
makedepends=('yasm')
options=('force')

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."
 
  if [ -d "${pkgname}" ] ; then
    cd "${pkgname}" && git pull origin
    msg "The local files are updated."
  else
    # Fetch the required tag from Git
    git clone --no-checkout git://git.videolan.org/ffmpeg.git
    cd "${pkgname}"
    git checkout -q "${pkgname}-${pkgver}"
  fi
 
  msg "GIT checkout done or server timeout"
  msg "Starting make..."
 
  rm -rf "${srcdir}/${pkgname}-build"
  git clone "${srcdir}/${pkgname}" "${srcdir}/${pkgname}-build"
  cd "${srcdir}/${pkgname}-build"

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-version3 \
    --enable-libmp3lame \
    --enable-libvorbis \
    --enable-libxvid \
    --enable-libx264 \
    --enable-libtheora \
    --enable-postproc \
    --enable-shared \
    --enable-pthreads \
    --enable-x11grab \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-libschroedinger \
    --enable-libopenjpeg \
    --enable-runtime-cpudetect \
    --enable-libvpx \
    --enable-libgsm \
    --disable-debug

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play,server}.1
}

package() {
  cd "${srcdir}/${pkgname}-build"

  make DESTDIR="${pkgdir}" install install-man
  install -D -m755 tools/qt-faststart "${pkgdir}/usr/bin/qt-faststart"
}

# vim:set ts=2 sw=2 et:
