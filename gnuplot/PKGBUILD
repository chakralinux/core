pkgname=gnuplot 
pkgver=5.0.1
pkgrel=1
pkgdesc="Plotting package which outputs to X11, PostScript, PNG, GIF, and others" 
arch=('x86_64') 
url="http://www.gnuplot.info" 
license=('custom') 
depends=('readline' 'gd' 'cairo' 'libjpeg' 'lua' 'qt5-svg' 'qt5-tools' 'gnutls') 
makedepends=('texinfo' 'texlive-core' 'emacs-nox' 'texlive-latexextra') 
options=('!makeflags')
install=gnuplot.install 
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha1sums=('a66f5211d3eb8d382b8dd9bdf320b65cf1315a13')

build() {
  cd $srcdir/$pkgname-$pkgver

  # fix default source location; use the GDFONTPATH variable to modify at runtime 
  sed -i 's|/usr/X11R6/lib/X11/fonts/truetype|/usr/share/fonts/TTF|' src/variable.c

  sed -i -e 's|/usr/X11R6/lib/X11/fonts/Type1|/usr/share/fonts/Type1|' \
  -e 's|$(X11ROOT)/X11R6/lib/X11/fonts/Type1|$(X11ROOT)/usr/share/fonts/Type1|' \
  src/variable.c
  
  # Fix our Qt complile option issue
  sed -i 's+-fPIE+-fPIC+' configure
  
  ./configure --prefix=/usr \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --with-readline=gnu \
              --disable-wxwidgets
  make pkglibexecdir=/usr/bin
}


package() {
  cd $srcdir/$pkgname-$pkgver
  
  msg "Installing core files"
  make pkglibexecdir=/usr/bin DESTDIR="$pkgdir" install
  
  msg "Installing info files"
  make pkglibexecdir=/usr/bin DESTDIR="$pkgdir" install-info

  install -Dm644 Copyright $pkgdir/usr/share/licenses/$pkgname/Copyright

  rm -f $pkgdir/usr/share/texmf-dist/ls-R
}
