#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dog]org>
# maintainer Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=util-linux
pkgver=2.25.1
pkgrel=2
pkgdesc="Miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('x86_64')
groups=('base')
depends=('pam' 'shadow' 'coreutils' 'glibc')
makedepends=('systemd' 'python3')
conflicts=('util-linux-ng' 'eject')
provides=("util-linux-ng=${pkgver}" 'eject')
license=('GPL2')
options=('strip' 'debug')
source=("ftp://ftp.kernel.org/pub/linux/utils/${pkgname}/v2.25/${pkgname}-${pkgver}.tar.xz"
        uuidd.tmpfiles
        pam-login
        pam-common
        pam-su
        system-auth
        system-local-login
        system-login
        system-remote-login
        system-services)
backup=(etc/pam.d/chfn
        etc/pam.d/chsh
        etc/pam.d/login
        etc/pam.d/su
        etc/pam.d/su-l)
install=util-linux.install
md5sums=('2ff36a8f8ede70f66c5ad0fb09e40e79'
         'a39554bfd65cccfd8254bb46922f4a67'
         '4368b3f98abd8a32662e094c54e7f9b1'
         'a31374fef2cba0ca34dfc7078e2969e4'
         'fa85e5cce5d723275b14365ba71a8aad'
         '5f169a4ffe7ed69f58e106cdd2d760df'
         '477237985820117a0e6e1b13a86eb599'
         '17c691f2da319df8fe851bc47cc1d662'
         '477237985820117a0e6e1b13a86eb599'
         '30fe7d41e054ee43fab7855bf88a07e5')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --bindir=/usr/bin \
              --sbindir=/usr/sbin \
              --localstatedir=/run \
              --enable-fs-paths-extra=/usr/bin:/usr/sbin \
              --enable-raw \
              --enable-vipw \
              --enable-newgrp \
              --enable-chfn-chsh \
              --enable-write \
              --enable-mesg \
              --enable-socket-activation \
              --with-python=3

  make
}

# only bc test fails, leaving dep to test the rest
#check() {
#  make -C "${pkgname}-${pkgver}" check
#}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"
  
  chmod 4755 "$pkgdir"/usr/bin/{newgrp,ch{sh,fn}}

  # install PAM files for login-utils
  install -Dm644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chfn"
  install -m644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chsh"
  install -m644 "$srcdir/pam-login" "$pkgdir/etc/pam.d/login"
  install -m644 "$srcdir/pam-su" "${pkgdir}/etc/pam.d/su"
  install -m644 "$srcdir/pam-su" "${pkgdir}/etc/pam.d/su-l"
  install -m644 "$srcdir/system-auth" "${pkgdir}/etc/pam.d/system-auth"
  install -m644 "$srcdir/system-local-login" "${pkgdir}/etc/pam.d/system-local-login"
  install -m644 "$srcdir/system-login" "${pkgdir}/etc/pam.d/system-login"
  install -m644 "$srcdir/system-remote-login" "${pkgdir}/etc/pam.d/system-remote-login"
  install -m644 "$srcdir/system-services" "${pkgdir}/etc/pam.d/system-services"

  install -Dm644 "$srcdir/uuidd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/uuidd.conf"
}
