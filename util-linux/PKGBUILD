#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=util-linux
pkgver=2.21
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('i686' 'x86_64')
groups=('base')
depends=('udev')
conflicts=('util-linux-ng')
provides=("util-linux-ng=${pkgver}")
license=('GPL2')
options=('!libtool')
source=(ftp://ftp.kernel.org/pub/linux/utils/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.xz
	libmount-canonicalize-all-paths-from-fs-tab.patch
	lib-canonicalize-always-remove-tailing-slash.patch
	libmount-canonicalize-targets-from-fstab-on-mount-a.patch
	mount-new-cleanup-mount-a-return-codes.patch)
optdepends=('perl: for chkdupexe support')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i include/pathnames.h

  patch -p1 -i ../libmount-canonicalize-all-paths-from-fs-tab.patch
  patch -p1 -i ../lib-canonicalize-always-remove-tailing-slash.patch
  patch -p1 -i ../libmount-canonicalize-targets-from-fstab-on-mount-a.patch
  patch -p1 -i ../mount-new-cleanup-mount-a-return-codes.patch 

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-write \
              --enable-raw \
              --disable-wall \
              --enable-new-mount

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"

  install -dm755 var/lib/hwclock

  # delete stray empty dir, fixed upstream
  rm -r usr/share/man/ru
}
md5sums=('208aa058f4117759d2939d1be7d662fc'
         'c4011222a19b020d9b8465c3c7e443f6'
         '8c1bf7b10e22e2b835441e6ae2d804d6'
         '7b92423c588ed35229a9a166349c29a9'
         'c483ab2d52609b5857d096b256a5075c')
