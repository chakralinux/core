#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dog]org>
# maintainer Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=util-linux
pkgver=2.21.2
pkgrel=2
pkgdesc="Miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('i686' 'x86_64')
groups=('base')
depends=('pam')
conflicts=('util-linux-ng')
provides=("util-linux-ng=${pkgver}")
license=('GPL2')
options=('!libtool')
source=("ftp://ftp.kernel.org/pub/linux/utils/${pkgname}/v2.21/${pkgname}-${pkgver}.tar.xz")
md5sums=('54ba880f1d66782c2287ee2c898520e9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --disable-wall \
              --enable-fs-paths-extra=/usr/bin:/usr/sbin \
              --enable-write \
              --enable-raw \
              --enable-new-mount \
              --enable-rename \
              --enable-schedutils \
              --disable-login-utils

  make
}

check() {
  make -C "${pkgname}-${pkgver}" check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"

  # broken tool, going away in next major release, so just remove it now
  rm "${pkgdir}"/usr/{bin/chkdupexe,share/man/man1/chkdupexe.1}

  # delete stray empty dir, fixed upstream
  rm -r usr/share/man/ru
}

# vim:set ts=2 sw=2 et:
