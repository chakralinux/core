# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/ocaml/

pkgname=ocaml	
pkgver=4.00.1
_pkgver=4.00
pkgrel=1
pkgdesc="A functional language with OO extensions"
arch=('x86_64')
license=('LGPL2' 'custom: QPL-1.0')
url="http://caml.inria.fr/"
depends=('gdbm')
makedepends=('tk' 'ncurses' 'libx11')
optdepends=('ncurses: advanced ncurses features' 'tk: advanced tk features')
source=("http://caml.inria.fr/distrib/ocaml-${_pkgver}/$pkgname-$pkgver.tar.gz"
        'tk8.6-build.patch')
options=('!makeflags' '!emptydirs')
md5sums=('91124a8eb12a57f1e56c02fe3db0f9e7'
         '46185b558c93471b19fd0c6753f90377')


build() {
  cd $srcdir/$pkgname-$pkgver
  # fix https://bugs.archlinux.org/task/33302
  patch -Np2 -i ../tk8.6-build.patch
  
  ./configure -prefix /usr 
  make world.opt 
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make PREFIX=$pkgdir/usr MANDIR=$pkgdir/usr/share/man install 
   
# Save >10MB with this one, makepkg only strips debug symbols.
#find ${pkgdir}/usr/lib -type f -name '*.so.*' -exec strip --strip-unneeded {} \;

  # install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
