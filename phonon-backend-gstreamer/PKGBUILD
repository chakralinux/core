# maintainer abveritas@chakra-project.org

pkgbase=phonon-backend-gstreamer
pkgname=('phonon-backend-gstreamer' 'phonon-qt5-backend-gstreamer')
pkgver=4.8.2
pkgrel=2
pkgdesc="Phonon GStreamer backend"
arch=('x86_64')
url='http://phonon.kde.org'
license=('LGPL')
makedepends=('pkg-config' 'cmake' 'automoc4' 'phonon' 'phonon-qt5')
optdepends=('gst-plugins-good: For media format support'
            'gst-plugins-bad: For media format support'
            'gst-plugins-ugly: For media format support'
            'gst-libav: For media format support')
groups=("kde-complete" "kde-uninstall")
source=("http://download.kde.org/stable/phonon/phonon-backend-gstreamer/${pkgver}/src/phonon-backend-gstreamer-${pkgver}.tar.xz"
        "phonon-gstreamer-gst15_include_dirs.patch")
md5sums=('ce441035dc5a00ffaac9a64518ab5c62'
         'c4a3d5230b07d4d84a8f05a8575c69aa')

prepare() {
  mkdir build-qt4
  mkdir build-qt5

  cd ${pkgname}-${pkgver}

  # https://bugzilla.redhat.com/show_bug.cgi?id=1239790
  patch -Np1 -i ../phonon-gstreamer-gst15_include_dirs.patch
}

build() {
  cd build-qt4
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DCMAKE_INSTALL_LIBDIR=lib
  make

  cd ../build-qt5
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_BUILD_PHONON4QT5=ON
  make
}

package_phonon-backend-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt4"
  depends=('gst-plugins-base')
  provides=('phonon-backend')

  cd build-qt4
  make DESTDIR="${pkgdir}" install
}

package_phonon-qt5-backend-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt5"
  depends=('gst-plugins-base')
  provides=('phonon-qt5-backend')

  cd build-qt5
  make DESTDIR="${pkgdir}" install

  # Those are not needed
  rm -rf "${pkgdir}"/usr/share
}
