# maintainer abveritas@chakra-project.org

pkgbase=phonon-backend-gstreamer
pkgname=('phonon-backend-gstreamer' 'phonon-qt5-backend-gstreamer')
pkgver=4.9.0
pkgrel=1
pkgdesc="Phonon GStreamer backend"
arch=('x86_64')
url='http://phonon.kde.org'
license=('LGPL')
makedepends=('gst-plugins-base' 'cmake' 'extra-cmake-modules' 'phonon' 'phonon-qt5' 'qt5-x11extras')
optdepends=('gst-plugins-good: For media format support'
            'gst-plugins-bad: For media format support'
            'gst-plugins-ugly: For media format support'
            'gst-libav: For media format support')
groups=("kde-complete" "kde-uninstall")
source=("http://download.kde.org/stable/phonon/phonon-backend-gstreamer/${pkgver}/phonon-backend-gstreamer-${pkgver}.tar.xz")
sha256sums=('cec3e5ece1261d344b68363ef0606ebf49772628ba94bb55b0c0d18773b885f1')

prepare() {
  mkdir build-qt4
  mkdir build-qt5
}

build() {
  cd build-qt4
  cmake ../phonon-gstreamer-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DCMAKE_INSTALL_LIBDIR=lib
  make

  cd ../build-qt5
  cmake ../phonon-gstreamer-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_BUILD_PHONON4QT5=ON
  make
}

package_phonon-backend-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt4"
  depends=('gst-plugins-base')
  provides=('phonon-backend')

  cd build-qt4
  make DESTDIR="${pkgdir}" install

  # Conflict with -qt5
  rm -r "$pkgdir"/usr/share/icons
}

package_phonon-qt5-backend-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt5"
  depends=('gst-plugins-base' 'qt5-x11extras')
  provides=('phonon-qt5-backend')

  cd build-qt5
  make DESTDIR="${pkgdir}" install
}
