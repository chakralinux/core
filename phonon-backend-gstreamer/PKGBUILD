# maintainer abveritas@chakra-project.org

pkgbase=phonon-backend-gstreamer
pkgname=('phonon-backend-gstreamer' 'phonon-qt5-backend-gstreamer')
pkgver=4.8.0
pkgrel=1
pkgdesc="Phonon GStreamer backend"
arch=('x86_64')
url='http://phonon.kde.org'
license=('LGPL')
makedepends=('pkg-config' 'cmake' 'automoc4' 'phonon' 'phonon-qt5')
groups=("kde-complete" "kde-uninstall")
source=("http://download.kde.org/stable/phonon/phonon-backend-gstreamer/${pkgver}/phonon-backend-gstreamer-${pkgver}.tar.xz")
sha256sums=('80b72a9092f540dd0a2abd2898328f3d5f9febfa073b0b1d44592a050a99d2b1')

prepare() {
  mkdir build-qt4
  mkdir build-qt5
}


build() {
  cd build-qt4
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib
  make

  cd ../build-qt5
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_BUILD_PHONON4QT5=ON
  make
}

package_phonon-backend-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt4"
  depends=('gst-plugins-base')
  provides=('phonon-backend')

  cd build-qt4
  make DESTDIR="${pkgdir}" install
}

package_phonon-qt5-backend-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt5"
  depends=('gst-plugins-base')
  provides=('phonon-qt5-backend')

  cd build-qt5
  make DESTDIR="${pkgdir}" install

  # Those are not needed
  rm -rf "${pkgdir}"/usr/share
}
sha256sums=('f819cbd6296e1e19579e365902f7930ec8571e8cbb08718484091a795f70587e')
