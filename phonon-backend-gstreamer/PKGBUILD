# maintainer abveritas@chakra-project.org

pkgbase=phonon-backend-gstreamer
pkgname=('phonon-backend-gstreamer' 'phonon-qt5-backend-gstreamer')
pkgver=4.8.0
pkgrel=2
pkgdesc="Phonon GStreamer backend"
arch=('x86_64')
url='http://phonon.kde.org'
license=('LGPL')
makedepends=('pkg-config' 'cmake' 'automoc4' 'phonon' 'phonon-qt5')
optdepends=('gst-plugins-good: For media format support'
            'gst-plugins-bad: For media format support'
            'gst-plugins-ugly: For media format support'
            'gst-libav: For media format support')
groups=("kde-complete" "kde-uninstall")
source=("http://download.kde.org/stable/phonon/phonon-backend-gstreamer/${pkgver}/phonon-backend-gstreamer-${pkgver}.tar.xz"
        "streaming-freeze.patch::http://quickgit.kde.org/?p=phonon-gstreamer.git&a=commitdiff&h=4bb52a061b669c3ce03bb07e49a35a034aa5fc7b&hp=17cc78282f588f7e914244be222289b74934e9d9&o=plain")
sha256sums=('f819cbd6296e1e19579e365902f7930ec8571e8cbb08718484091a795f70587e'
            '142876ac4f0cdab7b7e173ae738b1771d44df27790c58dde02c3a18b35797dce')

prepare() {
  mkdir build-qt4
  mkdir build-qt5

  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../streaming-freeze.patch
}


build() {
  cd build-qt4
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib
  make

  cd ../build-qt5
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_BUILD_PHONON4QT5=ON
  make
}

package_phonon-backend-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt4"
  depends=('gst-plugins-base')
  provides=('phonon-backend')

  cd build-qt4
  make DESTDIR="${pkgdir}" install
}

package_phonon-qt5-backend-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt5"
  depends=('gst-plugins-base')
  provides=('phonon-qt5-backend')

  cd build-qt5
  make DESTDIR="${pkgdir}" install

  # Those are not needed
  rm -rf "${pkgdir}"/usr/share
}
