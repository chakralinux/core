#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=lensfun
pkgver=0.2.8
pkgrel=1
pkgdesc="Database of photographic lenses and a library that allows advanced access to the database"
arch=('x86_64')
url="http://lensfun.berlios.de/"
license=('LGPL3')
depends=('glibc' 'glib2')
makedepends=('python2' 'libpng')
source=("http://download.berlios.de/${pkgname}/${pkgname}-${pkgver}.tar.bz2") 
md5sums=('db2988505e7432c6b331aa597789c639')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' configure
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib
    make all
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make INSTALL_PREFIX="$pkgdir" install
}
