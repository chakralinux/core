# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kdbusaddons
pkgver=5.48.0
pkgrel=1
pkgdesc='Provides API for registering services, KDED modules and applications according to Freedesktop standards.'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kdbusaddons'
license=('LGPL')
depends=("qt5-base" 'qt5-x11extras')
makedepends=("extra-cmake-modules>=5.48.0" 'qt5-tools')
checkdepends=("dbus-core")
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('39a3df3ad2092cc0a551356e45209a51b312abb5632e17b51b648b222323ed6e'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   eval `dbus-launch --sh-syntax `
#   make test 
#   kill $DBUS_SESSION_BUS_PID
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
