#
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor:  Mantas MikulÄ—nas <grawity@gmail.com>

pkgname=gummiboot
pkgver=45
pkgrel=1
pkgdesc="Simple UEFI Boot Manager"
url="http://freedesktop.org/wiki/Software/gummiboot"
arch=('x86_64')
license=('LGPL2.1')
makedepends=('gnu-efi-libs' 'docbook-xsl' 'git')
depends=('util-linux')
options=('!strip' '!makeflags')
source=("git://anongit.freedesktop.org/$pkgname#tag=$pkgver"
        'loader.conf'
        'chakra.conf')
install=gummiboot.install
md5sums=('SKIP'
         'f0a24fe3a473abcc863b24e02970526c'
         '0fdb041cc891292162f6c2f7d1af830c')

build() {
	cd ${srcdir}/${pkgname}

	./autogen.sh
	./configure --sysconfdir=/etc  --with-efi-ldsdir=/usr/lib --libexecdir=/usr/lib --libdir=/usr/lib
	
	make
}

package() {
	## Install gummiboot example configuration files (can go away with systemd's kernel-install)
	install -D -m0644 "${srcdir}/loader.conf" "${pkgdir}/usr/lib/gummiboot/loader/loader.conf"
	install -D -m0644 "${srcdir}/chakra.conf" "${pkgdir}/usr/lib/gummiboot/loader/entries/chakra.conf"

	cd ${srcdir}/${pkgname}

	make DESTDIR="${pkgdir}" install
}
