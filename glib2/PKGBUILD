pkgbase=glib2
pkgname=(glib2 glib2-docs)
pkgver=2.54.2
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=(x86_64)
depends=(pcre libffi ) #libutil-linux
makedepends=(gettext gtk-doc zlib shared-mime-info python3 libelf util-linux)
options=(!emptydirs)
source=("http://download.gnome.org/sources/glib/2.54/glib-${pkgver}.tar.xz"
        0001-docs-Fix-building-with-meson.patch
        0001-meson-Fix-permissions-of-installed-scripts.patch
        libs.diff )
sha256sums=('bb89e5c5aad33169a8c7f28b45671c7899c12f74caf707737f784d7102758e6c'
            '8b289f3e1a5a3b29d310d45610468199acfe6f2b38a0d1be38c9224437a0e40c'
            '12b1a2f4e304e4c03e48ae9564d73ae38619bbb7711a013138939ff8e5cc2327'
            '54c43d5d4d4b5fa591eb639e63050ce5eac30aff6691fa9cf56631782b2aad33')

build() {
    cd glib-$pkgver

    # Bug 1324770: Also explicitly remove PCRE sources since we use --with-pcre=system
    rm glib/pcre/*.[ch]

    PYTHON=/usr/bin/python3 ./configure --prefix=/usr --libdir=/usr/lib \
        --sysconfdir=/etc \
        --with-pcre=system \
        --enable-systemtap \
        --enable-static \
        --enable-installed-tests
    make
}

check() {
  cd glib-$pkgver
  #meson test -t 2
}

package_glib2() {
  depends=('pcre' 'libffi')
  optdepends=('python2: for gdbus-codegen and gtester-report'
              'libelf: gresource inspection tool')
  options=('!docs' '!emptydirs')
  license=('LGPL')

  cd glib-$pkgver
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

  # bash-completion scripts need not be executable
  for _i in "$pkgdir/usr/share/bash-completion/completions/"*; do
      chmod -x "$_i"
  done

  # Our gdb does not ship the required python modules, so remove it
  rm -rf "$pkgdir/usr/share/gdb/"
}

package_glib2-docs() {
  pkgdesc="Documentation for glib2"
  conflicts=('gobject2-docs')
  replaces=('gobject2-docs')
  license=('custom')
  options=('docs' '!emptydirs')
  
  cd glib-$pkgver/docs
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/glib2-docs"
  install -m644 reference/COPYING "${pkgdir}/usr/share/licenses/glib2-docs/"

  rm -rf "${pkgdir}/usr/share/man"
}
