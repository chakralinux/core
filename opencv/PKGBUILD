#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgbase=opencv
pkgname=('opencv' 'opencv-samples')
_realname=OpenCV
pkgver=2.4.6.1
pkgrel=1
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('BSD')
url="http://opencv.willowgarage.com/"
depends=('jasper' 'gstreamer0.10-base' 'intel-tbb' 'libcl' 
         'xine-lib' 'libdc1394' 'openexr' 'v4l-utils' 'ffmpeg' 'qt')
makedepends=('pkgconfig' 'cmake' 'python-numpy' 'eigen' 'python-sphinx' 'mesa')
optdepends=('python-numpy: Python 2.x interface'
            'eigen')
options=('!libtool')
source=("http://downloads.sourceforge.net/opencvlibrary/$pkgname-$pkgver.tar.gz"
        'pkgconfig.patch'
        'fsh.patch'
        'cldetect.patch')
sha1sums=('e015bd67218844b38daf3cea8aab505b592a66c0'
          '709a2fac455a00e0e8956ec8c8f109be88a083ff'
          '8d8c2ebccd8199fee2cf21b8dbbaadcc0a9f8bad'
          '5401c607287c10835fc95f490c294d4706399436')

_cmakeopts=('-D CMAKE_BUILD_TYPE=Release'
            '-D CMAKE_INSTALL_PREFIX=/usr'
            '-D CMAKE_SKIP_RPATH=ON'
            '-D BUILD_TESTS=OFF'
            '-D BUILD_EXAMPLES=ON'
            '-D INSTALL_C_EXAMPLES=ON'
            '-D INSTALL_PYTHON_EXAMPLES=ON'
            '-D WITH_XINE=ON'
            '-D WITH_QT=ON'
	    '-D WITH_OPENGL=ON'
            '-D WITH_GTK=OFF'
            '-D ENABLE_SSE3=OFF'
            '-D ENABLE_FAST_MATH=ON')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix missing opencl module
  # see https://bugs.archlinux.org/task/36076
  # and http://code.opencv.org/issues/3140
  patch -Np1 -i "$srcdir/cldetect.patch"

  # fix pkg-config mess
  # see https://bugs.archlinux.org/task/32430
  # and http://code.opencv.org/issues/1925
  patch -Np1 -i "$srcdir/pkgconfig.patch"

  # fix another upstream mess that they won't fix
  # see http://code.opencv.org/issues/2512
  patch -Np1 -i "$srcdir/fsh.patch"

  # no longer including docs, see https://bugs.archlinux.org/task/34185
  # python2 compatibility for generating (html) docs
  #sed -i 's/sphinx-build/sphinx-build2/' cmake/OpenCVDetectPython.cmake
}

build() {
  cd "$srcdir/$pkgbase-$pkgver"
 
  cmake ${_cmakeopts[@]} .

  make
}

package_opencv() {
  replaces=('opencv-docs')
  conflicts=('opencv-docs')
  provides=('opencv-docs')

  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # install license file
  install -Dm644 "$srcdir/$pkgbase-$pkgver/doc/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/share"

  # separate samples package
  [ -d $pkgname/samples ] && mv $pkgname/samples "$srcdir/opencv-samples"
}

package_opencv-samples() {
  pkgdesc+=" (samples)"
  depends=()
  optdepends=()
  options=()

  cd "$srcdir"

  mkdir -p "$pkgdir/usr/share/opencv"
  cp -r opencv-samples "$pkgdir/usr/share/opencv/samples"
}

