pkgbase=opencv
pkgname=('opencv' 'opencv-samples')
pkgver=3.1.0
pkgrel=1
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('BSD')
url="http://opencv.org"
depends=('gst-plugins-base' 'intel-tbb' 'libcl' 'libgphoto2'
         'xine-lib' 'libdc1394' 'openexr' 'v4l-utils' 'ffmpeg' 'qt5-base' 'glu')
makedepends=('pkgconfig' 'cmake' 'python2-numpy' 'mesa' 'python3-numpy' 'eigen3')
optdepends=('python2-numpy: Python 2.x interface'
            'python3-numpy: Python 3.x interface'
            'eigen3')
source=("https://github.com/Itseez/opencv/archive/${pkgver}.zip")
sha1sums=('8c932b68fe2e1575e88dde759ab1ed1d53d6f41b')
_cmakeopts=('-D CMAKE_BUILD_TYPE=Release'
            '-D CMAKE_INSTALL_PREFIX=/usr'
            '-D CMAKE_SKIP_RPATH=ON'
            '-D BUILD_SHARED_LIBS=ON'
            '-D BUILD_TESTS=OFF'
            '-D BUILD_EXAMPLES=ON'
            '-D INSTALL_C_EXAMPLES=ON'
            '-D INSTALL_PYTHON_EXAMPLES=ON'
            '-D WITH_XINE=ON'
            '-D WITH_OPENGL=ON'
            '-D WITH_OPENCL=ON'
            '-D WITH_QT=5'
            '-D WITH_GTK=OFF'
            '-D WITH_GSTREAMER=ON'
            '-D ENABLE_SSE3=ON'
            '-D ENABLE_SSSE3=ON'
            '-D ENABLE_SSE41=ON'
            '-D ENABLE_SSE42=ON'
            '-D ENABLE_FAST_MATH=ON')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
 
  cmake ${_cmakeopts[@]} .

  make
}

package_opencv() {
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # install license file
  install -Dm644 "$srcdir/$pkgbase-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/share"

  # separate samples package; also be -R friendly
  if [[ -d OpenCV/samples ]]; then
    mv OpenCV/samples "$srcdir/$pkgname-samples"
    mv OpenCV $pkgname # otherwise folder naming is inconsistent
  elif [[ ! -d OpenCV ]]; then
    warning "Directory naming issue; samples package may not be built!"
  fi
}

package_opencv-samples() {
  pkgdesc+=" (samples)"
  depends=("opencv=$pkgver")
  unset optdepends

  mkdir -p "$pkgdir/usr/share/opencv"
  cp -r "$srcdir/opencv-samples" "$pkgdir/usr/share/opencv/samples"
  
  # install license file
  install -Dm644 "$srcdir/opencv-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
