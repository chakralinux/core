#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgbase=opencv
pkgname=('opencv' 'opencv-docs' 'opencv-samples')
_realname=OpenCV
pkgver=2.4.3
pkgrel=2
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('BSD')
url="http://opencv.willowgarage.com/"
depends=('jasper' 'gstreamer0.10-base' 'openexr'
         'xine-lib' 'libdc1394' 'v4l-utils' 'ffmpeg' 'qt')
makedepends=('pkgconfig' 'cmake' 'python-numpy' 'eigen' 'python-sphinx' 'mesa')
optdepends=('python-numpy: Python 2.x interface'
            'eigen')
options=('!libtool')
source=("http://sourceforge.net/projects/opencvlibrary/files/opencv-unix/$pkgver/$_realname-$pkgver.tar.bz2")
md5sums=('c0a5af4ff9d0d540684c0bf00ef35dbe')

_cmakeopts=('-D CMAKE_BUILD_TYPE=Release'
            '-D CMAKE_INSTALL_PREFIX=/usr'
            '-D CMAKE_SKIP_RPATH=ON'
            '-D BUILD_TESTS=OFF'
            '-D ENABLE_SSE=OFF'
            '-D ENABLE_SSE2=OFF'
            '-D ENABLE_SSE3=OFF'
            '-D BUILD_EXAMPLES=ON'
            '-D INSTALL_C_EXAMPLES=ON'
            '-D INSTALL_PYTHON_EXAMPLES=ON'
            '-D WITH_XINE=ON'
            '-D WITH_QT=ON'
	    '-D WITH_OPENGL=ON'
            '-D WITH_UNICAP=OFF'
            '-D WITH_PVAPI=OFF'
            '-D WITH_OPENNI=OFF'
            '-D WITH_TBB=OFF'
            '-D WITH_IPP=OFF'
            '-D WITH_CUDA=OFF'
            '-D WITH_GTK=OFF'
            '-D ENABLE_FAST_MATH=ON')

build() {
  cd "$srcdir/$_realname-$pkgver"
 
  mkdir build && cd build

  cmake ${_cmakeopts[@]} ..
  make
}

package_opencv() {
  install=$pkgname.install

  cd "$srcdir/$_realname-$pkgver/build"

  make DESTDIR="$pkgdir" install

  # install license file
  install -Dm644 "$srcdir/$_realname-$pkgver/doc/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/share"

  # prepare FSH-friendly dirs
  #if [ -d OpenCV ]; then
  #  mv opencv/samples OpenCV/
  #  rm -r opencv
  #  mv OpenCV opencv
  #fi

  # separate docs package; also be -R friendly
  [ -d OpenCV/doc ] && mv OpenCV/doc "$srcdir/opencv-doc"

  # separate samples package
  [ -d OpenCV/samples ] && mv OpenCV/samples "$srcdir/opencv-samples"
}

package_opencv-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  optdepends=()
  options=('docs')

  cd "$srcdir"

  mkdir -p "$pkgdir/usr/share/doc"
  cp -r opencv-doc "$pkgdir/usr/share/doc/opencv"
}

package_opencv-samples() {
  pkgdesc+=" (samples)"
  depends=()
  optdepends=()
  options=()

  cd "$srcdir"

  mkdir -p "$pkgdir/usr/share/opencv"
  cp -r opencv-samples "$pkgdir/usr/share/opencv/samples"
}

