#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=flashplugin
_licensefile='Reader_Player_AIR_WWEULA-Combined-20080204_1313.pdf'
pkgver=10.3.183.5
_pkgver64=10.0.45.2
pkgrel=1
pkgdesc='Adobe Flash Player'
url='http://get.adobe.com/flashplayer'
arch=('i686') # 'x86_64')
depends=('mozilla-common' 'libxt' 'gtk2' 'nss' 'curl' 'hicolor-icon-theme')
optdepends=('libvdpau: GPU acceleration on Nvidia card')
replaces=('flashplugin-beta')
provides=('flashplayer')
license=('custom')
options=(!strip)
backup=(etc/adobe/mms.cfg)
source=('http://fpdownload.macromedia.com/get/flashplayer/current/install_flash_player_10_linux.tar.gz'
        "http://www.adobe.com/products/eulas/pdfs/${_licensefile}"
         mms.cfg)
md5sums=('3974e5a035f719915d35ac6a2f938fd8'
         '1636037610ee2aa35c5fb736a697b7e0'
         'f34aae6279b40e0bd2abfb0d9963d7b8')
#[ "${CARCH}" = "x86_64" ] && source[0]="http://download.macromedia.com/pub/labs/flashplayer10/libflashplayer-${_pkgver64}.linux-${CARCH}.so.tar.gz"
#[ "${CARCH}" = "x86_64" ] && md5sums[0]='4a4561e456612a6751653b58342d53df'

package () {
    cd ${srcdir}
    install -Dm755 libflashplayer.so "${pkgdir}/usr/lib/mozilla/plugins/libflashplayer.so"
    install -Dm755 usr/bin/flash-player-properties "${pkgdir}/usr/bin/flash-player-properties"
    for i in 16x16 22x22 24x24 32x32 48x48; do
        install -Dm644 usr/share/icons/hicolor/${i}/apps/flash-player-properties.png \
            "${pkgdir}/usr/share/icons/hicolor/${i}/apps/flash-player-properties.png"
    done
    install -Dm644 usr/share/applications/flash-player-properties.desktop "${pkgdir}/usr/share/applications/flash-player-properties.desktop"
    install -Dm644 "${_licensefile}" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.pdf"
    install -Dm644 ${srcdir}/mms.cfg "${pkgdir}/etc/adobe/mms.cfg"
}
