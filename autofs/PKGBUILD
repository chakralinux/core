#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=autofs
pkgver=5.1.1
pkgrel=1
pkgdesc='A kernel-based automounter for Linux.'
arch=('x86_64')
url='http://freshmeat.net/projects/autofs'
license=('GPL2')
depends=('libldap' 'libxml2' 'krb5')
backup=('etc/autofs/auto.master'
        'etc/autofs/auto.misc')
options=(!makeflags)
install='autofs.install'
source=("http://www.kernel.org/pub/linux/daemons/${pkgname}/v5/${pkgname}-${pkgver}.tar.xz"
        'autofs.conf.d'
        'auto.master'
        'auto.misc')
md5sums=('889c9d2f0e76f08ae0ff0d206f119098'
         '47f597c870410055e0fdb66103daf928'
         'a6cefb591e77b31b79dbb7243646c96b'
         'd8a15ec9186c5c0b36e5cea1e2739e8a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i "s:SUBDIRS = lib daemon modules man samples:SUBDIRS = lib daemon modules man:" \
    Makefile.rules

  ./configure --prefix=/usr --sysconfdir=/etc/autofs --with-mapdir=/etc/autofs --without-hesiod \
    --enable-ignore-busy --with-systemd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make INSTALLROOT="${pkgdir}" install

  install -Dm0644 "${srcdir}/auto.master" "${pkgdir}/etc/autofs/auto.master"
  install -Dm0644 "${srcdir}/auto.misc" "${pkgdir}/etc/autofs/auto.misc"
  install -Dm0644 "${srcdir}/autofs.conf.d" "${pkgdir}/etc/conf.d/autofs"
  install -Dm0644 "samples/autofs.service" "${pkgdir}/usr/lib/systemd/system/autofs.service"
}

