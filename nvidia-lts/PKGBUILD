_extramodules=extramodules-3.14-CHAKRA-LTS
_kernelver=3.14.66

pkgname=('nvidia-lts')
pkgdesc=('NVIDIA drivers for linux-lts')
pkgver=364.19
pkgrel=1
arch=('x86_64')
url="http://www.nvidia.com/"
depends=("linux-lts=${_kernelver}" "nvidia-libgl" "nvidia-utils=${pkgver}")
makedepends=("linux-lts-headers=${_kernelver}")
install=nvidia.install
license=('custom')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"

source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run")
#source=("NVIDIA-Linux-x86_64-${pkgver}.run::https://developer.nvidia.com/linux64bit")
md5sums=('ad7a0b1855b3913390fb75b4cc3a26dc')

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    # patches here
}

build() {
    _kver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kver}/build" module
}

package() {
    for ko in nvidia nvidia-uvm nvidia-modeset nvidia-drm; do
    install -Dm644 "${srcdir}/${_pkg}/kernel/${ko}.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/${ko}.ko"
    done
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/"*.ko

    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
}
