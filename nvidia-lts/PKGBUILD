#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# use ../makepkg -d
_kernver="2.6.35-CHAKRA-LTS"

pkgname=nvidia-lts
pkgver=260.19.36
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-lts>=2.6.35' 'kernel26-lts<2.6.36' "nvidia-utils=${pkgver}")
makedepends=('kernel26-lts-headers>=2.6.35' 'kernel26-lts-headers<2.6.36')
conflicts=('nvidia-96xx-lts' 'nvidia-173xx-lts')
license=('custom')
install=nvidia.install

if [ "$CARCH" = "i686" ]; then
        ARCH='x86'
        _pkg="NVIDIA-Linux-${ARCH}-${pkgver}"
        source=("http://us.download.nvidia.com/XFree86/Linux-${ARCH}/${pkgver}/${_pkg}.run")
        md5sums=('2826484d87827351774449382e45c4a9')
elif [ "$CARCH" = "x86_64" ]; then
        ARCH='x86_64'
        _pkg="NVIDIA-Linux-${ARCH}-${pkgver}-no-compat32"
        source=("http://us.download.nvidia.com/XFree86/Linux-${ARCH}/${pkgver}/${_pkg}.run")
        md5sums=('6ebc5db9066e920b0b0e48377ce5eeb5')
fi

build() {
        cd $srcdir
        sh ${_pkg}.run --extract-only
        cd ${_pkg}/kernel
        make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
        install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
                $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
}


