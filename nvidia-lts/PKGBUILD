# maintainer abveritas@chakra-project.org

# Find the kernel name inside the chroot
_extramodules=extramodules-3.2-lts
_kver="$(cat /usr/lib/modules/${_extramodules}/version)"
_kernelver=3.2.63

pkgname=nvidia-lts
pkgver=343.22
pkgrel=2
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=("linux-lts=${_kernelver}" "nvidia-utils=${pkgver}")
makedepends=("linux-lts-headers=${_kernelver}")
conflicts=('nvidia-96xx-lts' 'nvidia-173xx-lts')
license=('custom')
install=nvidia.install
_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
md5sums=('3d98eaa01a94e86abb04723ae2a702b1')

prepare() {
    sh ${_pkg}.run --extract-only
}
build() {
  cd "${_pkg}/kernel"
  make SYSSRC=/usr/lib/modules/"${_kver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
    "${pkgdir}/usr/lib/modules/${_extramodules}/kernel/drivers/video/nvidia.ko"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/kernel/drivers/video/"*.ko
  install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-lts.conf"
  echo "blacklist nvidiafb" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-lts.conf"
  install -D -m644 "${srcdir}/${_pkg}/LICENSE" "${pkgdir}/usr/share/licenses/nvidia-lts/LICENSE"
}
