# Platform packages for Chakra

pkgname=qwt
pkgver=6.1.3
pkgrel=1
pkgdesc="Qt Widgets for Technical Applications"
arch=('x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt5-base' 'qt5-svg')
optdepends=('qt5-tools: For Designer plugin')
makedepends=('qt5-tools')
conflicts=(${pkgname}6)
replaces=(${pkgname}6)
license=("custom:$pkgname")
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('19d1f5fa5e22054d22ee3accc37c54ba')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -e '/^\s*QWT_INSTALL_PREFIX/ s|=.*|= /usr|' \
      -e '/^QWT_INSTALL_DOCS/ s|/doc|/share/doc/qwt|' \
      -e '/^QWT_INSTALL_HEADERS/ s|include|&/qwt|' \
      -e '/^QWT_INSTALL_PLUGINS/ s|plugins/designer|lib/qt5/&|' \
      -e '/^QWT_INSTALL_FEATURES/ s|features|lib/qt5/mkspecs/&|' \
      -i qwtconfig.pri
}


build() {
  cd ${pkgname}-${pkgver}

  # build qwt:
  qmake-qt5 qwt.pro
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make INSTALL_ROOT=${pkgdir} QTDIR=/usr install
  
  # install licence
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
