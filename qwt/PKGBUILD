#Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/qwt5

pkgname=qwt
pkgver=5.2.3
pkgrel=3
pkgdesc="Qt Widgets for Technical Applications"
arch=('x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt')
license=('custom')
source=("http://downloads.sourceforge.net/sourceforge/qwt/$pkgname-$pkgver.tar.bz2" \
        "qwtconfig-chakra.pri")
md5sums=('7d37a11d02bc7d095d0ca6427ec97b8d'
         '2cb9135ec875d6de0214fcb032f184a3')

prepare() {
  # copy our config file to the right place
  install -Dm644 "qwtconfig-chakra.pri" "qwt-${pkgver}/qwtconfig.pri"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  qmake qwt.pro
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make INSTALL_ROOT=${pkgdir} QTDIR=/usr install
  
  # install licence
  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # remove docs
  rm -r "${pkgdir}/usr/share/html"
  rm -r "${pkgdir}/usr/share/man/"
}
