# maintainer Manuel Tortosa

# Note: you have to rebuild those pkgs
# Core: repo-clean
# Platform: akonadi (exempi uses boost just as makedep)
# Desktop: kdeedu (kdenetwork, kdepimlibs, kdepim, kdesdk and koffice using boost just as makedep)
 
pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.54.0
_boostver=${pkgver//./_}
pkgrel=1
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('x86_64')
url="http://www.boost.org/"
license=('custom')
makedepends=('icu' 'python2' 'python3' 'bzip2' 'zlib' 'openmpi') 
source=("http://downloads.sourceforge.net/${pkgbase}/${pkgbase}_${_boostver}.tar.gz"
        # Arch patches
        boost-1.53.0-python3.patch
        boost-1.54.0-Fix-macro-for-int128-detection.patch
        fix-new-glibc.patch
        # upstream patches
        coroutine.patch
        datetime.patch
        log.patch)
md5sums=('efbfbff5a85a9330951f243d0a46e4b9'
         'dc7ccb81b1bde9c90d29d2819ec387f7'
         '2ae6d41968d1ab119641cca8c6e6e6c3'
         '2b1cf6b9fb6044bf9436f219d809bbbb'
         '2db61ac2c62ea8e9d6c15786f133b9eb'
         'e37eb7782e2273f5b94c657ebd6721dd'
         'c4afb60ae7f40045ef3a9862311ca91d')
license=('custom')

_stagedir="${srcdir}/stagedir"

prepare() {
   cd "${srcdir}/${pkgbase}_${_boostver}"

   patch -Np2 -i ${srcdir}/boost-1.54.0-Fix-macro-for-int128-detection.patch
   patch -Np2 -i ${srcdir}/fix-new-glibc.patch

   patch -Np1 < ${srcdir}/coroutine.patch
   patch -Np1 < ${srcdir}/datetime.patch
   patch -Np1 < ${srcdir}/log.patch

   # Fix build errors with python 3
   sed -i "/PYTHON_ROOT/s/print sys.prefix/print(sys.prefix)/g" bootstrap.sh
   patch -Np1 -i ../boost-1.53.0-python3.patch

   # Shut up strict aliasing warnings
   echo "using gcc : : : <compileflags>-fno-strict-aliasing ;" >> ./tools/build/v2/user-config.jam
   # Add an extra python version. This does not replace anything and python 2.x need to be the default.
   echo "using python : 3.3 : /usr/bin/python3 : /usr/include/python3.3m : /usr/lib ;" >> ./tools/build/v2/user-config.jam
   # Support for OpenMPI
   echo "using mpi ;" >> ./tools/build/v2/user-config.jam

   ./bootstrap.sh --with-toolset=gcc --with-icu 

}

build() {
   cd "${srcdir}/${pkgbase}_${_boostver}"
   local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"


   _bindir="bin.linuxx86_64"

   install -d -m 755 "${_stagedir}"/bin
   install "${srcdir}"/${pkgbase}_${_boostver}/tools/build/v2/engine/${_bindir}/bjam "${_stagedir}"/bin/bjam

   pushd tools
   for _tool in bcp inspect quickbook compiler_status process_jam_log wave; do
      "${_stagedir}"/bin/bjam --toolset=gcc $_tool
   done
   "${_stagedir}"/bin/bjam --toolset=gcc cflags="-std=gnu++11" library_status
   popd
   cp -a dist/bin/* "${_stagedir}"/bin

   #boostbook is needed by quickbook
   install -d -m 755 "${_stagedir}"/share/boostbook
   cp -a tools/boostbook/{xsl,dtd} "${_stagedir}"/share/boostbook/

   # default "minimal" install: "release link=shared,static
   # runtime-link=shared threading=single,multi"
   # --layout=tagged will add the "-mt" suffix for multithreaded libraries
   # and installs includes in /usr/include/boost.
   # --layout=system no longer adds the -mt suffix for multi-threaded libs.
   # install to ${_stagedir} in preparation for split packaging
   "${_stagedir}"/bin/bjam \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared \
      toolset=gcc \
      python=2.7 \
      --layout=system \
      --prefix="${_stagedir}" \
      ${JOBS} \
      install

   find ${_stagedir} -name \*.a -exec rm -f {} \;
}

package_boost() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
   depends=("boost-libs=${pkgver}")
   optdepends=('python2: for python2 bindings'
               'python3: for python3 bindings'
               'boost-build: to use boost jam for building your project.')

   install -d -m 755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/{bin,include,share} "${pkgdir}"/usr

   install -D -m 644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt
}

package_boost-libs() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
   depends=('bzip2' 'zlib' 'icu')
   optdepends=('openmpi: for mpi support')

   install -d -m 755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/lib "${pkgdir}"/usr

   install -D -m 644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}

