# maintainer Manuel Tortosa

# Note: you have to rebuild those pkgs
# Core: repo-clean
# Platform: akonadi (exempi uses boost just as makedep)
# Desktop: kdeedu (kdenetwork, kdepimlibs, kdepim, kdesdk and koffice using boost just as makedep)
 
pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.60.0
_boostver=${pkgver//./_}
pkgrel=2
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('x86_64')
url="http://www.boost.org/"
license=('custom')
makedepends=('icu' 'python2' 'python3' 'bzip2' 'zlib') 
source=("http://downloads.sourceforge.net/${pkgbase}/${pkgbase}_${_boostver}.tar.gz")
sha1sums=('ac74db1324e6507a309c5b139aea41af624c8110')
license=('custom')

prepare() {
   cd "${srcdir}/${pkgbase}_${_boostver}"
}

build() {
   cd "${srcdir}/${pkgbase}_${_boostver}"
   _stagedir="${srcdir}/stagedir"

   local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

   ./bootstrap.sh --with-toolset=gcc --with-icu --with-python=/usr/bin/python2


   _bindir="bin.linuxx86_64"

   install -Dm755 tools/build/src/engine/${_bindir}/b2 "${_stagedir}"/bin/b2

   # Add an extra python version. This does not replace anything and python 2.x
   # need to be the default.
   echo "using python : 3.5 : /usr/bin/python3 : /usr/include/python3.5m : /usr/lib ;" \
      >> project-config.jam

   # boostbook is needed by quickbook
   install -dm755 "${_stagedir}"/share/boostbook
   cp -a tools/boostbook/{xsl,dtd} "${_stagedir}"/share/boostbook/

   # default "minimal" install: "release link=shared,static
   # runtime-link=shared threading=single,multi"
   # --layout=tagged will add the "-mt" suffix for multithreaded libraries
   # and installs includes in /usr/include/boost.
   # --layout=system no longer adds the -mt suffix for multi-threaded libs.
   # install to ${_stagedir} in preparation for split packaging
   "${_stagedir}"/bin/b2 \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared,static \
      toolset=gcc \
      python=2.7 \
      cflags="${CPPFLAGS} ${CFLAGS} -O3" linkflags="${LDFLAGS}" \
      --layout=system \
      --prefix="${_stagedir}" \
      ${JOBS} \
      install
}

package_boost() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
   depends=("boost-libs=${pkgver}")
   optdepends=('python2: for python2 bindings'
               'python3: for python3 bindings'
               'boost-build: to use boost jam for building your project.')
   options=('staticlibs')

   _stagedir="${srcdir}/stagedir"
   install -dm755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/{bin,include,share} "${pkgdir}"/usr

   install -d "${pkgdir}"/usr/lib
   find "${_stagedir}"/lib -name \*.a -exec mv {} "${pkgdir}"/usr/lib \;

   install -Dm644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt

   ln -s /usr/bin/b2 ${pkgdir}/usr/bin/bjam || msg "faulty"
}

package_boost-libs() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
   depends=('bzip2' 'zlib' 'icu')

   _stagedir="${srcdir}/stagedir"
   install -d -m 755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/lib "${pkgdir}"/usr
   rm -f "${pkgdir}"/usr/lib/*.a

   install -D -m 644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}
