#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# Contributor: Artyom Smirnov <smirnoffjr@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

# Note: you have to rebuild those pkgs
# Core: repo-clean
# Platform: akonadi (exempi uses boost just as makedep)
# Desktop: kdeedu (kdenetwork, kdepimlibs, kdepim, kdesdk and koffice using boost just as makedep)
 
pkgname=boost
pkgver=1.46.1
_boostver=1_46_1
pkgrel=1
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('i686' 'x86_64')
url="http://www.boost.org/"
license=('custom')
depends=('bzip2' 'zlib')
makedepends=('icu' 'python2' 'bzip2' 'zlib') # Add Python (3) whenever it is added to our repos.
optdepends=('python: for python bindings')
options=('!ccache')
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/${pkgname}_$_boostver.tar.gz)
md5sums=('341e5d993b19d099bf1a548495ea91ec')

build() {

  # Set Python path for Bjam.
  cd $srcdir/${pkgname}_$_boostver/tools
  echo "using python : 2.7 : /usr/bin/python2 ;" >> build/v2/user-config.jam

  # Build Bjam.
  cd $srcdir/${pkgname}_$_boostver/tools/build/v2/engine/src
  ./build.sh cc

  _bindir="bin.linuxx86"
  [ "${CARCH}" = "x86_64" ] && _bindir="bin.linuxx86_64"

  install -d $pkgdir/usr/bin
  install $_bindir/bjam $pkgdir/usr/bin/bjam

  # Build tools.
  cd $srcdir/${pkgbase}_$_boostver/tools/
  $pkgdir/usr/bin/bjam --toolset=gcc

  # Copy the tools.
  cd $srcdir/${pkgbase}_$_boostver/dist/bin
  for i in *
  do
      install -m755 "${i}" "$pkgdir/usr/bin/${i}"
  done

  # Boostbook needed by quickbook.
  cd $srcdir/${pkgbase}_$_boostver/dist/
  cp -r share $pkgdir

  # Build libs.
  cd $srcdir/${pkgname}_$_boostver
  
  # default "minimal" install: "release link=shared,static
  # runtime-link=shared threading=single,multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  # install to ${_stagedir} in preparation for split packaging

  $pkgdir/usr/bin/bjam \
      release debug-symbols=off threading=multi \
      runtime-link=shared link=shared,static \
      cflags=-fno-strict-aliasing \
      toolset=gcc \
      --prefix=$pkgdir \
      -sTOOLS=gcc \
      --layout=system \
      $MAKEFLAGS \
      install

  # License.
  install -d $pkgdir/usr/share/licenses/$pkgname
  install -m644 $srcdir/${pkgname}_$_boostver/LICENSE_1_0.txt \
                $pkgdir/usr/share/licenses/$pkgname/
}
