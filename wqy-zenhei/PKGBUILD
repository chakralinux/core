#$Id: PKGBUILD 16597 2010-05-04 09:34:41Z spupykin $
# Maintainer: JJDaNiMoTh <jjdanimoth.aur@gmail.com>
# Contributor: zlowly <zlowly [at] archlinux.org>

pkgname=wqy-zenhei
pkgver=0.9.45
pkgrel=1
pkgdesc="A Hei Ti Style (sans-serif) Chinese Outline Font."
arch=('any')
license=('GPL2' 'custom:"font embedding exception"')
install="wqy-zenhei.install"
url="http://wenq.org"
depends=('fontconfig' 'xorg-font-utils')
source=("http://downloads.sourceforge.net/project/wqy/${pkgname}/${pkgver}%20%28Fighting-state%20RC1%29/${pkgname}-${pkgver}.tar.gz")
md5sums=('4c6c3f4e902dd5ee0a121e8c41d040bd')

package() {
  cd "${srcdir}/${pkgname}"

  grep README -e "GPL v2 license with font embedding exception" -A 12 >| FONTEXCEPTION

  mkdir -p "${pkgdir}/usr/share/fonts/wenquanyi/wqy-zenhei"
  mkdir -p "${pkgdir}/etc/fonts/conf.avail"
  mkdir -p "${pkgdir}/etc/fonts/conf.d"

  install -m644 -D FONTEXCEPTION "${pkgdir}/usr/share/licenses/${pkgname}/FONTEXCEPTION"
  install -m644 *.ttc "${pkgdir}/usr/share/fonts/wenquanyi/wqy-zenhei"
  install -m644 *.conf "${pkgdir}/etc/fonts/conf.avail"
  install -m755 -D zenheiset "${pkgdir}/usr/bin/zenheiset"

  # https://bugs.archlinux.org/task/24177
  mv "${pkgdir}/etc/fonts/conf.avail/44-wqy-zenhei.conf" "${pkgdir}/etc/fonts/conf.avail/65-wqy-zenhei.conf"

  cd "${pkgdir}/etc/fonts/conf.d"
  ln -s ../conf.avail/65-wqy-zenhei.conf .

  # Commented out because it removed antialiasing from the rendered font, see FS#235.
  # ln -s ../conf.avail/66-wqy-zenhei-sharp.conf .
}

# vim:set ts=2 sw=2 et:
