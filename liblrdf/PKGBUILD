pkgname=liblrdf
pkgver=0.6.1
pkgrel=1
pkgdesc="A library for the manipulation of RDF file in LADSPA plugins"
arch=('x86_64')
url="https://github.com/swh/LRDF"
depends=('raptor' 'ladspa')
license=('GPL2')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/swh/LRDF/archive/v${pkgver}.tar.gz")
sha512sums=('7732813eec704aef984d056de254e4fa049fdd0a7444b6a88f75f012afe9c587cbd1295f027c77361fa42bc097cdce9d9cabdba6b86e99a3c14805d84258df1c')

prepare() {
  mv -v "LRDF-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  sed -e 's,raptor.h,raptor2/raptor.h,' -i lrdf.h
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,NEWS,README}
}
