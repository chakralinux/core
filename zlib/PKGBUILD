# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/zlib

pkgname=zlib
pkgver=1.2.8
pkgrel=2
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('x86_64')
license=('custom')
url="http://www.zlib.net/"
depends=('glibc')
options=('staticlibs')  # needed by binutils testsuite
source=("http://zlib.net/current/zlib-${pkgver}.tar.gz")
md5sums=('44d667c142d7cda120332623eab69f40')

build() {
	cd ${srcdir}/zlib-$pkgver
	./configure --prefix=/usr
	make

	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

check() {
	cd ${srcdir}/zlib-$pkgver
	make test
}

package() {
	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir}
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}
