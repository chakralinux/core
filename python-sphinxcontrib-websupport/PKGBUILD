pkgbase=python-sphinxcontrib-websupport
pkgname=(python3-sphinxcontrib-websupport python2-sphinxcontrib-websupport)
pkgver=1.0.1
pkgrel=1
pkgdesc="Sphinx API for Web Apps"
arch=('any')
url="http://www.sphinx-doc.org/"
license=('BSD')
makedepends=('python3' 'python3-setuptools' 'python2' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sphinx-doc/sphinxcontrib-websupport/archive/$pkgver.tar.gz")
sha512sums=('06ee11f2b2212333a6d01201824879e5b5931e85db5f20badd62bc28ddba88ce7c0b3f0b7ce4f4e3d43cb8f66173b31d10e1264f0acc48bb2db7385f50c68313')

prepare() {
  cp -a sphinxcontrib-websupport-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver
  python3 setup.py build

  cd "$srcdir"/sphinxcontrib-websupport-$pkgver-py2
  python2 setup.py build
}

package_python3-sphinxcontrib-websupport() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sphinxcontrib-websupport() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

