
pkgname=ndctl
pkgver=61.2
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL')
depends=('json-c' 'kmod' 'libsystemd' 'util-linux')
makedepends=('asciidoc' 'xmlto' 'bash-completion')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pmem/ndctl/archive/v$pkgver.tar.gz")
sha512sums=('da231a61654b0f863c8cac8d70b166a5a60df4be86ed74e1e8fb3a23b4692483200075f0606244720f1b919891c00eb3ae9b49c48662213bc08601f0fe11008b')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
