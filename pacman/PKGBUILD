
pkgname=pacman
pkgver=4.2.1
pkgrel=9
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash>=4.3.025-1' 'glibc>=2.20-2' 'libarchive=3.1.2' 'curl>=7.28.1'
         'gpgme' 'pacman-mirrorlist' 'nettle>=3.1')
checkdepends=('python2' 'fakechroot')
makedepends=('asciidoc')
optdepends=('fakeroot: for makepkg usage as normal user')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
options=(!libtool)
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        pacman.conf
        makepkg.conf)
sha1sums=('61a87226ff3676c0c1e6c8bfadafb6d5750d1e06'
          '41f4d1ca64bbeee0ec758c1b99615cb3a1619718'
          'bb2fe75a0e94444c5397d616988a35a51e9380a2')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-ldconfig=/usr/bin/ldconfig
  make
  make -C contrib
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C contrib install

  # install Chakra specific stuff
  install -dm755 "$pkgdir"/etc
  install -m644 "$srcdir"/pacman.conf "$pkgdir"/etc/pacman.conf

  mycarch="x86_64"
  mychost="x86_64-unknown-linux-gnu"
  myflags="-march=x86-64"

  install -m644 "$srcdir"/makepkg.conf "$pkgdir"/etc/
  # set things correctly in the default conf file
  sed -i "$pkgdir"/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"

  # put bash_completion in the right location
  install -dm755 ${pkgdir}/usr/share/bash-completion/completions
  mv ${pkgdir}/etc/bash_completion.d/pacman \
    ${pkgdir}/usr/share/bash-completion/completions
  rmdir ${pkgdir}/etc/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir"/usr/share/bash-completion/completions/$f
  done

  install -Dm644 contrib/PKGBUILD.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/PKGBUILD.vim
}
