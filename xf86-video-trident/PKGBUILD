# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributor: abveritas@chakra-project.org

pkgname=xf86-video-trident
pkgver=1.3.6
pkgrel=1
pkgdesc="X.org Trident video driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server' 'xf86dgaproto' 'xf86driproto' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.12.3')
groups=('xorg-video-drivers')
options=(!libtool)
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2" "git-fixes.patch")
sha1sums=('9af5d275e6f4247cde19765178c3a797b4a7351d'
          'd187c12e24a736a782bc4272710a93da14ebe30f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 < ${srcdir}/git-fixes.patch 
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" 
}
