# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kpty
pkgver=5.52.0
pkgrel=1
pkgdesc='Pty abstraction'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kpty'
license=('LGPL')
depends=('kcoreaddons' 'ki18n' 'libutempter')
makedepends=("extra-cmake-modules>=5.52.0" 'python3')
checkdepends=("cmake" "xorg-server-xvfb")
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('02cd57c6fb41946d76e79153387db24de664f3f55db6664b37d65286ef91c78a'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

check() {
  cd build
  make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
