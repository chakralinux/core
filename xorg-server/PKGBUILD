#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=xorg-server
pkgver=1.10.4
pkgrel=1
pkgdesc="X.Org X servers"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
depends=('xproto' 'randrproto' 'renderproto' 'xextproto' 'inputproto' 'kbproto' 'fontsproto' 'videoproto' 'dri2proto' 'xineramaproto' 'xorg-util-macros' \
         'pixman' 'libpciaccess' 'libxfont' 'libxi' 'libgl' 'libgcrypt' 'libxv' 'libxau' 'libxaw' 'libxrender' 'libdmx' 'libdrm' 'libxfixes' 'pixman' \
         'libxext' 'libxdmcp' 'libxau' 'udev' 'xf86-input-evdev' 'xkeyboard-config' 'xorg-xkb-utils' 'xorg-fonts-misc')
makedepends=('pixman' 'libx11' 'mesa' 'libgl' 'xf86driproto' 'xcmiscproto' 'xtrans' 'bigreqsproto' 'randrproto' 'inputproto' 'fontsproto' 'videoproto' \
             'compositeproto' 'recordproto' 'scrnsaverproto' 'resourceproto' 'xineramaproto' 'libxkbfile' 'libxfont' 'renderproto' 'libpciaccess' \ 
             'libxv' 'xf86dgaproto' 'libxmu' 'libxrender' 'libxi' 'dmxproto' 'libxaw' 'libdmx' 'libxtst' 'libxres' 'links')
options=('!libtool')
provides=('x-server')
groups=('xorg')
backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
source=(${url}/releases/individual/xserver/${pkgname}-${pkgver}.tar.bz2
        bg-none-revert.patch
        xserver-1.10-pointer-barriers.patch
        autoconfig-nvidia.patch
        xvfb-run
        xvfb-run.1
        10-quirks.conf
        10-evdev.conf)
sha1sums=('857d6377025c77851a3cc5f8ec2ce84164a2fdc6'
          '629c6d8d52126eab81ee1b72a9e4209535f8cb81'
          '1b95e91384a57d966428c7db98ed06f4cc562f91'
          'f9328fd7bc931bb02c8909ecfcef35403de33782'
          'c94f742d3f9cabf958ae58e4015d9dd185aabedc'
          '6838fc00ef4618c924a77e0fb03c05346080908a'
          '993798f3d22ad672d769dae5f48d1fa068d5578f'
          '56224adfce729782d3a0351abd07928911aa17c5')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  # Add pointer barrier support, patch from Fedora
  patch -Np1 -i "${srcdir}/xserver-1.10-pointer-barriers.patch"

  # Patches from ~ajax/xserver xserver-next branch
  patch -Np1 -i "${srcdir}/bg-none-revert.patch"

  # Use nouveau/nv/nvidia drivers for nvidia devices
  patch -Np1 -i "${srcdir}/autoconfig-nvidia.patch"

  autoreconf
  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make

  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package() {
  # xorg-server-xephyr
  cd "${srcdir}/${pkgname}-${pkgver}/hw/kdrive"
  make DESTDIR="${pkgdir}" install

  # xorg-server-xdmx
  cd "${srcdir}/${pkgname}-${pkgver}/hw/dmx"
  make DESTDIR="${pkgdir}" install

  # xorg-server-xvfb
  cd "${srcdir}/${pkgname}-${pkgver}/hw/vfb"
  make DESTDIR="${pkgdir}" install
  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  # xorg-server-xnest
  cd "${srcdir}/${pkgname}-${pkgver}/hw/xnest"
  make DESTDIR="${pkgdir}" install

  # xorg-server
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/var/log"

  install -m755 -d "${pkgdir}/var/lib/xkb"

  # Install sane config files in /etc. I don't see the need for config files in /usr/share here, users will edit them anyways.
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m644 "${srcdir}/10-evdev.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  rm -rf "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
