#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=xorg-server
pkgver=1.14.4
pkgrel=1
pkgdesc="X.Org X servers"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
depends=('xproto' 'randrproto' 'renderproto' 'xextproto' 'inputproto' 'kbproto' 'fontsproto'
         'videoproto' 'dri2proto' 'xineramaproto' 'xorg-util-macros' 'pixman' 'libpciaccess'
         'libxfont' 'libxi' 'mesa' 'libgcrypt' 'libxv' 'libxau' 'libxaw' 'libxrender' 'libdmx'
         'libdrm' 'libxfixes' 'libxext' 'libxdmcp' 'libxau' 'systemd' 'xf86-input-evdev'
         'xkeyboard-config' 'xorg-xkb-utils' 'xorg-fonts-misc')
makedepends=('libx11' 'xf86driproto' 'xcmiscproto' 'xtrans' 'bigreqsproto'
             'compositeproto' 'recordproto' 'scrnsaverproto' 'resourceproto' 'libxkbfile'
             'xf86dgaproto' 'libxmu' 'dmxproto' 'libxtst' 'libxres' 'links')
backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
options=('!libtool')
provides=('x-server')
groups=('xorg')
source=(${url}/releases/individual/xserver/${pkgname}-${pkgver}.tar.bz2
        autoconfig-nvidia.patch
        autoconfig-sis.patch
        xserver-1.6.0-less-acpi-brokenness.patch
        xephyr-glx.patch
        xvfb-run
        xvfb-run.1
        10-quirks.conf
        fb-rename-wfbDestroyGlyphCache.patch)

sha1sums=('5ee112b52f0cec043aa68bd909457b2ea2a11380'
          '9b85655d11f5f39c28757b1fc9dede2980d5275b'
          '175de5630b43dbc97778adfba5563b7fdd77f11f'
          '2f7778b5a20a9e8160195469c24be2213bed0cad'
          '8b91a8137850d6a26f7fbdc5f7b10a6af784846e'
          'c94f742d3f9cabf958ae58e4015d9dd185aabedc'
          '6838fc00ef4618c924a77e0fb03c05346080908a'
          '993798f3d22ad672d769dae5f48d1fa068d5578f'
          '07a1597ccc8b3f7194d2eeb1efd81bb32ab74107')

prepare() {
 cd "${pkgbase}-${pkgver}"
  # Use nouveau/nv/nvidia drivers for nvidia devices
  patch -Np1 -i ../autoconfig-nvidia.patch

  # Use unofficial imedia SiS driver for supported SiS devices
  patch -Np0 -i ../autoconfig-sis.patch

  # https://bugs.freedesktop.org/show_bug.cgi?id=59825 - fixes crash with vboxvideo
#  patch -Np1 -i ../bug59825.patch

  # From Fedora. Do not build acpid code, it is buggy and we do not need it
  patch --ignore-whitespace -Np1 -i ../xserver-1.6.0-less-acpi-brokenness.patch

  # Add GLX support for Xephyr - https://bugs.freedesktop.org/show_bug.cgi?id=62346
  patch -Np0 -i ../xephyr-glx.patch

  # http://cgit.freedesktop.org/xorg/xserver/commit/fb/wfbrename.h?id=5047810a4c20fab444b8c6eb146c55dcdb0d4219
  patch -Np1 -i ../fb-rename-wfbDestroyGlyphCache.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"


  autoreconf -fiv
  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dri2 \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"  
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/lib/xorg"
  install -m644 dix/protocol.txt "${pkgdir}/usr/lib/xorg/"

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/var/lib/xkb"

  # Install sane config files in /etc. 
  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  rmdir "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  rm -rf "${pkgdir}/var/log"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
