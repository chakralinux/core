#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=xorg-server
pkgver=1.9.3
pkgrel=1
pkgdesc="X.Org X servers"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
depends=('libgl' 'libxres' 'dri2proto' 'fontsproto' 'inputproto' 'kbproto' 'libdrm' 'libgcrypt' 'libpciaccess' 'libdmx' 'libxau' 'libxaw' 'libxdmcp' 'libxfixes' 'libxfont' 'libxi' 'libxrender' 'libxv' 'pixman' 'randrproto' 'renderproto' 'udev' 'videoproto' 'xextproto' 'xf86-input-evdev' 'xineramaproto' 'xkeyboard-config' 'xorg-fonts-misc' 'xorg-xkb-utils' 'xorg-util-macros' 'xproto')
makedepends=('bigreqsproto' 'compositeproto' 'dmxproto' 'libx11' 'libxkbfile' 'libxmu' 'libxtst' 'mesa' 'recordproto' 'resourceproto' 'scrnsaverproto' 'xcmiscproto' 'xf86dgaproto' 'xf86driproto' 'xtrans')
options=('!libtool')
provides=('x-server')
groups=('xorg')
source=(${url}/releases/individual/xserver/${pkgname}-${pkgver}.tar.bz2
        xorg-redhat-die-ugly-pattern-die-die-die.patch
        xvfb-run
        xvfb-run.1
        10-evdev.conf
        10-quirks.conf)
md5sums=('5bef6839a76d029204ab31aa2fcb5201'
         '1a336eb22e27cbf443ec5a2ecddfa93c'
         '52fd3effd80d7bc6c1660d4ecf23d31c'
         '376c70308715cd2643f7bff936d9934b'
         'e930e153d69968a795fe4e2ef5d9a5dc'
         'd4f7dfc6be4ef4e2c6dd7632a9d88abe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Get rid of the ugly pattern
  patch -Np3 -i "${srcdir}/xorg-redhat-die-ugly-pattern-die-die-die.patch"

  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-dri-driver-path=/usr/lib/xorg/modules/dri \
      --with-fontrootdir=/usr/share/fonts
  make

  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  rm -rf "${pkgdir}/var/log"

  install -m755 -d "${pkgdir}/var/lib/xkb"

  # Install sane config files in /etc. I don't see the need for config files in /usr/share here, users will edit them anyways.
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-evdev.conf" "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  rm -rf "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
