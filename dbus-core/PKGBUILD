#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer: abveritas@chakra-project.org

pkgname=dbus-core
pkgver=1.6.12
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('expat' 'coreutils' 'filesystem' 'shadow') # shadow for install scriptlet FS#29341
makedepends=('libx11' 'systemd')
options=(!libtool)
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz
        "systemd-user-session.patch")
md5sums=('a70edc50524f258eaf5c9a9994ed8748'
         'd8a1bd529b3ddca671ee1a695a143db9')


build() {
  cd dbus-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --enable-inotify --disable-dnotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-systemd
  patch -p1 < "$srcdir/systemd-user-session.patch"
  make
}

package(){
  cd dbus-$pkgver
  make DESTDIR="$pkgdir" install

  rm -f "$pkgdir/usr/bin/dbus-launch"
  rm -f "$pkgdir/usr/share/man/man1/dbus-launch.1"
  rm -rf "$pkgdir/var/run"

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|' "$pkgdir/etc/dbus-1/system.conf"

  install -dm755 "$pkgdir/usr/share/licenses/dbus-core"
  install -m644 COPYING "$pkgdir/usr/share/licenses/dbus-core/"
}
