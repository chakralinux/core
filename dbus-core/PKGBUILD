#
# Chakra Packages, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf
#
pkgname=dbus-core
pkgver=1.3.1
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('expat>=2.0.1' 'coreutils' 'filesystem')
conflicts=('dbus<1.3.1')
options=(!libtool)
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz
        dbus)
md5sums=('1aab02bdcac4d662d3760896790f0ede'
         '08f93dd19cffd1b45ab05c1fd4efb560')

build() {
  cd "${srcdir}/dbus-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/var/run/dbus.pid \
      --enable-inotify --disable-dnotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts --without-x || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm -f "${pkgdir}/usr/bin/dbus-launch"
  rm -f "${pkgdir}/usr/share/man/man1/dbus-launch.1"

  chown 81:81 "${pkgdir}/var/run/dbus" || return 1

  install -m755 -d "${pkgdir}/etc/rc.d" || return 1
  install -m755 "${srcdir}/dbus" "${pkgdir}/etc/rc.d/" || return 1

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|' "${pkgdir}/etc/dbus-1/system.conf" || return 1

  #install .keep files so pacman doesn't delete empty dirs
  touch "${pkgdir}/usr/share/dbus-1/services/.keep" || return 1
  touch "${pkgdir}/usr/share/dbus-1/system-services/.keep" || return 1
  touch "${pkgdir}/etc/dbus-1/session.d/.keep" || return 1
  touch "${pkgdir}/etc/dbus-1/system.d/.keep" || return 1

  rmdir "${pkgdir}/usr/lib/dbus-1.0/dbus-1" || return 1

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus-core"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/dbus-core/" || return 1
}
