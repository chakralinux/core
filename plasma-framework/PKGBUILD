# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=plasma-framework
pkgver=${KFVersion}
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/plasma-framework'
license=('LGPL')
depends=('qt5-quickcontrols' 'kactivities-frameworks' 'kpackage')
makedepends=("extra-cmake-modules>=${KFECMVersion}" 'qt5-tools' 'kdoctools')
options=("debug")
groups=('kf5')
source=("${KFServer}/${pkgname}-${pkgver}.tar.xz"
        'AppletQuickItem-Fix-finding-own-attached-layout.patch')
sha256sums=( $(getSum ${pkgname}) 
            '6094c6d9a7997554c74f930dc551517bf6e35e6e90700152aab12dec45455b86')

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  # https://bugs.kde.org/show_bug.cgi?id=358849
  patch -Np1 < $srcdir/AppletQuickItem-Fix-finding-own-attached-layout.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
