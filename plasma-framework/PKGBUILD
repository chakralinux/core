# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=plasma-framework
pkgver=5.47.0
pkgrel=2
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/plasma-framework'
license=('LGPL')
depends=('qt5-quickcontrols' 'kactivities' 'kpackage' 'kwayland' 'kirigami')
makedepends=("extra-cmake-modules>=5.47.0" 'qt5-tools' 'kdoctools')
options=("debug")
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        # will be in 5.48
        'calendar.patch::https://cgit.kde.org/plasma-framework.git/patch/?id=d15f0fa8322dc2632f15024457bea41e7dd058d7')
sha256sums=('4a43bb699e2d6e004b0d218c98079eea329d3a2d3de82b33b7d298e4cb146eb4'
            'SKIP'
            '4653e456c2b20d642a7d6e92eca1760e9f8ac1d89ab061eb65449a367b05781a')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver

  patch -Np1 -i ../calendar.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
