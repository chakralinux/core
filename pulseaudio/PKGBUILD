#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=pulseaudio
pkgname=('pulseaudio' 'libpulse')
pkgver=1.0
pkgrel=2
arch=('i686' 'x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
makedepends=('libasyncns' 'libcap' 'attr' 'libxtst' 'libsm' 'tcp_wrappers' 'libsndfile'
             'libtool' 'rtkit' 'speex' 'libsamplerate' 'tdb' 'udev' 'dbus-core' 'xcb-util'
             'avahi' 'bluez' 'intltool' 'jack' 'lirc-utils' 'pkgconfig' 'openssl')
options=(!emptydirs !libtool !makeflags)
source=("http://freedesktop.org/software/${pkgbase}/releases/${pkgbase}-${pkgver}.tar.xz"
        "${pkgbase}.xinit")
md5sums=('538e0b55ab9ba2987a88047ae332f5f4'
         'eb1cfae28eb2e4d3fdbe45a90e4318ab')
sha1sums=('24a408e762b2df4334e9ae4f644ac1b893c7ef86'
          'e33cce91a688104ccf718953711cb85b83e74a8f')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Don't break circular dependency between libpulse and libpulsecommon
  LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --with-database=tdb \
    --disable-hal \
    --disable-rpath \
    --disable-gconf \
    --disable-default-build-tests

  make
}

package_pulseaudio() {
  pkgdesc="A featureful, general-purpose sound server."
  depends=("libpulse=${pkgver}-${pkgrel}" 'rtkit' 'libtool' 'speex' 'libsamplerate' 'tdb'
           'udev')
  optdepends=('avahi: zeroconf support'
              'bluez: bluetooth support'
#             'gconf: configuration through gconf'
              'jack: jack support'
              'lirc-utils: infra-red support'
              'openssl: RAOP support')
  backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
  install=pulseaudio.install
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Lower resample quality
  sed -e '/resample-method/iresample-method=speex-float-0' \
      -i "${pkgdir}/etc/pulse/daemon.conf"

  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i "${pkgdir}/usr/bin/start-${pkgbase}-x11"

  install -Dm755 "${srcdir}/${pkgbase}.xinit" "${pkgdir}/etc/X11/xinit/xinitrc.d/${pkgbase}"

  rm "${pkgdir}/etc/dbus-1/system.d/${pkgbase}-system.conf"

### Split libpulse

  mkdir -p "${srcdir}"/libpulse/{etc/pulse,usr/{lib,share/{man/man5,vala/vapi}}}

  mv {"${pkgdir}","${srcdir}/libpulse"}/etc/pulse/client.conf

  mv "${pkgdir}"/usr/lib/libpulse{,dsp,-simple,-mainloop-glib}.so* \
     "${pkgdir}"/usr/lib/libpulsecommon-*.so \
     "${srcdir}/libpulse/usr/lib"

  mv {"${pkgdir}","${srcdir}/libpulse"}/usr/lib/pkgconfig
  mv {"${pkgdir}","${srcdir}/libpulse"}/usr/include

  mv {"${pkgdir}","${srcdir}/libpulse"}/usr/share/man/man5/pulse-client.conf.5
  mv {"${pkgdir}","${srcdir}/libpulse"}/usr/share/vala/vapi/libpulse.vapi
}

package_libpulse() {
  pkgdesc="A featureful, general-purpose sound server (client library)."
  depends=('tcp_wrappers' 'dbus-core' 'xcb-util' 'libasyncns' 'libcap' 'libxtst' 'libsm' 'libsndfile' 'json-c')
  optdepends=('alsa-plugins: ALSA support'
              'avahi: zeroconf support')
  backup=(etc/pulse/client.conf)
  mv "${srcdir}"/libpulse/* "${pkgdir}"
}

# vim:set ts=2 sw=2 et:
