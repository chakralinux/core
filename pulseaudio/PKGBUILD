# Note: remember to build lib32 counterpart when bumping pkgver

pkgbase=pulseaudio
pkgname=(pulseaudio libpulse pulseaudio-{gconf,zeroconf,lirc,jack,bluetooth,equalizer})
pkgdesc="A featureful, general-purpose sound server"
pkgver=11.1
pkgrel=1
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/PulseAudio"
license=(LGPL)
makedepends=(libasyncns libcap attr libxtst libsm libsndfile libtool rtkit libsoxr
             speexdsp tdb systemd dbus avahi bluez bluez-libs gconf intltool jack2-dbus sbc
             lirc openssl fftw orc webrtc-audio-processing
             check autoconf-archive git)
options=(!emptydirs)
_commit=f93e372b0b2cb960df04de3f0a8fbea683bb8229  # tags/v11.1^0
source=("git+https://anongit.freedesktop.org/git/pulseaudio/pulseaudio#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pulseaudio
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd pulseaudio
  NOCONFIGURE=1 ./bootstrap.sh
}

build() {
  cd pulseaudio

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-database=tdb \
    --disable-tcpwrap \
    --disable-bluez4 \
    --disable-samplerate \
    --disable-rpath \
    --disable-default-build-tests \
    DATADIRNAME=share

  # fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package_pulseaudio() {
  depends=("libpulse=$pkgver-$pkgrel" rtkit libltdl speexdsp tdb orc libsoxr
           webrtc-audio-processing sbc)
  optdepends=('pulseaudio-alsa: ALSA configuration (recommended)'
             'avahi: Zeroconf support for PulseAudio'
             'openssl: Zeroconf support for PulseAudio'
             'lirc: IR (lirc) support for PulseAudio'
             'jack2: Jack support for PulseAudio'
             'bluez: Bluetooth support for PulseAudio'
             'bluez-libs: Bluetooth support for PulseAudio' 
             'python3-pyqt4: Equalizer for PulseAudio'
             'fftw: Equalizer for PulseAudio'
             'gconf: GConf support for PulseAudio')

  backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
  install=pulseaudio.install
  replaces=('pulseaudio-xen<=9.0')

  cd pulseaudio

  _pulsever=$(./git-version-gen .tarball-version)
  while [[ $_pulsever = *.*.* ]]; do
    _pulsever=${_pulsever%.*}
  done
  _pulsever=${_pulsever%%-*}

  make -j1 DESTDIR="$pkgdir" install \
    bashcompletiondir=/usr/share/bash-completion/completions

  cd "$pkgdir"
  # Assumes that any volume adjustment is intended by the user, who can control
  # each app's volume. Misbehaving clients can trigger earsplitting volume
  # jumps. App volumes can diverge wildly and cause apps without their own
  # volume control to fall below sink volume; a sink-only volume control will
  # suddenly be unable to make such an app loud enough.
  sed -e '/flat-volumes/iflat-volumes = no' \
      -i etc/pulse/daemon.conf

  # Superseded by socket activation
  sed -e '/autospawn/iautospawn = no' \
      -i etc/pulse/client.conf

  # Disable cork-request module, can result in e.g. media players unpausing
  # when there's a Skype call incoming
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i usr/bin/start-pulseaudio-x11

  rm etc/dbus-1/system.d/pulseaudio-system.conf

  mkdir usr/lib/systemd/user/sockets.target.wants
  ln -s ../pulseaudio.socket \
    usr/lib/systemd/user/sockets.target.wants/pulseaudio.socket
  
  # Avoid kde to show the "remove unused sound devices" if bell sound is executed by an application
  sed -e '27i\    /usr/bin/pactl load-module module-x11-bell "display=$DISPLAY" "sample=bell.ogg" > /dev/null' \
      -i "$pkgdir/usr/bin/start-pulseaudio-x11"

  # Python fix
  sed -i '1s:python$:&2:' "$pkgdir/usr/bin/qpaeq"
    
  #### daemon.conf ####
  # Speed up pulseaudio shutdown
  # Lower resample quality, saves CPU
  sed -e '/exit-idle-time/iexit-idle-time=0' \
      -e '/resample-method/iresample-method=speex-float-1' \
      -i "$pkgdir/etc/pulse/daemon.conf"

### Split libpulse

  mkdir -p "$srcdir"/libpulse/{etc/pulse,usr/{bin,lib/pulseaudio,share/man/man{1,5}}}

  mv etc/pulse/client.conf \
     "$srcdir/libpulse/etc/pulse"

  mv usr/bin/pa{cat,ctl,dsp,mon,play,rec,record} \
     "$srcdir/libpulse/usr/bin"

  mv usr/lib/libpulse{,-simple,-mainloop-glib}.so* \
     usr/lib/{cmake,pkgconfig} \
     "$srcdir/libpulse/usr/lib"

  mv usr/lib/pulseaudio/libpulse{dsp,common-*}.so \
     "$srcdir/libpulse/usr/lib/pulseaudio"

  mv usr/include \
     "$srcdir/libpulse/usr"

  mv usr/share/man/man1/pa{cat,ctl,dsp,mon,play,rec,record}.1 \
     "$srcdir/libpulse/usr/share/man/man1"

  mv usr/share/man/man5/pulse-client.conf.5 \
     "$srcdir/libpulse/usr/share/man/man5"

  mv usr/share/vala \
     "$srcdir/libpulse/usr/share"


### Split modules

  mkdir -p "$srcdir"/{gconf,zeroconf,lirc,jack,bluetooth,equalizer}/usr/lib/pulse-$_pulsever/modules \
           "$srcdir"/{gconf/usr/lib/pulse,equalizer/usr/bin}

  mv usr/lib/pulse-$_pulsever/modules/module-gconf.so \
     "$srcdir/gconf/usr/lib/pulse-$_pulsever/modules"
  mv usr/lib/pulse/gconf-helper \
     "$srcdir/gconf/usr/lib/pulse"

  mv usr/lib/pulse-$_pulsever/modules/{libavahi-wrap,module-{zeroconf-{publish,discover},raop-discover}}.so \
     "$srcdir/zeroconf/usr/lib/pulse-$_pulsever/modules"

  mv usr/lib/pulse-$_pulsever/modules/module-lirc.so \
     "$srcdir/lirc/usr/lib/pulse-$_pulsever/modules"

  mv usr/lib/pulse-$_pulsever/modules/module-jack{-sink,-source,dbus-detect}.so \
     "$srcdir/jack/usr/lib/pulse-$_pulsever/modules"

  mv usr/lib/pulse-$_pulsever/modules/{libbluez5-util,module-{bluetooth-{discover,policy},bluez5-{discover,device}}}.so \
     "$srcdir/bluetooth/usr/lib/pulse-$_pulsever/modules"

  mv usr/lib/pulse-$_pulsever/modules/module-equalizer-sink.so \
     "$srcdir/equalizer/usr/lib/pulse-$_pulsever/modules"
  mv usr/bin/qpaeq \
     "$srcdir/equalizer/usr/bin"
}

package_libpulse() {
  pkgdesc="$pkgdesc (client library)"
  depends=(dbus libasyncns libcap libxtst libsm libsndfile systemd)
  license=(LGPL)
  backup=(etc/pulse/client.conf)

  mv "$srcdir"/libpulse/* "$pkgdir"
}

package_pulseaudio-gconf(){
  pkgdesc="GConf support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" gconf)

  mv "$srcdir"/gconf/* "$pkgdir"
}

package_pulseaudio-zeroconf(){
  pkgdesc="Zeroconf support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" avahi openssl)

  mv "$srcdir"/zeroconf/* "$pkgdir"
}

package_pulseaudio-lirc(){
  pkgdesc="IR (lirc) support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" lirc)

  mv "$srcdir"/lirc/* "$pkgdir"
}

package_pulseaudio-jack(){
  pkgdesc="Jack support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" jack)

  mv "$srcdir"/jack/* "$pkgdir"
}

package_pulseaudio-bluetooth(){
  pkgdesc="Bluetooth support for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" bluez bluez-libs sbc)

  mv "$srcdir"/bluetooth/* "$pkgdir"
}

package_pulseaudio-equalizer(){
  pkgdesc="Equalizer for PulseAudio"
  depends=("pulseaudio=$pkgver-$pkgrel" python-pyqt4 fftw)
  license=(AGPL3)

  mv "$srcdir"/equalizer/* "$pkgdir"
}
