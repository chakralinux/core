pkgname=soundtouch
pkgver=1.9.2
pkgrel=1
pkgdesc="SoundTouch is an open-source audio processing library for changing the Tempo, Pitch and Playback Rates of audio streams or audio files."
arch=('x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
source=("http://www.surina.net/soundtouch/${pkgname}-${pkgver}.tar.gz")
sha1sums=('847be1bc987e410cc7349fdd83f353f4372534d1')

prepare() {
  cd ${pkgname}
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
}

build() {
  cd "${srcdir}/${pkgname}"

  ./bootstrap

  ./configure --prefix=/usr --enable-shared --with-pic 

  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch install
}
