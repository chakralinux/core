#
# Platform Packages for Chakra, part of chakraos.org
#
# maintainer almack <gluca86 at gmail dot com>

pkgname=dbus-c++
pkgver=0.9.0
pkgrel=5
pkgdesc="dbus-c++ attempts to provide a C++ API for D-BUS"
url="http://sourceforge.net/projects/dbus-cplusplus/"
arch=('x86_64')
license=('GPL')
depends=('dbus' 'glib2')
makedepends=('autoconf' 'automake')
source=("http://sourceforge.net/projects/dbus-cplusplus/files/dbus-c%2B%2B/$pkgver/libdbus-c%2B%2B-$pkgver.tar.gz"
	"patch-gcc47.patch")
md5sums=('e752116f523fa88ef041e63d3dee4de2'
         '9319b402b15f37d881b51bc758368900')

build() {
    cd "${srcdir}/libdbus-c++-${pkgver}"
    patch -p0 < ${srcdir}/patch-gcc47.patch
    export LDFLAGS="${LDFLAGS} -lexpat -lpthread"
    ./autogen.sh --prefix=/usr --disable-static --enable-glib --disable-ecore
    make
}

package() {
    cd "${srcdir}/libdbus-c++-${pkgver}"
    make DESTDIR=${pkgdir} install
}
