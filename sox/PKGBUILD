#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf


pkgname=sox
pkgver=14.3.2
pkgrel=2
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('i686' 'x86_64')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('libtool' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr')
makedepends=('ffmpeg' 'libao' 'libmad' 'libid3tag' 'wavpack')
optdepends=('libao: for ao plugin'
            'ffmpeg: for ffmpeg plugin'
            'libmad: for mp3 plugin'
            'libid3tag: for mp3 plugin'
            'wavpack: for wavpack plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        sox-14.3.2-ffmpeg.patch)
md5sums=('e9d35cf3b0f8878596e0b7c49f9e8302'
         '217df87e92a3698ba1d7a78779ed72f2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/sox-14.3.2-ffmpeg.patch"
  sed -i 's|man1/sox.1 soxeffect.7|man1/sox.1.gz soxeffect.7.gz|' Makefile.am
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-dyn-default \
    --with-distro="Chakra-Linux" 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
