# $Id: PKGBUILD 26434 2009-02-07 23:16:46Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=smpeg
pkgver=0.4.5
pkgrel=1
pkgdesc="SDL MPEG Player Library"
arch=('x86_64')
url="http://icculus.org/smpeg/"
license=('LGPL')
depends=('sdl')
makedepends=('mesa' 'glu' 'svn')
optdepends=('glu: to use glmovie')
source=("$pkgname-$pkgver::svn://svn.icculus.org/smpeg/tags/release_${pkgver//./_}")
md5sums=('SKIP')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}