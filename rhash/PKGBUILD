# Arch Contributor: https://projects.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/rhash

pkgname=rhash
pkgver=1.3.5
pkgrel=1
pkgdesc='Utility for verifying hash sums of files'
url='http://rhash.anz.ru/?l=en'
license=('custom' 'MIT')
arch=('x86_64')
depends=('glibc')
backup=('etc/rhashrc')
source=("https://github.com/rhash/RHash/archive/v$pkgver.tar.gz")
sha256sums=('98e0688acae29e68c298ffbcdbb0f838864105f9b2bd8857980664435b1f1f2e')

build() {
  cd RHash-$pkgver
  make ADDCFLAGS="$CFLAGS" ADDLDFLAGS="$LDFLAGS" build-shared
  make -C librhash
}

check() {
  cd RHash-$pkgver
  make test-shared
}

package() {
  cd RHash-$pkgver

  # program
  make PREFIX=/usr DESTDIR="$pkgdir" install-shared

  # library
  make -C librhash PREFIX=/usr DESTDIR="$pkgdir" install-headers install-lib-shared install-lib-static

  # license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/rhash/LICENSE
}
