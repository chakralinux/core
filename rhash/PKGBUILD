# Arch Contributor: https://projects.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/rhash

pkgname=rhash
pkgver=1.3.4
pkgrel=1
pkgdesc='Utility for verifying hash sums of files'
url='http://rhash.anz.ru/?l=en'
license=('custom' 'MIT')
arch=('x86_64')
depends=('glibc')
backup=('etc/rhashrc')
source=("http://downloads.sourceforge.net/rhash/rhash-$pkgver-src.tar.gz")
sha256sums=('406662c4703bd4cb1caae26f32700951a5e12adf39f141d3f40e0b461b1e9e49')

build() {
  cd rhash-$pkgver
  make ADDCFLAGS="$CFLAGS" ADDLDFLAGS="$LDFLAGS" build-shared
}

check() {
  cd rhash-$pkgver
  make test-shared
}

package() {
  cd rhash-$pkgver

  # program
  make PREFIX=/usr DESTDIR="$pkgdir" install-shared

  # library
  make -C librhash PREFIX=/usr DESTDIR="$pkgdir" install-headers install-lib-shared install-lib-static

  # license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/rhash/LICENSE
}
