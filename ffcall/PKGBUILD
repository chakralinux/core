# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ffcall
pkgver=1.10
pkgrel=4
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=('i686' 'x86_64')
url="http://www.haible.de/bruno/packages-ffcall.html"
license=('GPL2')
options=('!libtool' '!makeflags')
source=(http://www.haible.de/bruno/gnu/${pkgname}-${pkgver}.tar.gz)
md5sums=('2db95007e901f3bc2ae7e5a9fe9ebea4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "$CARCH" = "x86_64" ] && CONFIGFLAG="--with-pic"
  ./configure --prefix=/usr --mandir=/usr/share/man $CONFIGFLAG
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}"/usr/share/{man,doc/ffcall}
  make DESTDIR="${pkgdir}" htmldir=/usr/share/doc/ffcall install
}
