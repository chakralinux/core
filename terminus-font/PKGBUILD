# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# contributions from Arch: https://www.archlinux.org/packages/community/any/terminus-font/

pkgname=terminus-font
pkgver=4.47
pkgrel=1
pkgdesc="A superb, monospace bitmap font (for X11 and console)"
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
makedepends=('xorg-bdftopcf' 'xorg-mkfontscale' 'xorg-mkfontdir' 'python3')
arch=('x86_64')
source=("https://downloads.sourceforge.net/project/terminus-font/terminus-font-$pkgver/terminus-font-$pkgver.tar.gz"
        fix-75-yes-terminus.patch)

sha256sums=('0f1b205888e4e26a94878f746b8566a65c3e3742b33cf9a4e6517646d5651297'
            'ddd86485cf6d54e020e36f1c38c56e8b21b57c23a5d76250e15c1d16fed9caa5')

prepare() {
  cd "terminus-font-$pkgver"

  chmod +x configure
  patch -p1 <"$srcdir"/fix-75-yes-terminus.patch
}

build() {
  cd "terminus-font-$pkgver"

  ./configure \
    --prefix=/usr \
    --x11dir=/usr/share/fonts/misc \
    --psfdir=/usr/share/kbd/consolefonts
  make
}

package() {
  make -C "terminus-font-$pkgver" DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/terminus-font-$pkgver/75-yes-terminus.conf" \
    "$pkgdir/etc/fonts/conf.avail/75-yes-terminus.conf"
  install -Dm644 "$srcdir/terminus-font-$pkgver/OFL.TXT" \
    "$pkgdir/usr/share/licenses/terminus-font/LICENSE"
  install -d "$pkgdir/etc/fonts/conf.d"

  cd "$pkgdir/etc/fonts/conf.d"
  ln -s ../conf.avail/75-yes-terminus.conf .
}
