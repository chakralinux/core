# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=xcb-util-keysyms
pkgver=0.3.9
pkgrel=3
pkgdesc="Utility libraries for XC Binding"
arch=('x86_64')
url="http://xcb.freedesktop.org"
license=('custom')
depends=('libxcb' 'xcb-util')
makedepends=('gperf' 'xorg-util-macros')
options=('!libtool')
source=(http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2
        LICENSE)
md5sums=('64e4aad2d48cd4a92e2da13b9f35bfd2'
         '2672c2e72dedb40f773e989dd622f298')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
