# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/polkit-kde-frameworks
source ../plasma.conf

pkgname=polkit-kde-agent
_pkgname=${pkgname}-1
pkgver=${PVersion}
pkgrel=1
pkgdesc='Daemon providing a polkit authentication UI for KDE'
arch=('x86_64')
url='https://projects.kde.org/projects/extragear/base/polkit-kde-agent-1/'
license=('LGPL')
depends=('knotifications')
replaces=('polkit-kde-agent-frameworks')
conflicts=('polkit-kde-agent-frameworks')
provides=('polkit-kde-agent-frameworks')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
source=("${PServer}/${pkgver}/${_pkgname}-${PVersion}.tar.xz"{,.sig})
sha256sums=( $(getSum ${_pkgname}) 
             'SKIP' )
validpgpkeys=(${Pvalidpgpkeys})

prepare() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  mkdir -p build
}

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}/build
  cmake_kf5 .. \
    -DLIBEXEC_INSTALL_DIR=lib/polkit-kde 
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}/build
  make DESTDIR="$pkgdir" install
}
