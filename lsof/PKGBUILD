# Platform packages for Chakra
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributors from Arch: Aaron Griffin <aaron@archlinux.org>
#                         Jochem Kossen <j.kossen@home.nl>

pkgname=lsof
pkgver=4.86
pkgrel=1
pkgdesc="Lists open files for running Unix processes"
arch=('x86_64')
url="http://people.freebsd.org/~abe/"
license=('custom')
depends=('glibc')
source=(ftp://sunsite.ualberta.ca/pub/Mirror/lsof/${pkgname}_${pkgver}.tar.bz2 license.txt)
md5sums=('9f1cda41f35add068c7b81f873fa56b5'
         '1b63c76bd10437cabf890508c8e58d36')

build() {
   cd "$srcdir/${pkgname}_${pkgver}"
   tar xf ${pkgname}_${pkgver}_src.tar
   cd ${pkgname}_${pkgver}_src
   sed "s|/\* #define\tHASSECURITY\t1 \*/|#define\tHASSECURITY\t1|" -i dialects/linux/machine.h
   ./Configure -n linux
   make
}

package() {
cd ${srcdir}/${pkgname}_${pkgver}/${pkgname}_${pkgver}_src

    FILES=("00CREDITS" "00DCACHE" "00DIALECTS" "00DIST" "00FAQ" "00LSOF-L" "00MANIFEST" "00PORTING" "00QUICKSTART" "00README" "00.README.FIRST" "00TEST" "00XCONFIG")
    for file in "${FILES[@]}"
    do
        install -D -m0644 -o root -g root $file \
        $pkgdir/usr/share/doc/$pkgname/$file
    done

   install -D -m0755 -o root -g root lsof "${pkgdir}/usr/sbin/lsof"
   install -D -m0644 -o root -g root lsof.8 "${pkgdir}/usr/share/man/man8/lsof.8"

   install -D -m0644 -o root -g root ${srcdir}/license.txt \
     ${pkgdir}/usr/share/licenses/lsof/LICENSE
}
