#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=gptfdisk
pkgver=0.8.10
pkgrel=2
pkgdesc="A text-mode partitioning tool that works on Globally Unique Identifier (GUID) Partition Table (GPT) disks"
arch=('x86_64')
url="http://www.rodsbooks.com/gdisk/"
license=('GPL2')
depends=('gcc-libs' 'util-linux' 'popt' 'icu')
provides=("gdisk=$pkgver")
conflicts=('gdisk')
replaces=('gdisk')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('9cf4246c181c324bdbd553fe9b348373')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make
}

package () {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir"/usr/{bin,share/{doc/gdisk,man/man8}}
  install -t "$pkgdir/usr/bin" {,c,s}gdisk fixparts
  install -m644 -t "$pkgdir/usr/share/man/man8" {{,c,s}gdisk,fixparts}.8
  install -m644 -t "$pkgdir/usr/share/doc/gdisk" README NEWS
}
