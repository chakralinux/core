# $Id: PKGBUILD 102633 2010-12-09 08:40:59Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=polkit-qt
pkgname=('polkit-qt4' 'polkit-qt5')
pkgver=0.112.0+git20180107
pkgrel=1
pkgdesc='A library that allows developers to access PolicyKit API with a nice Qt-style API'
arch=('x86_64')
url='https://projects.kde.org/projects/kdesupport/polkit-qt-1'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'polkit' 'qt' 'qt5-base')
#source=("http://download.kde.org/stable/apps/KDE4.x/admin/${pkgbase}-1-${pkgver}.tar.bz2")
source=("${pkgbase}::git+https://anongit.kde.org/polkit-qt-1.git#commit=cbecf60ac7b5c17cf37503d6675bf5542efbab29")
md5sums=('SKIP')

prepare() {
  mkdir build{,-qt5}
}

build() {
  cd build
  cmake ../${pkgbase} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib \
    -DUSE_QT4=ON
  make

  cd ../build-qt5
  cmake ../${pkgbase} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib
  make
}

package_polkit-qt4() {
  depends=('polkit' 'qt')
  replaces=("polkit-qt<${pkgver}")
  provides=("polkit-qt=${pkgver}")
  conflicts=("polkit-qt<${pkgver}")

  cd build
  make DESTDIR="${pkgdir}" install
}

package_polkit-qt5() {
  depends=('polkit' 'qt5-base')

  cd build-qt5
  make DESTDIR="${pkgdir}" install
}
