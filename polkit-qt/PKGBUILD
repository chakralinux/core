# $Id: PKGBUILD 102633 2010-12-09 08:40:59Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=polkit-qt
pkgname=('polkit-qt4' 'polkit-qt5')
pkgver=0.112.0+git20160226
pkgrel=1
pkgdesc='A library that allows developers to access PolicyKit API with a nice Qt-style API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/polkit-qt-1'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'polkit' 'qt' 'qt5-base')
#source=("http://download.kde.org/stable/apps/KDE4.x/admin/${pkgbase}-1-${pkgver}.tar.bz2")
source=("http://quickgit.kde.org/?p=polkit-qt-1.git&a=snapshot&h=15c7867e88c5278f61896b5531ac2b544add8220&fmt=tgz")
md5sums=('e74c39223328edf89cdce4e037ef3c38')

prepare() {
  mkdir build{,-qt5}
}

build() {
  cd build
  cmake ../${pkgbase}-1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib \
    -DUSE_QT4=ON
  make

  cd ../build-qt5
  cmake ../${pkgbase}-1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib
  make
}

package_polkit-qt4() {
  depends=('polkit' 'qt')
  replaces=("polkit-qt<${pkgver}")
  provides=("polkit-qt=${pkgver}")
  conflicts=("polkit-qt<${pkgver}")

  cd build
  make DESTDIR="${pkgdir}" install
}

package_polkit-qt5() {
  depends=('polkit' 'qt5-base')

  cd build-qt5
  make DESTDIR="${pkgdir}" install
}
