#
# Packages of Chakra, part of chakra-project.org
#
# Contributor:abveritas@chakra-project.org

pkgbase=gstreamer0.10-bad
pkgname=('gstreamer0.10-bad' 'gstreamer0.10-bad-plugins')
pkgver=0.10.23
pkgrel=14
arch=('x86_64')
license=('LGPL' 'GPL')
makedepends=('pkgconfig' 'gstreamer0.10-base>=0.10.36' 'xvidcore' 'libdca' 'bzip2' 'libdc1394' 
	     'neon' 'faac' 'libmusicbrainz' 'faad2' 'libmms' 'libcdaudio' 'libmpcdec' 'mjpegtools' 
	     'libdvdnav' 'libmodplug' 'jasper' 'liblrdf' 'libofa' 'soundtouch' 'libvdpau' 
	     'schroedinger' 'libass' 'libvpx' 'gsm' 'libgme' 'rtmpdump' 'libsndfile' 'opus')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-bad/gst-plugins-bad-${pkgver}.tar.xz "modplug_header.patch")
md5sums=('e4822fa2cc933768e2998311a1565979'
         '798274052c11c2d6fb56da29498df270')

prepare() {
  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  patch -p1 -i ${srcdir}/modplug_header.patch
}

build() {
  cd "${srcdir}/gst-plugins-bad-${pkgver}"

  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --localstatedir=/var \
              --disable-static \
	      --enable-experimental \
              --disable-celt \
              --disable-rsvg \
              --with-package-name="GStreamer Bad Plugins (Chakra GNU/Linux)" \
              --with-package-origin="http://www.chakra-project.org/" 

  make 
  sed -e 's/gst sys ext/gst/' -i Makefile 
}

package_gstreamer0.10-bad() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugin libraries (gst-plugins-bad)"
  depends=('gstreamer0.10-base>=0.10.34')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  make DESTDIR="${pkgdir}" install 
  mv ${pkgdir}/usr/lib/gstreamer-0.10/libgstvideoparsersbad.so ${pkgdir}/usr/lib/gstreamer-0.10/libgstvideoparsersbad.so.bak
}

package_gstreamer0.10-bad-plugins() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugins (gst-plugins-bad)"
  depends=("gstreamer0.10-bad=${pkgver}" 
	  'xvidcore' 'libdca' 'bzip2' 'libdc1394' 'neon' 'faac' 
          'libmusicbrainz' 'faad2' 'libmms' 'libcdaudio' 'libmpcdec' 'mjpegtools' 
          'libdvdnav' 'libmodplug' 'jasper' 'liblrdf' 'libofa' 'libvdpau' 
	  'soundtouch' 'libass' 'schroedinger' 'libvpx' 'gsm' 'rtmpdump' 
          'libgme' 'libsndfile' 'opus')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  make -C gst-libs DESTDIR="${pkgdir}" install 
  make -C ext DESTDIR="${pkgdir}" install 
  make -C sys DESTDIR="${pkgdir}" install 
  make -C gst-libs DESTDIR="${pkgdir}" uninstall 
}
