#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=rtkit
pkgver=0.11
pkgrel=4
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=('x86_64')
url="http://git.0pointer.de/?p=rtkit.git"
license=('GPL' 'custom:BSD')
depends=('dbus' 'polkit')
install=rtkit.install
source=(http://0pointer.de/public/$pkgname-$pkgver.tar.xz
        'libsystemd.patch'
        'systemd205.patch'
        '0001-SECURITY-Pass-uid-of-caller-to-polkit.patch')
md5sums=('a96c33b9827de66033d2311f82d79a5d'
         '35089c0a284005f4abcf45168415857e'
         '95195a70551057aca833da6bdbf2e35b'
         '70df212cba2a6366ff960b60d55858d3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i ../libsystemd.patch
  patch -Np1 -i ../systemd205.patch
  patch -Np1 -i ../0001-SECURITY-Pass-uid-of-caller-to-polkit.patch
  autoreconf -fi
}
  
build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/rtkit \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # DBus configuration
  mkdir -p "$pkgdir/usr/share/dbus-1/interfaces"
  ./rtkit-daemon --introspect >"$pkgdir/usr/share/dbus-1/interfaces/org.freedesktop.RealtimeKit1.xml"

  # Client license
  mkdir -p "$pkgdir/usr/share/licenses/rtkit"
  sed -ne '4,25p' rtkit.c >"$pkgdir/usr/share/licenses/rtkit/COPYING"
}


