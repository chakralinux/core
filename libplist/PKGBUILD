#
# Maintainer : Neophytos Kolokotronis <tetris4@gmail.com>
# Contributors from Arch: Ionut Biru <ibiru@archlinux.org>
#                         Gabriel Martinez < reitaka at gmail dot com >

pkgname=libplist
pkgver=1.8
pkgrel=1
pkgdesc="A library to handle Apple Property List format whereas it's binary or XML"
url="http://libimobiledevice.org/"
arch=('x86_64')
license=('GPL2' 'LGPL2.1')
depends=('glib2' 'libxml2' 'python2')
makedepends=('cmake' 'swig')
source=(http://www.libimobiledevice.org/downloads/$pkgname-$pkgver.tar.bz2)
sha256sums=('a418da3880308199b74766deef2a760a9b169b81a868a6a9032f7614e20500ec')

build() {
  mkdir build
  cd build
  cmake ../"${pkgname}-${pkgver}" \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
