#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=octave
pkgver=3.6.4
pkgrel=4
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'curl' 'graphicsmagick' 'glpk' 'hdf5' 'qhull' 'fltk' 'suitesparse' 'arpack')
makedepends=('texlive-core' 'gcc-fortran')
optdepends=('texinfo: for help-support in octave'
	    'gnuplot: alternative plotting')
source=("ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.bz2"
	"mex-gcc-4.8.patch")
options=('!emptydirs')
install='octave.install'
sha1sums=('3cc9366b6dbbd336eaf90fe70ad16e63705d82c4'
          '0608af9b7ccce8455f534d2fc4acf2f4c8f83a60')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  autoreconf -vfi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-shared \
    --enable-docs=no \
    --disable-static \
    --with-umfpack="-lumfpack -lsuitesparseconfig" 

  LANG=C make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # add path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${pkgname}/${pkgver}" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
}
