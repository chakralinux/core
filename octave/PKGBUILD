pkgname=octave
pkgver=4.0.0
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'curl' 'graphicsmagick' 'glpk' 'hdf5' 'qhull' 'fltk' 'suitesparse' 'arpack' 'pcre' 'qt')
makedepends=('texlive-core' 'gcc-fortran' 'texinfo' 'gnuplot')
optdepends=('texinfo: for help-support in octave'
	    'gnuplot: alternative plotting')
source=(ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.xz{,.sig})
options=('!emptydirs')
install='octave.install'
sha1sums=('795c7ef1fb8d92f4cf9cae9eabba374a0328b1a9'
          'SKIP')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-shared \
    --disable-static \
    --with-umfpack="-lumfpack -lsuitesparseconfig" 

  LANG=C make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # add path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${pkgname}/${pkgver}" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
}
