# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/khtml

pkgname=khtml
pkgver=5.48.0
pkgrel=1
pkgdesc='KHTML APIs'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/khtml'
license=('LGPL')
depends=('giflib' 'kparts' 'kjs')
makedepends=("extra-cmake-modules>=5.48.0")
checkdepends=("xorg-server-xvfb")
groups=('kf5-aids')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('a1923402f04f4a72e8688004a00e47fe79f7f72d703d53a14058eebdb7a531ff'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
