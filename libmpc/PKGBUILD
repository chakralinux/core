#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=libmpc
pkgver=1.0.2
pkgrel=1
# dash the next line if you want to skip the tests
_test=1
pkgdesc="Library for the arithmetic of complex numbers with arbitrarily high precision"
arch=('x86_64')
url="http://www.multiprecision.org"
license=('LGPL')
depends=('mpfr>=3.1.2')
options=('!libtool')
install=libmpc.install
source=(http://www.multiprecision.org/mpc/download/mpc-${pkgver/_/-}.tar.gz{,.sig})
md5sums=('68fadff3358fb3e7976c7a398a0af4c3'
         'SKIP')

build() {
  cd "${srcdir}/mpc-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/mpc-${pkgver}"
  make check
}


package() {
  cd "${srcdir}/mpc-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}/usr/share/info/{mpc,libmpc}.info
}
