#/Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
#Contributor: lilydjwg <lilydjwg@gmail.com>

pkgbase=python3-dateutil
pkgname=('python3-dateutil' 'python2-dateutil')
pkgver=2.4.1
pkgrel=1
pkgdesc="Provides powerful extensions to the standard datetime module"
arch=('any')
license=('custom')
url="https://github.com/dateutil/dateutil"
makedepends=('python3-setuptools' 'python2-setuptools' 'python3-six' 'python2-six')
source=("$pkgbase-$pkgver.tar.gz"::https://github.com/dateutil/dateutil/archive/2.4.1.tar.gz)
#source=(http://pypi.python.org/packages/source/p/python3-dateutil/python3-dateutil-$pkgver.tar.gz)
md5sums=('41972da724ee35eeb26cfb9e9600ad51')

build() {
  # Required for unittests
  cd dateutil-$pkgver
  ./updatezinfo.py

  cd $srcdir
  cp -r  dateutil-$pkgver python2-dateutil-$pkgver
}

package_python3-dateutil()
{
depends=('python3-six')
  cd dateutil-$pkgver
  python3 setup.py install --root=$pkgdir --optimize=1
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package_python2-dateutil()
{
depends=('python2-six')
  cd $srcdir/python2-dateutil-$pkgver
  python2 setup.py install --root=$pkgdir --optimize=1
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

check() {
  cd dateutil-$pkgver
  python3 setup.py test

  cd $srcdir/python2-dateutil-$pkgver
  python2 setup.py test
}
