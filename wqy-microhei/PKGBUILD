#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# contributor Philacorns <opuspace@gmail.com>

pkgname=wqy-microhei
pkgver=0.2.0_beta
pkgrel=3
pkgdesc="A Sans-Serif style high quality CJK outline font."
arch=('any')
license=('GPL3' 'APACHE' 'custom:"font embedding exception"')
install=wqy-microhei.install
url="http://wenq.org"
depends=('fontconfig' 'xorg-font-utils')
source=(http://downloads.sourceforge.net/project/wqy/$pkgname/${pkgver//_/-}/$pkgname-${pkgver//_/-}.tar.gz
'44-wqy-microhei.conf')
md5sums=('a124c5c6606f4f3b733d3477380e9d2f'
         '11dda88e7538086a82478359c6ec5e2e')
package() {
  cd $srcdir/$pkgname
  tail README.txt --lines=16|head --lines=-3>| FONTEXCEPTION
  mkdir -p $pkgdir/usr/share/fonts/wenquanyi/$pkgname/
  mkdir -p $pkgdir/usr/share/licenses/$pkgname/
  mkdir -p $pkgdir/etc/fonts/conf.avail/
  mkdir -p $pkgdir/etc/fonts/conf.d/
  install -m644 $srcdir/*.conf $pkgdir/etc/fonts/conf.avail/ || return 1
  install -m644 *.ttc $pkgdir/usr/share/fonts/wenquanyi/$pkgname/ || return 1
  install -m644 -D FONTEXCEPTION $pkgdir/usr/share/licenses/$pkgname/FONTEXCEPTION || return 1
  cd $pkgdir/etc/fonts/conf.d/
  ln -s ../conf.avail/44-wqy-microhei.conf .
}
