# Maintainer: AlmAck <almack@chakraos.org>

pkgname=xf86-input-libinput
pkgver=0.15.0
pkgrel=1
pkgdesc="Generic input driver for the X.Org server based on libinput"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libinput')
makedepends=('xorg-server-devel' 'libxi' 'libx11' 'resourceproto' 'scrnsaverproto')
provides=('xf86-input-driver')
conflicts=('xorg-server<1.16' 'X-ABI-XINPUT_VERSION<21' 'X-ABI-XINPUT_VERSION>=22')
groups=('xorg-drivers' 'xorg')
source=(http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('28707c504194648b11a1e26acf13039b60bf344efa6aa9788afed6d47f019a41')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
