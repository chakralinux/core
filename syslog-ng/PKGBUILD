#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=syslog-ng
pkgver=3.2.4
pkgrel=1
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('base')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glib2' 'eventlog' 'tcp_wrappers' 'openssl' 'libcap' 'awk')
makedepends=('flex' 'pkgconfig')
optdepends=('logrotate')
provides=('logger')
backup=('etc/syslog-ng/modules.conf' 'etc/syslog-ng/scl.conf' \
        'etc/syslog-ng/syslog-ng.conf' 'etc/logrotate.d/syslog-ng')
source=(http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/$pkgname\_$pkgver.tar.gz
        syslog-ng.conf
        syslog-ng.logrotate
        syslog-ng.rc
        cap_syslog.patch)
md5sums=('5995f7dad0053a478b60a63f6f754203'
         '344dddfff946300f5576b13a7e8ea19f'
         '735636090be4582885974542d2a75855'
         '9bd98250cfa49e13fa3fa731b6040549'
         '46e5dcff71f820d497898331a3f608fe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
# fix #22555 for kernels >=2.6.38
  patch -p1 -i ../cap_syslog.patch 
  ./configure --prefix=/usr --sysconfdir=/etc/syslog-ng \
    --libexecdir=/usr/lib --localstatedir=/var/lib/syslog-ng \
    --enable-tcp-wrapper \
    --with-pidfile-dir=/var/run \
    --disable-spoof-source

    make || return 1
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install || return 1
    install -dm755 "${pkgdir}/var/lib/syslog-ng" "${pkgdir}/etc/syslog-ng/patterndb.d"
    install -Dm644 "${srcdir}/syslog-ng.conf" "${pkgdir}/etc/syslog-ng/syslog-ng.conf"
    install -Dm644 "${srcdir}/syslog-ng.logrotate" "${pkgdir}/etc/logrotate.d/syslog-ng"
    install -Dm755 "${srcdir}/syslog-ng.rc" "${pkgdir}/etc/rc.d/syslog-ng"
}
