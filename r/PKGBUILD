#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=r
pkgver=2.13.0
pkgrel=3
pkgdesc="R is a language and environment for statistical computing and graphics"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.r-project.org/')
depends=('blas' 'lapack' 'bzip2'  'libpng' 'libjpeg' 'libtiff'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs'
         'tk' 'libxt' 'libxmu' 'pango')
makedepends=('openjdk6' 'gcc-fortran')
options=('!makeflags')
source=("http://cran.r-project.org/src/base/R-2/R-${pkgver}.tar.gz"
	'r.desktop'
	'r.png')
sha1sums=('878510e8a5fa1ccd1e0c4af5866f5416f3c27469'
          'bcc0320d7cc49d56408edc756b50a0f9ba98fe43'
          'a69a07ec363440efc18ce0a7f2af103375dea978')

build() {
   cd ${srcdir}/R-${pkgver}
   sed -i 's|#define NeedFunctionPrototypes 0|#define NeedFunctionPrototypes 1|g' src/modules/X11/dataentry.c
   ./configure  --prefix=/usr \
		--libdir=/usr/lib \
		--datarootdir=/usr/share \
		  rsharedir=/usr/share/R/ \
		  rincludedir=/usr/include/R/ \
		  rdocdir=/usr/share/R/docs/ \
                --with-readline \
                --with-x \
                --enable-R-shlib \
                --with-lapack \
		--with-blas \
                F77=gfortran \
		LIBnn=lib
   make
}

package() {
   cd ${srcdir}/R-${pkgver}
   make -j1 DESTDIR=${pkgdir} install

   #  Fixup R wrapper scripts.
   sed -i "s|${pkgdir} ||" ${pkgdir}/usr/bin/R
   rm ${pkgdir}/usr/lib/R/bin/R
   cd ${pkgdir}/usr/lib/R/bin
   ln -s ../../../bin/R

  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/r.desktop \
	${pkgdir}/usr/share/applications/r.desktop
  install -Dm644 ${srcdir}/r.png \
	${pkgdir}/usr/share/pixmaps/r.png
 }
