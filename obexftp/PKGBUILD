
pkgname=obexftp
pkgver=0.24.2
pkgrel=1
pkgdesc="A tool for transfer files to/from any OBEX enabled device"
arch=('x86_64')
url="http://dev.zuckschwerdt.org/openobex/wiki/ObexFtp"
license=('GPL')
depends=('openobex' 'expat' 'fuse2')
makedepends=('cmake' 'asciidoc' 'xmlto' 'swig' 'ruby' 'tk')
provides=("obexfs=${pkgver}")
replaces=('obexfs')
conflicts=('obexfs')
options=('!makeflags' '!docs')
source=(http://downloads.sourceforge.net/sourceforge/openobex/${pkgname}-${pkgver}-Source.tar.gz)
md5sums=('157a9d1b2ed220203f7084db906de73c')

build() {
  cd "${srcdir}/"

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver}-Source \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_SBINDIR=bin \
    -DENABLE_PERL=YES \
    -DENABLE_PYTHON=YES \
    -DENABLE_RUBY=YES \
    -DENABLE_TCL=YES
  make doc
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install
}
