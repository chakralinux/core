pkgname=ncurses
_pkgver=6.0-20170204
pkgver=${_pkgver/-/+}
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library"
arch=('x86_64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
depends=('glibc')
provides=('libncurses++w.so' 'libformw.so' 'libmenuw.so' 'libpanelw.so' 'libncursesw.so')
source=(http://invisible-mirror.net/archives/ncurses/current/ncurses-${_pkgver}.tgz{,.asc})
md5sums=('1d772a92023f83fc9acfa9baca4babe7'
         'SKIP')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB')  # Thomas Dickey

build() {
  cd $pkgname-${_pkgver}

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --with-pkg-config-libdir=/usr/lib/pkgconfig \
              --with-shared \
              --with-normal \
              --without-debug \
              --without-ada \
              --enable-widec \
              --enable-pc-files \
              --with-cxx-binding --with-cxx-shared \
              --enable-ext-colors --enable-ext-mouse
  make
}

package() {
  cd $pkgname-${_pkgver}
  make DESTDIR="$pkgdir" install

  # Fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu ; do
    echo "INPUT(-l${lib}w)" >"${pkgdir}/usr/lib/lib${lib}.so"
    ln -s "${lib}w.pc" "${pkgdir}/usr/lib/pkgconfig/${lib}.pc"
  done

  # Some packages look for -lcurses during build
  echo "INPUT(-lncursesw)" >"${pkgdir}/usr/lib/libcursesw.so"
  ln -sf libncurses.so "${pkgdir}/usr/lib/libcurses.so"

  # install license, rip it from the readme
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  grep -B 100 '$Id' README > "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

