pkgname=ncurses
pkgver=6.0
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library"
arch=('x86_64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
depends=('glibc')
source=("ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
md5sums=('ee13d052e1ead260d7c28071f46eefb1'
         'SKIP')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB')

build() {
  cd $pkgname-${pkgver/_/-}

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --with-pkg-config-libdir=/usr/lib/pkgconfig \
              --with-shared \
              --with-normal \
              --without-debug \
              --without-ada \
              --enable-widec \
              --enable-pc-files \
              --with-cxx-binding --with-cxx-shared \
              --enable-ext-colors --enable-ext-mouse
  make
}

package() {
  cd $pkgname-${pkgver/_/-}
  make DESTDIR="$pkgdir" install

  # Fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu ; do
    echo "INPUT(-l${lib}w)" >"${pkgdir}/usr/lib/lib${lib}.so"
    ln -s "lib${lib}w.pc" "${pkgdir}/usr/lib/pkgconfig/${lib}.pc"
  done

  # Some packages look for -lcurses during build
  echo "INPUT(-lncursesw)" >"${pkgdir}/usr/lib/libcursesw.so"
  ln -sf libncurses.so "${pkgdir}/usr/lib/libcurses.so"

  # install license, rip it from the readme
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  grep -B 100 '$Id' README > "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

