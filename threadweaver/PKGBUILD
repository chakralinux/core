# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=threadweaver
pkgver=5.51.0
pkgrel=1
pkgdesc='High-level multithreading framework'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/threadweaver'
license=('LGPL')
depends=("qt5-base")
makedepends=("extra-cmake-modules>=5.51.0")
checkdepends=("cmake")
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('95f6b2fd7fc94c1d81852c061d5e368bb5c87d5042e34e29ab7aa16ba452aa9d'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
