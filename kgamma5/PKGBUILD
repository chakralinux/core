
pkgname=kgamma5
_pkgname=kgamma
pkgver=5.14.3
pkgrel=1
pkgdesc="Adjust your monitor's gamma settings"
arch=(x86_64)
url='https://projects.kde.org/kgamma5'
license=(LGPL)
depends=(kconfigwidgets)
makedepends=(extra-cmake-modules python3 kdoctools)
replaces=('kdegraphics-kgamma')
conflicts=('kdegraphics-kgamma')
groups=('plasma')
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('986ca88140541279b8ae152d13ae33793df6d5787ddafb43c065dac5a231280f'
             'SKIP' )
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}
package() {
  cd build
  make DESTDIR="$pkgdir" install
}
