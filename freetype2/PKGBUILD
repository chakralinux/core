# Maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Maintainer: bohoomil <@zoho.com>

# Installation order: freetype2-iu, fontconfig-iu, cairo-iu
pkgname=freetype2
pkgver=2.5.3
pkgrel=1
pkgdesc="TrueType font rendering library with Infinality patches and custom settings by bohoomil (infinality-bundle)."
arch=(x86_64)
license=('GPL' 'MIT')
groups=('infinality-bundle')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh' 'xorg-server-utils' 'libpng' 'harfbuzz')
install='infinality.install'
backup=('etc/profile.d/infinality-settings.sh')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        infinality-settings.sh
        freetype-2.5.3-enable-valid.patch
        upstream-15.05.2014.patch
        infinality-2.5.3.patch)
sha1sums=('d3c26cc17ec7fe6c36f4efc02ef92ab6aa3f4b46'
          '8c15e24f37a3c0f74e5f901190247dc566cac79b'
          'abf7a8f726ad6359533651a8942636880febf9f6'
          '06cc4283cb797fbdc9c4dcef9730e9b41f45078f'
          'cff259cd69dba17e4b284619cf8f0adccd6af0ed')

prepare() {
  cd "${srcdir}/freetype-${pkgver}"

  patches=(freetype-2.5.3-enable-valid.patch
           upstream-15.05.2014.patch
           infinality-2.5.3.patch)

  # infinality & post release fixes
  for patch in "${patches[@]}"; do
    patch -Np1 -i ${srcdir}/${patch}
  done
}

build() {
  cd "${srcdir}/freetype-${pkgver}"

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm755 "${srcdir}/infinality-settings.sh" "${pkgdir}/etc/profile.d/infinality-settings.sh"
}
