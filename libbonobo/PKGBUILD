# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor from Arch: Jan de Groot <jgc@archlinux.org>

pkgname=libbonobo
_pkgver=2.32
pkgver="${_pkgver}.1"
pkgrel=1
pkgdesc="A set of language and system independant CORBA interfaces for creating reusable components"
arch=(x86_64)
license=('GPL' 'LGPL')
depends=('orbit2' 'libxml2' 'dbus-glib' 'glib2')
makedepends=('intltool' 'pkgconfig' 'flex')
options=('!libtool')
backup=('etc/bonobo-activation/bonobo-activation-config.xml')
replaces=('bonobo-activation')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/libbonobo/${_pkgver}/libbonobo-${pkgver}.tar.bz2
	bonobo-activation-config.xml)
install=libbonobo.install
sha256sums=('9160d4f277646400d3bb6b4fa73636cc6d1a865a32b9d0760e1e9e6ee624976b'
            '74895825706b60574f6075f1230c9fe3dc5433018c6ac5ac1d87f615bd9d1082')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --libexecdir=/usr/lib/bonobo
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 "${srcdir}/bonobo-activation-config.xml" "${pkgdir}/etc/bonobo-activation/"
}
