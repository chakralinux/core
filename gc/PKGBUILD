# Maintainer: Francesco Marinucci <franzmari[at]chakra-project[dot]it>
# Maintainer: Daniel Isenmann <daniel [at] archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgname=gc
pkgver=7.6.0
pkgrel=1
pkgdesc="A garbage collector for C and C++"
arch=('x86_64')
url="http://www.hboehm.info/gc/"
license=('GPL')
depends=('gcc-libs' 'libatomic_ops')
#source=(https://github.com/ivmai/bdwgc/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
source=(http://www.hboehm.info/gc/gc_source/${pkgname}-${pkgver}.tar.gz)
md5sums=('bf46ccbdaccfa3186c2ab87191c8855a')

prepare() {
  cd ${pkgname}-${pkgver}
  
  sed -i 's#pkgdata#doc#' doc/doc.am
}

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -fi

  #https://github.com/ivmai/bdwgc/issues/87
  CXXFLAGS+=' -std=gnu++98' ./configure --prefix=/usr --enable-cplusplus --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  sed -i -e 's/GC_MALLOC 1L/gc 3/' doc/gc.man
  install -m755 -d "${pkgdir}/usr/share/man/man3"
  install -m644 doc/gc.man "${pkgdir}/usr/share/man/man3/gc.3"
}
