
pkgname=knotifyconfig
pkgver=5.48.0
pkgrel=1
pkgdesc='Configuration system for KNotify'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/knotifyconfig'
license=('LGPL')
depends=('kio' 'phonon-qt5')
makedepends=("extra-cmake-modules>=5.48.0")
checkdepends=('cmake')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('95c12bf941a8607c2fd6340b755b8987d45607815fead2c456d1e64fc3ad9fe6'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
