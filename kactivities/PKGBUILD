
pkgname=kactivities
pkgver=5.48.0
pkgrel=1
arch=('x86_64')
pkgdesc="Core components for the KDE's Activities"
url='https://projects.kde.org/projects/frameworks/kactivities'
license=('LGPL')
depends=('kcmutils' 'kdeclarative' 'kactivitymanagerd>=5.5.95')
makedepends=("extra-cmake-modules>=5.48.0" 'boost' 'python3')
provides=("kactivities-frameworks=${pkgver}" 'libkactivities4')
replaces=("kactivities-frameworks" 'kactivities4' 'libkactivities4')
conflicts=('kactivities4' 'kactivities-frameworks' 'libkactivities4')
options=("debug")
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('e87ba05b07fa484b617b1fc48997de9dcf921c8c0e877d1a827010893effadff'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>


prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 
  make
}

package() {
  cd build
  
  make  DESTDIR="${pkgdir}"  install
}
