# Contributions from ArchLinux: https://git.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/mobile-broadband-provider-info

pkgname=mobile-broadband-provider-info
pkgver=20170310
pkgrel=1
pkgdesc="Network Management daemon"
arch=(any)
license=(custom)
url="https://git.gnome.org/browse/mobile-broadband-provider-info"
makedepends=(git)
checkdepends=(libxml2)
_commit=befcbbc9867e742ac16415660b0b7521218a530c  # tags/20170310^0
source=("git+https://git.gnome.org/browse/mobile-broadband-provider-info#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

check() {
  cd $pkgname
  make check
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
