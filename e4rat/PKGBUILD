pkgname=e4rat
pkgver=0.2.3
pkgrel=7
pkgdesc="Toolset to accelerate the boot process and application startups for systems with ext4 root partitions."
arch=('x86_64')
url="http://e4rat.sourceforge.net/"
license=('GPL')
depends=('e2fsprogs' 'boost-libs')
makedepends=('audit' 'boost' 'cmake')
options=('!strip')
backup=(etc/e4rat.conf)
source=( "http://downloads.sourceforge.net/project/$pkgname/$pkgver/e4rat_${pkgver}_src.tar.gz" \
        'boost-fs3-v2.patch'
        'cxx.patch' )

prepare () {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/boost-fs3-v2.patch"
  patch -Np1 -i "$srcdir/cxx.patch"
  [[ ! -d build ]] && mkdir build
}

build() {
  cd "$srcdir/$pkgname-$pkgver/build"

  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_CORE_LIBRARY_STATIC=true ..
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"/build

  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/var/lib/$pkgname"
}

sha256sums=('f6feda9618eff0f9442baf771b4776b634e5a2f39e678c10e4c7946b2c6a96a0'
            '9a9786d6167e4a47d1733735745b0eadeabf487886405df9e6771a28f3a20965'
            '19d163527955a9dfdb7a7c6e35f8e7b674422a8c5bd7888293e9e30fe58c4663')
