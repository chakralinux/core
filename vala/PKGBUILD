# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=vala
pkgver=0.23.1
pkgrel=1
pkgdesc="Compiler for the GObject type system."
arch=('x86_64')
url="http://live.gnome.org/Vala"
license=('LGPL')
depends=('glib2')
makedepends=('libxslt')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('3339d98b43038b4cb72b4c9bc40309d4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # No, please DON'T mark this as a development version only because Git tells you so;
  # Git is only watching my chroot status, not Vala's repository directory...
  sed -i 's#v="\$v-dirty" ;;$#;;#g' build-aux/git-version-gen

  ./configure --prefix=/usr --enable-vapigen

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
