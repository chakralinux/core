#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=vala
pkgver=0.15.0
pkgrel=2
pkgdesc="Compiler for the GObject type system."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Vala"
license=('LGPL')
depends=('glib2')
makedepends=('libxslt')
options=('!libtool')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('828f7341a3c4fc14b6d0f5a14ce3bdf4c40f5eae3e7ccf11c2b49061df9d2e23')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # No, please DON'T mark this as a development version only because Git tells you so;
  # Git is only watching my chroot status, not Vala's repository directory...
  sed -i 's#v="\$v-dirty" ;;$#;;#g' build-aux/git-version-gen

  ./configure --prefix=/usr --enable-vapigen

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
