pkgname=vala
pkgver=0.42.2
pkgrel=1
pkgdesc="Compiler for the GObject type system."
arch=('x86_64')
url="https://wiki.gnome.org/Projects/Vala"
license=('LGPL')
depends=('glib2' 'graphviz' 'ttf-font' 'pkg-config' 'gcc')
makedepends=('libxslt' 'gtk-doc' 'help2man')
checkdepends=('dbus' 'libx11' 'gobject-introspection')
source=(https://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('9e89aa42152b1cef551568f827aa2deea2a9b5487d78c91474c8617b618e5f07')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

check() {
    cd $pkgname-$pkgver
    make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
