#
# Core Packages for Chakra, part of chakraos.org

pkgname=procps-ng
pkgver=3.3.10
pkgrel=1
pkgdesc='Utilities for monitoring your system and its processes'
url='http://sourceforge.net/projects/procps-ng/'
license=('GPL' 'LGPL')
arch=('x86_64')
groups=('base')
depends=('systemd' 'ncurses')
source=("http://downloads.sourceforge.net/project/${pkgname}/Production/${pkgname}-${pkgver}.tar.xz")
sha1sums=('484db198d6a18a42b4011d5ecb2cb784a81b0e4f')

conflicts=('procps')
provides=('procps')
replaces=('procps')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's:<ncursesw/:<:g' -i watch.c
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--exec-prefix=/ \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
                --enable-watch8bit \
		--with-systemd
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# provided by util-linux
	rm "${pkgdir}/usr/bin/kill"
	rm "${pkgdir}/usr/share/man/man1/kill.1"
}
