pkgname=procps-ng
pkgver=3.3.15
pkgrel=1
pkgdesc='Utilities for monitoring your system and its processes'
url='https://gitlab.com/procps-ng/procps'
license=('GPL' 'LGPL')
arch=('x86_64')
groups=('base')
depends=('libsystemd' 'ncurses')
makedepends=('systemd')
source=("https://downloads.sourceforge.net/project/${pkgname}/Production/${pkgname}-${pkgver}.tar.xz")
sha1sums=('2929bc64f0cf7b2db997eef79b7187658e47230d')

conflicts=('procps')
provides=('procps')
replaces=('procps')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's:<ncursesw/:<:g' -i watch.c
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--exec-prefix=/ \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
                --enable-watch8bit \
		--with-systemd \
		--disable-kill
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
