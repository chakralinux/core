#
# Core Packages for Chakra, part of chakra-project.org

pkgname=procps-ng
pkgver=3.3.7
pkgrel=1
pkgdesc='Utilities for monitoring your system and its processes'
url='http://gitorious.org/procps'
license=('GPL' 'LGPL')
arch=('x86_64')
groups=('base')
depends=('ncurses')
backup=('etc/sysctl.conf')
source=("procps-ng-${pkgver}.tar.gz::http://gitorious.org/procps/procps/archive-tarball/v${pkgver}"
        'sysctl.conf')
sha1sums=('436ce9e585210340ff1673157e402e07879a6239'
          '97ff07bab9aa5daa8d54a1346f73ba74f8e12a53')

conflicts=('procps')
provides=('procps')
replaces=('procps')

options=('!libtool')

build() {
	cd "${srcdir}/procps-procps"
	echo ${pkgver} > .tarball-version

	./autogen.sh
	./configure \
		--prefix=/usr \
		--exec-prefix=/ \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--disable-static
	make
}

package() {
	cd "${srcdir}/procps-procps"
	make DESTDIR="${pkgdir}" install
	install -D -m644 ../sysctl.conf "${pkgdir}/etc/sysctl.conf"

	# provided by util-linux
	rm "${pkgdir}/usr/bin/kill"
	rm "${pkgdir}/usr/share/man/man1/kill.1"
}
