# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kemoticons
pkgver=${KFVersion}
pkgrel=1
pkgdesc='Support for emoticons and emoticons themes'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kemoticons'
license=('LGPL')
depends=('karchive' 'kservice')
makedepends=('extra-cmake-modules' 'python3')
checkdepends=('cmake' 'kded')
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgname}-${pkgver}.tar.xz")
sha256sums=( $(getSum ${pkgname}) )

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

check() {
  cd build
  eval `dbus-launch --sh-syntax`
  xvfb-run -s "-screen 0 640x480x24" sh -c 'kded5 & sleep 3; make test'
  kill $DBUS_SESSION_BUS_PID
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
