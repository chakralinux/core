
pkgname=qt5-webkit
_qtver=5.212.0-alpha2
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
options=("debug")
depends=(qt5-location qt5-sensors qt5-webchannel libwebp libxslt libxcomposite gst-plugins-base hyphen)
makedepends=(cmake ruby gperf python2 qt5-doc qt5-tools)
optdepends=('gst-plugins-good: For media format support'
            'gst-plugins-bad: For media format support'
            'gst-plugins-ugly: For media format support'
            'gst-libav: For media format support')
_pkgfqn="${pkgname/5-/}-opensource-src-${pkgver}"
source=("https://github.com/annulen/webkit/releases/download/qtwebkit-$_qtver/qtwebkit-$_qtver.tar.xz" qt5-webkit-gcc7.patch)
sha256sums=('f8f901de567e11fc5659402b6b827eac75505ff9c5072d8e919aa306003f8f8a'
            'f108833562d766d4c086026f9202b2ebc8c19e97fddedce952afed78df3c0720')

prepare() {
  [[ -d build ]] && rm -rf build
  mkdir -p build

# Fix crashes with GCC7 https://bugs.webkit.org/show_bug.cgi?id=173407
  cd qtwebkit-$_qtver
  patch -p1 -i ../qt5-webkit-gcc7.patch
}

build() {
  cd build

  cmake ../qtwebkit-$_qtver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=Qt \
    -DENABLE_TOOLS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}

# Fix pkgconfig files
  sed -e 's|qt/Qt5WebKit|qt/QtWebKit|' -i "$pkgdir"/usr/lib/pkgconfig/Qt5WebKit.pc
  sed -e 's|qt/Qt5WebKitWidgets|qt/QtWebKitWidgets|' -i "$pkgdir"/usr/lib/pkgconfig/Qt5WebKitWidgets.pc
  sed -e '/Name/a Description: Qt WebKit module' -i "$pkgdir"/usr/lib/pkgconfig/Qt5WebKit.pc
  sed -e '/Name/a Description: Qt WebKitWidgets module' -i "$pkgdir"/usr/lib/pkgconfig/Qt5WebKitWidgets.pc
}
