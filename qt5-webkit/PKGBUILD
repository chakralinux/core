source ../qt5.conf

pkgname=qt5-webkit
pkgver=${QSubVersion}
pkgrel=1
arch=('x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
license=('LGPL2.1' 'GPL3')
options=("debug")
depends=('qt5-location' 'qt5-sensors' 'qt5-webchannel' 'libwebp' 'libxslt' 'libxcomposite' 'gst-plugins-base')
makedepends=('qt5-tools' 'ruby' 'gperf' 'python2')
optdepends=('gst-plugins-good: For media format support'
            'gst-plugins-bad: For media format support'
            'gst-plugins-ugly: For media format support'
            'gst-libav: For media format support')
_pkgfqn="${pkgname/5-/}-opensource-src-${pkgver}"
source=("("http://download.qt.io/community_releases/${pkgver%.*}/${pkgver}/qtwebkit-opensource-src-${pkgver}.tar.xz"")
md5sums=('9379b8829639645c184fa63532035098')

prepare() {
  [[ -d build ]] && rm -rf build
  mkdir -p build
}

build() {
  cd build
  qmake-qt5 ../${_pkgfqn}

  make
  make docs
}

package() {
  cd build
  make INSTALL_ROOT="${pkgdir}" install
  make INSTALL_ROOT="${pkgdir}" install_docs
  
  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;
  
  # Fix wrong path in pc file
  perl -pi -e "s, -L${srcdir}/?\S+,,g" "${pkgdir}"/usr/lib/pkgconfig/Qt5WebKit.pc

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
