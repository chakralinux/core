
pkgname=qt5-translations
pkgver=5.11.3
pkgrel=2
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework (Translations)'
depends=('qt5-base')
makedepends=('qt5-tools')
groups=('qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${pkgver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/submodules/${_pkgfqn}.tar.xz")
md5sums=('e746cb72991b7a30e05bfd743c548b0f')

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake-qt5 ../${_pkgfqn}  
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
