#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=imagemagick
pkgver=6.8.6.4
pkgrel=2
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="http://www.imagemagick.org/"
license=('custom')
depends=('libltdl' 'lcms' 'lcms2' 'libxt' 'gcc-libs' 'bzip2' 'freetype2' 'fontconfig'
	 'libxext' 'fftw' 'liblqr' 'libwmf' 'librsvg' 'libxml2' 'perl' )
makedepends=('ghostscript' 'openexr' 'jasper')
optdepends=('ghostscript: for Ghostscript support' 
            'openexr: for OpenEXR support'  
            'jasper: for JPEG-2000 support')
options=('!makeflags' '!docs')
source=("ftp://ftp.sunet.se/pub/multimedia/graphics/ImageMagick/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz"
	'perlmagick.rpath.patch')
sha1sums=('ab62d1777ef3ae18820bdb9f757ac65f9d2f944d'
          '23405f80904b1de94ebd7bd6fe2a332471b8c283')

prepare() {
	cd ImageMagick-${pkgver%.*}-${pkgver##*.}
	sed '/AC_PATH_XTRA/d' -i configure.ac
	autoreconf --force --install
	patch -p0 -i "${srcdir}/perlmagick.rpath.patch"
}

build() {
	cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

	./configure --prefix=/usr \
		--with-modules \
		--disable-static \
	    	--enable-openmp \
		--with-x \
		--with-wmf \
		--with-openexr \
		--with-xml \
	    	--with-gslib \
		--with-gs-font-dir=/usr/share/fonts/Type1 \
	    	--with-perl \
		--with-perl-options="INSTALLDIRS=vendor" \
	    	--without-gvc \
		--without-djvu \
		--without-autotrace \
		--with-jp2 \
	    	--without-jbig \
		--without-fpx \
		--without-dps \
		--without-fftw 
	make 
}

package() {
	cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

	make DESTDIR=${pkgdir} install 

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
	install -Dm644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE 

	#Cleaning
	rm -f ${pkgdir}/usr/lib/*.la 
}
