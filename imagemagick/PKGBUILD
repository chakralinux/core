pkgname=imagemagick
pkgver=7.0.8.13
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="http://www.imagemagick.org/"
license=('custom')
depends=('perl' 'libltdl' 'lcms2' 'libxt' 'fontconfig' 'libxext' 'liblqr' 'libcl' 'fftw')
makedepends=('lcms2' 'fontconfig' 'ghostscript'
             'openexr' 'libwmf' 'librsvg' 'libxml2' 'opencl-headers' 'libwebp')
optdepends=('ghostscript: for Ghostscript support'
            'openexr: for OpenEXR support'
            'jasper: for JPEG-2000 support'
            'libwmf: for WMF support'
            'librsvg: for SVG support'
            'libxml2: for XML support'
            'libpng: for PNG support'
            'libwebp: for WEBP support')
options=('libtool' '!emptydirs')
source=(https://github.com/ImageMagick/ImageMagick/archive/${pkgver%.*}-${pkgver##*.}.tar.gz)
sha1sums=('66d8c10fc248da208cf8fe12039169522252008e')
backup=("etc/ImageMagick-${pkgver%%.*}/coder.xml"
        "etc/ImageMagick-${pkgver%%.*}/colors.xml"
        "etc/ImageMagick-${pkgver%%.*}/delegates.xml"
        "etc/ImageMagick-${pkgver%%.*}/log.xml"
        "etc/ImageMagick-${pkgver%%.*}/magic.xml"
        "etc/ImageMagick-${pkgver%%.*}/mime.xml"
        "etc/ImageMagick-${pkgver%%.*}/policy.xml"
        "etc/ImageMagick-${pkgver%%.*}/quantization-table.xml"
        "etc/ImageMagick-${pkgver%%.*}/thresholds.xml"
        "etc/ImageMagick-${pkgver%%.*}/type.xml"
        "etc/ImageMagick-${pkgver%%.*}/type-dejavu.xml"
        "etc/ImageMagick-${pkgver%%.*}/type-ghostscript.xml"
        "etc/ImageMagick-${pkgver%%.*}/type-windows.xml")

prepare() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf --force --install
}

build() {
  cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-hdri \
    --enable-opencl \
    --with-dejavu-font-dir=/usr/share/fonts/TTF \
    --with-fftw \
    --with-gs-font-dir=/usr/share/fonts/Type1 \
    --with-gslib \
    --with-openjp2 \
    --with-modules \
    --with-openexr \
    --with-perl \
    --with-perl-options="INSTALLDIRS=vendor" \
    --with-rsvg \
    --with-webp \
    --with-wmf \
    --with-xml \
    --without-autotrace \
    --without-dps \
    --without-fpx \
    --without-gvc \
    --without-jbig
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE
}
