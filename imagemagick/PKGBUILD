# Maintainer: Chaoting Liu <brli@sourceforge.net>

pkgname=imagemagick
pkgver=6.8.8.9
pkgrel=4
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="http://www.imagemagick.org/"
license=('custom')
depends=('libltdl' 'lcms' 'lcms2' 'libxt' 'gcc-libs' 'bzip2' 'freetype2' 'fontconfig'
         'libxext' 'fftw' 'liblqr' 'libwmf' 'librsvg' 'libxml2' 'perl' )
makedepends=('ghostscript' 'openexr' 'jasper' 'subversion' 'ttf-dejavu')
optdepends=('ghostscript: for Ghostscript support'
            'openexr: for OpenEXR support'
            'jasper: for JPEG-2000 support'
            'libwmf: for WMF support'
            'librsvg: for SVG support'
            'libxml2: for XML support'
            'libpng: for PNG support'
            'libwebp: for WEBP support')
options=('!makeflags' '!docs')
source=(ftp://ftp.sunet.se/pub/multimedia/graphics/ImageMagick/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz{,.asc}
        'perlmagick.rpath.patch')
sha1sums=('3ad2deadbc7e7c6e61eb5590fcc49d71fd96b247'
          'SKIP'
          '23405f80904b1de94ebd7bd6fe2a332471b8c283')

prepare() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf --force --install
  patch -p0 -i "${srcdir}/perlmagick.rpath.patch"
}

build() {
  cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --localstatedir=/var \
    --enable-hdri \
    --with-modules \
    --disable-static \
    --enable-openmp \
    --with-x \
    --with-wmf \
    --with-openexr \
    --with-xml \
    --with-gslib \
    --with-gs-font-dir=/usr/share/fonts/Type1 \
    --with-perl \
    --with-perl-options="INSTALLDIRS=vendor" \
    --without-gvc \
    --with-dejavu-font-dir=/usr/share/fonts/TTF \
    --without-autotrace \
    --with-jp2 \
    --without-jbig \
    --without-fpx \
    --without-dps \
    --with-fftw
  make
}

package() {
  cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE
}
