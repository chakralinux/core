#
# Platform Packages for Chakra, part of chakra-project.org
#
# Maintainer:  Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org
# Contributor: Phil Miller <philm[at]chakra-project[dog]org>
# Contributor: Drake Justice <djustice[at]chakra-linux[dot]org>


pkgname=texlive-fontsextra
pkgver=2013.30955
_revnr=${pkgver#*.}
pkgrel=1
pkgdesc="TeX Live - all sorts of extra fonts"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps")
options=('!emptydirs')
options=(!strip)
install=texlive.install
md5sums=('9e10d4fb01d87179bfd8e184e1be9dd7'
         '2135455716c1e7356ce6a9c213827904')

build() {
   cd $srcdir
   for p in *.tar.xz; do
       bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source}
}

package() {
   cd "$srcdir"

   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps $pkgdir/var/lib/texmf/arch/installedpkgs/
   install -m755 -d $pkgdir/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done)
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
}
