#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=pcmciautils
pkgver=018
pkgrel=3
pkgdesc="Utilities for inserting and removing PCMCIA cards"
arch=('i686' 'x86_64')
url="http://kernel.org/pub/linux/utils/kernel/pcmcia/pcmcia.html"
license=('GPL')
groups=('base')
depends=('sysfsutils' 'udev')
conflicts=('pcmcia-cs')
options=(!makeflags)
# The sources on kernel.org seem to be missing after the server got hacked...
#source=("http://kernel.org/pub/linux/utils/kernel/pcmcia/${pkgname}-${pkgver}.tar.bz2")
source=("http://ftp.de.debian.org/debian/pool/main/p/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
        'initcpio-install-pcmcia')
md5sums=('3c388cb559975b37a75cb5965e9800a9'
         '2659576a4637c58c4e79a96f8e43e316')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's,/usr/bin/install,install,g;
             s,/lib/udev,/usr/lib/udev,g' Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # install the mkinitpcio hook
  install -D -m644 ../initcpio-install-pcmcia ${pkgdir}/usr/lib/initcpio/install/pcmcia
}

