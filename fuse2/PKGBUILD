# Arch contributor: /chakra/core/e4rat/src/e4rat-0.2.3/src/logging.hh:96:51:

pkgname=fuse2
pkgver=2.9.7
pkgrel=2
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('x86_64')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
provides=(fuse=$pkgver) # TODO: remove it once all packages start to use 'fuse2' name
replaces=('fuse')
conflicts=('fuse')
depends=('glibc' 'fuse-common')
makedepends=('pkg-config')
options=(!emptydirs)
source=(https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.gz{,.asc})
sha1sums=('cd174e3d37995a42fad32fac92f76cd18e24174f'
          'SKIP')
validpgpkeys=(ED31791B2C5C1613AF388B8AD113FCAC3C4E599F) # Nikolaus Rath <Nikolaus@rath.org>

build() {
  cd fuse-$pkgver

  export MOUNT_FUSE_PATH=/usr/bin
  ./configure --prefix=/usr --libdir=/usr/lib --enable-lib --enable-util --disable-example
  make
}

package() {
  cd fuse-$pkgver

  make DESTDIR=${pkgdir} install

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -r ${pkgdir}/etc/init.d

  # remove udev rules (is in the udev package}
  rm -rf ${pkgdir}/etc/udev

  # static device nodes are handled by udev
  rm -r ${pkgdir}/dev

  # part of fuse-common package
  rm ${pkgdir}/usr/share/man/man8/mount.fuse.8 ${pkgdir}/usr/bin/mount.fuse
}
