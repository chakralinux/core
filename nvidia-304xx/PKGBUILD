# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
source ../linux.conf

pkgname=('nvidia-304xx' 'chd-nvidia-304xx')
pkgver=304.131
pkgrel=4
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=("linux-headers=${_kernelver}")
license=('custom')
_pkg="NVIDIA-Linux-${arch}-${pkgver}-no-compat32"
source=("http://git.chakraos.org/chakra-hardware-detection.git/blob_plain/HEAD:/hwdb/nvidia_supported"
        "ftp://download.nvidia.com/XFree86/Linux-${arch}/${pkgver}/${_pkg}.run"
         'nv-drm.patch' 'disable-mtrr.patch' 'linux-4.6.patch')
md5sums=('62d0cdb856cfae64753c0f3acf729d47'
         '24c9c6a8679edae3b2a608b191fdc727'
         '79671a27131da619a33eb02ed0c2c031'
         'c4becf1145a139cc0121be9ad340bcd8'
         '63041b8272d0177cd776114f418ffece')

prepare() {
    cd "${srcdir}"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    # patches here
    patch -p0 -i "$srcdir/nv-drm.patch"
    patch -p1 --no-backup-if-mismatch -i ../linux-4.6.patch
    cd kernel
    patch -p1 --no-backup-if-mismatch -i "$srcdir"/disable-mtrr.patch
}

build() {
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kver}/build" module
}

package_nvidia-304xx() {
    pkgdesc="NVIDIA drivers for linux."
    depends=("linux=${_kernelver}" "nvidia-304xx-libgl"  "nvidia-304xx-utils=${pkgver}")
    conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nouveau_blacklist.conf"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}

package_chd-nvidia-304xx() {
    pkgdesc="CHD ids for nvidia $pkgver"
    arch=('x86_64')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/chakra/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-kernel.o \
        > "${pkgdir}/opt/chakra/hwdb/hw_nvidia304xx_hwdb"
}
