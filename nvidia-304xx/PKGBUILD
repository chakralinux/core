# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
source ../linux.conf

pkgname=('nvidia-304xx' 'chd-nvidia-304xx')
pkgver=304.135
pkgrel=1
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=("linux-headers=${_kernelver}")
license=('custom')
_pkg="NVIDIA-Linux-${arch}-${pkgver}-no-compat32"
source=("https://chakralinux.org/code/chakra-hardware-detection.git/plain/hwdb/nvidia_supported"
        "ftp://download.nvidia.com/XFree86/Linux-${arch}/${pkgver}/${_pkg}.run"
        'drm-driver-legacy.patch'
        'kernel_4.10.patch')
md5sums=('62d0cdb856cfae64753c0f3acf729d47'
         '8ee9bd0b020508bca9953181811422fa'
         'a5328715e210e36e5d9bbed0f9e55223'
         'a8d331a75fbc541f1730e95b95e9ef14')

prepare() {
    cd "${srcdir}"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    # patches here
    patch -Np1 --no-backup-if-mismatch -i ../kernel_4.10.patch

    # FS#47092
    cd kernel
    patch -p1 --no-backup-if-mismatch -i "$srcdir"/drm-driver-legacy.patch

}

build() {
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kver}/build" module
}

package_nvidia-304xx() {
    pkgdesc="NVIDIA drivers for linux."
    depends=("linux=${_kernelver}" "nvidia-304xx-libgl"  "nvidia-304xx-utils=${pkgver}")
    conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nouveau_blacklist.conf"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}

package_chd-nvidia-304xx() {
    pkgdesc="CHD ids for nvidia $pkgver"
    arch=('x86_64')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/chakra/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-kernel.o \
        > "${pkgdir}/opt/chakra/hwdb/hw_nvidia304xx_hwdb"
}
