#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

_extramodules=extramodules-3.7-CHAKRA
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=('nvidia-304xx' 'chd-nvidia-304xx')
pkgver=304.64
pkgrel=1
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=('linux-headers' 'linux>=3.7' 'linux<3.8' "nvidia-304xx-utils=${pkgver}")
license=('custom')
source=("https://gitorious.org/chakra/chakra-hardware-detection/blobs/raw/master/hwdb/nvidia_supported"
        'uapi-3.7.patch')
md5sums=('62d0cdb856cfae64753c0f3acf729d47'
        '42f2ad1cf9b48b1da8ed82200a045a8a')

    _ARCH='x86_64'
    _pkg="NVIDIA-Linux-${_ARCH}-${pkgver}-no-compat32"
    source+=("ftp://download.nvidia.com/XFree86/Linux-${_ARCH}/${pkgver}/${_pkg}.run")
    md5sums+=('76007ed4b72afd334e26377b646f5f7e')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    patch -Np0 -i "${srcdir}/uapi-3.7.patch"
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package_nvidia-304xx() {
    pkgdesc="NVIDIA drivers for linux."
    depends=('linux>=3.7' 'linux<3.8' "nvidia-304xx-utils=${pkgver}")
    conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

package_chd-nvidia-304xx() {
    pkgdesc="CHD ids for nvidia $pkgver"
    arch=('any')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/chakra/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-kernel.o \
        > "${pkgdir}/opt/chakra/hwdb/hw_nvidia304xx_hwdb"
}
