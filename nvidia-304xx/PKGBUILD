# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

_extramodules=extramodules-3.15.1-CHAKRA
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=('nvidia-304xx' 'chd-nvidia-304xx')
pkgver=304.121
pkgrel=3
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=('linux-headers' 'linux>=3.15' 'linux<3.16' "nvidia-304xx-utils=${pkgver}")
license=('custom')
source=("https://gitorious.org/chakra/chakra-hardware-detection/blobs/raw/master/hwdb/nvidia_supported")
_pkg="NVIDIA-Linux-${arch}-${pkgver}-no-compat32"
source+=("ftp://download.nvidia.com/XFree86/Linux-${arch}/${pkgver}/${_pkg}.run")
md5sums=('62d0cdb856cfae64753c0f3acf729d47'
         '41e6d2b6325e5b0ae9889cfbd6bec696')

build() {
    cd "${srcdir}"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package_nvidia-304xx() {
    pkgdesc="NVIDIA drivers for linux."
    depends=('linux>=3.15' 'linux<3.16' "nvidia-304xx-utils=${pkgver}")
    conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

package_chd-nvidia-304xx() {
    pkgdesc="CHD ids for nvidia $pkgver"
    arch=('x86_64')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/chakra/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-kernel.o \
        > "${pkgdir}/opt/chakra/hwdb/hw_nvidia304xx_hwdb"
}
