#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>
# contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord
pkgver=0.1.28
pkgrel=1
pkgdesc="System service easily manage, install and generate color profiles to manage input and output devices"
arch=('x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lcms2' 'polkit' 'shared-color-profiles' 'sqlite3' 'systemd' 'libgusb')
makedepends=('intltool' 'gobject-introspection' 'vala' 'docbook-xsl')
source=($url/releases/$pkgname-$pkgver.tar.xz)
options=('!libtool')
sha1sums=('d53167727ab0f1014ded86675ac8b518c188a88b')

build() {
  cd "$pkgname-$pkgver"
  
  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc --libexecdir=/usr/lib/colord \
    --localstatedir=/var --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install \
    bashcompletiondir=/usr/share/bash-completion/completions
}

