#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>
# contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord
pkgver=1.0.6
pkgrel=1
pkgdesc="System daemon for managing color devices"
arch=('x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lcms2' 'polkit' 'shared-color-profiles' 'sqlite3' 'systemd' 'libgusb' 'dbus')
makedepends=('intltool' 'gobject-introspection' 'vala' 'docbook-xsl' 'sane' 'bash-completion')
optdepends=('sane: UseSANE support [/etc/colord.conf]')
install=colord.install
backup=(etc/colord.conf)
source=($url/releases/$pkgname-$pkgver.tar.xz{,.asc}
        udev-systemd.patch)
sha1sums=('b3ad9d1cd90ff0ea4d955b253dc0491d851c2778'
          'SKIP'
          '3b467b9674f767c804943d191beaf6bd1ba3a606')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../udev-systemd.patch
}

build() {
  cd "$pkgname-$pkgver"
  
  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-vala --enable-sane --disable-volume-search
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

