# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=libxrandr
pkgver=1.4.2
pkgrel=1
pkgdesc="X11 RandR extension library"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxext' 'libxrender' 'randrproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXrandr-${pkgver}.tar.bz2)
sha1sums=('00865ccefe1040e4d9866d878556710cdc4b2ae6')

build() {
  cd "${srcdir}/libXrandr-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/libXrandr-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
