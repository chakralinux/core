# Maintainer:
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Anke Boersma (abveritas) abveritas@chakra-project.org
#               Tobias Powalowski <tpowa@archlinux.org>
#               Sarah Hay <sarahhay@mb.sympatico.ca>
#               Simo L. <neotuli@yahoo.com>
#               eric <eric@archlinux.org>

pkgname=sane
pkgver=1.0.24
pkgrel=2
pkgdesc='Scanner Access Now Easy'
url='http://www.sane-project.org'
arch=('x86_64')
license=('GPL')
depends=('libtiff>=4.0.0'
         'libgphoto2'
         'libjpeg-turbo'
         'libusb-compat'
         'libcups'
         'libieee1284'
         'v4l-utils'
         'avahi'
         'bash'
         'net-snmp')
makedepends=('texlive-latexextra')
backup=(etc/sane.d/{abaton.conf,agfafocus.conf,apple.conf,artec.conf,artec_eplus48u.conf,avision.conf,bh.conf,canon.conf,canon630u.conf,canon_dr.conf,canon_pp.conf,cardscan.conf,coolscan2.conf,coolscan3.conf,coolscan.conf,dc25.conf,dc210.conf,dc240.conf,dell1600n_net.conf,dll.conf,dmc.conf,epjitsu.conf,epson.conf,epson2.conf,fujitsu.conf,genesys.conf,gphoto2.conf,gt68xx.conf,hp.conf,hp3900.conf,hp4200.conf,hp5400.conf,hpsj5s.conf,hs2p.conf,ibm.conf,kodak.conf,leo.conf,lexmark.conf,ma1509.conf,magicolor.conf,matsushita.conf,microtek.conf,microtek2.conf,mustek.conf,mustek_pp.conf,mustek_usb.conf,mustek_usb2.conf,nec.conf,net.conf,p5.conf,pie.conf,pixma.conf,plustek.conf,plustek_pp.conf,qcam.conf,ricoh.conf,rts8891.conf,s9036.conf,saned.conf,sceptre.conf,sharp.conf,sm3840.conf,snapscan.conf,sp15c.conf,st400.conf,stv680.conf,tamarack.conf,teco1.conf,teco2.conf,teco3.conf,test.conf,u12.conf,umax.conf,umax1220u.conf,umax_pp.conf,xerox_mfp.conf,v4l.conf} etc/xinetd.d/sane)
source=(https://alioth.debian.org/frs/download.php/file/3958/sane-backends-1.0.24.tar.gz
        'sane.xinetd'
        'saned.socket'
        'saned.service'
        'network.patch'
        'segfault-avahi-fix-kodakio.patch')
install=$pkgname.install
sha512sums=('1e9f962bb9d27f17067eb662dabfc0a1aa0d06e16bec4bd28868124b468578d82f14f6d2c1f5de63d2cec57ca4584ec12d3b2c2b1de20f1199aece581453ebc5'
            '7e1109fa5e3221849419149c7e67589c1b66e66cb1ac8c58c38cf36c4c249c7598c09431d997b31d55104fe3b0e357c6627ffe1fa9f76b8127c6c10c7c8b8465'
            '62654a59fae3216be9c0ae4f810375de1232133ab12732529a3e064cc83d94563560e01950dea4b25aa298294c5f0421436c5e095a3a00ad8ef78b471f3105f4'
            'db8c5310c3bfd4c8d4c354a07e59f0e0297caa614e8084dfb740904e59eed7c389a88cde6ef4ca62d20c0de6fe0b93a58bf9d542f3ae7a36b0d2247fbd9fdbdb'
            '04b65a06d2c76dde6fce501ba823e999eb89ea41573f887f9e7d5347f3aa5e35310fad69313e99f873640e29a9849372d2ff995678593a0781fba73b464bec0d'
            '43e91fe2452eca4fbfbd256aae3f44a28674af7210f59b60b34c1149f0cd208318678a810313aabff84f18bb712982e10ae320ef3179b6437e413adda6298fbe')

prepare() {
    cd "${srcdir}/${pkgname}-backends-${pkgver}"

    # fix http://vasks.debian.org/tracker/?func=detail&atid=410366&aid=313760&group_id=30186
    patch -Np1 -i ${srcdir}/network.patch

    # fix http://anonscm.debian.org/gitweb/?p=sane/sane-backends.git;a=commit;h=37523b867d411c2f82d08128246be7e38bc9812c
    # #37525
    # https://alioth.debian.org/tracker/?group_id=30186&atid=410366&func=detail&aid=314301
    patch -Np1 -i ${srcdir}/segfault-avahi-fix-kodakio.patch
}

build() {
    cd "${srcdir}/${pkgname}-backends-${pkgver}"

    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-docdir=/usr/share/doc/sane \
        --enable-avahi \
        --enable-pthread \
        --enable-libusb_1_0 \
        --disable-rpath \
        --disable-locking
    make
}

package () {
    cd "${srcdir}/${pkgname}-backends-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # Fix HP OfficeJets
    echo "#hpaio" >> "${pkgdir}/etc/sane.d/dll.conf"

    # Install udev files
    install -D -m0644 tools/udev/libsane.rules \
        "${pkgdir}/usr/lib/udev/rules.d/49-sane.rules"

    # Fix udev rules
    sed -i 's|NAME="%k", ||g' "${pkgdir}/usr/lib/udev/rules.d/49-sane.rules"

    # Install xinetd file
    install -D -m644 "${srcdir}/sane.xinetd" "${pkgdir}/etc/xinetd.d/sane"

    # Install the pkg-config file
    install -D -m644 tools/sane-backends.pc \
        "${pkgdir}/usr/lib/pkgconfig/sane-backends.pc"

    # Install systemd files
    install -D -m644 ${srcdir}/saned.socket \
        "${pkgdir}/usr/lib/systemd/system/saned.socket"
    install -D -m644 ${srcdir}/saned.service \
        "${pkgdir}/usr/lib/systemd/system/saned@.service"
}
