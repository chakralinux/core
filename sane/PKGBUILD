pkgname=sane
pkgver=1.0.25
pkgrel=1
pkgdesc='Scanner Access Now Easy'
url='http://www.sane-project.org'
arch=('x86_64')
license=('GPL')
depends=('libtiff>=4.0.0'
         'libgphoto2'
         'libjpeg-turbo'
         'libusb'
         'libcups'
         'libieee1284'
         'v4l-utils'
         'avahi'
         'bash'
         'net-snmp')
makedepends=('texlive-latexextra')
backup=(etc/sane.d/{abaton.conf,agfafocus.conf,apple.conf,artec.conf,artec_eplus48u.conf,avision.conf,bh.conf,canon.conf,canon630u.conf,canon_dr.conf,canon_pp.conf,cardscan.conf,coolscan2.conf,coolscan3.conf,coolscan.conf,dc25.conf,dc210.conf,dc240.conf,dell1600n_net.conf,dll.conf,dmc.conf,epjitsu.conf,epson.conf,epson2.conf,fujitsu.conf,genesys.conf,gphoto2.conf,gt68xx.conf,hp.conf,hp3900.conf,hp4200.conf,hp5400.conf,hpsj5s.conf,hs2p.conf,ibm.conf,kodak.conf,leo.conf,lexmark.conf,ma1509.conf,magicolor.conf,matsushita.conf,microtek.conf,microtek2.conf,mustek.conf,mustek_pp.conf,mustek_usb.conf,nec.conf,net.conf,p5.conf,pie.conf,pixma.conf,plustek.conf,plustek_pp.conf,qcam.conf,ricoh.conf,rts8891.conf,s9036.conf,saned.conf,sceptre.conf,sharp.conf,sm3840.conf,snapscan.conf,sp15c.conf,st400.conf,stv680.conf,tamarack.conf,teco1.conf,teco2.conf,teco3.conf,test.conf,u12.conf,umax.conf,umax1220u.conf,umax_pp.conf,xerox_mfp.conf,v4l.conf} etc/xinetd.d/sane)
source=(https://alioth.debian.org/frs/download.php/file/4146/sane-backends-${pkgver}.tar.gz
        'sane.xinetd'
        'saned.socket'
        'saned.service'
        'network.patch')
install=$pkgname.install
sha512sums=('b9548646fff13558a4da4a4e9e8bad730f05121bcc65b72d24ded3bfddc6fe574e08c39c8651544b8fbfeab036fffd76cc14d76ef5d5d47e7513a2ce3afcc9df'
            '8f9f18d432087e5445aa533be375a811daf320512069c108d41a79121868937e1a7ffa21ee7d33adcf9fbb670bca460ff06423ce39602f35151eccc243d9d4ec'
            '62654a59fae3216be9c0ae4f810375de1232133ab12732529a3e064cc83d94563560e01950dea4b25aa298294c5f0421436c5e095a3a00ad8ef78b471f3105f4'
            'c4cd4427ee8e5cb128c5d54006fafbb4f846508582dffc293f16ab14ab638e6ee3dc5df0eb06f6d0d3839941ab74623c7163ac13bcdfec87b10308587a697536'
            '04b65a06d2c76dde6fce501ba823e999eb89ea41573f887f9e7d5347f3aa5e35310fad69313e99f873640e29a9849372d2ff995678593a0781fba73b464bec0d')

prepare() {
    cd "${srcdir}/${pkgname}-backends-${pkgver}"

    # fix http://vasks.debian.org/tracker/?func=detail&atid=410366&aid=313760&group_id=30186
    patch -Np1 -i ${srcdir}/network.patch
}

build() {
    cd "${srcdir}/${pkgname}-backends-${pkgver}"

    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-docdir=/usr/share/doc/sane \
        --enable-avahi \
        --enable-pthread \
        --enable-libusb_1_0 \
        --disable-rpath \
        --disable-locking
    make
}

package () {
    cd "${srcdir}/${pkgname}-backends-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # Fix HP OfficeJets
    echo "#hpaio" >> "${pkgdir}/etc/sane.d/dll.conf"

    # Install udev files
    install -D -m0644 tools/udev/libsane.rules \
        "${pkgdir}/usr/lib/udev/rules.d/49-sane.rules"

    # Fix udev rules
    sed -i 's|NAME="%k", ||g' "${pkgdir}/usr/lib/udev/rules.d/49-sane.rules"

    # Install xinetd file
    install -D -m644 "${srcdir}/sane.xinetd" "${pkgdir}/etc/xinetd.d/sane"

    # Install the pkg-config file
    install -D -m644 tools/sane-backends.pc \
        "${pkgdir}/usr/lib/pkgconfig/sane-backends.pc"

    # Install systemd files
    install -D -m644 ${srcdir}/saned.socket \
        "${pkgdir}/usr/lib/systemd/system/saned.socket"
    install -D -m644 ${srcdir}/saned.service \
        "${pkgdir}/usr/lib/systemd/system/saned@.service"
}
