# Contributor: abveritas@chakra-project.org
# Maintainer: Bruce Liu <brli [AT] users.sourceforge.net>

pkgname=udisks2
pkgver=2.1.6
pkgrel=1
pkgdesc="Disk Management Service"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/udisks"
license=('GPL2')
depends=('glib2' 'systemd' 'polkit' 'libatasmart' 'util-linux' 'libgudev')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'python2')
optdepends=('parted: partition management'
            'gptfdisk: GUID partition table support'
            'ntfs-3g: NTFS filesystem management support'
            'dosfstools: VFAT filesystem management support')
source=("http://udisks.freedesktop.org/releases/udisks-$pkgver.tar.bz2"
        "http://udisks.freedesktop.org/releases/udisks-$pkgver.tar.bz2.sign"
        '50-udisks.rules')
md5sums=('ea181c13c43af039fded88f0ef546e24'
         'SKIP'
         '6b69afb6a0e0bcd325006af2bafc25f6')

build() {
  cd "${srcdir}/udisks-${pkgver}"
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --with-systemdsystemunitdir=/usr/lib/systemd/system --disable-static
  make
}

package() {
  cd "${srcdir}/udisks-${pkgver}"
  make DESTDIR="${pkgdir}" install
     bash_completiondir=/usr/share/bash-completion/completions
  
  #fix ntfs mounts
  install -dm 700 -o polkitd $pkgdir/etc/polkit-1/rules.d/
  install -Dm 644 $srcdir/50-udisks.rules $pkgdir/etc/polkit-1/rules.d/50-udisks.rules
}

