#
# Platform Packages for Chakra, part of www.chakra-project.org
#
# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>
#               F Kosmale (Inkane) <inkane@chakra-project.org>
#               A Scarpino (ascarpino) <andrea@archlinux.org>
#               D S de Andrade (dsa) <douglas@archlinux.org>
#               riai <riai@bigfoot.com>
#               Ben <ben@benmazer.net>
#               Gerardo
# NOTE: PyQt5 supports Qt v5. PyQt4 supports Qt v4 and will build against Qt v5.

pkgbase=pyqt
pkgname=('python-pyqt4-common' 'python2-pyqt4' 'python3-pyqt4')
pkgver=4.10.3
pkgrel=10
pkgdesc="A set of Python bindings for the Qt toolkit"
arch=('x86_64')
url='http://riverbankcomputing.co.uk/software/pyqt/intro'
license=('GPL')
makedepends=('dbus-python2'
             'dbus-python3'
             'mesa'
             'phonon'
             'python-opengl'
             'qt-assistant-compat'
             'qtwebkit'
             'python2-sip>=4.15' 
             'python3-sip>=4.15')
source=("http://sourceforge.net/projects/pyqt/files/PyQt4/PyQt-${pkgver}/PyQt-x11-gpl-${pkgver}.tar.gz")
sha512sums=('4edd3539e1855bc3358dcba866433e53d4b9444b867c14cc27115cf5a6c123b0af3bb0bae3085f6d71a31f0b6716da40809ac4ffcaaff415673ed7462f1a0420')

build() {
    cp -r ${srcdir}/PyQt-x11-gpl-${pkgver} ${srcdir}/python2-PyQt-x11-gpl-${pkgver}

    # Python 3 version
    cd ${srcdir}/PyQt-x11-gpl-${pkgver}
    msg "Configuring the Python 3 version"
    python3 configure.py \
        --confirm-license \
        --qsci-api \
        --verbose \
        --enable QtAssistant \
        --enable QtCore \
        --enable QtDBus \
        --enable QtDeclarative \
        --enable QtDesigner \
        --enable QtGui \
        --enable QtHelp \
        --enable QtMultimedia \
        --enable QtNetwork \
        --enable QtOpenGL \
        --enable QtScript \
        --enable QtScriptTools \
        --enable QtSql \
        --enable QtSvg \
        --enable QtTest \
        --enable QtWebKit \
        --enable QtXml \
        --enable QtXmlPatterns \
        --enable dbus \
        --enable phonon
    find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
    msg "Making the Python 3 version"
    make

    # Python 2 version
    cd ${srcdir}/python2-PyQt-x11-gpl-${pkgver}
    msg "Configuring the Python 2 version"
    python2 configure.py \
        --confirm-license \
        -v /usr/share/sip \
        --qsci-api \
        --verbose \
        --enable QtAssistant \
        --enable QtCore \
        --enable QtDBus \
        --enable QtDeclarative \
        --enable QtDesigner \
        --enable QtGui \
        --enable QtHelp \
        --enable QtMultimedia \
        --enable QtNetwork \
        --enable QtOpenGL \
        --enable QtScript \
        --enable QtScriptTools \
        --enable QtSql \
        --enable QtSvg \
        --enable QtTest \
        --enable QtWebKit \
        --enable QtXml \
        --enable QtXmlPatterns \
        --enable dbus \
        --enable phonon
    find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
    msg "Making the Python 2 version"
    make
}

package_python-pyqt4-common() {
    pkgdesc="Parts of PyQt for Qt4 shared between python3-pyqt4 and python2-pyqt4"
    depends=('qt')
    conflicts=('pyqt4-common' 'pyqt-common')
    replaces=('pyqt4-common' 'pyqt-common')

    cd ${srcdir}/PyQt-x11-gpl-${pkgver}
    msg "Packaging python-pyqt4-common"
    make -C pyrcc DESTDIR="${pkgdir}" install
    make -C pylupdate DESTDIR="${pkgdir}" install
    install -Dm644 PyQt4.api "${pkgdir}"/usr/share/qt/qsci/api/python/PyQt4.api
}

package_python3-pyqt4() {
    pkgdesc="A set of Python 3 bindings for the Qt toolkit"
    depends=('dbus-python3'
             'python-pyqt4-common'
             'python3'
             'qtwebkit'
             'python3-sip>=4.15')
    optdepends=('phonon: enable audio and video in PyQt applications'
                'python-opengl: enable OpenGL 3D graphics in PyQt applications'
                'qscintilla-python3: QScintilla API'
                'qt-assistant-compat: add PyQt online help in Qt Assistant')
    conflicts=('pyqt4-python3' 'pyqt')
    replaces=('pyqt4-python3' 'pyqt')
    provides=("pyqt=${pkgver}")

    cd "${srcdir}/PyQt-x11-gpl-${pkgver}"
    msg "Packaging python3-pyqt4"

    # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    #  Fix naming conflict with pyqt4-python2
    mv "${pkgdir}"/usr/bin/pyuic4{,-3.3}

    # Provided by python-pyqt4-common
    rm "${pkgdir}"/usr/bin/{pylupdate4,pyrcc4}
    rm -r "${pkgdir}"/usr/share/qt
}

package_python2-pyqt4() {
    pkgdesc="A set of Python 2 bindings for the Qt toolkit"
    depends=('dbus-python2'
             'python-pyqt4-common'
             'python2'
             'qtwebkit'
             'python2-sip>=4.15')
    optdepends=('phonon: enable audio and video in PyQt applications'
                'python-opengl: enable OpenGL 3D graphics in PyQt applications'
                'qscintilla-python2: QScintilla API'
                'qt-assistant-compat: add PyQt online help in Qt Assistant')
    conflicts=('pyqt4-python2' 'pyqt-python2')
    replaces=('pyqt4-python2' 'pyqt-python2')
    provides=("pyqt4-python2=${pkgver}")

    cd "${srcdir}/python2-PyQt-x11-gpl-${pkgver}"
    msg "Packaging pyqt4-python2"

    # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    #  Fix naming conflict with pyqt4-python3
    mv "${pkgdir}"/usr/bin/pyuic4{,-2.7}

    # Default /usr/bin/pyuic4
    ln -s /usr/bin/pyuic4-2.7 "${pkgdir}"/usr/bin/pyuic4

    # Provided by python-pyqt4-common
    rm "${pkgdir}"/usr/bin/{pylupdate4,pyrcc4}
    rm -r "${pkgdir}"/usr/share/qt

    # Provided by python3-pyqt4
    rm -r "${pkgdir}"/usr/lib/qt
}
