#
# Chakra Packages for Chakra, part of chakra-project.org
#

pkgbase=hsqldb
pkgname=hsqldb-java
pkgver=2.3.1
pkgrel=1
pkgdesc="HSQLDB Java libraries"
arch=('any')
license=('custom')
makedepends=('openjdk' 'unzip' 'apache-ant')
source=("http://downloads.sourceforge.net/project/hsqldb/hsqldb/hsqldb_2_3/hsqldb-${pkgver}.zip")
url="http://hsqldb.org/"
md5sums=('9c1c022a887c9db0d6fd4624e80dc1f1')

build() {
  [ -z "${JAVA_HOME}" ] && . /etc/profile.d/openjdk.sh
  [ -z "${ANT_HOME}" ] && . /etc/profile.d/apache-ant.sh

  cd "${srcdir}/${pkgbase}-${pkgver}/hsqldb/build"

  ant hsqldb
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}/hsqldb/build"
  
  mkdir -p "${pkgdir}/usr/share/java"
  install -m644 \
          "${srcdir}/hsqldb-${pkgver}/hsqldb/lib/"{hsqldb,sqltool}.jar \
          "${pkgdir}/usr/share/java/"

  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 \
          "${srcdir}/hsqldb-${pkgver}/hsqldb/doc/hsqldb_lic.txt" \
          "${pkgdir}/usr/share/licenses/${pkgname}/"
}