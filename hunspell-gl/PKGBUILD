# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=hunspell-gl
pkgver=20120819
pkgrel=1
pkgdesc="Galician spellchecker for Hunspell"
arch=('any')
url="http://trasno.net/"
license=('GPL3')
depends=('hunspell')
source=("http://webs.uvigo.es/miguelsolla/gl_ES.zip")
md5sums=('209fd16dd47e5cb334fa39563d23c79f')

lang="gl"
lang2="gl_ES"

package() {
  install -d $pkgdir/usr/share/{hunspell,myspell/dicts,enchant}
    
  # Actual installation...
  install -Dm644 "$srcdir/$lang2.aff"	"$pkgdir/usr/share/hunspell/$lang.aff"
  install -Dm644 "$srcdir/$lang2.dic"	"$pkgdir/usr/share/hunspell/$lang.dic"

  # Locale aliases.
  pushd $pkgdir/usr/share/hunspell
    ln -s $lang.aff $lang2.aff
    ln -s $lang.dic $lang2.dic
  popd

  # Myspell aliases.
  pushd ${pkgdir}/usr/share/myspell/dicts
    for file in ${pkgdir}/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename ${file}) .
    done
  popd

  # Enchant aliases.
  ln -s /usr/share/hunspell $pkgdir/usr/share/enchant/hunspell
}
