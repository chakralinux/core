
pkgname=nvidia-340xx-lts
pkgver=340.107
pkgrel=4
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=("linux-lts=4.4.165"  "nvidia-340xx-utils=${pkgver}")
makedepends=("linux-lts-headers=4.4.165")
conflicts=('nvidia-304xx-lts' 'nvidia-lts')
license=('custom')
_extramodules=extramodules-CHAKRA-LTS

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
md5sums=('ffca1879d77dfc491dfb0276ceb56cc1')

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
}

build() {    
    KERNEL_VERSION=$(</usr/lib/modules/$_extramodules/version)
    msg2 "Kernel = $KERNEL_VERSION"
    
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/$KERNEL_VERSION/build module
    
    cd uvm
    make SYSSRC=/usr/lib/modules/$KERNEL_VERSION/build module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/uvm/nvidia-uvm.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-uvm.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/"*.ko
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" |
      install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"
}
