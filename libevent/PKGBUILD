
pkgname=libevent
pkgver=2.0.22
pkgrel=1
pkgdesc="An event notification library"
license=('BSD')
arch=('x86_64')
url="http://www.monkey.org/~provos/libevent/"
depends=('openssl')
source=(http://sourceforge.net/projects/levent/files/libevent/libevent-${pkgver%.*}/${pkgname}-$pkgver-stable.tar.gz{,.asc})
md5sums=('c4c56f986aa985677ca1db89630a2e11'
         'SKIP')
validpgpkeys=('B35BF85BF19489D04E28C33C21194EBB165733EA')
         
build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  sed -i 's#python#python2#' event_rpcgen.py
  ./configure --prefix=/usr --sysconfdir=/etc
  make 
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/libevent/LICENSE
}

