# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/faad2

pkgname=faad2
pkgver=2.7
pkgrel=2
pkgdesc="ISO AAC audio decoder"
arch=('x86_64')
url="http://www.audiocoding.com/"
license=('GPL')
depends=('glibc')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/faac/${pkgname}-${pkgver}.tar.bz2
        libmp4ff-install-mp4ff_int_types_h.patch
	libmp4ff-shared-lib.patch
	man1_MANS.patch)
sha1sums=('b0e80481d27ae9faf9e46c8c8dfb617a0adb91b5'
          '970fe01640f5543f7926884b2b4003a9f6dbd61a'
          'b3932d8adc08927cab7bdc6a8c6ed0c3beee211f'
          'a8d9b3487803a484494a39ca166c61130dfd3df6')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/libmp4ff-shared-lib.patch"
  patch -p0 -i "${srcdir}/libmp4ff-install-mp4ff_int_types_h.patch"
  patch -p1 -i "${srcdir}/man1_MANS.patch"
  autoreconf --force --install
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make CFLAGS="${CFLAGS} -fPIC"
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}