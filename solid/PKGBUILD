# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=solid
pkgver=5.47.0
pkgrel=1
pkgdesc='Hardware integration and detection'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/solid'
license=('LGPL')
depends=('qt5-declarative' 'media-player-info' 'upower' 'udisks2')
makedepends=("extra-cmake-modules>=${KFECMVersion}" 'qt5-tools')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('5437f3f3a70ff37251d5d88636c39819112c91fcfd36660b6d777d457bdf2e10'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   make test 
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
