#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=parted
pkgver=3.0
pkgrel=1
pkgdesc="A program for creating, destroying, resizing, checking and copying partitions"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/parted/parted.html"
depends=('device-mapper' 'e2fsprogs')
makedepends=('pkgconfig')
options=('!libtool')
install=${pkgname}.install
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        linux.c.patch)
md5sums=('c415e5c97f86b5ff65a2d925e5a3feb7'
         '08c942e212424e238271217f52c59706')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#25307
  patch -Np1 -i ${srcdir}/linux.c.patch

  ./configure --prefix=/usr \
              --disable-debug \
              --disable-rpath
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
