# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Renan Biegelmeyer (RenanBS) <renan.biegel@gmail.com>
#               Archlinux

pkgbase=python-markupsafe
pkgname=('python3-markupsafe' 'python2-markupsafe')
pkgver=0.18
pkgrel=1
pkgdesc="Implements an XML/HTML/XHTML Markup safe string for Python"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/MarkupSafe"
license=('custom')
makedepends=('python3-setuptools' 'python2-setuptools')
source=(http://pypi.python.org/packages/source/M/MarkupSafe/MarkupSafe-${pkgver}.tar.gz)
sha512sums=('0438ddf0fdab465c40d9afba8c14ad346be0868df654c11130d05e329992d456a9bc278551970cbd09244a29c77213885d0c363c951b0cfd4d9aa95b248ecff5')

build() {
  cp -r MarkupSafe-${pkgver} python2-MarkupSafe-${pkgver}
  cd "${srcdir}/MarkupSafe-${pkgver}"
  python3 setup.py build

  cd "${srcdir}/python2-MarkupSafe-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${srcdir}/MarkupSafe-${pkgver}"
  python3 setup.py test

  cd "${srcdir}/python2-MarkupSafe-${pkgver}"
  python2 setup.py test
}

package_python3-markupsafe() {
    depends=('python3')

    cd MarkupSafe-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-markupsafe() {
    depends=('python2')
    replaces=('python-markupsafe')
    provides=('python-markupsafe')
    conflicts=('python-markupsafe')

    cd python2-MarkupSafe-${pkgver}
    python2 setup.py install --root="${pkgdir}" --optimize=1
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
