#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=libvpx
pkgver=1.1.0
pkgrel=2
pkgdesc="The VP8 Codec SDK"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=("http://webm.googlecode.com/files/${pkgname}-v${pkgver}.tar.bz2")
sha1sums=('356af5f770c50cd021c60863203d8f30164f6021')

build() {
    cd "${srcdir}/${pkgname}-v${pkgver}"
    ./configure --prefix=/usr \
        --enable-vp8 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd "${srcdir}/${pkgname}-v${pkgver}"

    make DIST_DIR="$pkgdir/usr" install
    chmod -R o+r "${pkgdir}/usr/include/vpx"
    install -D -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
