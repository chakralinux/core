
pkgname=libvpx
pkgver=1.6.1
pkgrel=2
pkgdesc='VP8 and VP9 codec'
arch=('x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc' 'gcc-libs')
makedepends=('yasm' 'git')
provides=('libvpx.so')
source=($pkgname-$pkgver.tar.gz::https://github.com/webmproject/libvpx/archive/v$pkgver.tar.gz)
md5sums=('b0925c8266e2859311860db5d76d1671')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix='/usr' \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-pic \
    --disable-install-docs \
    --disable-install-srcs \
    --enable-vp8 \
    --enable-postproc \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-experimental \
    --enable-spatial-svc

  make
}

package() {
  cd $pkgname-$pkgver
  make DIST_DIR="$pkgdir/usr" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
