# Maintainer: almack[at]chakraos[dot]org>

pkgname=libvpx
pkgver=1.5.0
pkgrel=1
pkgdesc="The VP8 Codec SDK"
arch=('x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm' 'git')
source=($pkgname-$pkgver.tar.gz::https://github.com/webmproject/libvpx/archive/v$pkgver.tar.gz)
md5sums=('0c662bc7525afe281badb3175140d35c')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr \
        --enable-vp8 \
        --enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --enable-experimental --enable-spatial-svc \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DIST_DIR="$pkgdir/usr" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
