#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgbase="sqlite3"
pkgname=('sqlite3' 'sqlite3-tcl' 'sqlite3-doc') 
_amalgamationver=3070500
_amalgamationver2="${_amalgamationver/00/}"
_docver=${_amalgamationver}
pkgver="${_amalgamationver2//0/.}"
pkgrel=4

pkgdesc="A C library that implements an SQL database engine."
arch=('i686' 'x86_64')
license=('custom')
url="http://www.sqlite.org/"
makedepends=('tcl' 'readline>=6.0.00' 'icu')
options=('!libtool' '!emptydirs')

source=("http://www.sqlite.org/sqlite-autoconf-${_amalgamationver}.tar.gz" # tarball containing the amalgamation for SQLite 3.7.5 together with an configure script and makefile for building it.
	"http://www.sqlite.org/sqlite-tea-${_amalgamationver}.tar.gz" # tarball of the amalgamation together with a Tcl Extension Architecture (TEA) compatible configure script and makefile
	"http://www.sqlite.org/sqlite-doc-${_amalgamationver}.zip"
	license.txt)
md5sums=('a9604a82613ade2e7f4c303f233e477f'
         '3413e8cfb7990e39260bf45dca017bf0'
         '74f9b304143a76e3da8c0063888770ee'
         'c1cdbc5544034d9012e421e75a5e4890')

build() {
	export CFLAGS="${CFLAGS} -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY=1 -DSQLITE_SECURE_DELETE=1 -DSQLITE_ENABLE_ICU=1 $(icu-config --cflags)"
	export LDFLAGS="${LDFLAGS} $(icu-config --ldflags)"

	# build sqlite
	cd "${srcdir}/sqlite-autoconf-${_amalgamationver}"
	./configure --prefix=/usr \
		    --disable-static
	make
  
	# build the tcl extension    
	cd "${srcdir}/sqlite-tea-${_amalgamationver}"
	./configure --prefix=/usr \
		    --with-system-sqlite
	make
}

package_sqlite3() {
 
	pkgdesc="A C library that implements an SQL database engine."
	depends=('readline>=6.0.00' 'icu')
	conflicts=('sqlite')
	replaces=('sqlite')
 
	cd "${srcdir}/sqlite-autoconf-${_amalgamationver}"
	make DESTDIR="${pkgdir}" install
  
	# license
	install -D -m644 "${srcdir}/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

package_sqlite3-tcl() {
 
	pkgdesc="Sqlite Tcl Extension Architecture (TEA)."
	depends=('sqlite3>=3.7.5')

	cd "${srcdir}/sqlite-tea-${_amalgamationver}"
	make DESTDIR="${pkgdir}" install
}

package_sqlite3-doc() {

	pkgdesc="Most of the static HTML files that comprise the Sqlite website, including all of the SQL Syntax and the C/C++ interface specs and other miscellaneous documentation."

	cd ${srcdir}/sqlite-doc-${_docver}
	mkdir -p ${pkgdir}/usr/share/doc/${pkgbase}
	cp -R *  ${pkgdir}/usr/share/doc/${pkgbase}/
  
	# fix permissions and remove obsolete files; https://bugs.archlinux.org/task/24605
	find ${pkgdir} -type f -perm 755 -exec ls -lha {} \;
	find ${pkgdir} -type f -perm 755 -exec chmod 644 {} \;

	find ${pkgdir} -type f -name '*~' -exec ls -lha {} \;
	find ${pkgdir} -type d -name '*~' -exec ls -lha {} \;
	find ${pkgdir} -name '*~' -exec rm -f {} \;

	find ${pkgdir} -type f -name '.~*' -exec ls -lha {} \; # /build/pkg/sqlite3-doc/usr/share/doc/sqlite3/images/fileformat/.~lock.indexpage.odg#
	find ${pkgdir} -type d -name '.~*' -exec ls -lha {} \;
	find ${pkgdir} -name '.~*' -exec rm -f {} \;
}

