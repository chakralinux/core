pkgname=yaz
pkgver=5.22.0
pkgrel=1
pkgdesc="A toolkit supporting the development of Z39.50/SRW/SRU clients and servers"
arch=('x86_64')
license=('BSD')
url="http://www.indexdata.com/yaz"
depends=('openssl' 'libxslt' 'icu' 'libgcrypt' 'gnutls')
source=("http://ftp.indexdata.dk/pub/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('a96e788f480f2dcee361678fa308ad308a97d3bcc050c306ec92890e7067e016')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --enable-shared=yaz \
    --with-openssl=/usr
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
