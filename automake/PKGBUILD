
pkgname=automake
pkgver=1.15
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
checkdepends=('dejagnu' 'python3' 'gcc-fortran' 'java-environment' 'vala' 'emacs')
install=automake.install
source=(http://mirror.bibleonline.ru/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('9a1ddb0e053474d9d1105cfe39b0c48d'
         'SKIP')
validpgpkeys=('E1622F96D2BB4E58018EEF9860F906016E407573')   # Stefano Lattarini

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # skip failing test...
  sed -i "/instmany-python.sh/d" Makefile.in
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}