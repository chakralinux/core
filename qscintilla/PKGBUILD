#
# Platform Packages for Chakra, part of www.chakra-project.org
#
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>

pkgbase=qscintilla
pkgname=('qscintilla'
         'qscintilla-python2'
         'qscintilla-python3'
         'qscintilla-python-common')
pkgver=2.7.2
pkgrel=1
license=('GPL')
arch=('x86_64')
url="http://www.riverbankcomputing.co.uk/software/qscintilla/intro"
makedepends=("python2-pyqt4" "python3-pyqt4" "chrpath" "python2-sip" "python3-sip")
source=("http://downloads.sourceforge.net/pyqt/QScintilla-gpl-${pkgver}.tar.gz"
        "configure.py-objdir-support.diff")
sha512sums=('d3ca9d75687687210ca6ae3599fd44bfbbad8c99d6d38aaf1c92c83250dc0c5af1c713bdd74d077d0fb9ba1eccbd5e2910eef855d3656392c33c4832d46ff7fb'
            'e0a7d3348a25200ee40b2e08333e898da1a796d20ac0215c32be23faf84d74dbba3d2893c2b621cdf6508923697db561d22ed6f870769463df46e228c419c6dd')

build() {
    cd $srcdir/QScintilla-gpl-$pkgver
    patch -p1 -i "${srcdir}/configure.py-objdir-support.diff"

    cd Qt4Qt5
    qmake qscintilla.pro
    make
    
    cd ../designer-Qt4Qt5
    qmake designer.pro INCLUDEPATH+=../Qt4Qt5 QMAKE_LIBDIR+=../Qt4Qt5
    make

    cp -r ../Python ../Python3

    cd ../Python
    python2 configure.py -n ../Qt4Qt5/ -o ../Qt4Qt5 -c
    make

   cd ../Python3
   python3 configure.py -n ../Qt4Qt5/ -o ../Qt4Qt5/ -c
   make
}

package_qscintilla() {
    pkgdesc="A port to Qt of Neil Hodgson's Scintilla C++ editor class"
    provides=('qscintilla2')
    conflicts=('qscintilla2')
    replaces=('qscintilla2')
    depends=('qt')

    cd "${srcdir}/QScintilla-gpl-${pkgver}/"Qt4Qt5
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    cd ${srcdir}/QScintilla-gpl-${pkgver}/designer-Qt4Qt5
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}

package_qscintilla-python-common() {
    pkgdesc="Common python qscintilla bindings files shared between the Python2 and Python3 version"
    depends=('qscintilla')
    conflicts=('python-qscintilla<=2.7')

    cd "${srcdir}/QScintilla-gpl-${pkgver}"/Python
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
      
    # Provided by qscintilla-python2
    rm "${pkgdir}/usr/lib/python2.7/site-packages/PyQt4/Qsci.so"
}

package_qscintilla-python2() {
    pkgdesc="Python bindings for QScintilla2 (for Python 2)"
    conflicts=('python-qscintilla')
    replaces=('python-qscintilla')
    provides=("python-qscintilla=${pkgver}")
    depends=('qscintilla' 'qscintilla-python-common' 'python2-pyqt4')

   cd "${srcdir}/QScintilla-gpl-${pkgver}/Python"
   install -Dm755 Qsci.so \
      "${pkgdir}/usr/lib/python2.7/site-packages/PyQt4/Qsci.so"
}

package_qscintilla-python3() {
    pkgdesc="Python bindings for QScintilla2 (for Python 3)"
    depends=('qscintilla' 'qscintilla-python-common' 'python3-pyqt4')

   cd "${srcdir}/QScintilla-gpl-${pkgver}/Python3"
   install -Dm755 Qsci.so \
      "${pkgdir}/usr/lib/python3.3/site-packages/PyQt4/Qsci.so"
}
