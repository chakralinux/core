# Maintainer:   H W Tovetjärn (totte) <totte@tott.es>
# Contributors: Aaron Schaefer <aaron@elasticdog.com>
#               Alexander Rødseth <rodseth@gmail.com

pkgbase=python-pexpect
pkgname=('python3-pexpect' 'python2-pexpect')
pkgver=3.0rc3
pkgrel=1
pkgdesc='Python module for spawning and controlling other applications'
arch=('any')
url='http://pexpect.sourceforge.net/'
license=('custom:ISC')
makedepends=('python3' 'python2')
source=("https://github.com/pexpect/pexpect/releases/download/$pkgver/pexpect-$pkgver.tar.gz")
#source=("http://pypi.python.org/packages/source/p/pexpect/pexpect-$pkgver.tar.gz")
sha512sums=('c7f9b7ecaae2ea584de228d3a6ccc2d2643ce5bbe9ca3b949dfeae3f96c38719ca80591ff6d9d851e4e672ad9aea64326edf5d78445aa103c7c4dc39d2ef0d91')

prepare() {
    cp -r ${srcdir}/pexpect-$pkgver ${srcdir}/python2-pexpect-$pkgver
}

package_python3-pexpect() {
    depends=('python3')

    cd ${srcdir}/pexpect-$pkgver

    python3 setup.py install --root=${pkgdir}
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python3|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python3|" \
           -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python3|" \
               $(find ${pkgdir} -name '*.py')
}

package_python2-pexpect() {
    depends=('python2')
    conflicts=('python-pexpect')
    replaces=('python-pexpect')
    provides=('python-pexpect')

    cd ${srcdir}/python2-pexpect-$pkgver

    python2 setup.py install --root=${pkgdir}
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
           -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
               $(find ${pkgdir} -name '*.py')
}
