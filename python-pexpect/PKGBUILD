# Maintainer:   H W Tovetjärn (totte) <totte@tott.es>
# Contributors: Aaron Schaefer <aaron@elasticdog.com>
#               Alexander Rødseth <rodseth@gmail.com

pkgbase=python-pexpect
pkgname=('python3-pexpect' 'python2-pexpect')
pkgver=4.0.1
pkgrel=1
pkgdesc='Python module for spawning and controlling other applications'
arch=('any')
url='http://pexpect.sourceforge.net/'
license=('custom:ISC')
makedepends=('python3' 'python2')
source=("http://pypi.python.org/packages/source/p/pexpect/pexpect-${pkgver}.tar.gz")
sha512sums=('c2476f977964379faa3ecef1bc4800f3a87bd6cdd9e2d52e4e4f33a4060a97bb54f0770dfb5804d5a863eaf6ddef6b11be1d24f1617cc4837015202868ca7e87')

prepare() {
    cp -r ${srcdir}/pexpect-$pkgver ${srcdir}/python2-pexpect-$pkgver
}

package_python3-pexpect() {
    depends=('python3' 'python3-ptyprocess')

    cd ${srcdir}/pexpect-$pkgver

    python3 setup.py install --root=${pkgdir}
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python3|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python3|" \
           -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python3|" \
               $(find ${pkgdir} -name '*.py')
}

package_python2-pexpect() {
    depends=('python2' 'python2-ptyprocess')
    conflicts=('python-pexpect')
    replaces=('python-pexpect')
    provides=('python-pexpect')

    cd ${srcdir}/python2-pexpect-$pkgver

    python2 setup.py install --root=${pkgdir}
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
           -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
               $(find ${pkgdir} -name '*.py')
}

