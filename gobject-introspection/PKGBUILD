#
# Chakra Packages for Chakra, part of chakra-project.org
#

pkgname=gobject-introspection
pkgver=1.38.0
pkgrel=1
pkgdesc="Introspection system for GObject-based libraries"
url="https://live.gnome.org/GObjectInstrospection"
arch=('x86_64')
license=('LGPL' 'GPL')
depends=('libffi' 'glib2' 'python2' 'python2-mako')
makedepends=('cairo')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver::4}/$pkgname-$pkgver.tar.xz)
md5sums=('4851028e950ec0f809e26676b22aba97')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static --enable-doctool
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  sed -i '1s|#!/usr/bin/env python$|&2|' \
    "${pkgdir}"/usr/lib/gobject-introspection/giscanner/*.py
}

