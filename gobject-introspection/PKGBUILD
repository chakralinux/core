
pkgbase=gobject-introspection
pkgname=('gobject-introspection' 'gobject-introspection-runtime')
pkgver=1.54.1
pkgrel=1
url="https://live.gnome.org/GObjectIntrospection"
arch=('x86_64')
license=('LGPL' 'GPL')
makedepends=('cairo' 'python3' 'python2-mako')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('b88ded5e5f064ab58a93aadecd6d58db2ec9d970648534c63807d4f9a7bb877e')

prepare() {
  cd $pkgbase-$pkgver
  sed -i '1s|#!/usr/bin/env python$|&2|' giscanner/*.py
  
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static --enable-doctool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_gobject-introspection-runtime() {
  pkgdesc="Introspection system for GObject-based libraries - runtime files"
  depends=('glib2')
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-libLTLIBRARIES install-typelibsDATA
}

package_gobject-introspection() {
  pkgdesc="Introspection system for GObject-based libraries"
  depends=("gobject-introspection-runtime=$pkgver" 'python2-mako' 'python2')
  options=('!emptydirs')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" uninstall-libLTLIBRARIES uninstall-typelibsDATA
}
