#
# Chakra Packages for Chakra, part of chakra-project.org
#

pkgname=gobject-introspection
pkgver=1.42.0
pkgrel=1
pkgdesc="Introspection system for GObject-based libraries"
url="https://live.gnome.org/GObjectInstrospection"
arch=('x86_64')
license=('LGPL' 'GPL')
depends=('glib2')
makedepends=('cairo' 'python2' 'python2-mako')
optdepends=('python2: gir development tools'
            'python2-mako: g-ir-doc-tool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver::4}/$pkgname-$pkgver.tar.xz)
md5sums=('4fa52f6b67367d9c1b99b98683ced202')

prepare() {
  cd $pkgname-$pkgver
  sed -i '1s|#!/usr/bin/env python$|&2|' giscanner/*.py
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static --enable-doctool
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

