# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/automoc4

pkgname=automoc4
pkgver=0.9.88
pkgrel=2
pkgdesc="KDE automoc4"
arch=('x86_64')
url='http://www.kde.org'
license=('custom')
depends=('qt')
makedepends=('pkgconfig' 'cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'license.txt' 
        'fix-cmake-warnings.patch')
md5sums=('91bf517cb940109180ecd07bc90c69ec'
         '4aede6d1f68b46a684e9d723d463532d'
         '67f0ea22665c5a235401731e6802bcdd')

prepare() {
  mkdir -p build

# Fix CMake warnings with newer policies
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-cmake-warnings.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
