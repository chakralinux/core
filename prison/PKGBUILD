# Include global configuration

pkgname=prison
pkgver=5.53.0
pkgrel=1
arch=('x86_64')
pkgdesc="A barcode api to produce QRCode barcodes and DataMatrix barcode"
url="https://projects.kde.org/projects/kdesupport/prison"
license=('GPL' 'LGPL' 'FDL')
makedepends=('cmake' 'extra-cmake-modules')
depends=('qt5-base' 'libdmtx' 'qrencode')
groups=('kf5')
options=("debug")
replaces=('prison-kf5')
provides=('prison-kf5')
conflicts=('prison-kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('61b5f10b48d847ce60769d10525a8f4c3c7120d71dd8e2e6285d303ea519b579'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
