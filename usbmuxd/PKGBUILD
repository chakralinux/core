#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=usbmuxd
pkgver=1.0.8
pkgrel=2
pkgdesc="USB Multiplex Daemon"
url="http://marcansoft.com/blog/iphonelinux/usbmuxd/"
arch=('x86_64')
license=('GPL2' 'GPL3' 'LGPL2.1')
depends=('libusb' 'libplist')
makedepends=('cmake')
install=usbmuxd.install
source=("http://cgit.sukimashita.com/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.bz2"
        'usbmuxd.service'
	'85-usbmuxd.rules')
md5sums=('87fd27773a84e97ac6e2dc28b08d682e'
         '099ab951e94be0803f5706f90dc63054'
         'dc4b2893651149eccbf7b2c8467cd288')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  echo "${pkgver}" > version.tag
  mkdir build
  cd build
  cmake ../ -DCMAKE_INSTALL_PREFIX:PATH=/usr -DLIB_SUFFIX=""
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

 # Fix dirty versioning
  if [ -e ${pkgdir}/usr/lib/libusbmuxd.so.-dirty ] ; then
  sed -i -e "s~-dirty~${pkgver}~g" ${pkgdir}/usr/lib/pkgconfig/libusbmuxd.pc
  mv ${pkgdir}/usr/lib/libusbmuxd.so.-dirty ${pkgdir}/usr/lib/libusbmuxd.so.${pkgver}
  elif [ -e ${pkgdir}/usr/lib/libusbmuxd.so.UNKNOWN ] ; then
  sed -i -e "s~-UNKNOWN~${pkgver}~g" ${pkgdir}/usr/lib/pkgconfig/libusbmuxd.pc
  mv ${pkgdir}/usr/lib/libusbmuxd.so.UNKNOWN ${pkgdir}/usr/lib/libusbmuxd.so.${pkgver}
  fi

 # install systemd service file
  install -D -m644 ${srcdir}/usbmuxd.service ${pkgdir}/usr/lib/systemd/system/usbmuxd.service

  # udev moved to /usr/lib
  mv "${pkgdir}/lib/udev" "${pkgdir}/usr/lib/"
  rmdir "${pkgdir}/lib"

  # use udev rule that works with systemd
  install -m644 ${srcdir}/85-usbmuxd.rules ${pkgdir}/usr/lib/udev/rules.d/
}

