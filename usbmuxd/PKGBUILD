pkgname=usbmuxd
pkgver=1.1.0
pkgrel=2
pkgdesc="USB Multiplex Daemon"
url="http://marcansoft.com/blog/iphonelinux/usbmuxd/"
arch=('i686' 'x86_64')
license=('GPL2' 'GPL3')
depends=('libimobiledevice')
install=usbmuxd.install
source=("http://www.libimobiledevice.org/downloads/usbmuxd-$pkgver.tar.bz2"
	'usbmuxd.service'
	'39-usbmuxd.rules')
md5sums=('34361c59320cb0b1f9ebcd2798ee1b39'
         'dae0660f972510a6483992b5b81e1b5a'
         'dc4b2893651149eccbf7b2c8467cd288')

prepare() {
  cd usbmuxd-$pkgver
  sed -i -e 's|udevrulesdir = /lib/udev/rules.d|udevrulesdir = /usr/lib/udev/rules.d|' Makefile.am
  autoreconf -fi
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # install systemd service file
  install -D -m644 ${srcdir}/usbmuxd.service ${pkgdir}/usr/lib/systemd/system/usbmuxd.service
  # use udev rule that works with systemd
  install -m644 ${srcdir}/39-usbmuxd.rules ${pkgdir}/usr/lib/udev/rules.d/
}
