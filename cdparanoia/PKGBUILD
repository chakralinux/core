# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/cdparanoia

pkgname=cdparanoia
pkgver=10.2
pkgrel=4
pkgdesc="Compact Disc Digital Audio extraction tool"
arch=('x86_64')
url="http://www.xiph.org/paranoia/"
license=('GPL')
depends=('glibc')
options=('!makeflags' '!staticlibs')
source=(http://downloads.xiph.org/releases/cdparanoia/cdparanoia-III-$pkgver.src.tgz gcc.patch)
md5sums=('b304bbe8ab63373924a744eac9ebc652'
         '12da14958d2b84c6719fe69890436445')

prepare() {
  cd "$srcdir/cdparanoia-III-$pkgver"
  patch -p0 -i ${srcdir}/gcc.patch
}

build() {
  cd "$srcdir/cdparanoia-III-$pkgver"
  ./configure --prefix=/usr --mandir=/usr/share/man 
  make
}
  
package() {
  cd "$srcdir/cdparanoia-III-$pkgver"
  make prefix="$pkgdir/usr" MANDIR="$pkgdir/usr/share/man" install
}
