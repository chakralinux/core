# Contribution from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kscreen-frameworks

pkgname=kscreen
pkgver=5.13.5
pkgrel=1
pkgdesc="KDE's screen management software"
arch=('x86_64')
url='projects.kde.org/'
license=('LGPL')
depends=(kxmlgui libkscreen qt5-graphicaleffects hicolor-icon-theme)
makedepends=(extra-cmake-modules python3 plasma-framework)
groups=('plasma')
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('43f78c55cca7c14aa1ae697fbda2f2179e73e65c8850d5e38ad164d538854b51'
             'SKIP' )
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p ${srcdir}/build
}

build() {
  cd ${srcdir}/build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
     -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
