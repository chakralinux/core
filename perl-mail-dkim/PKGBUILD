# Platform packages for Chakra

pkgname=perl-mail-dkim
pkgver=0.39
pkgrel=1
pkgdesc="Implements DomainKeys Identified Mail (DKIM)"
arch=('any')
url="http://search.cpan.org/dist/Mail-DKIM"
license=('GPL' 'PerlArtistic')
depends=('perl-crypt-openssl-rsa>=0.24' 'perl-digest-sha1' 'perl-mailtools' 'perl-net-dns')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/J/JA/JASLONG/Mail-DKIM-$pkgver.tar.gz)
md5sums=('0b62c91f855f47e818d96680746572ca')

build() {
  cd "$srcdir/Mail-DKIM-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Mail-DKIM-$pkgver"

  make test
}

package() {
  cd "$srcdir/Mail-DKIM-$pkgver"

  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
