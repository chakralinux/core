# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=libxxf86dga
pkgver=1.1.4
pkgrel=1
pkgdesc="X11 Direct Graphics Access extension library"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxext' 'xf86dgaproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXxf86dga-${pkgver}.tar.bz2)
sha1sums=('51f2fc8d5d5fb398cf3c66735789feadb9aaeba9')

build() {
  cd "${srcdir}/libXxf86dga-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/libXxf86dga-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
