# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/farstream-0.1/

pkgname=farstream-0.1
_pkgname=farstream
pkgver=0.1.2
pkgrel=1
pkgdesc="Audio/Video Communications Framework (legacy version)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/Farstream"
license=('LGPL2.1')
depends=('libnice' 'gstreamer0.10-base' 'gstreamer0.10-bad-plugins'
         'gstreamer0.10-good' 'gstreamer0.10-ffmpeg')
makedepends=('gobject-introspection' 'gstreamer0.10-python' 'python2-gobject')
options=('!libtool')
source=(http://freedesktop.org/software/$_pkgname/releases/$_pkgname/$_pkgname-$pkgver.tar.gz
        farstream-0.1.2-prefer-vp8.patch
        farstream-0.1.2-introspection-tag-order.patch)
sha256sums=('fa5a40597b47b59d124f7fd57f66704b030d576cdc0d90e107c7a592f05e73d9'
            '42f4a278a35491bc540980625c0d89f38c4ec30a858344ec2c7e98467567ea16'
            'c4a5ee0a3a86e5e10fb6a296fe05337e0cf4865a63ea708ef9027cd1a7ebc200')

build() {
  cd "$_pkgname-$pkgver"

  patch -Np1 -i "$srcdir/farstream-0.1.2-prefer-vp8.patch"
  patch -Np1 -i "$srcdir/farstream-0.1.2-introspection-tag-order.patch"

  ./configure \
    --prefix=/usr \
    --with-package-name='Arch Linux Farstream 0.1 Package' \
    --with-package-origin='httpw://www.archlinux.org/' \
    --disable-static
  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
