# Arch Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-attrs
pkgname=('python3-attrs' 'python2-attrs')
pkgver=17.2.0
pkgrel=2
pkgdesc="Attributes without boilerplate."
arch=('any')
license=('MIT')
url="https://attrs.readthedocs.org/"
makedepends=('python3-setuptools' 'python2-setuptools')
checkdepends=('python3-pytest-runner' 'python2-pytest-runner' 'python3-zope-interface'
              'python2-zope-interface' 'python3-hypothesis' 'python2-hypothesis' 'python3-six' 'python2-six')
source=("https://pypi.io/packages/source/a/attrs/attrs-$pkgver.tar.gz")
sha512sums=('02243e59ff417deddb330e841d42c0f43d3bea4435f114d3a9a8f4c3bd424c2ef565c630d179db7b3687d22091bf6e619de661171248c0ff0a0e5ec5f9bf7e21')

prepare() {
  cp -a attrs-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/attrs-$pkgver
  python3 setup.py build
 
  cd "$srcdir"/attrs-$pkgver-py2
  python2 setup.py build
}

# Disable tests for now (missing python-coverage pkg)
#check() {
#  cd "$srcdir"/attrs-$pkgver
#  python3 setup.py pytest
#
#  cd "$srcdir"/attrs-$pkgver-py2
#  python2 setup.py pytest
#}
 
package_python3-attrs() {
  depends=('python3')
 
  cd attrs-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
 
package_python2-attrs() {
  depends=('python2')
 
  cd attrs-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
