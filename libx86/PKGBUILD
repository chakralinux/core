#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=libx86
pkgver=1.1
pkgrel=3
pkgdesc="Provides an lrmi interface that works on x86, am64 and alpha"
arch=('i686' 'x86_64')
url="http://www.codon.org.uk/~mjg59/libx86/"
license=('custom')
depends=('glibc')
source=(http://www.codon.org.uk/~mjg59/libx86/downloads/libx86-$pkgver.tar.gz 
        libx86-ifmask.patch)
md5sums=('41bee1f8e22b82d82b5f7d7ba51abc2a'
         '573897186eb8670d8d97c64ea7614001')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  # lrmi.c patch courtesy of Gentoo
  patch -Np0 -i "${srcdir}"/libx86-ifmask.patch

  # compensate for x86_64
  if [ "$CARCH" = "x86_64" ]; then
    make BACKEND=x86emu
  else
    make
  fi
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}"/usr/lib/libx86.a

  install -D -m 644 COPYRIGHT "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

