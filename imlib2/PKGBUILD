# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=imlib2
pkgver=1.4.5
pkgrel=2
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng>=1.5.0' 'libid3tag' 'libjpeg-turbo')
options=('!libtool')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2")
sha1sums=('af86a2c38f4bc3806db57e64e74dc9814ad474a0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # disable optimizations, they cause problems 
  EXTRAOPTS="--disable-amd64"

  # Configure and Build
  ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib $EXTRAOPTS
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make DESTDIR="$pkgdir" install

  # Install License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
