# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/imlib2

pkgname=imlib2
pkgver=1.4.6
pkgrel=1
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng>=1.5.0' 'libid3tag' 'libjpeg-turbo')
options=('!libtool')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2"
        'giflib51.patch')
sha1sums=('20e111d822074593e8d657ecf8aafe504e9e2967'
          '5ef58774e83b580362c8b8fd0bde15e6737ba73e')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../giflib51.patch
  sed -i 's/@my_libs@//' imlib2-config.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib $EXTRAOPTS
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Install License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
