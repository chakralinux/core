# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/imlib2

pkgname=imlib2
pkgver=1.4.9
pkgrel=1
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng>=1.5.0' 'libid3tag' 'libjpeg-turbo')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2")
sha1sums=('f389d67c337b604a365e620b0083b2d342dd724e')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/@my_libs@//' imlib2-config.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib \
              --enable-amd64
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Install License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
