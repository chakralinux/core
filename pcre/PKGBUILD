#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): AlmAck <almack[at]chakraos[dot]org>

pkgname=pcre
pkgver=8.36
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('x86_64')
url="http://www.pcre.org/"
license=('BSD')
depends=('gcc-libs' 'readline' 'zlib' 'bzip2')
source=("ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2"
'001-fix-heap-overflow.patch')
md5sums=('b767bc9af0c20bc9c1fe403b0d41ad97'
         '44a7e4fca07eff04d07bef65c982de9a')

prepare(){
  patch -d $pkgname-$pkgver < 001-fix-heap-overflow.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-unicode-properties \
    --enable-utf8 \
    --enable-pcre16 \
    --enable-pcre32 \
    --enable-jit \
    --enable-pcregrep-libz \
    --enable-pcregrep-libbz2 \
    --enable-pcretest-libreadline
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}