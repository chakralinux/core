#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): AlmAck <almack[at]chakraos[dot]org>

pkgname=pcre
pkgver=8.38
pkgrel=2
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('x86_64')
url="http://www.pcre.org/"
license=('BSD')
depends=('gcc-libs' 'readline' 'zlib' 'bzip2')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8') # Philip Hazel
source=("ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/$pkgname-$pkgver.tar.bz2"{,.sig}
        "pcre-8.38-Fix-negated-POSIX-class-within-negated-overall-class.patch"
        "pcre-8.38-Fix-workspace-overflow-for-ACCEPT-with-deeply-nested.patch"
)
md5sums=('00aabbfe56d5a48b270f999b508c5ad2'
         'SKIP'
         '7da5ec0bcce0de711d802c26741a7b05'
         '64b1c23179fcea4534417f8654b6138a')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/pcre-8.38-Fix-negated-POSIX-class-within-negated-overall-class.patch
  patch -Np1 -i ${srcdir}/pcre-8.38-Fix-workspace-overflow-for-ACCEPT-with-deeply-nested.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-unicode-properties \
    --enable-utf8 \
    --enable-pcre16 \
    --enable-pcre32 \
    --enable-jit \
    --enable-pcregrep-libz \
    --enable-pcregrep-libbz2 \
    --enable-pcretest-libreadline
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
