pkgname=libsrtp
pkgver=2.0.0
epoch=1
pkgrel=2
pkgdesc="Open-source implementation of the Secure Real-time Transport Protocol (SRTP)"
url="https://github.com/cisco/libsrtp"
arch=('x86_64')
license=('BSD')
depends=('glibc')
source=("${url}/archive/v${pkgver}.tar.gz")
md5sums=('54b7fbc8ed45aa3c1c02511ee8976dd9')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --enable-openssl \
              --disable-debug
  make
  make shared_library # Silly upstream, doesn't build dynamic link
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm0644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
