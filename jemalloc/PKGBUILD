pkgname=jemalloc
pkgver=4.5.0
pkgrel=1
pkgdesc="General-purpose scalable concurrent malloc implementation"
arch=('x86_64')
license=('BSD')
url="http://jemalloc.net/"
depends=('glibc')
optdepends=('perl: memory profiler')
source=(https://github.com/jemalloc/jemalloc/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('9409d85664b4f135b77518b0b118c549009dc10f6cba14557d170476611f6780')


build() {
        cd "$srcdir/$pkgname-$pkgver"
        ./configure --prefix=/usr
        make
}

package() {
        cd "$srcdir/$pkgname-$pkgver"
        make DESTDIR="$pkgdir" install
        find "$pkgdir" -name \*.a -type f -exec chmod 644 '{}' \;
        install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

