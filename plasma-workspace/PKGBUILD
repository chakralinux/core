#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/plasma-workspace
source ../plasma.conf

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'drkonqi')
pkgver=${PVersion}
pkgrel=4
pkgdesc='KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('knewstuff' 'kjsembed' 'knotifyconfig' 'libxdamage' 'kwayland' 'kglobalaccel'
         'libksysguard' 'libkscreen' 'ktexteditor' 'kxmlrpcclient' 'libqalculate' 'networkmanager-qt' 'kfilemetadata'
         'qt5-tools' 'kded' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot' 'xorg-apps' 'xorg-utils'
         'prison-kf5' 'milou' 'chakra-common')
makedepends=('extra-cmake-modules' 'kdoctools' 'kwin' 'gpsd' 'baloo' 'krunner' 'kdewebkit')
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PSubVersion}.tar.xz"
        'kde.pam'
        'panelview_edge_events.diff')
sha256sums=( $(getSum ${pkgname} | head -n1)
            '44a7c7acee30547dd41d0a593486b29df4eaa5c3707287b87e23b1104de3964a'
            '7ea3b330f23b7ca5307255abb5ff22c3c74757eb2a39603a660e1d818b0bf301')

prepare() {
  mkdir -p build
  
  cd ${pkgbase}-${pkgver}
  # be sure to use the Qt5 version of qtpaths
  sed -i 's:qtpaths:qtpaths-qt5:' startkde/startkde.cmake
  
  # revert patch: https://bugs.kde.org/show_bug.cgi?id=354651 
  patch -p1 -R -i $srcdir/panelview_edge_events.diff
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver}
  make
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation')
  replaces=('xembed-sni-proxy' 'xembed-sni-proxy-git')
  provides=('xembed-sni-proxy' 'xembed-sni-proxy-git')
  conflicts=('xembed-sni-proxy' 'xembed-sni-proxy-git')

  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/kde.pam \
    "${pkgdir}"/etc/pam.d/kde

  # Remove conflicts with drkonqi
  rm "${pkgdir}"/usr/lib/drkonqi
  rm -r "${pkgdir}"/usr/share/drkonqi
}

package_drkonqi() {
  pkgdesc='KDE crash handler'
  depends=('kdewebkit' 'kxmlrpcclient' 'gdb')

  cd build/drkonqi
  make DESTDIR="${pkgdir}" install
}
