#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/plasma-workspace
source ../plasma.conf

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'drkonqi' 'plasma-wayland-session')
pkgver=${PVersion}
pkgrel=2
pkgdesc='KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
# note on libxdamage:
# not detected by namcap because libgl depends on it
# but nvidia providing libgl does not depend on libxdamage
depends=('kjsembed' 'knotifyconfig' 'libxdamage' 'libksysguard' 'libkscreen' 'ktexteditor' 'libqalculate'
         'qt5-tools' 'kded' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot' 'kholidays'
         'xorg-apps' 'xorg-utils' 'milou' 'breeze' 'prison-kf5' 'kwin' 'chakra-common' 'plasma-integration')
makedepends=('extra-cmake-modules' 'kdoctools' 'gpsd' 'baloo'
             'krunner' 'kxmlrpcclient' 'networkmanager-qt' 'kdewebkit')
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "kde.pam"
        'systemtray_365570.patch::https://quickgit.kde.org/?p=plasma-workspace.git&a=commitdiff&h=7f060403a2d44fc76b2b14fc9ebf43ee272d1c54&o=plain')
sha256sums=( $(getSum ${pkgname} | head -n1) 
            '44a7c7acee30547dd41d0a593486b29df4eaa5c3707287b87e23b1104de3964a'
            '170b6acd60d0b81fdcb4499d4cd8d150a4b5dfe8755d64b12b25a1b65728bc2e')

prepare() {
  mkdir -p build
  
  cd ${pkgbase}-${pkgver}
  # be sure to use the Qt5 version of qtpaths
  sed -e 's:qtpaths:qtpaths-qt5:' -i startkde/start*.cmake
  
  # Bug 365570 - Systray icons are big and pixelated after update to 5.7.1
  patch -Np1 -i "${srcdir}/systemtray_365570.patch"
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver}
  make
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation')
  replaces=('xembed-sni-proxy' 'xembed-sni-proxy-git' 'simplesystray')
  provides=('xembed-sni-proxy' 'xembed-sni-proxy-git' 'simplesystray')
  conflicts=('xembed-sni-proxy' 'xembed-sni-proxy-git' 'simplesystray')

  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/kde.pam \
    "${pkgdir}"/etc/pam.d/kde

  # Remove conflicts with drkonqi
  rm "${pkgdir}"/usr/lib/drkonqi
  rm -r "${pkgdir}"/usr/share/drkonqi

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_drkonqi() {
  pkgdesc='KDE crash handler'
  depends=('kdewebkit' 'kxmlrpcclient' 'gdb')

  cd build/drkonqi
  make DESTDIR="${pkgdir}" install
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=('plasma-workspace' 'qt5-wayland' 'kwayland-integration' 'xorg-server-xwayland')

  install -Dm644 build/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
