#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/plasma-workspace

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'plasma-wayland-session')
pkgver=5.13.5
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=(kjsembed knotifyconfig libksysguard ktexteditor libqalculate qt5-tools kde-cli-tools bc
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-utils milou prison kwin
         plasma-integration kdelibs4support)
makedepends=(extra-cmake-modules kdoctools gpsd baloo krunner networkmanager-qt kdesignerplugin) #appstream-qt
groups=('plasma')
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        "kde.pam")
sha256sums=('d0e2f16e07275cd706d48d30d4a3f5f49db9360602604ad1a6f4700cd73574cf'
            'SKIP'
            '44a7c7acee30547dd41d0a593486b29df4eaa5c3707287b87e23b1104de3964a')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd ${pkgbase}-${pkgver}
  # be sure to use the Qt5 version of qtpaths
  sed -e 's:qtpaths:qtpaths-qt5:' -i startkde/start*.cmake
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation')
  replaces=('xembed-sni-proxy' 'xembed-sni-proxy-git' 'simplesystray' 'ksuperkey')
  provides=('xembed-sni-proxy' 'xembed-sni-proxy-git' 'simplesystray' 'ksuperkey')
  conflicts=('xembed-sni-proxy' 'xembed-sni-proxy-git' 'simplesystray' 'ksuperkey')

  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/kde.pam \
    "${pkgdir}"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=('plasma-workspace' 'qt5-wayland' 'kwayland-integration' 'xorg-server-xwayland')

  install -Dm644 build/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
