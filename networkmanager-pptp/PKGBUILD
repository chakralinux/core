pkgname=networkmanager-pptp
pkgver=1.0.8
_pppver=2.4.7
pkgrel=1
pkgdesc="NetworkManager VPN plugin for pptp "
arch=('x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('networkmanager' 'pptpclient' "ppp=$_pppver")
makedepends=('intltool')
source=("http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-pptp/1.0/NetworkManager-pptp-${pkgver}.tar.xz")
sha256sums=('6ff031baf170d890f779cfd15533ff0429999f1513aa3903a60c9d21ae09244c')

build() {
  cd "${srcdir}/NetworkManager-pptp-${pkgver}"
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--localstatedir=/var \
	--libexecdir=/usr/lib/networkmanager \
        --without-gnome \
        --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
	--disable-static 
  make 
}

package() {
  cd "${srcdir}/NetworkManager-pptp-${pkgver}"
  make DESTDIR="${pkgdir}" install 
}
