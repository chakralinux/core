#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=pkg-config
pkgver=0.28
pkgrel=2
pkgdesc="A system for managing library compile/link flags"
arch=('x86_64')
url="http://pkgconfig.freedesktop.org/wiki/"
license=('GPL')
groups=('base-devel')
depends=('glibc' 'popt' 'glib2')
provides=("pkgconfig=${pkgver}")
conflicts=('pkgconfig')
replaces=('pkgconfig')
source=(http://pkg-config.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz)
md5sums=('aa3c86e67551adc3ac865160e34a2a0d')

# In case of new pcre major version use --with-internal-glib
# then build glib2 and then again pkg-config without the option above

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
