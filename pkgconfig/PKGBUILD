pkgname=pkg-config
pkgver=0.29.2
pkgrel=1
pkgdesc="A system for managing library compile/link flags"
arch=('x86_64')
url="http://pkgconfig.freedesktop.org/wiki/"
license=('GPL')
groups=('base-devel')
depends=('glibc' 'popt' 'glib2')
provides=("pkgconfig=${pkgver}")
conflicts=('pkgconfig')
replaces=('pkgconfig')
source=(https://pkgconfig.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz{,.asc})
md5sums=('f6e931e319531b736fadc017f470e68a'
         'SKIP')
validpgpkeys=('6B99CE97F17F48C27F722D71023A4420C7EC6914')  # Dan Nicholson

# In case of new pcre major version use --with-internal-glib
# then build glib2 and then again pkg-config without the option above

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
