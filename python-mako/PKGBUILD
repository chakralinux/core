#
# Chakra Packages for Chakra, part of chakra-project.org
#
# Maintainer : Almack <almack[at]chakra-project[dot]org>
# Contributor: george <rpubaddr0 {at} gmail [dot] com>

pkgbase=python3-mako
pkgname=('python3-mako' 'python2-mako')
pkgver=1.0.4
pkgrel=1
pkgdesc="Hyperfast and lightweight templating for the Python2 platform"
arch=('any')
url="http://www.makotemplates.org/"
license=('MIT')
makedepends=('python3-setuptools' 'python2-setuptools' 'python3-markupsafe' 'python2-markupsafe'
             'python3-beaker' 'python2-beaker')
checkdepends=('python3-pytest-runner' 'python2-pytest-runner' 'python3-mock' 'python2-mock'
              'python3-babel' 'python2-babel' 'python3-pygments' 'python2-pygments'
              'python3-dogpile.cache' 'python2-dogpile.cache')
source=(https://pypi.python.org/packages/source/M/Mako/Mako-$pkgver.tar.gz{,.asc})
sha1sums=('64f9cadffd5cc264ea91d81df6e24ce660a15836'
          'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')  # Michael Bayer

prepare() {
  cp -a Mako-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/Mako-$pkgver
  python3 setup.py build

  cd "$srcdir"/Mako-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/Mako-$pkgver
  python setup.py ptr

  cd "$srcdir"/Mako-$pkgver-py2
  python2 setup.py ptr
}

package_python3-mako() {
  depends=('python3-markupsafe' 'python3-beaker' 'python3-setuptools')

  cd Mako-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-mako() {
  depends=('python2-markupsafe' 'python2-beaker' 'python2-setuptools')

  cd Mako-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/COPYING

  mv "$pkgdir"/usr/bin/mako-render{,2}
}
