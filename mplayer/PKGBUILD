#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=mplayer
pkgver=34104
pkgrel=2
pkgdesc="A movie player for linux"
arch=('i686' 'x86_64')
depends=('a52dec' 'aalib' 'alsa-lib' 'bzip2' 'cdparanoia' 'directfb' 'enca' 'faac' 'faad2' 'fontconfig' 'freetype2'
         'gcc-libs' 'giflib' 'glibc' 'jack' 'lame' 'libcaca' 'libdca' 'libgl' 'libjpeg' 'libmad' 'libmng' 'libogg'
         'libpng' 'libtheora' 'libvdpau>=0.4' 'libvpx' 'libx11' 'libxext' 'libxinerama' 'libxv' 'libxvmc' 'libxxf86dga'
         'libxxf86vm' 'libxss' 'lirc-utils' 'mpg123' 'ncurses' 'opencore-amr' 'openjpeg' 'orc' 'rtmpdump' 'schroedinger'
         'sdl' 'smbclient' 'ttf-dejavu' 'x264' 'xvidcore' 'zlib')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'mesa' 'live-media>=2010.01.13' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(http://chakra-linux.org/sources/${pkgname}/${pkgname}-${pkgver}.tar.xz mplayer.desktop mplayer.png)
md5sums=('6973a2c9982f003829548a4c4d637b6b'
         'c0d6ef795cf6de48e3b87ff7c23f0319'
         'd00874ccc644b7f43d6ef1c942fcef28')

# create tarball: source PKGBUILD && mksource

mksource() {
    rm -vRf ${pkgname}
    echo "checkout mplayer"
    _pkgver=`svn info svn://svn.mplayerhq.hu/mplayer/trunk | grep Revision: | cut -d' ' -f2`
    svn co svn://svn.mplayerhq.hu/mplayer/trunk --config-dir ./ ${pkgname} || return 1
    cd ${pkgname}
    echo "checkout ffmpeg"
    git clone git://git.videolan.org/ffmpeg.git ffmpeg
    echo "clean checkout"
    rm -vRf ffmpeg/.git
    find . -name ".svn" -exec rm -rf {} \;
    cd ..
    pushd ${pkgname}
    popd
    echo "pack checkout"
    tar -cvJf ${pkgname}-${_pkgver}.tar.xz ${pkgname}/*
    md5sum ${pkgname}-${_pkgver}.tar.xz
    echo "change pkgver to ${_pkgver}"
}

build() {
  # Custom CFLAGS break the mplayer build
  unset CFLAGS LDFLAGS

  cd ${srcdir}/${pkgname}

  ./configure --prefix=/usr \
      --enable-runtime-cpudetection \
      --disable-gui \
      --disable-arts \
      --disable-liblzo \
      --disable-speex \
      --disable-openal \
      --disable-fribidi \
      --disable-libdv \
      --disable-musepack \
      --disable-esd \
      --disable-pulse \
      --disable-mga \
      --enable-xvmc \
      --language=all \
      --confdir=/etc/mplayer

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make
}

package() {
  cd ${srcdir}/${pkgname}
  make -j1 DESTDIR=${pkgdir} install
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
  install -dm755 ${pkgdir}/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
  rm -rf ${pkgdir}/usr/share/mplayer/font
  #desktop file  FS#14770
  install -Dm644 ${srcdir}/mplayer.desktop ${pkgdir}/usr/share/applications/mplayer.desktop
  install -Dm644 ${srcdir}/mplayer.png ${pkgdir}/usr/share/pixmaps/mplayer.png
}
