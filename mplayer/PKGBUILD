#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgbase=mplayer
pkgname=('mplayer' 'mencoder')
pkgver=34707
pkgrel=4
pkgdesc="A movie player for linux"
arch=('i686' 'x86_64')
makedepends=('libxxf86dga' 'libxxf86vm' 'libmad' 'cdparanoia' 'libxinerama' 'sdl' 'lame' 'libtheora' 'xvidcore' 'libmng' 'libxss' 'libgl' 'smbclient'
'aalib' 'jack' 'libcaca' 'x264' 'faac' 'faad2' 'lirc-utils'  'libxvmc' 'enca' 'libvdpau' 'opencore-amr' 'libdca' 'a52dec' 'schroedinger' 'libvpx'
'libpulse' 'fribidi' 'unzip' 'mesa' 'live-media' 'yasm' 'git' 'fontconfig' 'mpg123' 'ladspa' 'libbluray' 'videoproto' 'libxv')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'mesa' 'live-media>=2010.01.13' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(http://chakra-linux.org/sources/${pkgname}/${pkgname}-${pkgver}.tar.xz mplayer.desktop mplayer.png)
md5sums=('fad5faddafdb0146d10aa21fa278e194'
         'c0d6ef795cf6de48e3b87ff7c23f0319'
         'd00874ccc644b7f43d6ef1c942fcef28')

# create tarball: source PKGBUILD && mksource

mksource() {
    rm -vRf ${pkgname}
    echo "checkout mplayer"
    _pkgver=`svn info svn://svn.mplayerhq.hu/mplayer/trunk | grep Revision: | cut -d' ' -f2`
    svn co svn://svn.mplayerhq.hu/mplayer/trunk --config-dir ./ ${pkgname} || return 1
    cd ${pkgname}
    echo "checkout ffmpeg"
    git clone git://git.videolan.org/ffmpeg.git ffmpeg
    echo "clean checkout"
    rm -vRf ffmpeg/.git
    find . -name ".svn" -exec rm -rf {} \;
    cd ..
    pushd ${pkgname}
    popd
    echo "pack checkout"
    tar -cvJf ${pkgname}-${_pkgver}.tar.xz ${pkgname}/*
    md5sum ${pkgname}-${_pkgver}.tar.xz
    echo "change pkgver to ${_pkgver}"
}

build() {
    # Custom CFLAGS break the mplayer build
    unset CFLAGS LDFLAGS

    cd "${srcdir}/${pkgbase}"

    ./configure --prefix=/usr \
        --enable-runtime-cpudetection \
        --disable-gui \
        --disable-arts \
        --disable-liblzo \
        --disable-speex \
        --disable-openal \
        --disable-libdv \
        --disable-musepack \
        --disable-esd \
        --disable-mga \
        --enable-xvmc \
        --enable-radio \
        --language=all \
        --confdir=/etc/mplayer
    [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

    make
}

package_mplayer() {
    pkgdesc="A movie player for linux"
    install=mplayer.install
    backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
    depends=('desktop-file-utils' 'ttf-dejavu' 'enca' 'libxss' 'a52dec' 'libvpx' 'lirc-utils' 'x264' 'libmng' 'libdca' 'aalib' 'lame' 'fontconfig'  'libgl'
    'libxinerama' 'libvdpau' 'libpulse' 'smbclient' 'xvidcore' 'opencore-amr' 'jack' 'cdparanoia' 'libmad' 'sdl' 'libtheora' 'libcaca' 'libxxf86dga' 'fribidi'
    'libjpeg' 'faac' 'faad2' 'libxvmc' 'schroedinger' 'mpg123' 'libxxf86vm' 'libbluray')

    cd "${srcdir}/${pkgbase}"

    make DESTDIR="${pkgdir}" install-mplayer install-mplayer-man
    install -Dm644 etc/{codecs.conf,input.conf,example.conf} "${pkgdir}/etc/mplayer/"
    install -dm755 "${pkgdir}/usr/share/mplayer/"
    ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "${pkgdir}/usr/share/mplayer/subfont.ttf"
    rm -rf "${pkgdir}/usr/share/mplayer/font"
    #desktop file  FS#14770
    install -Dm644 "${srcdir}/mplayer.desktop" "${pkgdir}/usr/share/applications/mplayer.desktop"
    install -Dm644 "${srcdir}/mplayer.png" "${pkgdir}/usr/share/pixmaps/mplayer.png"
}

package_mencoder() {
    pkgdesc="Free command line video decoding, encoding and filtering tool"
    depends=('enca' 'a52dec' 'libvpx' 'x264' 'libmng' 'libdca' 'bzip2' 'lame' 'alsa-lib' 'fontconfig' 'giflib' 'libpng' 'smbclient' 'xvidcore' 'opencore-amr' 'cdparanoia'
    'libmad' 'libtheora' 'fribidi' 'libjpeg' 'faac' 'faad2' 'schroedinger' 'mpg123' 'libbluray')

    cd "${srcdir}/${pkgbase}"

    # fix mencoder man
    sed -i -e 's/ln -sf mplayer.1 mencoder.1/mv mplayer.1 mencoder.1/g' Makefile

    make DESTDIR="${pkgdir}" install-mencoder install-mencoder-man
}