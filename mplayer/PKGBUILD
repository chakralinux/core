#
# Platform packages of Chakra Project, part of chakra-project.org
#
# Contributor: abveritas@chakra-project.org
# Maintainer: Bruce Liu <rainman59118@gmail.com>

pkgbase=mplayer
pkgname=('mplayer' 'mencoder')
pkgver=37379
pkgrel=1
arch=('x86_64')
makedepends=(
  'subversion' 'libxxf86vm' 'libmad' 'libxinerama' 'sdl' 'lame' 'libtheora'
  'xvidcore' 'libmng' 'libxss' 'libgl' 'smbclient' 'aalib' 'jack' 'libcaca'
  'faac' 'faad2' 'lirc'  'libxvmc' 'enca' 'libvdpau' 'opencore-amr'
  'libdca' 'a52dec' 'schroedinger' 'libvpx' 'libpulse' 'fribidi' 'unzip' 'mesa'
  'live-media' 'yasm' 'git' 'fontconfig' 'mpg123' 'ladspa' 'libass' 'libbluray'
  'libcdio-paranoia' 'opus' 'subversion' 'x264' 'rtmpdump' 'gnutls'
  'gsm' 'libdvdcss' 'libdvdread' 'libdvdnav'
)
license=('GPL')
url='http://www.mplayerhq.hu/'
categories=('multimedia')
screenshot=("http://www.mplayerhq.hu/images/screenshots/gui-preview-01.jpg")
options=('!buildflags' '!emptydirs')
source=("${pkgbase}-${pkgver}::svn://svn.mplayerhq.hu/mplayer/trunk#revision=${pkgver}"
        "http://ffmpeg.org/releases/ffmpeg-2.6.tar.bz2"
        mplayer.desktop
        cdio-includes.patch
        include-samba-4.0.patch
        revert-icl-fixes.patch)
md5sums=('SKIP'
         '5bf6097833a3fea6a9e51b0a456c6922'
         '62f44a58f072b2b1a3c3d3e4976d64b3'
         '7b5be7191aafbea64218dc4916343bbc'
         '868a92bdef148df7f38bfa992b26ce9d'
         '3579402002b7302fdf2d146639333efd')

prepare() {
  cd $pkgbase-$pkgver
  mv ../ffmpeg-*/ ./ffmpeg

  patch -p0 -i ../cdio-includes.patch
  patch -p1 -i ../include-samba-4.0.patch
  patch -Np0 -i ../revert-icl-fixes.patch

  ./version.sh
}

build() {
  cd $pkgbase-$pkgver

  ./configure --prefix=/usr \
    --enable-runtime-cpudetection \
    --disable-gui \
    --disable-arts \
    --disable-liblzo \
    --disable-speex \
    --disable-openal \
    --disable-libdv \
    --disable-musepack \
    --disable-esd \
    --disable-mga \
    --disable-ass-internal \
    --disable-cdparanoia \
    --enable-xvmc \
    --enable-radio \
    --enable-radio-capture \
    --enable-smb \
    --language=all \
    --confdir=/etc/mplayer

  make
}

package_mplayer() {
  pkgdesc="A movie player for linux"
  install=mplayer.install
  backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
  depends=(
    'desktop-file-utils' 'ttf-dejavu' 'enca' 'libxss' 'a52dec' 'libvpx'
    'lirc' 'x264' 'libmng' 'libdca' 'aalib' 'lame' 'fontconfig'
    'libgl' 'libxinerama' 'libvdpau' 'libpulse' 'smbclient' 'xvidcore'
    'opencore-amr' 'jack' 'libmad' 'sdl' 'libtheora' 'libcaca' 'libxxf86dga'
    'fribidi' 'libjpeg' 'faac' 'faad2' 'libxvmc' 'schroedinger' 'mpg123'
    'libass' 'libxxf86vm' 'libbluray' 'libcdio-paranoia' 'opus' 'rtmpdump'
    'gsm' 'gnutls' 'libdvdnav' 'giflib'
  )
  provides=('mplayer2')
  replaces=('mplayer2')
  conflicts=('mplayer2')

  cd $pkgbase-$pkgver
  make DESTDIR=${pkgdir} install-mplayer install-mplayer-man

  install -Dm644 etc/{codecs.conf,input.conf,example.conf} \
    "${pkgdir}/etc/mplayer/"

  install -dm755 ${pkgdir}/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
  rm -rf ${pkgdir}/usr/share/mplayer/font

  # Desktop Integration.
  install -Dm644 ../mplayer.desktop \
    "$pkgdir"/usr/share/applications/mplayer.desktop
  install -Dm644 etc/mplayer256x256.png \
    "$pkgdir"/usr/share/pixmaps/mplayer.png
}

package_mencoder() {
  pkgdesc='Free command line video decoding, encoding and filtering tool'
  depends=(
    'enca' 'a52dec' 'libvpx' 'x264' 'libmng' 'libdca' 'bzip2' 'lame'
    'alsa-lib' 'fontconfig' 'giflib' 'libpng' 'smbclient' 'xvidcore'
    'opencore-amr' 'libmad' 'libtheora' 'fribidi' 'libjpeg' 'faac' 'faad2'
    'schroedinger' 'mpg123' 'libass' 'libbluray' 'libcdio-paranoia'
    'libvorbis' 'opus' 'rtmpdump' 'gsm' 'libdvdnav'
  )

  make -C $pkgbase-$pkgver DESTDIR="$pkgdir" install-mencoder install-mencoder-man
  find "$pkgdir/usr/share/man" -name mplayer.1 -exec rename mplayer.1 mencoder.1 {} +
}
