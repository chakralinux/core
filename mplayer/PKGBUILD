#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=mplayer
pkgver=33159
pkgrel=1
pkgdesc="A movie player for linux"
arch=('i686' 'x86_64')
depends=('libxxf86dga' 'libxxf86vm'  'libmad' 'cdparanoia' 'libxinerama' 'sdl' 'lame' 'libtheora' 'xvidcore' 'libmng' 'libxss'
         'libgl' 'smbclient' 'aalib' 'jack' 'libcaca' 'x264' 'faac' 'lirc-utils' 'ttf-dejavu' 'libxvmc' 'enca' 'libvdpau>=0.4'
         'opencore-amr' 'libdca' 'a52dec' 'schroedinger' 'libvpx' 'openjpeg' 'mpg123')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'mesa' 'live-media>=2010.01.13' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(http://chakra-project.org/sources/${pkgname}/${pkgname}-${pkgver}-${pkgrel}.tar.xz mplayer.desktop mplayer.png)
md5sums=('caedcdc1e50b5575c38c2a85f9c53afc'
         'c0d6ef795cf6de48e3b87ff7c23f0319'
         'd00874ccc644b7f43d6ef1c942fcef28')

# create tarball: source PKGBUILD && mksource

mksource() {
    rm -vRf ${pkgname}
    msg "checkout mplayer"
    _pkgver=`svn info svn://svn.mplayerhq.hu/mplayer/trunk | grep Revision: | cut -d' ' -f2`
    svn co svn://svn.mplayerhq.hu/mplayer/trunk --config-dir ./ ${pkgname} || return 1
    cd ${pkgname}
    msg "checkout ffmpeg"
    git clone git://git.videolan.org/ffmpeg.git ffmpeg
    msg "clean checkout"
    rm -vRf ffmpeg/.git
    find . -name ".svn" -exec rm -rf {} \;
    cd ..
    pushd ${pkgname}
    popd
    msg "pack checkout"
    tar -cvJf ${pkgname}-${_pkgver}-${pkgrel}.tar.xz ${pkgname}/*
    md5sum ${pkgname}-${_pkgver}-${pkgrel}.tar.xz
    msg "change pkgver to ${_pkgver}"
}

build() {
  # Custom CFLAGS break the mplayer build
  unset CFLAGS LDFLAGS

  cd ${srcdir}/${pkgname}

  ./configure --prefix=/usr \
      --enable-runtime-cpudetection \
      --disable-gui \
      --disable-arts \
      --disable-liblzo \
      --disable-speex \
      --disable-openal \
      --disable-fribidi \
      --disable-libdv \
      --disable-musepack \
      --disable-esd \
      --disable-pulse \
      --disable-mga \
      --enable-xvmc \
      --language=all \
      --confdir=/etc/mplayer

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make
  make -j1 DESTDIR=${pkgdir} install
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
  install -dm755 ${pkgdir}/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
  rm -rf ${pkgdir}/usr/share/mplayer/font
  #desktop file  FS#14770
  install -Dm644 ${srcdir}/mplayer.desktop ${pkgdir}/usr/share/applications/mplayer.desktop
  install -Dm644 ${srcdir}/mplayer.png ${pkgdir}/usr/share/pixmaps/mplayer.png
}
