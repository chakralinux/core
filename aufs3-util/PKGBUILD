#
# Chakra Packages for Chakra, part of chakra-project.org

pkgname=aufs3-util
_gitbranch=3.9
pkgver=20140811
pkgrel=1
pkgdesc="Another Unionfs Implementation that supports NFS branches"
arch=('x86_64')
url="http://aufs.sourceforge.net/"
license=('GPL2')
makedepends=('linux-headers' 'git')
replaces=('aufs2-util')
source=("${pkgname}::git://git.code.sf.net/p/aufs/aufs-util#branch=aufs${_gitbranch}")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  date -d @$(git log -n1 --pretty=format:%ct) '+%Y%m%d'
}

prepare() {
  cd "${srcdir}/${pkgname}"
  sed -i 's/($|\\\\.x)/($|\\\\.x|-[0-9]*$)/' ver.c
}

build() {
  cd "${srcdir}/${pkgname}"
  export CFLAGS="$CFLAGS -I/usr/src/linux-3.16.3-1-CHAKRA/include/uapi/ -D__user="
  unset LDFLAGS

  make -j1
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install

  #fixes for the archlinux /usr/bin move
  mv "${pkgdir}"/sbin/* "${pkgdir}"/usr/bin/
  rmdir "${pkgdir}"/sbin
}
