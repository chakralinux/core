# Maintainer: Francesco Marinucci < franzmari[at]chakra-project[dot]it>
# contributor Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-good
pkgver=1.12.4
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libpulse' 'jack' 'libsoup' 'gst-plugins-base-libs' 'wavpack' 'aalib' 'taglib' 'libdv' 'libshout' 'libvpx' 'gdk-pixbuf2' 'libcaca' 'libavc1394' 'libiec61883' 'libxdamage' 'v4l-utils' 'cairo' 'libgudev')
makedepends=('gstreamer' 'speex' 'flac' 'libraw1394' 'python3')
options=(!emptydirs)
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('649f49bec60892d47ee6731b92266974c723554da1c6649f21296097715eb957'
            'SKIP')
validpgpkeys=('7F4BC7CC3CA06F97336BBFEB0668CC1486C2D7B5')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer Good Plugins (Chakra GNU/Linux)" \
    --with-package-origin="https://chakralinux.org/"
  
  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

# Failed at gdkpixbufoverlay, full log committed
check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
