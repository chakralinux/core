# maintainer abveritas@chakra-project.org
# contributor Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-good
pkgver=1.0.5
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('pulseaudio' 'jack' 'libsoup' 'gst-plugins-base' 'wavpack'
         'aalib' 'taglib' 'libdv' 'libshout' 'libvpx' 'gdk-pixbuf2' 'libcaca'
         'libavc1394' 'libiec61883' 'libxdamage' 'v4l-utils')
makedepends=('gstreamer' 'speex' 'flac' 'libraw1394')
options=(!libtool !emptydirs)
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('e1d451b8b198ab50f645635c1fc8a3cb')

build() {
  cd $pkgname-$pkgver
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (Chakra GNU/Linux)" \
    --with-package-origin="http://www.chakra-project.org/"
  make
}

check() {
  cd $pkgname-$pkgver
  
  make check
}

package() {
  cd $pkgname-$pkgver
  
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}
