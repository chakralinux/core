# maintainer almack@chakraos.org

# Find the kernel name inside the chroot
_extramodules=extramodules-3.2-lts
_kver="$(cat /usr/lib/modules/${_extramodules}/version)"

pkgname=madwifi-lts
pkgver=0.9.4.4182
_ver=r4182-20140204
pkgrel=2
pkgdesc="Madwifi drivers for Atheros wireless chipsets"
arch=('x86_64')
license=('GPL')
url="http://madwifi-project.org"
depends=('madwifi-utils' 'linux-lts>=3.2' 'linux-lts<3.4')
makedepends=('linux-lts-headers' 'sharutils')
install=madwifi-ng.install
# subversion source: svn checkout http:/madwifi-project.org/svn/madwifi/trunk madwifi
source=(http://snapshots.madwifi-project.org/madwifi-0.9.4-current.tar.gz)
	#http://downloads.sourceforge.net/madwifi/madwifi-${pkgver}.tar.gz)
md5sums=('8c373ac12f551b197586a5bc398bc7a6')
groups=("linux-lts-modules")

build()
{
  cd madwifi-*-${_ver}

  # kernel 3.2 abi change
  sed -i -e "s/.ndo_set_multicast_list/.ndo_set_rx_mode/" ath/if_ath.c
  sed -i -e "s/ieee80211_set_multicast_list/ieee80211_set_rx_mode/" net80211/ieee80211.c
  sed -i -e "s/ndo_set_multicast_list/ndo_set_rx_mode/" net80211/ieee80211.c

  sed -i 's/-Werror//' Makefile.inc
  sed -i 's/\[4-9\]\\\./[0-9]-/' Makefile
  make KMODPATH="/usr/lib/modules/$_extramodules" KERNELPATH=/usr/lib/modules/${_kver}/build KERNELRELEASE=${_kver} modules
}

package()
{
  cd madwifi-*-${_ver}
  make KMODPATH="/usr/lib/modules/$_extramodules" KERNELPATH=/usr/lib/modules/${_kver}/build KERNELRELEASE=${_kver} modules \
       DESTDIR="${pkgdir}" KERNELRELEASE=${_kver} install-modules
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" ${startdir}/*.install
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
}


