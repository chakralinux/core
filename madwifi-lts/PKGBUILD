#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

# Find the kernel name inside the chroot
_extramodules=extramodules-3.2-CHAKRA-LTS
_kver="3.2.51-1-lts"

pkgname=madwifi-lts
pkgver=0.9.4.4181
_ver=r4181-20130909
pkgrel=7
pkgdesc="Madwifi drivers for Atheros wireless chipsets"
arch=('x86_64')
license=('GPL')
url="http://madwifi-project.org"
depends=('madwifi-utils' 'linux-lts>=3.2' 'linux-lts<3.4')
makedepends=('linux-lts-headers' 'sharutils')
install=madwifi-ng.install
# subversion source: svn checkout http:/madwifi-project.org/svn/madwifi/trunk madwifi
source=(http://snapshots.madwifi-project.org/madwifi-trunk-current.tar.gz)
	#http://downloads.sourceforge.net/madwifi/madwifi-${pkgver}.tar.gz)
md5sums=('6505f0a18a3115915daf4dba9ddba4fc')
groups=("linux-lts-modules")

build()
{
  cd madwifi-trunk-$_ver

  # kernel 3.2 abi change
  sed -i -e "s/.ndo_set_multicast_list/.ndo_set_rx_mode/" ath/if_ath.c
  sed -i -e "s/ieee80211_set_multicast_list/ieee80211_set_rx_mode/" net80211/ieee80211.c
  sed -i -e "s/ndo_set_multicast_list/ndo_set_rx_mode/" net80211/ieee80211.c

  sed -i 's/-Werror//' Makefile.inc
  sed -i 's/\[4-9\]\\\./[0-9]-/' Makefile
  make KERNELPATH=/lib/modules/${_kver}/build KERNELRELEASE=${_kver} modules
}

package()
{
  cd madwifi-trunk-$_ver
  make KERNELPATH=/lib/modules/${_kver}/build KERNELRELEASE=${_kver} modules \
       DESTDIR="${pkgdir}" KERNELRELEASE=${_kver} install-modules
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" ${startdir}/*.install
  mkdir -p "${pkgdir}/lib/modules/$_extramodules"
  mv "${pkgdir}"/lib/modules/${_kver}/net/* "${pkgdir}/lib/modules/$_extramodules"
  rm -r "${pkgdir}"/lib/modules/${_kver}
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
}


