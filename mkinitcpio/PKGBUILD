#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=mkinitcpio
pkgver=0.8.2
pkgrel=4
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="http://www.archlinux.org/"
license=('GPL')
depends=('mkinitcpio-busybox>=1.16.1-2' 'kmod>=3' 'util-linux>=2.20.1-2' 'libarchive' 'coreutils'
         'bash' 'findutils' 'sed' 'grep' 'filesystem>=2011.10-1' 'file' 'gzip')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"
        '0001-autodetect-resolve-guessed-filesystem-modules.patch')
md5sums=('2e0ac0c657ca7a78aa0583b3368c0e44'
         '9a5f9cf5e9ca429824e927662a5095ea')
sha256sums=('9a951f9e2a158b65840f476c1b9e1e45c7b06383a90f182f76eab64bd3a398cb'
            '769e7a5eabb9b77802b1b55f4d123566e13c26dc4966189e4101a38069b6fe8f')

build() {
  cd "$pkgname-$pkgver"
  patch -Np1 <"$srcdir/0001-autodetect-resolve-guessed-filesystem-modules.patch"
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # compat symlink
  install -dm755 "$pkgdir/sbin"
  ln -s /usr/bin/mkinitcpio "$pkgdir/sbin/mkinitcpio"

  # XXX: remove udev hooks -- they belong to the udev package
  rm "$pkgdir"/lib/initcpio/{hooks,install}/udev
}

