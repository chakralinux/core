#
# Core Packages for Chakra, part of chakra-project.org
#

pkgname=mkinitcpio
pkgver=19
pkgrel=1
pkgdesc="Modular initramfs image creation utility"
arch=('x86_64')
url="http://www.archlinux.org/"
license=('GPL')
epends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive=3.1.2'
         'coreutils' 'bash' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        "microcode.patch::https://github.com/wengxt/mkinitcpio/commit/b948a50f03f9c243bb392a8f96f40b41c1e09a93.patch")
install=mkinitcpio.install
sha256sums=('7170e7a3d1b9ed21b0961941a327886febe80e62e2dfee6b88359ed72d0da620'
            'SKIP'
            'f3b0eacc41940840904da7a56755b4641da622c4f93983a3a04131a81d4cbdc2')
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53'   # Dave Reisner
              '86CFFCA918CF3AF47147588051E8B148A9999C34')  # Evangelos Foutras

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../microcode.patch
  sed -i 's/HOOKS="base udev autodetect modconf block filesystems keyboard fsck"/HOOKS="base udev autodetect resume keymap  modconf block filesystems keyboard fsck microcode"/g' mkinitcpio.conf
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
