#
# Core Packages for Chakra, part of chakra-project.org
#

pkgname=mkinitcpio
pkgver=18
pkgrel=1
pkgdesc="Modular initramfs image creation utility"
arch=('x86_64')
url="http://www.archlinux.org/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.21' 'libarchive=3.1.2'
         'coreutils' 'bash' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-avoid-compound-conditional-leading-to-spurious-error.patch'
        "microcode.patch::https://github.com/wengxt/mkinitcpio/commit/5dbfa177c4578957c001c537e816c945e4084020.patch")
install=mkinitcpio.install
sha256sums=('187bdeeade08996010fbff480ccc91e47722d275c22fd6feb4a4b63061e9fc22'
            'SKIP'
            'f6a77a34a5d97b8c3f3aef21b97da0b5d6992244e28e9b3f71e83cbaa4473341'
            'f1c61fd8f06689c35e4c965f3a61de45bc16420bf7c0c33c564f46b101112d79')
prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../0001-avoid-compound-conditional-leading-to-spurious-error.patch
  patch -Np1 -i ../microcode.patch
  sed -i 's/HOOKS="base udev autodetect modconf block filesystems keyboard fsck"/HOOKS="base udev autodetect resume keymap  modconf block filesystems keyboard fsck microcode"/g' mkinitcpio.conf
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
