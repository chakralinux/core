

pkgname=kfilemetadata
pkgver=5.54.0
pkgrel=1
pkgdesc='A library for extracting file metadata'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kfilemetadata'
license=('LGPL')
depends=('ki18n' 'qt5-base' 'karchive' 'kservice' 'taglib')
makedepends=('extra-cmake-modules' 'python3' 'exiv2' 'poppler-qt5' 'ffmpeg' 'ebook-tools')
optdepends=('ffmpeg: extract video data'
            'exiv2: extract image data'
            'taglib: extract audio data'
            'poppler-qt5: extract document data'
            'ebook-tools: extract epub file data')
groups=('frameworks')
options=("debug")
replaces=('kfilemetadata5' 'kfilemetadata<5')
provides=('kfilemetadata5')
conflics=('kfilemetadata5' 'kfilemetadata<5' 'kfilemetadata4')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('4b6de53b791ad8cf19d5aa331555e3863f22e54adf6149668125635b820f86c2'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
