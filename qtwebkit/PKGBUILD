#
# QT Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=qtwebkit
pkgver=2.3.3
_pkgver=23
pkgrel=1
_qtver=4.8.5
arch=('x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
options=("debug")
depends=('qt' 'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg'
         'gstreamer0.10-ugly-plugins' 'systemd')
makedepends=('gperf' 'python2' 'mesa' 'ruby' 'git')
_pkgfqn="qt-everywhere-opensource-src-${_qtver}"
# edit this source later, sf won't let us dl files that big
source=("http://chakra.sourceforge.net/sources/qtwebkit/qtwebkit-${pkgver}.tar.xz"
        "http://download.qt-project.org/official_releases/qt/4.8/${_qtver}/${_pkgfqn}.tar.gz"
        'qwebview.patch'
        'bison3.patch'
	'use-python2.patch')
sha1sums=('98bf1211fdd2fb931cc12c045432464353620cac'
          '745f9ebf091696c0d5403ce691dc28c039d77b9e'
          '88ed73e6bd7ea9c6d227ceaed0ff2f1cf0200667'
          'd2c6182512e4bcbdf30a9e9d513c52fe4b16d9f2'
          '315b6ff603f35e5492a036f7082f6aa075dfb607')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i "${srcdir}"/bison3.patch
  patch -p1 -i "${srcdir}"/use-python2.patch

  # QWebView plugin
  cd ../$_pkgfqn
  patch -p1 -i "${srcdir}"/qwebview.patch
}

build() {
  cd ${pkgname}-${pkgver}

  export QTDIR=/usr
  Tools/Scripts/build-webkit --qt \
    --makeargs="${MAKEFLAGS}" --prefix=/usr --no-webkit2

  cd ../${_pkgfqn}/tools/designer/src/plugins/qwebview
  qmake
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  cd ../$_pkgfqn/tools/designer/src/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
