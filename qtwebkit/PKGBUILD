#
# QT Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=qtwebkit
pkgver=2.3
_pkgver=23
pkgrel=5
_qtver=4.8.4
arch=('x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt' 'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg'
         'gstreamer0.10-ugly-plugins' 'systemd')
makedepends=('gperf' 'sqlite3' 'fontconfig' 'perl' 'python2' 'mesa' 'ruby' 'git')
source=("${pkgname}-${pkgver}.tar.gz"::"https://gitorious.org/webkit/qtwebkit-23/archive-tarball/qtwebkit-2.3-beta1"
        "http://releases.qt-project.org/qt4/source/qt-everywhere-opensource-src-${_qtver}.tar.gz"
        'qwebview.patch'
        'Notifications.diff'
        'MediaControl.diff'
        'WindowOpen.diff')
sha1sums=('e5a8ffe62fc57dbbea2110241077bebbcd605fd2'
          'f5880f11c139d7d8d01ecb8d874535f7d9553198'
          'ef467fcfc9e74aa88356f27acc21792706ed1e4d'
          '37cef3fb9f853e49c2394abb722634b4cdb3a744'
          'd92bc12d2d0f45a7343ee2acdb66df0ebf446d18'
          '1d98d3505c85f6b70a81444939be342cf51d3540')

build() {
  cd "${srcdir}"/webkit-qtwebkit-${_pkgver}
  
  patch -p1 -i "${srcdir}"/Notifications.diff
  patch -p1 -i "${srcdir}"/MediaControl.diff
  patch -p1 -i "${srcdir}"/WindowOpen.diff

  export QTDIR=/usr
  Tools/Scripts/build-webkit --qt \
    --makeargs="${MAKEFLAGS}" --prefix=/usr --no-webkit2 

  cd ../

  # QWebView plugin 
  cd qt-everywhere-opensource-src-${_qtver}
  patch -p1 -i "${srcdir}"/qwebview.patch
  cd tools/designer/src/plugins/qwebview
  qmake
  make
}

package() {
  cd "${srcdir}"/webkit-qtwebkit-${_pkgver}
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  cd ../

  cd qt-everywhere-opensource-src-${_qtver}/tools/designer/src/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
