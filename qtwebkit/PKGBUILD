#
# QT Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=qtwebkit
pkgver=2.3.3
_pkgver=23
pkgrel=2
_qtver=4.8.6
arch=('x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
options=("debug")
depends=('qt' 'gstreamer' 'gst-plugins-base' 'systemd')
makedepends=('gperf' 'python2' 'mesa' 'ruby' 'git')
optdepends=('gst-plugins-good: For media format support'
            'gst-plugins-bad: For media format support'
            'gst-plugins-ugly: For media format support'
            'gst-libav: For media format support')
_pkgfqn="qt-everywhere-opensource-src-${_qtver}"
# edit this source later, sf won't let us dl files that big
source=("http://pkgs.fedoraproject.org/repo/pkgs/qtwebkit/qtwebkit-2.3.3.tar.xz/6f9b0af3c401a43fa29cd3a9c393729a/qtwebkit-2.3.3.tar.xz"
        "http://download.qt-project.org/official_releases/qt/4.8/${_qtver}/${_pkgfqn}.tar.gz"
        'qtwebkit-2.3.3-gstreamer1.patch'
        'qwebview.patch'
        'bison3.patch'
	'use-python2.patch')
sha1sums=('ce2ed072585149e9c2787c5909efd16647e8b03b'
          'ddf9c20ca8309a116e0466c42984238009525da6'
          '4b4ce9465ea9b107de7a93ce57afce43a2a1877f'
          '88ed73e6bd7ea9c6d227ceaed0ff2f1cf0200667'
          'd2c6182512e4bcbdf30a9e9d513c52fe4b16d9f2'
          '315b6ff603f35e5492a036f7082f6aa075dfb607')

prepare() {
  cd webkit-qtwebkit-23

  patch -p1 -i ../qtwebkit-2.3.3-gstreamer1.patch

  patch -p1 -i "${srcdir}"/bison3.patch
  patch -p1 -i "${srcdir}"/use-python2.patch

  # QWebView plugin
  cd ../$_pkgfqn
  patch -p1 -i "${srcdir}"/qwebview.patch
}

build() {
  cd webkit-qtwebkit-23

  export QTDIR=/usr
  Tools/Scripts/build-webkit --qt \
    --makeargs="${MAKEFLAGS}" --prefix=/usr --no-webkit2

  cd ../${_pkgfqn}/tools/designer/src/plugins/qwebview
  qmake
  make
}

package() {
  cd webkit-qtwebkit-23
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  cd ../$_pkgfqn/tools/designer/src/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
