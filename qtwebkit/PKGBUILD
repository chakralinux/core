#
# QT Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=qtwebkit
pkgver=2.3.2
_pkgver=23
pkgrel=1
_qtver=4.8.5
arch=('x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt' 'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg'
         'gstreamer0.10-ugly-plugins' 'systemd')
makedepends=('gperf' 'python2' 'mesa' 'ruby' 'git')
_pkgfqn="qt-everywhere-opensource-src-${_qtver}"
source=("${pkgname}-${pkgver}.tar.gz"::"https://gitorious.org/webkit/qtwebkit-23/archive-tarball/qtwebkit-2.3.0"
        "http://download.qt-project.org/official_releases/qt/4.8/${_qtver}/${_pkgfqn}.tar.gz"
        'qwebview.patch'
	'qtwebkit-gcc48.diff'
	'use-python2.patch')
sha1sums=('8e0aa2f895376a0a88b55548bfadb270da3708ba'
          '745f9ebf091696c0d5403ce691dc28c039d77b9e'
          'ef467fcfc9e74aa88356f27acc21792706ed1e4d'
          '1eb137548a927fa06270212c865ff69f69facb22'
          '315b6ff603f35e5492a036f7082f6aa075dfb607')

prepare() {
  cd webkit-qtwebkit-${_pkgver}

  patch -p1 -i "${srcdir}"/qtwebkit-gcc48.diff
  patch -p1 -i "${srcdir}"/use-python2.patch
}

build() {
  cd webkit-qtwebkit-${_pkgver}
  
  export QTDIR=/usr
  sed -e "s#-Werror##g" -i configure.ac
  sed -e "/QtTestBrowser.pro/d" -i Tools/Tools.pro
  
  Tools/Scripts/build-webkit --qt \
    --makeargs="${MAKEFLAGS}" --prefix=/usr --no-webkit2 

  # QWebView plugin 
  cd ../$_pkgfqn
  patch -p1 -i "${srcdir}"/qwebview.patch

  cd tools/designer/src/plugins/qwebview
  qmake
  make
}

package() {
  cd webkit-qtwebkit-${_pkgver}
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  cd ../$_pkgfqn/tools/designer/src/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
