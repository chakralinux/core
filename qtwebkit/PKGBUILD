#
# QT Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=qtwebkit
pkgver=2.3
_pkgver=23
pkgrel=2
_qtver=4.8.4
arch=('x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt' 'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg'
         'gstreamer0.10-ugly-plugins' 'systemd')
makedepends=('gperf' 'sqlite3' 'fontconfig' 'perl' 'python2' 'mesa' 'ruby' 'git')
source=("${pkgname}-${pkgver}.tar.gz"::"http://gitorious.org/webkit/qtwebkit-${_pkgver}/archive-tarball/${pkgname}-2.3-staging"
        "http://releases.qt-project.org/qt4/source/qt-everywhere-opensource-src-${_qtver}.tar.gz"
        'qwebview.patch'
        'QGLWidget.diff'
        'QGLWidget2.diff')
sha1sums=('e5f122892f827afa892e8b5fc9e45f6078a14b05'
          'f5880f11c139d7d8d01ecb8d874535f7d9553198'
          'ef467fcfc9e74aa88356f27acc21792706ed1e4d'
          '8d856897f3e14bc21c9c3fdbb4027deb84009cf9'
          '31883c8fd80291d533050466b3760f34b11b37f7')

build() {
  cd "${srcdir}"/webkit-qtwebkit-${_pkgver}
  
  patch -p1 -i "${srcdir}"/QGLWidget.diff
  patch -p1 -i "${srcdir}"/QGLWidget2.diff

  export QTDIR=/usr
  Tools/Scripts/build-webkit --qt \
    --makeargs="${MAKEFLAGS}" --prefix=/usr --no-webkit2 

  cd ../

  # QWebView plugin 
  cd qt-everywhere-opensource-src-${_qtver}
  patch -p1 -i "${srcdir}"/qwebview.patch
  cd tools/designer/src/plugins/qwebview
  qmake
  make
}

package() {
  cd "${srcdir}"/webkit-qtwebkit-${_pkgver}
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  cd ../

  cd qt-everywhere-opensource-src-${_qtver}/tools/designer/src/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
