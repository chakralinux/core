#
# QT Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# contributor Mladen PejakoviÄ‡ <pejakm at gmail dot com>

pkgname=qtwebkit
pkgver=2.2.2
pkgrel=1
_rev=r118179
arch=('i686' 'x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt' 'qt-mobility' 'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg' 'gstreamer0.10-ugly-plugins')
makedepends=('gperf' 'sqlite3' 'fontconfig' 'perl' 'python2' 'mesa')
conflicts=('qt<4.8')

source=("http://builds.nightly.webkit.org/files/trunk/src/WebKit-${_rev}.tar.bz2"
        "http://chakra-linux.org/sources/$pkgname/qwebview-4.8.2.tar.xz")
sha1sums=('8d8e21163951b55c17e27f88631bf36fba33d41d'
          '33c83272ed8110180ee6e7e3733e68cc513e2802')

build() {
  cd "${srcdir}"/WebKit-${_rev}
  #rm -f "${srcdir}"/WebKit-${_rev}/qwebview-4.8.2
  #ln -s "${srcdir}"/QtWebKit-${pkgver}/qwebview-4.8.2 "${srcdir}"/WebKit-${_rev}/

  cd Tools/Scripts

  QTDIR=/usr/share/qt ./build-webkit --prefix=/usr --qt --webgl --release --microdata \
         --link-prefetch --accelerated-2d-canvas --3d-rendering \
         --animation-api --input-type-date \
         --input-type-datetime --input-type-datetimelocal --input-type-month --input-type-week --style-scoped \
         --svg-dom-objc-bindings --svg-fonts --tiled-backing-store \
         --web-sockets --web-timing --page-visibility-api \
         --touch-events --touch-icon-loading --icon-database \
         --javascript-debugger --progress-tag --download-attribute \
         --sql-database --xslt \
         --makeargs="${MAKEFLAGS}"  

  # Build the QWebView plugin
  cd "${srcdir}"/qtwebkit-${pkgver}-source/qwebview-4.8.2/plugins/qwebview
  qmake
  make
}

package() {
  cd "${srcdir}"/WebKit-${_rev}
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  # Fix RPATH
  #chrpath -r /usr/lib/ "${pkgdir}"/usr/lib/qt/imports/QtWebKit/libqmlwebkitplugin.so

  cd "${srcdir}"/qtwebkit-${pkgver}-source/qwebview-4.8.2/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
