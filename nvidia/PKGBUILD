#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# Find the kernel name inside the chroot
_kernver=$(pacman -Q linux | cut -c7-9 | sed 's/linux //g')-CHAKRA

pkgname=nvidia
pkgver=285.05.09
pkgrel=3
pkgdesc="NVIDIA drivers for linux."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux>=3.1' 'linux<3.2' "nvidia-utils=${pkgver}")
makedepends=('linux-headers')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install

if [ "$CARCH" = "i686" ]; then
    ARCH='x86'
    _pkg="NVIDIA-Linux-${ARCH}-${pkgver}"
    source=("http://us.download.nvidia.com/XFree86/Linux-${ARCH}/${pkgver}/${_pkg}.run")
    md5sums=('2d469a90abef50320f548cfa8085e3a0')
elif [ "$CARCH" = "x86_64" ]; then
    ARCH='x86_64'
    _pkg="NVIDIA-Linux-${ARCH}-${pkgver}-no-compat32"
    source=("http://us.download.nvidia.com/XFree86/Linux-${ARCH}/${pkgver}/${_pkg}.run")
    md5sums=('2f4a0e78f5560c07220b7ed6fc1e27aa')
fi

build() {
	echo "==> Building for kernel version ${_kernver}"
        cd $srcdir
        sh ${_pkg}.run --extract-only
        cd ${_pkg}/kernel
        make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
        install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
                $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
        install -d -m755 $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
        sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install
}
