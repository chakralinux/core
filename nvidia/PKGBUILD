#
# Chakra Packages for Chakra, part of chakraos.org

_extramodules=extramodules-3.18-CHAKRA
_kver=$(cat /usr/lib/modules/${_extramodules}/version)
_kernelver=3.18.3

pkgname=('nvidia' 'chd-nvidia')
pkgver=346.35
pkgrel=3
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=("linux-headers=${_kernelver}")
license=('custom')
source=("https://gitorious.org/chakra/chakra-hardware-detection/blobs/raw/master/hwdb/nvidia_supported_new"
        "nv-drm-343.36.patch")

_ARCH='x86_64'
_pkg="NVIDIA-Linux-${_ARCH}-${pkgver}-no-compat32"
source+=("ftp://download.nvidia.com/XFree86/Linux-${_ARCH}/${pkgver}/${_pkg}.run")
md5sums=('874fc31d9a647e51ce20372d090083f5'
         'ff8a5f979e4428f8c847423fb007042c'
         '9ca9f07f91361362f304ca8317ddd453')

prepare() {
    sh "${_pkg}.run" --extract-only

    cd "${_pkg}"
    patch -p1 -i ../nv-drm-343.36.patch
}

build() {
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kver}/build" module

    cd uvm
    make SYSSRC=/usr/lib/modules/"${_kver}/build" module
}

package_nvidia() {
    pkgdesc="NVIDIA drivers for linux."
    depends=("linux=${_kernelver}" "nvidia-libgl" "nvidia-utils=${pkgver}")
    conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia-304xx' 'nvidia-bumblebee')
    replaces=('nvidia-bumblebee')
    provides=('nvidia-bumblebee')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/uvm/nvidia-uvm.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-uvm.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/"*.ko
    
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
}

package_chd-nvidia() {
    pkgdesc="CHD ids for nvidia $pkgver"
    arch=('x86_64')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/chakra/hwdb"
    sh -e "${srcdir}/nvidia_supported_new" README.txt kernel/nv-kernel.o \
        > "${pkgdir}/opt/chakra/hwdb/hw_nvidia_hwdb"
}
