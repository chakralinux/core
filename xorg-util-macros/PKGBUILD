# Part of X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=xorg-util-macros
pkgver=1.19.1
pkgrel=1
pkgdesc="X.Org Autotools macros"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
source=("https://ftp.x.org/pub/individual/util/util-macros-${pkgver}.tar.gz"{,.sig})
sha1sums=('4bc1a51277bf0ffd9290b12f7b4071d1cea7dedd'
          'SKIP')
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # Matt Turner

build() {
  cd "${srcdir}/util-macros-${pkgver}"

  ./configure --prefix=/usr 
  make
}

package() {
  cd "${srcdir}/util-macros-${pkgver}" 

  make DESTDIR="${pkgdir}" install 
  install -dm755 "${pkgdir}/usr/lib"

  mv "${pkgdir}/usr/share/pkgconfig" "${pkgdir}/usr/lib/" 
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" 
}

