#Contributions from Arch: https://aur.archlinux.org/packages/qrupdate/

pkgname=qrupdate
pkgver=1.1.2
_pkgver=1.2
pkgrel=3
pkgdesc="Fortran library for fast updates of QR and Cholesky decompositions"
url="http://sourceforge.net/projects/qrupdate"
makedepends=('gcc-fortran')
depends=('openblas' 'lapack')
arch=('x86_64')
license=('GPL3')
source=( http://downloads.sourceforge.net/project/qrupdate/qrupdate/$_pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('6d073887c6e858c24aeda5b54c57a8c4')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -i "s/BLAS=-lblas/BLAS=-lopenblas/g" Makeconf
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  make PREFIX=/usr solib
}

check() {
  cd "$srcdir"/$pkgname-$pkgver

  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install
}
