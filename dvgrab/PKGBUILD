pkgname=dvgrab
pkgver=3.5
_commit=e46042e0c7b3523b6854ee547b0534e8310b7460
pkgrel=3
pkgdesc="Saves audio and video data from an IEEE (FireWire) digital source"
arch=('x86_64')
url="http://www.kinodv.org"
license=('GPL')
depends=('libdv' 'libavc1394' 'libjpeg-turbo' 'libiec61883' 'gcc-libs')
makedepends=('pkgconfig')
source=("git+https://github.com/ddennedy/dvgrab.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
