#
# Core packages for Chakra, part of chakra-project.org
#
# Maintainer: Ram-Z <ram-z[at]chakra-project[dot]org>

pkgname=curl
pkgver=7.39.0
pkgrel=1
pkgdesc="An URL retrival utility and library"
arch=('x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'krb5' 'libssh2' 'libidn' 'openssl' 'zlib')
source=("http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2"
        'curlbuild.h')
md5sums=('1efecb5b0e43c17d968f0d228bbbbbbd'
         '751bd433ede935c8fae727377625a8ae')

build() {
    cd ${pkgname}-${pkgver}

    ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-ldap \
        --disable-ldaps \
        --enable-ipv6 \
        --enable-manual \
        --enable-threaded-resolver \
        --enable-versioned-symbols \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
        --with-gssapi \
        --with-libidn \
        --with-random=/dev/urandom
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR=${pkgdir} install

    # license
    install -Dm644 COPYING \
        ${pkgdir}/usr/share/licenses/${pkgname}/COPYING

    # devel
    local _curlbuild=curlbuild-64.h
    mv "${pkgdir}/usr/include/curl/curlbuild.h" \
        "${pkgdir}/usr/include/curl/${_curlbuild}"
    install -m 644 ${srcdir}/curlbuild.h \
        "${pkgdir}/usr/include/curl/curlbuild.h"
}
