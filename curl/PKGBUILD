#
# Core packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=curl
pkgver=7.37.1
pkgrel=2
pkgdesc="An URL retrival utility and library"
arch=('x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('zlib' 'openssl' 'bash' 'ca-certificates' 'libssh2')
source=("http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2"
        'curlbuild.h')
md5sums=('95c627abcf6494f5abe55effe7cd6a57'
         '751bd433ede935c8fae727377625a8ae')

_curlbuild=curlbuild-64.h

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure \
        --with-random=/dev/urandom \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-dependency-tracking \
        --enable-ipv6 \
        --disable-ldaps \
        --disable-ldap \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
        --without-libidn \
        --without-librtmp \
        --enable-threaded-resolver
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make DESTDIR=${pkgdir} install
    install -Dm644 COPYING \
        ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
    install -Dm644 docs/libcurl/libcurl.m4 \
        ${pkgdir}/usr/share/aclocal/libcurl.m4
    mv "${pkgdir}/usr/include/curl/curlbuild.h" "${pkgdir}/usr/include/curl/${_curlbuild}"
    install -m 644 ${srcdir}/curlbuild.h "${pkgdir}/usr/include/curl/curlbuild.h"
}
