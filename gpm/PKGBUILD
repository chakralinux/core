#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=gpm
pkgver=1.20.6
pkgrel=7
pkgdesc="A mouse server for the console and xterm"
arch=('i686' 'x86_64')
url="http://unix.schottelius.org/gpm/"
license=('GPL')
depends=('ncurses' 'bash')
backup=('etc/conf.d/gpm')
options=('!makeflags')
install=gpm.install
source=(http://www.nico.schottelius.org/software/gpm/archives/${pkgname}-${pkgver}.tar.bz2 \
        gpm gpm.conf.d gpm.sh)
md5sums=('6b534da16dc1b28ba828dea89e520f6f' '945ecde0836687e05c13ea837092ea6c'\
         '63e599950946e5f5a649432510aacf4c' 'e391fe4599ce102615f9cd449c380d41')
sha1sums=('f3bb9272878b7934968381c8b25edab359114150' '19e1feb1493373512a77801699df012d186336ea'\
         'e483f893b4e6b9e1221a208527d728ffefe5438e' '88fe5ff10916c68a87abc8418a56eb0073f69fa9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m755 ../gpm "${pkgdir}/etc/rc.d/gpm"
  install -D -m644 ../gpm.conf.d "${pkgdir}/etc/conf.d/gpm"
  install -D -m755 ../gpm.sh "${pkgdir}/etc/profile.d/gpm.sh"

# library fixes
  cd "${pkgdir}/usr/lib/"
  ln -s libgpm.so.2.* libgpm.so
  chmod 755 "${pkgdir}"/usr/lib/libgpm.so.*
}
