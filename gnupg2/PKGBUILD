# Maintainer: abveritas[at]chakra-project[dot]org>

pkgname=gnupg2
pkgver=2.0.19
pkgrel=1
pkgdesc="GNU Privacy Guard 2 - a PGP replacement tool"
arch=('x86_64')
depends=('libldap' 'curl' 'bzip2' 'zlib' 'libksba' 'libgcrypt'
	'pth' 'libusb-compat' 'libassuan' 'texinfo' 'readline' 'pinentry' 'dirmngr')
license=('GPL')
url="http://www.gnupg.org/"
install=${pkgname}.install
source=("ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-$pkgver.tar.bz2"
        'valid-keyblock-packet.patch'
        'protect-tool-env.patch')
md5sums=('6a8589381ca1b0c1a921e9955f42b016'
         '3a18a71eac65eabe1e2472639e8b03fc'
         '7e5167aa042dbe18ab79d8b0df3f3cab')

build() {
  cd ${srcdir}/gnupg-$pkgver
  patch -p1 -i ${srcdir}/protect-tool-env.patch 
  patch -p1 -i ${srcdir}/valid-keyblock-packet.patch
	
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
	      --libexecdir=/usr/lib/gnupg \
	      --enable-maintainer-mode \
	      --enable-symcryptrun \
	      --enable-gpgtar 
  make
}

check() {
	cd "${srcdir}/gnupg-${pkgver}"
	make check
}

package() {
  cd ${srcdir}/gnupg-$pkgver
  make DESTDIR=${pkgdir} install
  # move conflicting files
  mv ${pkgdir}/usr/share/gnupg{,2}
  rm -f ${pkgdir}/usr/share/info/dir
  rm "${pkgdir}/usr/share/gnupg2/com-certs.pem"
}

