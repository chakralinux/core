# Part of X.org group
# maintainer AlmAck almack@chakraos.org

pkgname=xf86-video-chips
pkgver=1.2.5
pkgrel=4
pkgdesc="X.org Chips and Technologies video driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server' 'X-ABI-VIDEODRV_VERSION=15')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16')
groups=('xorg-video-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2 iopl.h git-fix.diff)
sha1sums=('5642e1d1bf3a05d24a0b8db48ca3b522c1962b00'
          'ec143f9a8c2958ba5fcd6759780362a70fa8abba'
          'dea86c6eaab38ebbbdf62f3e937472691d3e454d')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp ${srcdir}/iopl.h util/
  patch -Np1 -i ${srcdir}/git-fix.diff
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
