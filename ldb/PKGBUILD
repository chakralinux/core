# Contributions from Arch Linux: https://www.archlinux.org/packages/extra/x86_64/ldb/

pkgname=ldb
pkgver=1.1.24
pkgrel=1
pkgdesc="A schema-less, ldap like, API and database"
arch=('x86_64')
url="http://ldb.samba.org/"
source=(http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz
        http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.asc)
license=('GPL3')
depends=('talloc' 'tevent' 'tdb' 'popt')
makedepends=('python2' 'docbook-xsl')
optdepends=('python2: for python bindings')
md5sums=('152f061422de5b90f8ce51e0ea316292'
         'SKIP')
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') # Samba Library Distribution Key <samba-bugs@samba.org> 

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# change to use python2
	sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" buildtools/bin/waf
	export PYTHON=/usr/bin/python2

	./configure --prefix=/usr \
		--disable-rpath \
		--disable-rpath-install \
		--bundled-libraries=NONE \
		--builtin-libraries=replace \
                --with-modulesdir=/usr/lib/ldb/modules \
                --with-privatelibdir=/usr/lib/ldb
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir}/ install
}
