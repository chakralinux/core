#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer almack

pkgname=('x264' 'libx264' 'libx264-10bit')
pkgver=20141218
pkgrel=2
arch=('x86_64')
url="http://www.videolan.org/developers/x264.html"
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'ffmpeg' 'libvpx>=1.1.0')
source=("ftp://ftp.videolan.org/pub/videolan/x264/snapshots/x264-snapshot-$pkgver-2245-stable.tar.bz2")
md5sums=('f82e4f39453ba8f25a9b614a96741ee3')

prepare() {
  cp -r "$srcdir/$pkgname-snapshot-$pkgver-2245-stable" $pkgbase
  cp -r $pkgbase $pkgbase-10bit
}

build() {
  cd $pkgbase
  ./configure --prefix=/usr \
    --enable-shared --enable-pic
  make

  cd ../$pkgbase-10bit
  ./configure --prefix=/usr \
    --enable-shared --enable-pic \
    --bit-depth=10
  make
}

package_x264() {
  pkgdesc='CLI tools for encoding H264/AVC video streams'
  depends=('ffmpeg' 'libvpx' 'libx264')
  optdepends=('libx264-10bit: Library for encoding H264/AVC video streams. 10bit-depth.')

  make -C $pkgbase DESTDIR="$pkgdir" install-cli
  install -Dm755 $pkgbase-10bit/x264 "$pkgdir"/usr/bin/x264-10bit
}

package_libx264() {
  pkgdesc='Library for encoding H264/AVC video streams'

  install -d "$pkgdir"/usr/lib
  make -C $pkgbase DESTDIR="$pkgdir" install-lib-shared
}

package_libx264-10bit() {
  pkgdesc='Library for encoding H264/AVC video streams. 10bit-depth.'

  install -d "$pkgdir"/usr/lib
  make -C $pkgbase-10bit DESTDIR="$pkgdir" install-lib-shared
}