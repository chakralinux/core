# Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/python-cx_freeze

pkgbase=python-cx_freeze
pkgname=('python3-cx_freeze' 'python2-cx_freeze')
pkgver=4.3.4
pkgrel=1
pkgdesc='Set of utilities for freezing Python scripts into executables'
arch=('x86_64')
url='http://cx-freeze.sourceforge.net/'
license=('PSF')
makedepends=('python3' 'python2')
source=("https://pypi.python.org/packages/source/c/cx_Freeze/cx_Freeze-$pkgver.tar.gz")
md5sums=('5bd662af9aa36e5432e9144da51c6378')

package_python3-cx_freeze() {
  depends=('python3')
  conflicts=('cx_freeze' 'python2-cx_freeze')

  cd "cx_Freeze-$pkgver"
  python3 setup.py install --root "$pkgdir" --optimize 1
}

package_python2-cx_freeze() {
  depends=('python2')
  conflicts=('cx_freeze' 'python3-cx_freeze')
  replaces=('cx_freeze')

  cd "cx_Freeze-$pkgver"
  python2 setup.py install --root "$pkgdir" --optimize 1

  # Python 2 shebang fix
  find "$pkgdir" -name qotd.py -exec sed -i '0,/on/s//on2/' {} \;
}
