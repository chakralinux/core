#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# NOTE If fails to compile restart build without clearing src

pkgname=('ruby' 'ruby-docs')
pkgver=1.9.2_p180
_pkgver=1.9.2-p180
pkgrel=3
pkgdesc="An object-oriented language for quick and easy programming"
arch=('i686' 'x86_64')
url="http://www.ruby-lang.org/en/"
license=('custom')
makedepends=('gdbm' 'db' 'openssl' 'zlib' 'readline' 'tk' 'libffi'
             'doxygen' 'graphviz')
options=('!emptydirs')
source=("ftp://ftp.ruby-lang.org/pub/ruby/1.9/${pkgname}-${_pkgver}.tar.bz2")
md5sums=('68510eeb7511c403b91fe5476f250538')
sha256sums=('06520c4d4b4512d08000f7dfff11d1fabc1d458c3c289c76a2f1ddb7f5a03f4d')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --prefix=/usr \
      --enable-shared \
      --enable-pthread \
      --disable-rpath
  make
}

package_ruby()
{
  depends=('gdbm' 'db' 'openssl' 'zlib' 'readline' 'libffi')
  provides=('rubygems' 'rake')
  conflicts=('rubygems' 'rake')
  optdepends=('tk: for Ruby/TK')

  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install-nodoc
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_ruby-docs()
{
  pkgdesc="Documentation files for ruby"
  #arch=('any')
  depends=('ruby')

  cd "${srcdir}/${pkgname}-${_pkgver}"
  make -j1 DESTDIR="${pkgdir}" install-doc install-capi
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/ruby-docs/COPYING"
}
