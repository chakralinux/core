# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kparts
pkgver=${KFVersion}
pkgrel=2
pkgdesc='Document centric plugin system'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kparts'
license=('LGPL')
depends=('kio')
makedepends=("extra-cmake-modules>=${KFECMVersion}" "python3")
checkdepends=("cmake" "xorg-server-xvfb")
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgname}-${pkgver}.tar.xz"
        bug355711.patch::"https://quickgit.kde.org/?p=kparts.git&a=commitdiff&h=5ac5df&o=plain")
sha256sums=( $(getSum ${pkgname}) 
            'eca6530da8665856d55ee652f0701720045bff81f9e8225edf33ceb2c5b34137')

prepare() {
  mkdir -p build
  
  # Fix crash in systemsettings http://bugs.kde.org/show_bug.cgi?id=355711
  cd $pkgname-$pkgver
  patch -p1 -i ../bug355711.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver} \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

# check() {
#   cd build
#   xvfb-run make  test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
