# Arch Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-automat
pkgname=('python3-automat' 'python2-automat')
pkgver=0.6.0
pkgrel=1
arch=('any')
license=('MIT')
pkgdesc="Self-service finite-state machines for the programmer on the go."
url="https://github.com/glyph/automat"
makedepends=('m2r' 'python2-m2r' 'python3-attrs' 'python2-attrs' 'python3-setuptools-scm' 'python2-setuptools-scm')
checkdepends=('python3-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/glyph/automat/archive/v$pkgver.tar.gz")
sha512sums=('18488b7815ee6f1c19db17fef7b63b7c8b46639a7a2cd5060dd611c880d9c7828bf2ebf978c2eef560140b66333a94060d9f77c9e8f89342425229065a0a6f7f')

prepare() {
  cp -a automat-$pkgver{,-py2}

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/automat-$pkgver
  python3 setup.py build

  cd "$srcdir"/automat-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/automat-$pkgver
  python3 setup.py pytest || msg "Failed"

  cd "$srcdir"/automat-$pkgver-py2
  python2 setup.py pytest || msg "Failed"
}

package_python3-automat() {
  depends=('python3-attrs' 'python3-setuptools')

  cd automat-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/python-automat/LICENSE
}

package_python2-automat() {
  depends=('python2-attrs' 'python2-setuptools')

  cd automat-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/python2-automat/LICENSE

  mv "$pkgdir"/usr/bin/automat-visualize{,2}
}
