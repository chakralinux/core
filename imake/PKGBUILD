#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# contributor from Arch: Jan de Groot <jgc@archlinux.org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=imake
pkgver=1.0.5
pkgrel=1
pkgdesc="X.Org imake program and related utilities"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(glibc)
makedepends=(pkg-config xproto perl)
optdepends=('perl:	for mkhtmlindex')
source=("http://xorg.freedesktop.org/releases/individual/util/imake-${pkgver}.tar.bz2"
	http://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.2.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/lndir-1.0.3.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.4.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.4.tar.bz2
	linuxconfig.patch
	LICENSE)
md5sums=('797ec5dcad4df56eaafa0c701d6aa5ce'
         'b533c0771dbbaf9b041ff35bb941d3a2'
         'a47bcd6cecda1a6ebd3395926ea4f53a'
         '7acb9a831817fdd11ba7f7aaa3c74fd8'
         '700c6d040d36a569e657a3ba5e1d8b24'
         'b3385d8efbbe7c7fc50ed3a96ce37a16'
         'f5767133ef7a98a0bfa15139e0e3d32e')

build() {
  cd ${srcdir}/xorg-cf-files-1.0.4
  patch -p1 -i "${srcdir}/linuxconfig.patch"
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      popd
    fi
  done
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

