pkgname=gdk-pixbuf2
pkgver=2.36.11
pkgrel=1
pkgdesc="An image loading library"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11' 'jasper' 'shared-mime-info')
makedepends=('gobject-introspection')
install=gdk-pixbuf2.install
source=(https://download.gnome.org/sources/gdk-pixbuf/${pkgver:0:4}/gdk-pixbuf-$pkgver.tar.xz)
sha256sums=('ae62ab87250413156ed72ef756347b10208c00e76b222d82d9ed361ed9dde2f3')

build() {
  cd gdk-pixbuf-$pkgver

  ./configure --prefix=/usr \
    --with-x11 \
    --with-libjasper \
    --with-included-loaders=png
  make
}

package() {
  cd gdk-pixbuf-$pkgver

  make DESTDIR="$pkgdir" install
}
