# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=gdk-pixbuf2
pkgver=2.26.5
pkgrel=1
pkgdesc="An image loading library"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11')
makedepends=('gobject-introspection')
options=('!libtool')
install=gdk-pixbuf2.install
source=("http://download.gnome.org/sources/gdk-pixbuf/2.26/gdk-pixbuf-${pkgver}.tar.xz")
md5sums=('339329e6d619ee3e1cb93979111b04c0')

build() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  ./configure --prefix=/usr \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
  make
}

package() {
  cd "gdk-pixbuf-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
