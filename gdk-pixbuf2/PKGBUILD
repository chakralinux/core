pkgname=gdk-pixbuf2
pkgver=2.36.0
pkgrel=1
pkgdesc="An image loading library"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11' 'jasper')
makedepends=('gobject-introspection')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver:0:4}/gdk-pixbuf-$pkgver.tar.xz)
sha256sums=('85ab52ce9f2c26327141b3dcf21cca3da6a3f8de84b95fa1e727d8871a23245c')

build() {
  cd gdk-pixbuf-$pkgver

  ./configure --prefix=/usr \
    --with-x11 \
    --with-libjasper \
    --with-included-loaders=png
  make
}

package() {
  cd gdk-pixbuf-$pkgver

  make DESTDIR="$pkgdir" install
}
