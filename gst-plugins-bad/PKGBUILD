#
# Platform packages of Chakra Project, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# contributor Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-bad
pkgver=1.2.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('mjpegtools' 'gst-plugins-base' 'curl' 'chromaprint' 'libmms' 'faad2'
         'mpg123' 'faac' 'celt' 'libdca' 'soundtouch' 'spandsp' 'libdvdnav'
         'libmodplug' 'libgme' 'opus' 'ffmpeg' 'openal' 'libass')
makedepends=('schroedinger' 'libexif' 'libdvdread' 'libvdpau' 'libmpeg2' 
	     'x264' 'bluez' 'opencore-amr' 'libshairport' 'libcdaudio' 'libdc1394' 'libwebp' 'lv2' 'librsvg' 'timidity++'
	     'rtmpdump' 'xvidcore' 'gobject-introspection')
options=(!libtool !emptydirs)
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('8ee6863cbb917bb4bd235698b87192a4')

build() {
  cd $pkgname-$pkgver
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (Chakra GNU/Linux)" \
    --with-package-origin="http://www.chakra-project.org/"
  make
}

check() {
  cd $pkgname-$pkgver
  
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR="${pkgdir}" install
}
