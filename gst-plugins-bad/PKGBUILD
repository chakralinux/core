#contributions from Arch; https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/gst-plugins-bad

pkgname=gst-plugins-bad
pkgver=1.12.4
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('mjpegtools' 'gst-plugins-base-libs' 'curl' 'chromaprint' 'libmms' 'faad2' 'mpg123' 'faac' 'celt' 'libdca'
         'soundtouch' 'spandsp' 'libdvdnav' 'libmodplug' 'libgme' 'opus' 'wayland' 'neon' 'libofa' 'fluidsynth'
         'openjpeg' 'libwebp' 'libsrtp' 'gnutls' 'wildmidi' 'glu' 'sbc' 'rtmpdump' 'libgudev' 'graphene')
makedepends=('schroedinger' 'libexif' 'libdvdread' 'libvdpau' 'libmpeg2' 'python' 'valgrind' 'wildmidi' 'librsvg'
             'gobject-introspection' 'opencv' 'libtiger' 'ladspa' 'openal' 'libusb'
             'bluez' 'vulkan-headers' 'vulkan-icd-loader')
options=(!emptydirs)
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('0c7857be16686d5c1ba6e34bd338664d3d4599d32714a8eca5c8a41a101e2d08'
            'SKIP')
validpgpkeys=('7F4BC7CC3CA06F97336BBFEB0668CC1486C2D7B5')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer Bad Plugins (Chakra GNU/Linux)" \
    --with-package-origin="https://chakralinux.org/"
    
  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  # don't have a camera for the camerabin test
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
