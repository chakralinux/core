#
# Platform packages of Chakra Project, part of chakra-project.org
#
# maintainer almack
# contributor Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-bad
pkgver=1.4.4
pkgrel=2
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('mjpegtools' 'gst-plugins-base' 'curl' 'chromaprint' 'libmms' 'faad2'
         'mpg123' 'faac' 'celt' 'libdca' 'soundtouch' 'spandsp' 'libdvdnav'
         'libmodplug' 'libgme' 'opus' 'ffmpeg' 'openal' 'libass')
makedepends=('schroedinger' 'libexif' 'libdvdread' 'libvdpau' 'libmpeg2' 
	     'x264' 'bluez' 'opencore-amr' 'libshairport' 'libcdaudio' 'libdc1394' 'libwebp' 'lv2' 'librsvg' 'timidity++'
	     'rtmpdump' 'xvidcore' 'gobject-introspection' 'python2' 'python3')
options=('!emptydirs')
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('972c6e22dd2e44fcf0b04b9d810a56be')

build() {
  cd $pkgname-$pkgver
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (Chakra GNU/Linux)" \
    --with-package-origin="http://www.chakraos.org/"
  make
}

check() {
  cd $pkgname-$pkgver
  
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR="${pkgdir}" install
}
