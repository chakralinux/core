# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sean Middleditch <elanthis@awesomeplay.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=docbook-xsl
pkgver=1.79.2
pkgrel=1
pkgdesc="XML stylesheets for Docbook-xml transformations."
arch=('any')
license=('custom')
url="http://docbook.org/"
depends=('libxml2' 'libxslt' 'docbook-xml')
source=("https://github.com/docbook/xslt10-stylesheets/releases/download/release%2F$pkgver/docbook-xsl-nons-$pkgver.tar.gz"
        765567_non-recursive_string_subst.patch)
install=$pkgname.install
md5sums=('ca2a6aa27de09ed676d5fc0c9385cbba'
         'f77800752a4815cbcf138a3578032628')

prepare() {
  cd ${srcdir}/${pkgname}-nons-${pkgver}
  patch -Np2 -i ../765567_non-recursive_string_subst.patch
}

package() {
  cd ${srcdir}/${pkgname}-nons-${pkgver}

  _pkgroot=${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}

  install -dm755 ${_pkgroot}
  install -m644 VERSION VERSION.xsl ${_pkgroot}

  for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib \
      manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
          install -dm755 ${_pkgroot}/${fn}
	  install -m644 ${fn}/*.{xml,xsl,dtd,ent} ${_pkgroot}/${fn} || true  # ignore missing files
  done

  install -dm755 ${pkgdir}/etc/xml
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
