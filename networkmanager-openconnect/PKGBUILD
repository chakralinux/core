pkgname=networkmanager-openconnect
pkgver=1.2.4
pkgrel=1
pkgdesc="NetworkManager VPN plugin for OpenConnect"
url="https://wiki.gnome.org/Projects/NetworkManager"
arch=('x86_64')
license=('GPL')
depends=('networkmanager' 'openconnect' 'dbus-glib')
makedepends=('intltool') 
install=$pkgname.install
_commit=5123d7622fcce22da3cea75f878410e8c6dcfd69  # tags/1.2.4^0
source=("git+https://git.gnome.org/browse/network-manager-openconnect#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd network-manager-openconnect
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd network-manager-openconnect
}

build() {
  cd network-manager-openconnect
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --enable-more-warnings=yes \
    --without-gnome \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd network-manager-openconnect
  make DESTDIR="$pkgdir" install
}
