pkgbase=unrar
pkgname=('unrar' 'libunrar')
pkgver=5.6.5
pkgrel=1
pkgdesc="The RAR uncompression program"
arch=('x86_64')
url="https://www.rarlab.com/rar_add.htm"
license=('custom')
makedepends=('hardening-wrapper')
source=("https://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz")
sha256sums=('eba36a421bf41491818dee9507d934064622bc0bd9db6bbb8422a4706f200898')

build() {
  cp -a unrar libunrar
  make -C libunrar lib
  make -C unrar STRIP="true"
}

package_unrar() {
  pkgdesc="The RAR uncompression program"
  depends=('gcc-libs')
  cd "${srcdir}/${pkgbase}"
  install -Dm755 unrar "${pkgdir}/usr/bin/unrar"
  # install license
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libunrar() {
  pkgdesc="Library and header file for applications that use libunrar"
  depends=('gcc-libs')
  cd "${srcdir}/libunrar"
  install -Dm755 libunrar.so "${pkgdir}/usr/lib/libunrar.so"
  install -Dm644 dll.hpp "${pkgdir}/usr/include/unrar/dll.hpp"
  # install license
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
