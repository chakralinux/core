# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/netctl

pkgname=netctl
pkgver=1.11
pkgrel=1
pkgdesc='Profile based systemd network management'
url='http://projects.archlinux.org/netctl.git/'
license=('GPL')
groups=('base')
arch=('x86_64')
depends=('coreutils' 'iproute2' 'openresolv' 'systemd')
# The source tarball includes pre-built (using asciidoc) documentation.
makedepends=('pkg-config')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support (or dhcpcd)'
            'dhcpcd: for DHCP support (or dhclient)'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ppp: for PPP connections'
            'openvswitch: for Open vSwitch connections')
provides=('netcfg')
replaces=('netcfg')
conflicts=('netcfg')
source=(https://sources.archlinux.org/other/packages/netctl/netctl-${pkgver}.tar.xz)
md5sums=('1465a0595af16fd88a5f1ecdcebc0f0c')

package() {
  cd "$srcdir/netctl-${pkgver}"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
}
