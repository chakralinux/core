# maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/netctl

pkgname=netctl
pkgver=1.4
pkgrel=1
pkgdesc="Profile based systemd network management"
url="http://projects.archlinux.org/netctl.git/"
license=("GPL")
arch=('x86_64')
depends=("coreutils" "iproute2" "openresolv")
makedepends=('asciidoc') 
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support (or dhcpcd)'
            'dhcpcd: for DHCP support (or dhclient)'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ifenslave: for bond connections'
            'bridge-utils: for bridge connections'
            'ppp: for pppoe connections')
conflicts=("netcfg")
source=("ftp://ftp.archlinux.org/other/packages/netctl/netctl-${pkgver}.tar.xz")
md5sums=('825f07c432bff1de0254ab800f22cb61')

package() {
  cd "$srcdir/netctl-${pkgver}"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
}
