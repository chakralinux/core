# Contribution from Arch:
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-reportlab
pkgname=('python2-reportlab' 'python3-reportlab')
_origname=reportlab
pkgver=3.3.0
pkgrel=1
pkgdesc="A proven industry-strength PDF generating solution"
arch=('x86_64')
url="http://www.reportlab.org/rl_toolkit.html"
depends=('freetype2')
makedepends=('python3' 'python2')
checkdepends=('python2-pillow' 'python3-pillow')
conflicts=('python-reportlab<=2.5-1')
replaces=('python-reportlab<=2.5-1')
license=('CUSTOM')
source=("https://pypi.python.org/packages/source/r/reportlab/${_origname}-${pkgver}.tar.gz")
md5sums=('8ad6181b69ec515d4f6d8bb894682d5d')

build() {
    cd ${srcdir}
    cp -rf ${_origname}-${pkgver} "${_origname}2-${pkgver}"
}

package_python2-reportlab() {
    depends=('python2')
    cd "${srcdir}/${_origname}2-${pkgver}"
    python2 setup.py install --root=${pkgdir} --optimize=1
    install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

package_python3-reportlab() {
    depends=('python3')
    cd "${srcdir}/${_origname}-${pkgver}"
    python3 setup.py install --root=${pkgdir} --optimize=1
    install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}


check() {
    cd ${srcdir}/${_origname}-${pkgver}
    python2 setup.py tests
    python3 setup.py tests
}
