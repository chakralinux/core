#
# Core Packages for Chakra, part of chakraos.org
#
# maintainer almack

pkgname=libffi
pkgver=3.2.1
pkgrel=1
pkgdesc="A portable, high level programming interface to various calling conventions."
arch=('x86_64')
license=('MIT')
url="http://sourceware.org/libffi"
depends=('glibc')
checkdepends=('dejagnu')
install=libffi.install
source=("ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz")
md5sums=('83b89587607e3eb65c70d361f13bab43')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static \
    --enable-pax_emutramp
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}