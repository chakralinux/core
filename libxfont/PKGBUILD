# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=libxfont
pkgver=1.4.7
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc' 'xproto' 'fontsproto')
makedepends=('xorg-util-macros' 'xtrans')
options=('!libtool')
source=(${url}/archive/individual/lib/libXfont-${pkgver}.tar.bz2
        CVE-2014-209-210-211.patch
        fix-for-fontsproto213.patch)
sha1sums=('77f60d0a2190cb36c07c2217693f46d5e8942ca2'
          '028e2ca692f6f847322bf00a9361d5a324396e95' 
          '5495cd81d0d7ef7773c8f214ad50d21e258afed3')

prepare() {
  cd "${srcdir}/libXfont-${pkgver}"
  patch -Np1 -i ../fix-for-fontsproto213.patch
  # fix CVE-2014-209 CVE-2014-210 CVE-2014-211 - merged upstream
  patch -Np1 -i ../CVE-2014-209-210-211.patch
}


build() {
  cd "${srcdir}/libXfont-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXfont-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

