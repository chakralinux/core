#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=hibernate-script
pkgver=2.0
pkgrel=1
pkgdesc="Set of scripts for managing tux on ice, hibernation and suspend to RAM"
arch=('any')
url="http://www.tuxonice.net"
license=('GPL')
depends=('sh')
options=(!strip)
backup=('etc/hibernate/hibernate.conf' 'etc/hibernate/tuxonice.conf' \
        'etc/hibernate/disk.conf' 'etc/hibernate/ram.conf' \
        'etc/hibernate/common.conf' 'etc/hibernate/blacklisted-modules' \
        'etc/hibernate/ususpend-both.conf' 'etc/hibernate/sysfs-ram.conf' \
        'etc/hibernate/ususpend-ram.conf' 'etc/hibernate/sysfs-disk.conf' \
        'etc/hibernate/ususpend-disk.conf')
source=(http://tuxonice.net/files/${pkgname}-${pkgver}.tar.gz
        hibernate-script-${pkgver}-chakra.patch
        hibernate.rc)
md5sums=('5c21770afbae503450e3c4a5502bf29d'
         '09e0d47e0d46d94da64c26a12a29afee'
         '5d7e83902a00bf72b1de97a5450a558e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # Fix scriptlets to work properly with Chakra GNU/Linux
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-chakra.patch
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  export BASE_DIR=${pkgdir}
  export PREFIX=/usr
  export MAN_DIR=$BASE_DIR$PREFIX/share/man
  install -dm755 ${pkgdir}/etc/{rc,logrotate}.d
  ./install.sh || return 1
  install -m 755 $srcdir/hibernate.rc $pkgdir/etc/rc.d/hibernate-cleanup
  # Allow for behaviour as in man-page (calling by hibernate-* uses
  # /etc/hibernate/*.conf file.
  ln -s /usr/sbin/hibernate ${pkgdir}/usr/sbin/hibernate-ram
  ln -s /usr/sbin/hibernate ${pkgdir}/usr/sbin/hibernate-disk
}
