# Part of the X.org group

pkgname=xscreensaver
pkgver=5.34
pkgrel=1
pkgdesc="Screen saver and locker for the X Window System"
arch=('x86_64')
url="http://www.jwz.org/xscreensaver/"
license=('BSD')
depends=('libxmu' 'glu' 'xorg-appres' 'perl-libwww' 'libxft' 'libjpeg-turbo')
makedepends=('bc' 'intltool' 'libxpm' 'chakra-wallpapers-lichene')
backup=('etc/pam.d/xscreensaver')
source=(http://www.jwz.org/xscreensaver/${pkgname}-${pkgver}.tar.gz
        add-electricsheep.diff
        xscreensaver.pam
        LICENSE)
sha1sums=('a6d9a391981730cedcd51e75c35ffb4101928cc2'
          'a68407b91ebff2629f1928d4e909d3565116906c'
          '106635aa1aae51d6f0668b1853f6c49a4fe9d3d8'
          '4209ea586b204fd1d81c382a0522c654f9fd9134')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p0 -i "${srcdir}/add-electricsheep.diff" 
}

build() {
  cd ${pkgname}-${pkgver}
  # added -lpthread from http://wiki.linuxfromscratch.org/blfs/ticket/5112#comment:7
  LIBS=-lpthread ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --with-x-app-defaults=/usr/share/X11/app-defaults \
    --with-pam --without-motif --without-gtk --with-gl \
    --without-gle --with-xpm --with-jpeg --with-xft
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make install_prefix="${pkgdir}" install 
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" 
  install -D -m644 ../xscreensaver.pam "${pkgdir}/etc/pam.d/xscreensaver" 
  chmod 755 "${pkgdir}/usr/bin/xscreensaver" 
  #echo "NotShowIn=KDE;" >> "${pkgdir}/usr/share/applications/xscreensaver-properties.desktop" 
}
