# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=cairo
pkgver=1.12.6
pkgrel=1
pkgdesc="Cairo vector graphics library"
arch=('x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng>=1.4.0' 'libxrender' 'libxext' 'fontconfig>=2.8.0' 'pixman' 'glib2' 'sh')
makedepends=('librsvg' 'poppler-glib' 'libspectre')
options=('!libtool')
source=("http://cairographics.org/releases/${pkgname}-${pkgver}.tar.xz"
        'cairo-1.10.0-buggy_gradients.patch'
        'git_fixes.diff')
sha1sums=('a383c6cb4495e18848ea43e1031c294aa9417a43'
          'e867f1020391686b972d5bedcce3c6d1193d7953'
          '2184e8d633cfaf09aa87746f7b90e619a8a5afeb')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/cairo-1.10.0-buggy_gradients.patch"
  patch -p1 -i "${srcdir}/git_fixes.diff"

#  autoreconf -vfi
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-tee \
	--disable-xlib-xcb 

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
