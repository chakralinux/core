# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=cairo
pkgver=1.12.14
pkgrel=3
_ubuntu_diff_version=${pkgver}-0ubuntu1
pkgdesc="Cairo vector graphics library"
arch=('x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng>=1.6.0' 'libxrender' 'libxext' 'fontconfig>=2.8.0' 'pixman' 'glib2' 'sh' 'mesa>=9.0' 'libgl' 'lzo2')
makedepends=('librsvg' 'poppler-glib' 'libspectre' 'valgrind' 'ttf-dejavu' 'gsfonts' 'xorg-server')
provides=('cairo-xcb')
rpelaces=('cairo-xcb')
options=('!libtool')
source=("http://cairographics.org/releases/${pkgname}-${pkgver}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_${_ubuntu_diff_version}.debian.tar.gz"
        "libpng16.patch")
sha1sums=('9106ab09b2e7b9f90521b18dd4a7e9577eba6c15'
          'c4c89d212f1e3a131ea4e228562c91a0a632118f'
          'c9911f185637d266ce1d2985bd6fb7d0df3d75b2')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # apply ubuntu patches
  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  patch -p1 < ../libpng16.patch
}
  
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-static \
              --enable-tee \
              --enable-gl \
              --enable-egl \
              --enable-svg \
              --enable-ps \
              --enable-pdf \
              --enable-gobject
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
