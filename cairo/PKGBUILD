# maintainer abveritas@chakra-project.org

pkgname=cairo
pkgver=1.14.10
pkgrel=1
_ubuntu_diff_version=${pkgver}-1
pkgdesc="Cairo vector graphics library"
arch=('x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman' 'glib2' 'sh' 'mesa>=9.0' 'libgl' 'lzo2')
makedepends=('librsvg' 'poppler-glib' 'libspectre' 'valgrind' 'ttf-dejavu' 'gsfonts' 'xorg-server')
source=("http://cairographics.org/releases/${pkgname}-${pkgver}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_${_ubuntu_diff_version}ubuntu1.debian.tar.xz")
sha1sums=('28c59d85d6b790c21b8b59ece73a6a1dda28d69a'
          '89429dcce2a2efe930361b98f6d8210b885bba93')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # apply ubuntu patches
  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  autoreconf -vfi
}
  
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # --enable-qt? interesting feature, but still experimental (2014)
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-static \
              --enable-tee \
              --enable-gl \
              --enable-egl \
              --enable-svg \
              --enable-ps \
              --enable-pdf \
              --enable-gobject
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
