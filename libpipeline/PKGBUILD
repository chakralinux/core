#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=libpipeline
pkgver=1.2.0
pkgrel=2
pkgdesc="a C library for manipulating pipelines of subprocesses in a flexible and convenient way"
arch=('i686' 'x86_64')
url="http://libpipeline.nongnu.org/"
license=('GPL')
groups=('base')
depends=('glibc')
options=('!libtool')
source=(http://download.savannah.gnu.org/releases/libpipeline/$pkgname-$pkgver.tar.gz)
md5sums=('dd3a987a0d2b594716baee2f73d61ae3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}