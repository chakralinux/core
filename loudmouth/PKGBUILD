#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org

pkgname=loudmouth
pkgver=1.4.3
pkgrel=2
pkgdesc="A lightweight Jabber client library written in C/Glib"
arch=('x86_64')
license=('LGPL')
url="http://developer.imendio.com/wiki/Loudmouth"
depends=('glib2>=2.18.3' 'openssl' 'gnutls')
options=('!libtool')
makedepends=('perlxml' 'pkg-config')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.4/${pkgname}-${pkgver}.tar.bz2"
        '01-fix-sasl-md5-digest-uri.patch'
        '03-drop-stanzas-on-fail.patch'
        '04-use-pkg-config-for-gnutls.patch')
md5sums=('55339ca42494690c3942ee1465a96937'
         'dc799cea18b24847b1e008c7424010a3'
         'b7b2d81b01a5eee5fd5e21cae67b4af7'
         'bffb25b9551df43255fe1706588582f3')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    patch -p1 -i "${srcdir}/01-fix-sasl-md5-digest-uri.patch"
    patch -p1 -i "${srcdir}/03-drop-stanzas-on-fail.patch"
    patch -p1 -i "${srcdir}/04-use-pkg-config-for-gnutls.patch"
           
    ./configure --prefix=/usr \
    		--disable-debug \
                --disable-gtk-doc \
                --with-ssl=openssl \
                --disable-static 
    make 
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    make DESTDIR="${pkgdir}" install 
}
