pkgname=libmirage
pkgver=3.0.4
pkgrel=1
pkgdesc="CD-ROM image (B6T/CCD/CDI/CUE/ISO/MDS/NRG/TOC) access library"
arch=('x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('glib2' 'libsndfile' 'zlib' 'xz' 'bzip2' 'libsamplerate')
makedepends=('bison' 'flex' 'cmake')
optdepends=('gobject-introspection')
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2")
sha256sums=('cc05370f0cc15348910a149207384f03b48b204a19215c1510924244996a3f3f')

build() {
  cd $srcdir/$pkgname-$pkgver

  mkdir -p build && cd build
  
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPOST_INSTALL_HOOKS:BOOL=OFF \
    -DWITH_GTKDOC=OFF
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
}
