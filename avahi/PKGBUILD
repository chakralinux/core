# Maintainer: Phil Miller <philm@chakra-project.org>

pkgname=avahi
pkgver=0.6.30
pkgrel=4
pkgdesc="A multicast/unicast DNS-SD framework"
arch=('i686' 'x86_64')
url="http://www.avahi.org/"
license=('LGPL')
depends=('dbus>=1.1.20-1' 'libcap>=2.16' 'libdaemon>=0.11' 'gdbm' 'glib2' 'expat')
optdepends=('qt: qt bindings'
	'nss-mdns: NSS support for mDNS'
	'dbus-python: avahi-discover'
        'twisted: avahi-bookmarks')
makedepends=('qt' 'dbus-python' 'gobject-introspection' 'pygtk' 'intltool')
backup=(etc/avahi/avahi-daemon.conf etc/avahi/services/{sftp-,}ssh.service)
install=avahi.install
conflicts=('howl' 'mdnsresponder')
provides=('howl' 'mdnsresponder')
replaces=('howl' 'mdnsresponder')
options=('!libtool')
source=(http://www.avahi.org/download/avahi-${pkgver}.tar.gz gnome-nettool.png avahi-daemon-dbus.patch)
sha1sums=('5b77443537600a00770e4c77e3c443eeb5861d06'
          'cf56387c88aed246b9f435efc182ef44de4d52f3'
          '36735096a6eeb3a4012fe14f875259ee8558e220')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i 's/netdev/network/g' avahi-daemon/avahi-dbus.conf
  patch -p1 -i "${srcdir}/avahi-daemon-dbus.patch"

  PYTHON=python2 \
  PKG_CONFIG_PATH=/opt/qt/lib/pkgconfig \
    ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-qt3 \
    --enable-qt4 \
    --disable-mono \
    --disable-monodoc \
    --disable-doxygen-doc \
    --disable-xmltoman \
    --disable-gtk \
    --disable-gtk3 \
    --enable-compat-libdns_sd \
    --enable-compat-howl \
    --with-distro=archlinux \
    --with-avahi-priv-access-group=network \
    --with-autoipd-user=avahi \
    --with-autoipd-group=avahi \
    --with-systemdsystemunitdir=/lib/systemd/system

  make 
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -D -m 644 ../gnome-nettool.png "${pkgdir}"/usr/share/pixmaps/gnome-nettool.png

	cd "${pkgdir}"
	sed -i '1c #!/usr/bin/python2' usr/bin/avahi-{bookmarks,discover}
	# howl and mdnsresponder compatability
	(cd usr/include; ln -s avahi-compat-libdns_sd/dns_sd.h dns_sd.h; ln -s avahi-compat-howl howl)
	(cd usr/lib/pkgconfig; ln -s avahi-compat-howl.pc howl.pc)
}

