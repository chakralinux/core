pkgname=gnupg
pkgver=2.0.24
pkgrel=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
arch=('x86_64')
optdepends=('curl: gpg2keys_curl'
            'libldap: gpg2keys_ldap'
            'libusb-compat: scdaemon')
makedepends=('curl' 'libldap' 'libusb-compat')
depends=('bzip2' 'libksba' 'libgcrypt' 'pth' 'libassuan' 'readline' 'pinentry' 'dirmngr')
license=('GPL')
url="http://www.gnupg.org/"
install=${pkgname}.install
source=("ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-$pkgver.tar.bz2"
        'protect-tool-env.patch')
md5sums=('94cd984321b44ab622aa50f93ee66671'
         '7e5167aa042dbe18ab79d8b0df3f3cab')
conflicts=('gnupg2')
provides=('gnupg2')
replaces=('gnupg2')
         
prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/protect-tool-env.patch 

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
	      --libexecdir=/usr/lib/gnupg \
	      --enable-maintainer-mode \
	      --enable-symcryptrun \
	      --enable-gpgtar 
  make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
  ln -s gpg2 "${pkgdir}"/usr/bin/gpg
  ln -s gpgv2 "${pkgdir}"/usr/bin/gpgv
  ln -s gpg2.1.gz "${pkgdir}"/usr/share/man/man1/gpg.1.gz
  rm "${pkgdir}/usr/share/gnupg/com-certs.pem"
}

