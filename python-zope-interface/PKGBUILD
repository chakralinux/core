#
# Platform Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrian Chaves Fernandez (Gallaecio) <adriyetichaves@gmail.com>

pkgbase=python-zope-interface
pkgname=('python3-zope-interface' 'python2-zope-interface')
pkgver=4.1.1
pkgrel=2
pkgdesc="Separate distribution of the zope.interface package used in Zope 3"
license=('ZPL')
arch=('x86_64')
url="http://pypi.python.org/pypi/zope.interface"
makedepends=('python3-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/z/zope.interface/zope.interface-${pkgver}.tar.gz")
md5sums=('edcd5f719c5eb2e18894c4d06e29b6c6')

prepare() {
  cp -a zope.interface-${pkgver}{,-python2}
}

build() {
  #build python3 module
  cd "${srcdir}/zope.interface-${pkgver}"
  python3 setup.py build

  #build python2 module
  cd "${srcdir}/zope.interface-${pkgver}-python2"
  python2 setup.py build
}

# Fail with TypeError: unorderable types: str() < NoneType()
#check() {
#  cd "${srcdir}/zope.interface-${pkgver}"
#  python3 setup.py test
#
#  cd "${srcdir}/zope.interface-${pkgver}-python2"
#  python2 setup.py test
#}

package_python3-zope-interface() {
  pkgdesc=('Zope Interfaces for Python 3.x')
  depends=('python')
 
  cd "${srcdir}/zope.interface-${pkgver}"
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  cp src/zope/__init__.py "${pkgdir}/usr/lib/python3.5/site-packages/zope/"
}

package_python2-zope-interface(){
  pkgdesc=('Zope Interfaces for Python 2.x')
  depends=('python2')
  replaces=('zope-interface')
  provides=('zope-interface')

  cd "${srcdir}/zope.interface-${pkgver}-python2"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  cp src/zope/__init__.py "${pkgdir}/usr/lib/python2.7/site-packages/zope/"
}
