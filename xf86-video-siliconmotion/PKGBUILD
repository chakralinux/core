# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributor: abveritas@chakra-project.org

pkgname=xf86-video-siliconmotion
pkgver=1.7.7
pkgrel=1
pkgdesc="X.org siliconmotion video driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server' 'xf86driproto' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.12.3')
groups=('xorg-video-drivers')
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2" 0001-Remove-miInitializeBackingStore.patch)
sha1sums=('60dc57c6ff6e0cd3f73076fce3c11d7e788ce7c1'
          '83ba6d0ef3dfbcde4a04b77055c0df5189216433')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 < ${srcdir}/0001-Remove-miInitializeBackingStore.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
