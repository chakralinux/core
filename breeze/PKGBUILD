# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/breeze

pkgbase=breeze
pkgname=('breeze' 'breeze-kde4')
pkgver=5.13.0
pkgrel=2
pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
makedepends=(extra-cmake-modules frameworkintegration kdelibs automoc4 kdecoration kcmutils plasma-framework python3)
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a03193bbc1c7a385c302ba988fb79996edcb09b5673ac399a4062e0409825129'
             'SKIP' )
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build{,-kde4}
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver}
  make

  cd ../build-kde4
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_KDE4=ON \
    -DBUILD_TESTING=OFF
  make
}

package_breeze() {
  depends=('frameworkintegration' 'kdecoration' 'breeze-icons')
  pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
  optdepends=('breeze-kde4: Breeze widget style for KDE4 applications'
              'breeze-gtk: Breeze widget style for GTK applications'
              'kcmutils: for breeze-settings')
  groups=('plasma')

  cd build
  make DESTDIR="${pkgdir}" install

  # make QtCurve preset and color scheme available for KDE4 too
  install -d "$pkgdir"/usr/share/apps/{QtCurve,color-schemes}
  ln -s /usr/share/QtCurve/Breeze.qtcurve "$pkgdir"/usr/share/apps/QtCurve/
  ln -s /usr/share/color-schemes/Breeze{,Dark}.colors "$pkgdir"/usr/share/apps/color-schemes/
}

package_breeze-kde4() {
  pkgdesc='Artwork, styles and assets for the Breeze visual style for the KDE4'
  depends=('kdelibs')

  cd build-kde4
  make DESTDIR="${pkgdir}" install

  # needed for pure Qt4 apps
  install -d "$pkgdir"/usr/lib/qt/plugins/styles
  ln -s /usr/lib/kde4/plugins/styles/breeze.so "$pkgdir"/usr/lib/qt/plugins/styles
}
