# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/breeze
source ../plasma.conf

pkgbase=breeze
pkgname=('breeze' 'breeze-kde4')
pkgver=${PVersion}
pkgrel=1
pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
makedepends=('extra-cmake-modules' 'frameworkintegration' 'kdecoration' 'kdelibs' 'automoc4' 'kcmutils' 'plasma-framework')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PVersion}.tar.xz"{,.sig})
sha256sums=( $(getSum ${pkgbase} | head -n 1) 
             'SKIP' )
validpgpkeys=(${Pvalidpgpkeys})

prepare() {
  mkdir -p build{,-kde4}
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver}
  make

  cd ../build-kde4
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_KDE4=ON \
    -DBUILD_TESTING=OFF
  make
}

package_breeze() {
  depends=('frameworkintegration' 'kdecoration' 'breeze-icons')
  pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
  optdepends=('breeze-kde4: Breeze widget style for KDE4 applications'
              'breeze-gtk: Breeze widget style for GTK applications'
              'kcmutils: for breeze-settings')
  groups=('plasma')

  cd build
  make DESTDIR="${pkgdir}" install

  # make QtCurve preset and color scheme available for KDE4 too
  install -d "$pkgdir"/usr/share/apps/{QtCurve,color-schemes}
  ln -s /usr/share/QtCurve/Breeze.qtcurve "$pkgdir"/usr/share/apps/QtCurve/
  ln -s /usr/share/color-schemes/Breeze{,Dark}.colors "$pkgdir"/usr/share/apps/color-schemes/
}

package_breeze-kde4() {
  pkgdesc='Artwork, styles and assets for the Breeze visual style for the KDE4'
  depends=('kdelibs')

  cd build-kde4
  make DESTDIR="${pkgdir}" install

  # needed for pure Qt4 apps
  install -d "$pkgdir"/usr/lib/qt/plugins/styles
  ln -s /usr/lib/kde4/plugins/styles/breeze.so "$pkgdir"/usr/lib/qt/plugins/styles
}
