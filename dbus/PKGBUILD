#
# Core Packages for Chakra, part of chakraos.org
#

pkgbase=dbus
pkgname=('dbus' 'libdbus')
pkgver=1.8.14
pkgrel=2
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=('x86_64')
license=('GPL' 'custom')
makedepends=('libx11' 'systemd' 'xmlto' 'docbook-xsl')
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz
        30-dbus)
md5sums=('dfb810eda8780cf9debf7169b3ca5fe1'
         '3314d727fa57fc443fce25b5cbeebbcc')

# note: remember to add the dbus group in the chroot, groupadd -g 81 dbus

build() {
  cd dbus-$pkgver
  # Note, group dbus is defined in the pkg filesystem, under the gourp file
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=dbus \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify --disable-dnotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-systemd
  make
}

package_dbus(){
  depends=('libdbus' 'expat')
  optdepends=('libx11: dbus-launch support')
  provides=('dbus-core')
  conflicts=('dbus-core')
  replaces=('dbus-core')

  cd dbus-$pkgver

  # Disable installation of libdbus
  sed -i -e 's/^SUBDIRS = dbus/SUBDIRS =/' Makefile

  make DESTDIR="$pkgdir" install

  # Remove libdbus files
  rm -rf "${pkgdir}/var/run"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"

  install -Dm755 ../30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/30-dbus"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}

package_libdbus(){
  pkgdesc="DBus library"
  depends=('glibc')

  cd dbus-$pkgver
  make DESTDIR="$pkgdir" -C dbus install
  make DESTDIR="$pkgdir" install-data-am
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/libdbus/COPYING"
}