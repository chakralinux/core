#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: inkane@chakra-project.org
# contributor: abveritas@chakra-project.org

pkgname=dbus
pkgver=1.6.12
pkgrel=4
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=('i686' 'x86_64')
license=('GPL' 'custom')
depends=('expat' 'coreutils' 'libx11' 'systemd' 'dbus-core')
source=("http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "systemd-user-session.patch"
        '30-dbus')
install=dbus.install
md5sums=('a70edc50524f258eaf5c9a9994ed8748'
         'baa6689b6df440d2036edebd40e3c41e'
         '3314d727fa57fc443fce25b5cbeebbcc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ECHO="echo" ./configure --prefix=/usr \
			 --sysconfdir=/etc \
                          --localstatedir=/var \
                          --libexecdir=/usr/lib/dbus-1.0 \
                          --with-dbus-user=81 \
                          --with-system-pid-file=/var/run/dbus.pid \
                          --disable-verbose-mode \
                          --disable-static \
                          --enable-inotify \
                          --disable-dnotify \
                          --disable-tests \
                          --disable-asserts \
                          --with-x \
                          --enable-x11-autolaunch \
                          --enable-sytemd

  patch -p1 < "$srcdir/systemd-user-session.patch"

  make -C tools dbus-launch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m755 tools/dbus-launch "${pkgdir}/usr/bin/"
  install -m644 doc/dbus-launch.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/30-dbus" "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/dbus/"
}
