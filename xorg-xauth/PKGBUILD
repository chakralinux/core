# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=xorg-xauth
pkgver=1.0.10
pkgrel=1
pkgdesc="X.Org authorization settings program"
arch=(x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxmu')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(${url}/releases/individual/app/xauth-${pkgver}.tar.bz2)
sha256sums=('5afe42ce3cdf4f60520d1658d2b17face45c74050f39af45dccdc95e73fafc4d')

build() {
  cd xauth-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xauth-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
