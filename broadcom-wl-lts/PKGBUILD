#
# Chakra Packages for Chakra, part of chakra-project.org
#

_extramodules=extramodules-3.2-lts
_kernver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=broadcom-wl-lts
pkgver=6.30.223.141
pkgrel=3
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux-lts>=3.2' 'linux-lts<3.4')
makedepends=('linux-lts-headers')
backup=('etc/modprobe.d/broadcom-wl-lts.conf')
install=install

source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz"
        'modprobe.d'
        'license.patch')
sha1sums=('3a2453c71a07030f41a8fc613c87badf8452848c'
          '89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373')

build() {
	cd "${srcdir}"

	patch -p1 -i ${srcdir}/license.patch

	make -C /lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 ${srcdir}/modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl-lts.conf
}