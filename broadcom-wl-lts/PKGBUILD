#
# Chakra Packages for Chakra, part of chakraos.org
#

_extramodules=extramodules-3.14-CHAKRA-LTS
_kver="$(cat /lib/modules/${_extramodules}/version)"
_kernelver=3.14.44

pkgname=broadcom-wl-lts
pkgver=6.30.223.248
pkgrel=4
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=("linux-lts=${_kernelver}")
makedepends=("linux-lts-headers=${_kernelver}")
backup=('etc/modprobe.d/broadcom-wl-lts.conf')
install=install
source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz"
         #"http://ftp.de.debian.org/debian/pool/non-free/b/broadcom-sta/broadcom-sta_5.100.82.112.orig.tar.gz" "http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz" we need the debian version, because upstream URL doesn't exist anymore
        'modprobe.d'
        'license.patch'
        'gcc.patch')
sha1sums=('bd55f1fdc6b0a4d0a70dc4ba4bb4169a824e238c'
          '89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
          '8a940675e8c0baa65494c378e6155543b26a9a58')

prepare() {
        cd "${srcdir}"
        patch -p1 -i license.patch
        patch -p1 -i gcc.patch

        sed -e "/BRCM_WLAN_IFNAME/s:eth:wlan:" \
            -i src/wl/sys/wl_linux.c
}

build() {
	cd "${srcdir}"

	make -C /lib/modules/"${_kver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 ${srcdir}/modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl-lts.conf
}
