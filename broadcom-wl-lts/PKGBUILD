#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# use ../fakeuname -r 2.6.35-CHAKRA-LTS ../makepkg
_kernver=`uname -r`

pkgname=broadcom-wl-lts
pkgver=5.100.82.38
pkgrel=1
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('i686' 'x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('kernel26-lts')
makedepends=('kernel26-lts-headers')
groups=("kernel26-lts-modules")

[ "$CARCH" = 'x86_64' ] && ARCH=x86_64 || ARCH=x86_32
source=("http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz"
        'license.patch'
        'semaphore.patch'
        'mutex-sema.patch')

sha1sums=('3062830f86c5f1f41d8a4b26d6e0072dc989c7ba'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
          '105f8e1d48ebe1f25d53859e5ab9326a27435c66'
          'adf0138194ae204ad70ce44e98b2dc18ead4ae2a')
[ "$CARCH" = 'x86_64' ] && sha1sums[0]='6076de3bd8206b3e7b8bfc2eadbe3a97eec4e735'

install=install

build() {
	cd "$srcdir"
	patch -p1 < license.patch
	patch -p1 < semaphore.patch
	patch -p1 < mutex-sema.patch
	make -C /lib/modules/$_kernver/build M=`pwd`
}

package() {
	cd "$srcdir"
	install -D -m 755 wl.ko "$pkgdir/lib/modules/$_kernver/kernel/drivers/net/wireless/wl.ko"
	install -D -m 644 lib/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/*.install
}
