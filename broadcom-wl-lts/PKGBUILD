#
# Chakra Packages for Chakra, part of chakra-project.org
#

_extramodules=extramodules-3.2-CHAKRA-LTS
_kernver="3.2.51-1-lts"

pkgname=broadcom-wl-lts
pkgver=5.100.82.112
pkgrel=7
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux-lts>=3.2' 'linux-lts<3.4')
makedepends=('linux-lts-headers')
backup=('etc/modprobe.d/broadcom-wl-lts.conf')
install=install

source=("http://ftp.de.debian.org/debian/pool/non-free/b/broadcom-sta/broadcom-sta_5.100.82.112.orig.tar.gz" #"http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz" we need the debian version, because upstream URL doesn't exist anymore
        'modprobe.d'
        'linux3.patch'
        'license.patch'
        'semaphore.patch'
        'linux-3.2.0.patch')
sha1sums=('e51da1ca8f7b200f0760eafe321f710e1805d7e0'
          '89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
          '3e18f905bbe5e8b99a53d6ecb3b80a919f3531f2'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
          '105f8e1d48ebe1f25d53859e5ab9326a27435c66'
          '78bdb9da6df583a766cd3edb9c35b181400ba79c')

build() {
	cd "${srcdir}/broadcom-sta-${pkgver}/amd64"

	patch -p1 -i ${srcdir}/linux3.patch
	patch -p2 -i ${srcdir}/linux-3.2.0.patch
	patch -p1 -i ${srcdir}/license.patch
	patch -p1 -i ${srcdir}/semaphore.patch

	make -C /lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	cd "${srcdir}/broadcom-sta-${pkgver}/amd64"

	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 ${srcdir}/modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl-lts.conf
}