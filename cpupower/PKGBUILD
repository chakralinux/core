#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=cpupower
pkgver=3.4
pkgrel=1
pkgdesc='Linux kernel tool to examine and tune power saving related features of your processor'
license=('GPL2')
arch=('i686' 'x86_64')
url='http://www.kernel.org'
depends=('pciutils')
makedepends=('asciidoc' 'xmlto')
conflicts=('cpufrequtils')
backup=('etc/conf.d/cpupower')
options=('!strip')
source=("http://ftp.kernel.org/pub/linux/kernel/v3.x/linux-$pkgver.tar.xz"
        'cpupower.rc'
        'cpupower.conf'
        'cpupower.service')
md5sums=('967f72983655e2479f951195953e8480'
         'ad84b0bf8459cac1de9de9241b5ae017'
         'fb4b3d184850f5972c5a1bba3ead4e05'
         '20870541e88109d2f153be3c58a277f1')

build() {
  pushd linux-$pkgver/tools/power/cpupower
  
  LDFLAGS=${LDFLAGS:+"$LDFLAGS,--no-as-needed"}
  make VERSION=$pkgver-$pkgrel
  popd
}

package() {
  pushd linux-$pkgver/tools/power/cpupower
  make \
    DESTDIR="$pkgdir" \
    mandir='/usr/share/man' \
    docdir='/usr/share/doc/cpupower' \
    install install-man
  popd

  install -D -m 755 cpupower.rc "$pkgdir/etc/rc.d/cpupower"
  install -D -m 644 cpupower.conf "$pkgdir/etc/conf.d/cpupower"
  install -D -m 644 cpupower.service "$pkgdir/usr/lib/systemd/system/cpupower.service"
}

