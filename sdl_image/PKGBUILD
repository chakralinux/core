#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=sdl_image
pkgver=1.2.12
pkgrel=2
pkgdesc="A simple library to load images of various formats as SDL surfaces"
arch=('x86_64')
license=('custom')
depends=('sdl>=1.2.13' 'libpng' 'libjpeg-turbo' 'libtiff' 'zlib')
options=('!libtool')
url="http://www.libsdl.org/projects/SDL_image/"
source=("http://www.libsdl.org/projects/SDL_image/release/SDL_image-${pkgver}.tar.gz")
sha1sums=('5e3e393d4e366638048bbb10d6a269ea3f4e4cf2')

build() {
  cd "${srcdir}/SDL_image-${pkgver}"
  
  ./configure --prefix=/usr --disable-static
  make 
}

package() {
  cd "${srcdir}/SDL_image-${pkgver}"
  
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
