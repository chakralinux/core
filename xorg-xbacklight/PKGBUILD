# Maintainer: Drake Justice <djustice@chakraos.org>

pkgname=xorg-xbacklight
pkgver=1.2.1
pkgrel=1
pkgdesc="RandR-based backlight control application"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('xcb-util')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xbacklight-${pkgver}.tar.bz2)
sha256sums=('17f6cf51a35eaa918abec36b7871d28b712c169312e22a0eaf1ffe8d6468362b')

build() {
  cd xbacklight-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xbacklight-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

