# Maintainer: almack[at]chakraos[dot]org>

pkgname=texinfo
pkgver=5.2
pkgrel=3
pkgdesc="Utilities to work with and produce manuals, ASCII text, and on-line documentation from a single source file"
arch=('x86_64')
url="http://www.gnu.org/software/texinfo/"
license=('GPL3')
groups=('base')
depends=('ncurses' 'findutils' 'gzip' 'sh' 'perl')
install=texinfo.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
	texinfo-5.2-C-n-fix.patch)
md5sums=('cb489df8a7ee9d10a236197aefdb32c5'
         'SKIP'
         '89724bac1ecbd1a57b1ea81e87f96f44')
validpgpkeys=('17D3311B14BC0F248267BF020716748A30D155AD') # Karl Berry

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 -i ${srcdir}/texinfo-5.2-C-n-fix.patch
}

build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   
   ./configure --prefix=/usr
   make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}
   
   make DESTDIR=${pkgdir} install
}
