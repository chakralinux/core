# Maintainer: almack[at]chakraos[dot]org>

pkgname=texinfo
pkgver=6.3
pkgrel=2
pkgdesc="Utilities to work with and produce manuals, ASCII text, and on-line documentation from a single source file"
arch=('x86_64')
url="http://www.gnu.org/software/texinfo/"
license=('GPL3')
groups=('base')
depends=('ncurses' 'findutils' 'gzip' 'sh' 'perl')
install=texinfo.install
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
	texinfo-6.0-disable-failing-info-test.patch)
md5sums=('32baefe5c7080dfb512a4eac5ce67b2a'
         'SKIP'
	 '8bfaa21315a8a6184811de8478005d22')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ../texinfo-6.0-disable-failing-info-test.patch
}

build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   
   ./configure --prefix=/usr
   make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}  
   make DESTDIR=${pkgdir} install
   rm -rfv $pkgdir/usr/share/libalpm/
}
