#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=xf86-video-rendition
pkgver=4.2.4
pkgrel=6
pkgdesc="X.org Rendition video driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server')
conflicts=('xorg-server<1.12.3')
options=('!libtool' '!strip')
groups=('xorg-video-drivers')
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"
        'rendition-4.2.4-git.patch')
sha1sums=('16148c25183f7829c22bc9dfdb588c1a16bf4565'
          '1715a29b953df5f2aecb900de5e73bed96f6d4e0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/rendition-4.2.4-git.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  strip --strip-unneeded "${pkgdir}/usr/lib/xorg/modules/drivers/rendition_drv.so"
}
