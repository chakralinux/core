# maintainer abveritas@chakra-project.org

pkgname=cdemu-daemon
# See https://groups.google.com/forum/?hl=en&fromgroups#!topic/chakra-devel/7F1Ze0zChnM
# & packaging guidelines on http://cdemu.sourceforge.net/about/daemon/
pkgver=2.1.1
pkgrel=1
pkgdesc="CD/DVD-ROM device emulator daemon"
arch=('x86_64')
backup=('etc/conf.d/cdemu-daemon'
	'etc/dbus-1/system.d/cdemu-daemon-dbus.conf')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('glib2' 'dbus' 'dbus-glib' 'libdaemon' 'vhba-module' "libmirage=$pkgver" 'libao')
makedepends=('cmake')
optdepends=('alsa-lib: to enable the ALSA audio driver'
            'pulseaudio: to enable the PA audio driver')
install=$pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        'cdemu-daemon.conf')
md5sums=('a0ede84ba3278d2886358b4f3fec5e18'
         '109d1eb46eb2943e511715611907d1e9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib/$pkgname  
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  
  install -d "${pkgdir}/usr/lib/modules-load.d"
  echo "vhba" > "${pkgdir}/usr/lib/modules-load.d/cdemu.conf"
  
  cd ../..
  
  # Chakra settings
  install -Dm644 "${srcdir}/cdemu-daemon.conf" "${pkgdir}/etc/conf.d/cdemu-daemon"
}
