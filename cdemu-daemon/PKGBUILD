#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=cdemu-daemon
pkgver=1.4.0
pkgrel=6
pkgdesc="CD/DVD-ROM device emulator daemon"
arch=('i686' 'x86_64')
backup=('etc/conf.d/cdemud'
	'etc/dbus-1/system.d/cdemud-dbus.conf')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('glib2' 'dbus' 'dbus-glib' 'libdaemon' 'vhba-module' "libmirage=$pkgver" 'libao')
optdepends=('alsa-lib: to enable the ALSA audio driver'
            'pulseaudio: to enable the PA audio driver')
install=cdemud.install
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.gz
        cdemud.conf
        cdemud.rc)
md5sums=('f7ccc0abc33057d552ec7b0925fce63a'
         'c3097a01d9e79093485181ff47a81542'
         '29b9bc0023ff17e14091f3fb8438ccb4')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -m 644 -D ../cdemud.conf $pkgdir/etc/conf.d/cdemud
  install -m 755 -D ../cdemud.rc $pkgdir/etc/rc.d/cdemud

  # for systemd
  install -d "$pkgdir/usr/lib/modules-load.d"
  echo "vhba" > "$pkgdir/usr/lib/modules-load.d/cdemu.conf"

}
