#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=cdemu-daemon
pkgver=1.4.0
pkgrel=1
pkgdesc="CD/DVD-ROM device emulator daemon"
arch=('i686' 'x86_64')
backup=('etc/conf.d/cdemud'
	'etc/dbus-1/system.d/cdemud-dbus.conf')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('glib2' 'dbus' 'dbus-glib' 'libdaemon' 'vhba-module' "libmirage=$pkgver" 'libao')
install=cdemud.install
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.gz
        cdemud.conf
        cdemud.rc
        60-vhba.rules)
md5sums=('f7ccc0abc33057d552ec7b0925fce63a'
         '26b94a626c2c1d0a83ad5a7593dff70f'
         '655b2bf46802f2fa7821a649cecd1c4d'
         'f551504552d7693ac79e8e0d302f62b9')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -m 644 -D ../cdemud.conf $pkgdir/etc/conf.d/cdemud
  install -m 755 -D ../cdemud.rc $pkgdir/etc/rc.d/cdemud

  # Install udev rule for non-root mounting of iso's 
  install -D -m 755 "${srcdir}/60-vhba.rules"  "$pkgdir/lib/udev/rules.d/60-vhba.rules"

}
