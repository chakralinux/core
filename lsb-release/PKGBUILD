# maintainer (x86_64): almack <almack[at]chakralinux[dot]org>

pkgname=lsb-release
pkgver=1.4
pkgrel=23
pkgdesc="LSB version query program."
arch=('x86_64')
url="http://www.linuxbase.org/"
depends=('bash')
makedepends=('perl') # to generate the man page
license=('GPL2')
source=("http://downloads.sourceforge.net/lsb/${pkgname}-${pkgver}.tar.gz")
md5sums=('30537ef5a01e0ca94b7b8eb6a36bb1e4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" mandir="${pkgdir}/usr/share/man" install

  # Create an /etc/lsb-release file suitable for Chakra
  install -d -m755 "${pkgdir}/etc"
  cat > "${pkgdir}/etc/lsb-release" <<-EOF
LSB_VERSION=${pkgver}
DISTRIB_ID="Chakra"
DISTRIB_RELEASE="2018.12"
DISTRIB_CODENAME="Hawking"
DISTRIB_DESCRIPTION="Chakra"
EOF

  install -Dm 644 lsb_release.1.gz "$pkgdir/usr/share/man/man1/lsb_release.1.gz"
  install -Dm 755 lsb_release "$pkgdir/usr/bin/lsb_release"
}

# vim:set ts=2 sw=2 et:
