#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=lsb-release
pkgver=1.4
pkgrel=3
pkgdesc="LSB version query program"
arch=('i686' 'x86_64')
url="http://www.linuxbase.org/"
license=('GPL2')
source=(http://downloads.sourceforge.net/lsb/$pkgname-$pkgver.tar.gz)
md5sums=('30537ef5a01e0ca94b7b8eb6a36bb1e4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -dm755 "$pkgdir/etc"
  echo "DISTRIB_ID=\"Chakra\"" > "$pkgdir/etc/lsb-release"
  echo "DISTRIB_RELEASE=\"2011.04\"" >> "$pkgdir/etc/lsb-release"
  echo "DISTRIB_CODENAME=\"Aida\"" >> "$pkgdir/etc/lsb-release"
  echo "DISTRIB_DESCRIPTION=\"Chakra Linux\"" >> "$pkgdir/etc/lsb-release"

  install -Dm 644 lsb_release.1.gz "$pkgdir/usr/share/man/man1/lsb_release.1.gz"
  install -Dm 755 lsb_release "$pkgdir/usr/bin/lsb_release"
}
