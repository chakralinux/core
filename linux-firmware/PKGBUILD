#
# Chakra Packages for Chakra, part of chakralinux.org

pkgname=linux-firmware
_commit=813fa1fc6fba6638decd974c4a6a2e485069ea15 # master
pkgver=20181214.813fa1f
_b43=6.30.163.46
_classic=5.100.138
_legacy=3.130.20.0
pkgrel=1
pkgdesc="Firmware files for Linux"
makedepends=('b43-fwcutter' 'git')
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
provides=("b43-firmware"{,-{legacy,classic}}'amd-ucode')
conflicts=('linux-firmware-git'
           'kernel26-firmware'
           'ar9170-fw'
           'carl9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw'
           'b43-firmware'
           'b43-firmware-legacy'
           'amd-ucode')
replaces=('kernel26-firmware'
          'ar9170-fw'
          'carl9170'
          'iwlwifi-1000-ucode'
          'iwlwifi-3945-ucode'
          'iwlwifi-4965-ucode'
          'iwlwifi-5000-ucode'
          'iwlwifi-5150-ucode'
          'iwlwifi-6000-ucode'
          'rt2870usb-fw'
          'rt2x00-rt61-fw'
          'rt2x00-rt71w-fw'
          'b43-firmware'
          'b43-firmware-legacy'
          'amd-ucode')
options=(!strip)
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git#commit=${_rev}"
        "http://www.lwfinger.com/b43-firmware/"broadcom-wl-{$_b43,$_classic}.tar.bz2
        "http://downloads.openwrt.org/sources/wl_apsta-${_legacy}.o")
sha256sums=('SKIP'
            'a07c3b6b277833c7dbe61daa511f908cd66c5e2763eb7a0859abc36cd9335c2d'
            'f1e7067aac5b62b67b8b6e4c517990277804339ac16065eb13c731ff909ae46f'
            '7dba610b1d96dd14e901bcbce14cd6ecd1b1ac6f5c0035b0d6b6dc46a7c3ef90')

pkgver() {
  cd "${srcdir}/${pkgname}"

  # Commit date + short rev
  echo $(TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" FIRMWAREDIR=/usr/lib/firmware install
  rm "${pkgdir}/usr/lib/firmware/"{Makefile,README,configure,GPL-3}

  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 LICEN* WHENCE "${pkgdir}/usr/share/licenses/linux-firmware/"

  # install b43-firmware
  b43-fwcutter -w "${pkgdir}/usr/lib/firmware/" ${srcdir}/broadcom-wl-${_b43}.wl_apsta.o

  # install b43-classic-firmware
  b43-fwcutter -w "${pkgdir}/usr/lib/firmware/" ${srcdir}/broadcom-wl-${_classic}/linux/wl_apsta.o

  # install b43-legacy-firmware
  b43-fwcutter -w "${pkgdir}/usr/lib/firmware/" ${srcdir}/wl_apsta-${_legacy}.o

  # remove asc file from amd-ucode
  rm "${pkgdir}"/usr/lib/firmware/amd-ucode/*.asc
  
  # Trigger a microcode reload for configurations not using early updates
  install -d "${pkgdir}/usr/lib/tmpfiles.d"
  echo 'w /sys/devices/system/cpu/microcode/reload - - - - 1' \
    >"${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
