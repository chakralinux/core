#
# Chakra Packages for Chakra, part of chakraos.org
#
# maintainer almack@chakraos.org
# contributor abveritas@chakra-project.org

pkgname=libva
pkgver=1.3.1
pkgrel=2
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libgl' 'libdrm' 'libxfixes')
makedepends=('mesa')
optdepends=('libva-vdpau-driver: VDPAU backend for VA API'
            'libva-driver-intel: back-end for intel cards')
source=(http://freedesktop.org/software/vaapi/releases/$pkgname/$pkgname-$pkgver.tar.bz2
        0001-va-release-VADisplayContext-at-the-end-of-vaTerminat.patch)
md5sums=('eb4db967f068854444b597071c66b480'
         'e564ae7c20962c821ac2b5cb1693e1a2')

prepare() {
  cd $pkgname-$pkgver
  # backport patch fixing flashplugin and firefox segfaults (FS#40914, FS#40807)
  patch -p1 -i ../0001-va-release-VADisplayContext-at-the-end-of-vaTerminat.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr  
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m644 -D COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
