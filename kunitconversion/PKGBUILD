# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kunitconversion
pkgver=5.50.0
pkgrel=1
pkgdesc='Support for unit conversion'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kunitconversion'
license=('LGPL')
depends=('ki18n')
makedepends=("extra-cmake-modules>=5.50.0" 'python3')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('44f27dada125563a7abab6d5f400b06fcbb54281100627773985fd8cd2556738'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

# check() {
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
