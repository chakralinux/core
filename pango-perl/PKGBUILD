# Maintainer: Bruce Liu <brli@users.sourceforge.net>

pkgname=pango-perl
pkgver=1.224
pkgrel=1
pkgdesc="Perl bindings for Pango"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends')
depends=('pango' 'glib-perl' 'cairo-perl')
options=('!emptydirs')
source=("http://sourceforge.net/projects/gtk2-perl/files/Pango/${pkgver}/Pango-${pkgver}.tar.gz")
md5sums=('8c411f367cffb0aa16d0e3963294d32e')

build() {
  cd "${srcdir}/Pango-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/Pango-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/Pango-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
