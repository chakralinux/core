#
# Core Packages for Chakra, part of chakra-project.org
#

pkgname=cryptsetup
pkgver=1.7.5
pkgrel=1
pkgdesc="Userspace setup tool for transparent encryption of block devices using the Linux 2.6 cryptoapi"
arch=('x86_64')
license=('GPL')
url="https://gitlab.com/cryptsetup/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'util-linux')
options=('!emptydirs')
source=(https://www.kernel.org/pub/linux/utils/cryptsetup/v1.7/${pkgname}-${pkgver}.tar.xz
        https://www.kernel.org/pub/linux/utils/cryptsetup/v1.7/${pkgname}-${pkgver}.tar.sign
        encrypt_hook
        encrypt_install
        sd-encrypt)
validpgpkeys=('2A2918243FDE46648D0686F9D9B0577BD93E98FC') # Milan Broz <gmazyland@gmail.com>
sha256sums=('2b30cd1d0dd606a53ac77b406e1d37798d4b0762fa89de6ea546201906a251bd'
            'SKIP'
            '4406f8dc83f4f1b408e49d557515f721d91b358355c71fbe51f74ab27e5c84ff'
            '00c2da113d23d05fc13c4929e38a25111ba23a49da86e835d9fb55d45c7aaee2'
            'd442304e6a78b3513ebc53be3fe2f1276a7df470c8da701b3ece971d59979bdd')

build() {
  cd "${srcdir}"/$pkgname-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-cryptsetup-reencrypt
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install hook
  install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
  install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
  install -D -m644 "${srcdir}"/sd-encrypt "${pkgdir}"/usr/lib/initcpio/install/sd-encrypt
}
