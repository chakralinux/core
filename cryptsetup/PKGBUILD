#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=cryptsetup
pkgver=1.6.2
pkgrel=1
pkgdesc="Userspace setup tool for transparent encryption of block devices using the Linux 2.6 cryptoapi"
arch=('i686' 'x86_64')
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'util-linux')
conflicts=('mkinitcpio<0.5.99')
options=('!libtool' '!emptydirs')
source=(http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
        http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2.asc
        encrypt_hook
        encrypt_install)
sha256sums=('15723f0198303d4bcb99d480b7a773918e2d319f0348457988c063bdd03e109a'
            'SKIP'
            '4406f8dc83f4f1b408e49d557515f721d91b358355c71fbe51f74ab27e5c84ff'
            '00c2da113d23d05fc13c4929e38a25111ba23a49da86e835d9fb55d45c7aaee2')

build() {
  cd "${srcdir}"/$pkgname-${pkgver}
  ./configure --prefix=/usr --disable-static --enable-cryptsetup-reencrypt
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install hook
  install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
  install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
}
