#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=cryptsetup
pkgver=1.3.1
pkgrel=2
pkgdesc="Userspace setup tool for transparent encryption of block devices using the Linux 2.6 cryptoapi"
arch=(i686 x86_64)
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt')
conflicts=('mkinitcpio<0.5.99')
options=('!libtool' '!emptydirs')
source=(http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
        encrypt_hook
	encrypt_install)
sha256sums=('7ceb18a0c91fa1546077b41b93463dd2ec9d7f83e6fd93757fb84cc608206a6a'
            '64601eae6fbf3e3afceccec5877557aa208a82497c33cc94ad0a686b4022b5dc'
            '07918fc349b3a7c4a73e8ad1cc3d97a2a79ebc7655090ab9ebb00b1e092c85aa')

build() {
  cd $srcdir/$pkgname-${pkgver}
  ./configure --prefix=/usr --disable-static --sbindir=/sbin --libdir=/lib
  make
}

package() {
  cd $srcdir/$pkgname-${pkgver}
  make DESTDIR=$pkgdir install
  # install hook
  install -D -m644 $srcdir/encrypt_hook $pkgdir/lib/initcpio/hooks/encrypt
  install -D -m644 $srcdir/encrypt_install $pkgdir/lib/initcpio/install/encrypt
  # Fix pkgconfig location
  install -d -m755 $pkgdir/usr/lib
  mv $pkgdir/lib/pkgconfig $pkgdir/usr/lib/
}
