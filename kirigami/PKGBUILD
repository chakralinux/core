
pkgname=kirigami
_pkgname=kirigami2
pkgver=5.46.0
pkgrel=1
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://projects.kde.org/projects/frameworks/kirigami'
license=('LGPL')
depends=(qt5-quickcontrols2)
makedepends=("extra-cmake-modules>=${KFECMVersion}" qt5-tools plasma-framework)
replaces=(kirigami2)
provides=(kirigami2)
conflicts=(kirigami2)
groups=('kf5')
options=("debug")
_url="https://download.kde.org/stable/frameworks/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz"
source=(${_url}{,.sig})
sha256sums=($(curl "$_url.sha256" | cut -c-64)
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
      cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
