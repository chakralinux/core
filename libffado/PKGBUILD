# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/libffado

pkgname=libffado
pkgver=2.2.1
pkgrel=3
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libsigc++' 'libxml++' 'alsa-lib'
         'dbus' 'libconfig' 'dbus-c++')
makedepends=('scons' 'python2-pyqt4' 'jack2' 'python2-dbus')
optdepends=('python2-pyqt4: mixer applet'
            'python2: ffado-diag')
source=("http://www.ffado.org/files/libffado-$pkgver.tgz"
	'fix-test-errno-usage.patch')
md5sums=('e113d828835051f835fb4a329cb0cbd4'
         'd9cf3bed720e82ea97e29003f20b3c6b')


prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=805211
  patch -Np1 -i "$srcdir/fix-test-errno-usage.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  scons DEBUG=False \
        PREFIX=/usr \
        MANDIR=/usr/share/man \
        UDEVDIR=/usr/lib/udev/rules.d \
        COMPILE_FLAGS="$CFLAGS -std=gnu++11"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  scons DESTDIR="$pkgdir" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # desktop files & icons
  install -Dm644 support/xdg/ffado.org-ffadomixer.desktop \
    "$pkgdir/usr/share/applications/ffadomixer.desktop"

  install -Dm644 support/xdg/hi64-apps-ffado.png \
    "$pkgdir/usr/share/pixmaps/ffado.png"
}
