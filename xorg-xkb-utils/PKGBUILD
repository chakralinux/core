#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=xorg-xkb-utils
pkgver=7.6
pkgrel=3
pkgdesc="X.org keyboard utilities"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw>=1.0.5' 'libxkbfile')
makedepends=(pkgconfig inputproto)
source=(${url}/releases/individual/app/xkbutils-1.0.4.tar.bz2
        ${url}/releases/individual/app/xkbcomp-1.2.4.tar.bz2
        ${url}/releases/individual/app/xkbevd-1.1.3.tar.bz2
        ${url}/releases/individual/app/setxkbmap-1.3.0.tar.bz2
        ${url}/releases/individual/app/xkbprint-1.0.3.tar.bz2
        LICENSE.setxkbmap LICENSE.xkbevd LICENSE.xkbutils)
md5sums=('502b14843f610af977dffc6cbf2102d5'
         'a0fc1ac3fc4fe479ade09674347c5aa0'
         '37ed71525c63a9acd42e7cde211dcc5b'
         '1001771344608e120e943a396317c33a'
         '526b5ba87cccd05aa700fa628bcfda3e'
         '7c20baa6a26c32a04b1b1d6cbaa626a7'
         '7b1fc39455375a25afe6f9594ff979ac'
         '7b1fc39455375a25afe6f9594ff979ac')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man || return 1
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}  || return 1
      popd
    fi
  done
# Replacing stub license files
  install -D -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/"  || return 1
}
