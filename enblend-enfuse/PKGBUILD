# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=enblend-enfuse
pkgver=4.1.3
pkgrel=1
pkgdesc="intelligent blend tool for overlapping picture"
arch=("x86_64")
license=('GPL')
url="http://enblend.sourceforge.net"
depends=('glew' 'libxmi' 'freeglut' 'libgl' 'lcms2' 
         'texinfo' 'libpng' 'boost-libs' 'vigra' 'openexr' 'gsl')
makedepends=('boost' 'mesa')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/sourceforge/enblend/${pkgname}-${pkgver}.tar.gz")
md5sums=('a025137ba53fc8a355347fe721c6267c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 doc/enblend.info "${pkgdir}/usr/share/info/enblend.info"
  install -D -m644 doc/enfuse.info "${pkgdir}/usr/share/info/enfuse.info"
}
