
pkgname=qt5-gamepad
pkgver=5.11.2
pkgrel=1
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='Adds support for getting events from gamepad devices'
depends=('qt5-base' 'sdl2' 'libevdev')
makedepends=('qt5-declarative')
optdepends=('qt5-declarative: QML bindings')
groups=('qt5')
options=('debug')
_pkgfqn="${pkgname/5-/}-everywhere-src-${pkgver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/submodules/${_pkgfqn}.tar.xz")
md5sums=('6424f750bd7e0ed1d0cf85c6936b6381'

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake-qt5 ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
