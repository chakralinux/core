#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=expect
pkgver=5.45
pkgrel=2
pkgdesc="A tool for automating interactive applications"
arch=(i686 x86_64)
url="http://www.nist.gov/el/msid/expect.cfm"
license=('custom')
depends=('tcl>=8.5.3')
conflicts=('expectk')
options=('log')
install=expect.install
source=(http://downloads.sourceforge.net/project/expect/Expect/${pkgver}/expect${pkgver}.tar.gz)
# CVS checkout on gerolde.
#source=(ftp://ftp.archlinux.org/other/expect/expect-${pkgver}.tar.bz2)
# old nist source=(http://expect.nist.gov/src/expect-${pkgver}.tar.bz2)
md5sums=('44e1a4f4c877e9ddc5a542dfa7ecc92b')

build() {
  cd ${srcdir}/expect${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --mandir=/usr/share/man --with-tcl=/usr/lib \
      --with-tclinclude=/usr/include

  # Keep $startdir/pkg out of library search paths.
  sed -e 's/-rpath,${LIB_RUNTIME_DIR}:/-rpath,/' -i Makefile
  make
  make DESTDIR=${pkgdir} install

  # license.
  install -D -m644 README ${pkgdir}/usr/share/licenses/${pkgname}/README

  (cd ${pkgdir}/usr/lib;
    ln -s expect${pkgver}/libexpect${pkgver}.so libexpect.so)
}
