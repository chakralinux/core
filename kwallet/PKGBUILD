# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kwallet
pkgver=5.50.0
pkgrel=1
pkgdesc='Secure and unified container for user passwords'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kwallet'
license=('LGPL')
depends=('knotifications' 'kiconthemes' 'kservice' 'gpgme')
makedepends=('extra-cmake-modules' 'python3' 'kdoctools' 'boost')
checkdepends=('cmake')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('25ca8214829567a22aa59e760541d701aa79f6d3dc0ef3fa67cba76e3c651492'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
