# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kcmutils
pkgver=5.48.0
pkgrel=1
pkgdesc='Utilities for interacting with KCModules'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kcmutils'
license=('LGPL')
depends=('kxmlgui' 'kdeclarative')
makedepends=("extra-cmake-modules>=5.48.0" 'python3')
checkdepends=("cmake" "xorg-server-xvfb")
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('bc523c83d317409ef5b6d9af645e02d23256c9ef71191ec28782d527f6fe745b'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

# check() {
#   cd build
#   xvfb-run make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
