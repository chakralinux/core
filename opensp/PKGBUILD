# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=opensp
pkgver=1.5.2
pkgrel=2
pkgdesc="A library and a set of tools for validating, parsing and manipulating SGML and XML documents"
arch=('i686' 'x86_64')
url="http://openjade.sourceforge.net/"
license=('BSD')
depends=('gcc-libs')
makedepends=('xmlto' 'docbook-xsl')
options=('!libtool')
source=("http://download.sourceforge.net/openjade/OpenSP-$pkgver.tar.gz")
md5sums=('670b223c5d12cee40c9137be86b6c39b')

build() {
  cd "${srcdir}"/OpenSP-$pkgver
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-nls \
    --enable-http \
    --enable-default-catalog=/etc/sgml/catalog:/etc/xml/catalog \
    --enable-default-search-path=/usr/share/sgml:/usr/share/xml \
    --enable-xml-messages
  make
}

package() {
  cd "${srcdir}"/OpenSP-$pkgver
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

