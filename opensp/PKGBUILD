pkgname=opensp
pkgver=1.5.2
pkgrel=4
pkgdesc="A library and a set of tools for validating, parsing and manipulating SGML and XML documents"
arch=('x86_64')
url="http://openjade.sourceforge.net/"
license=('BSD')
depends=('gcc-libs')
makedepends=('xmlto' 'docbook-xsl')
source=("http://download.sourceforge.net/openjade/OpenSP-$pkgver.tar.gz")
md5sums=('670b223c5d12cee40c9137be86b6c39b')

build() {
  cd OpenSP-$pkgver
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-nls \
    --enable-http \
    --enable-default-catalog=/etc/sgml/catalog:/etc/xml/catalog \
    --enable-default-search-path=/usr/share/sgml:/usr/share/xml \
    --enable-xml-messages
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd OpenSP-$pkgver
  make check || /bin/true
}

package() {
  cd OpenSP-$pkgver
  make DESTDIR="${pkgdir}" install

  # add symlinks for compatibility with jade, see FS#49775
  for file in nsgmls sgmlnorm spam spcat spent sx ; do
    ln -s o$file $pkgdir/usr/bin/$file
    echo ".so man1/o${file}.1" > $pkgdir/usr/share/man/man1/${file}.1
  done
  ln -v -sf libosp.so $pkgdir/usr/lib/libsp.so

  # Rename sx to sgml2xml; FS#49792
  mv $pkgdir/usr/bin/sx $pkgdir/usr/bin/sgml2xml
  mv $pkgdir/usr/share/man/man1/{sx,sgml2xml}.1
  
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
