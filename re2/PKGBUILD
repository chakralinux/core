# Arch contributor: https://projects.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/re2

pkgname=re2
_tag='2017-01-01'
pkgver=${_tag//-/}
pkgrel=1
pkgdesc='C++ library for fast, safe, thread-friendly alternative to backtracking regular expression engines like those used in PCRE, Perl, and Python'
url='https://github.com/google/re2'
depends=(gcc-libs)
arch=(i686 x86_64)
license=(BSD)
source=(re2-$pkgver.zip::https://github.com/google/re2/archive/$_tag.zip)
sha1sums=('0c19ad0370af8a6dcff73e714c7f21363dc709db')

build() {
  cd re2-$_tag
  make prefix=/usr
}

check() {
  cd re2-$_tag
  make prefix=/usr test
}

package() {
  cd re2-$_tag
  make prefix=/usr DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
