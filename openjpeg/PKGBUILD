# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/openjpeg

pkgname=openjpeg
pkgver=2.1.2
pkgrel=1
pkgdesc="An open source JPEG 2000 codec"
arch=('x86_64')
license=('BSD')
url="http://www.openjpeg.org"
depends=('zlib')
makedepends=('libtiff' 'lcms2' 'libpng' 'doxygen' 'cmake')
optdepends=('lcms2: j2k_to_image and image_to_j2k programs'
            'libpng: j2k_to_image and image_to_j2k programs')
source=("https://github.com/uclouvain/openjpeg/archive/v${pkgver}.tar.gz")
md5sums=('40a7bfdcc66280b3c1402a0eb1a27624')

prepare() {
  cd $pkgname-$pkgver
  [[ -d build ]] && rm -rf build
  mkdir build
}

build() {
  cd $pkgname-$pkgver/build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

