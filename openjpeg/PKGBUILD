# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/openjpeg

pkgname=openjpeg
pkgver=1.5.2
pkgrel=3
pkgdesc="An open source JPEG 2000 codec"
arch=('x86_64')
license=('BSD')
url="http://www.openjpeg.org"
depends=('zlib')
makedepends=('libtiff' 'lcms2' 'libpng' 'doxygen')
optdepends=('lcms2: j2k_to_image and image_to_j2k programs'
            'libpng: j2k_to_image and image_to_j2k programs')
source=(http://downloads.sourceforge.net/openjpeg.mirror/${pkgname}-${pkgver}.tar.gz)
md5sums=('c41772c30fb1c272358b3707233134a1')

build() {
  cd $pkgname-$pkgver
  autoreconf -fi
  # make sure we use system libs with --disable-static
  # we want the most informative output during build,
  # thus we pass --disable-silent-rules to configure
  rm -rf thirdparty
  ./configure --prefix=/usr \
        --enable-shared --disable-static --disable-silent-rules
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/openjpeg/LICENSE"
}

