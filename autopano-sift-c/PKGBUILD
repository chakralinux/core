#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>


pkgname=autopano-sift-c
pkgver=2.5.1
pkgrel=3
pkgdesc="Identify key feature points within arbitrary images"
arch=('x86_64')
url="http://hugin.sourceforge.net/"
license=('GPL2')
depends=('libxml2' 'libpano13>=2.9.17' 'gcc-libs')
makedepends=('cmake')
source=("http://sourceforge.net/projects/hugin/files/autopano-sift-C/autopano-sift-C-2.5.1/autopano-sift-C-${pkgver}.tar.gz")
md5sums=('b9bade07e8c4f2ea383c22a082c260e0')

build() {
  cd "${srcdir}/autopano-sift-C-${pkgver}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  sed -i 's/-lz/-lz -lm/' CMakeFiles/{autopano,generatekeys}.dir/link.txt
  make
}

package() {
  cd "${srcdir}/autopano-sift-C-${pkgver}"
  
  make DESTDIR=${pkgdir} install
}
