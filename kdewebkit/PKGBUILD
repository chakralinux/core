# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kdewebkit
pkgver=5.54.0
pkgrel=1
pkgdesc='KDE Integration for QtWebKit'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kdewebkit'
license=('LGPL')
depends=('qt5-webkit' 'kwallet' 'kparts')
makedepends=("extra-cmake-modules>=5.54.0")
options=("debug")
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('0e5a09c820c4c53f68a3ba882b78389052809fc768bbf3203edb2215ca49345f'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check(){
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
