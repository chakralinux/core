# Part of X.org group
# maintainer AlmAck almack@chakraos.org

source ../xorg.conf

pkgname=xf86-video-fbdev
pkgver=0.4.4+13+g9af7f81
pkgrel=1
pkgdesc="X.org framebuffer video driver"
arch=(x86_64)
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-server-devel' "X-ABI-VIDEODRV_VERSION=${X_ABI_VIDEODRV_VERSION}")
conflicts=('xorg-server<1.16' "X-ABI-VIDEODRV_VERSION<${X_ABI_VIDEODRV_MAJOR}" "X-ABI-VIDEODRV_VERSION>=$((X_ABI_VIDEODRV_MAJOR+1))")
groups=('xorg-drivers' 'xorg')
_commit=9af7f81534aa03fe01d88786805b00729ac321e9  # master
source=("git+https://anongit.freedesktop.org/git/xorg/driver/xf86-video-fbdev#commit=$_commit"
        gcc8.diff)
sha256sums=('SKIP'
            '050db537aae39b6deb1b2531b0064d62e2725f92b6c5c8ee6ade32e73cd3d5e8')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^xf86-video-fbdev-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np0 -i ../gcc8.diff
  
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}


