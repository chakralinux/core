# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributor: abveritas@chakra-project.org

pkgname=xf86-video-sisusb
pkgver=0.9.6
pkgrel=3
pkgdesc="X.org SiS USB video driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server' 'X-ABI-VIDEODRV_VERSION=15')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16')
groups=('xorg-video-drivers')
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2" "git-fixes.patch")
sha1sums=('acb96457a0f3f5631129841cb4477d61fd2707dd'
          '6516bba2443ad24ef7a10f16fa43e23ece6fb87e')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i ${srcdir}/git-fixes.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
