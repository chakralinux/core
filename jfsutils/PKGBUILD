# $Id: PKGBUILD 37639 2009-05-03 15:42:28Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=jfsutils
pkgver=1.1.15
pkgrel=2
pkgdesc="JFS filesystem utilities"
url="http://jfs.sourceforge.net"
arch=('i686' 'x86_64')
license=('GPL')
depends=('util-linux')
source=("http://jfs.sourceforge.net/project/pub/${pkgname}-${pkgver}.tar.gz"
        'inttypes.patch')
sha1sums=('291e8bd9d615cf3d27e4000117c81a3602484a50'
          '88551e7d8418bab0df4643dce248110d4c9b0a53')

groups=('base')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../inttypes.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --sbindir=/usr/bin 
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
