#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
 
pkgname=gnash
pkgver=0.8.10
pkgrel=1
pkgdesc="GNU Flash player and kpath plugin for KDE 4."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gnash/"
license=('GPL')
depends=('agg' 'ffmpeg' 'libjpeg' 'libpng' 'giflib' 'zlib' 'curl' 'giflib' 'boost-libs' 'libldap' 'sdl'
         'speex' 'fontconfig' 'libva' 'libxinerama' 'gstreamer0.10-base' 'gstreamer0.10-ffmpeg'
         'libdca' 'kdebase' 'xulrunner' 'hicolor-icon-theme' 'desktop-file-utils' 'pkgconfig' 'libtool')
makedepends=('boost')
provides=('gnash' 'gnash-common')
conflicts=('flashplugin' 'flashplugin-square')
options=(!libtool !emptydirs)
backup=(etc/gnashrc etc/gnashthumbnailrc)
source=("http://ftp.gnu.org/gnu/gnash/${pkgver}/gnash-${pkgver}.tar.bz2"
        "nodebug.patch")
md5sums=('63e9f79c41d93d48c5a2fa94856548c4'
         '371a91b3eb90df268607b731e7ec03c2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -Np1 -i "$srcdir/nodebug.patch"
	./autogen.sh

	sed -i "s/moc-qt4/moc/g; s/uic-qt4/uic/g" configure

	./configure \
		--enable-renderer=agg \
		--enable-media=gst \
		--enable-gui=kde4 \
		--enable-sound=sdl \
		--enable-hwaccel=vaapi \
		--without-gconf \
		--with-plugins-install=system \
		--with-npapi-plugindir=/usr/lib/mozilla/plugins \
		--prefix=/usr \
		--sysconfdir=/etc

	sed -i "s:KDE4_APPSDATADIR =.*:KDE4_APPSDATADIR = /usr/share/apps:" ./plugin/klash4/Makefile
	sed -i "s:KDE4_SERVICESDIR =.*:KDE4_SERVICESDIR = /usr/share/kde4/services:" ./plugin/klash4/Makefile
	sed -i "s:KDE4_PLUGINDIR =.*:KDE4_PLUGINDIR = /usr/lib/kde4:" ./plugin/klash4/Makefile
	make 
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install 
	make DESTDIR="${pkgdir}" install-plugins 
}

# vim:set ts=2 sw=2 et:
