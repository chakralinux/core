#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=xf86-video-nouveau
_gitdate=20110505
pkgver=0.0.16_git${_gitdate} # see configure.ac
pkgrel=2
pkgdesc="Open Source 3D acceleration driver for nVidia cards (experimental)"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/wiki/"
license=('GPL') #and MIT, not yet a license file, see http://nouveau.freedesktop.org/wiki/FAQ#head-09f75d03eb30011c754038a3893119a70745de4e
depends=("nouveau-drm" 'udev' 'libdrm')
optdepends=('nouveau-dri:	highly experimental gallium3d features')
makedepends=('pkgconfig' 'xorg-server>=1.10.1' 'mesa>=7.10' 'glproto>=1.4.11' 'xorg-util-macros' 'xf86driproto' 'autoconf' 'nouveau-dri')
conflicts=('xorg-server<1.10.1')
options=('!libtool') # 'force'
install=xf86-video-nouveau.install
source=(http://chakra-project.org/sources/xf86-video-nouveau/xf86-video-nouveau-${_gitdate}.tar.bz2)
md5sums=('725e71d37702a4db5a6f0755367ecd23')

# source PKGBUILD && mksource
mksource() {
	mkdir -p /tmp/xf86-video-nouveau-${_gitdate}
	pushd /tmp/xf86-video-nouveau-${_gitdate}
	git clone -v --depth 1 git://anongit.freedesktop.org/nouveau/xf86-video-nouveau
	cd xf86-video-nouveau
	git archive --prefix=xf86-video-nouveau-${_gitdate}/ --format=tar HEAD | bzip2 > /tmp/xf86-video-nouveau-${_gitdate}/xf86-video-nouveau-${_gitdate}.tar.bz2
	popd
}

build() {
  cd ${srcdir}/xf86-video-nouveau-${_gitdate}
  # fix build
  sed -i -e "s~nv04_pushbuf.h~nouveau_pushbuf.h~g" src/*
  ./autogen.sh --prefix=/usr || return 1
  make || return 1
}

package() {
  cd ${srcdir}/xf86-video-nouveau-${_gitdate}
  make DESTDIR=${pkgdir} install || return 1
}
