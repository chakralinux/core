# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=knotifications
pkgver=5.48.0
pkgrel=1
pkgdesc='Abstraction for system notifications'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/knotifications'
license=('LGPL')
depends=('phonon-qt5' 'libdbusmenu-qt5' 'kwindowsystem' 'kconfig' 'kcodecs' 'kcoreaddons' 'qt5-speech')
makedepends=("extra-cmake-modules>=5.48.0" 'qt5-tools' 'python3')
checkdepends=('xorg-server-xvfb')
groups=('kf5')
options=('debug')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('ebd778d052ba4a0d445082fb6ced8cfd4ea8cbef7e60439576fb233e9f1ce086'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

check() {
  cd build
  xvfb-run make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
