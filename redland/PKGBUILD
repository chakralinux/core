#
# Platform Packages for Chakra, part of chakra-project.org
#

pkgbase=redland
pkgname=('redland' 'redland-storage-mysql' 'redland-storage-postgresql' 'redland-storage-virtuoso' 'redland-storage-sqlite')
pkgver=1.0.17
pkgrel=2
url="http://librdf.org/"
license=("GPL")
arch=('x86_64')
makedepends=('rasqal' 'raptor' 'postgresql-libs' 'libmariadbclient' 'unixodbc' 'sqlite3')
source=("http://download.librdf.org/source/$pkgname-$pkgver.tar.gz"
        'rpath.diff')
md5sums=('e5be03eda13ef68aabab6e42aa67715e'
         'acc85e784f01a656bd56777f95880787')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--enable-release \
	--disable-static \
        --disable-gtk-doc \
	--with-raptor=system \
	--with-rasqal=system \
	--with-sqlite=3 \
	--with-bdb=no

  # nuke rpath
  patch -p0 -i ${srcdir}/rpath.diff

  make
}

package_redland() {
 pkgdesc="Library that provides a high-level interface to RDF data"
 depends=('rasqal>=0.9.26' 'raptor>=2.0.6' 'libltdl')
 
  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/lib/redland
}

package_redland-storage-mysql() {
 pkgdesc="MySQL storage support for Redland"
 depends=('redland' 'libmariadbclient')

  cd ${srcdir}/${pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_mysql.so ${pkgdir}/usr/lib/redland/librdf_storage_mysql.so 
}

package_redland-storage-postgresql() {
 pkgdesc="PostgreSQL storage support for Redland"
 depends=('redland' 'postgresql-libs')

  cd ${srcdir}/${pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_postgresql.so ${pkgdir}/usr/lib/redland/
}

package_redland-storage-virtuoso() {
 pkgdesc="Virtuoso storage support for Redland"
 depends=('redland' 'unixodbc')

  cd ${srcdir}/${pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_virtuoso.so ${pkgdir}/usr/lib/redland/
}

package_redland-storage-sqlite() {
 pkgdesc="SQLite storage support for Redland"
 depends=('redland' 'sqlite3')
 
  cd ${srcdir}/${pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_sqlite.so ${pkgdir}/usr/lib/redland/
}
