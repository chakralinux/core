#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=redland
epoch=1
pkgname=('redland' 'redland-storage-mysql' 'redland-storage-postgresql' 'redland-storage-virtuoso' 'redland-storage-sqlite')
pkgver=1.0.14
pkgrel=1
url="http://librdf.org/"
license=("GPL")
arch=('i686' 'x86_64')
makedepends=('rasqal>=0.9.26' 'raptor>=2.0.4' 'db' 'postgresql-libs' 'libmysqlclient' 'unixodbc' 'sqlite3')
options=('!libtool')
source=(http://download.librdf.org/source/$pkgname-$pkgver.tar.gz
        rpath.diff)
md5sums=('3400579d137da82c4175debf9f6d53ba'
         'acc85e784f01a656bd56777f95880787')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--enable-release \
	--disable-static \
        --disable-gtk-doc \
	--with-raptor=system \
	--with-rasqal=system \
	--with-sqlite=3

  # nuke rpath
  patch -Np0 -i ${srcdir}/rpath.diff

  make
}

package_redland() {
 pkgdesc="Library that provides a high-level interface to RDF data"
 depends=('rasqal>=0.9.26' 'raptor>=2.0.4' 'libtool')
 
  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/lib/redland
}

package_redland-storage-mysql() {
 pkgdesc="MySQL storage support for Redland"
 depends=('redland' 'libmysqlclient')

  cd ${srcdir}/${pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_mysql.so ${pkgdir}/usr/lib/redland/librdf_storage_mysql.so 
}

package_redland-storage-postgresql() {
 pkgdesc="PostgreSQL storage support for Redland"
 depends=('redland' 'postgresql-libs')

  cd ${srcdir}/${pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_postgresql.so ${pkgdir}/usr/lib/redland/
}

package_redland-storage-virtuoso() {
 pkgdesc="Virtuoso storage support for Redland"
 depends=('redland' 'unixodbc' 'db')

  cd ${srcdir}/${pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_virtuoso.so ${pkgdir}/usr/lib/redland/
}

package_redland-storage-sqlite() {
 pkgdesc="SQLite storage support for Redland"
 depends=('redland' 'sqlite3' 'db')
 
  cd ${srcdir}/${pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_sqlite.so ${pkgdir}/usr/lib/redland/
}