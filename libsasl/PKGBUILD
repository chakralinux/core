#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=libsasl
pkgver=2.1.23
pkgrel=6
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('i686' 'x86_64')
url="http://cyrusimap.web.cmu.edu/downloads.html#sasl"
license=('custom')
depends=('db>=4.8')
optdepends=('cyrus-sasl: saslauthd'
            'cyrus-sasl-plugins: authentication plugins other than sasldb')
source=(ftp://ftp.andrew.cmu.edu/pub/cyrus-mail/cyrus-sasl-${pkgver}.tar.gz
	cyrus-sasl-2.1.19-checkpw.c.patch cyrus-sasl-db.patch)
options=('!makeflags')
md5sums=('2eb0e48106f0e9cd8001e654f267ecbc'
         'e27ddff076342e7a3041c4759817d04b'
         '0658201497aad359c0d66b0ab8032859')

build() {
  cd "${srcdir}/cyrus-sasl-${pkgver}"
  patch -Np0 -i ${srcdir}/cyrus-sasl-2.1.19-checkpw.c.patch
  patch -Np1 -i ${srcdir}/cyrus-sasl-db.patch
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
  	--localstatedir=/var \
	--disable-anon \
	--disable-cram \
	--disable-digest \
	--disable-gssapi \
	--enable-login \
	--disable-otp \
	--enable-plain \
        --mandir=/usr/share/man
  for dir in include lib sasldb plugins utils; do
    pushd ${dir}
    make
    popd
  done
}

package() {
  cd "${srcdir}/cyrus-sasl-${pkgver}"
  for dir in include lib sasldb plugins utils; do
    pushd ${dir} || return 1
    make DESTDIR="${pkgdir}" install
    popd
  done

  # install license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
