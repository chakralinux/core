# maintainer abveritas@chakra-project.org
# contributor Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-base
pkgver=1.4.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('x86_64')
license=('LGPL')
depends=('gstreamer' 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual'
         'libvorbis' 'libtheora' 'pango')
makedepends=('pkgconfig' 'gobject-introspection')
options=('!emptydirs')
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/$pkgname/$pkgname-${pkgver}.tar.xz)
md5sums=('1ff06280b03b9098a706d1290d8bb3bd')

build() {
  cd $pkgname-$pkgver
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (Chakra GNU/Linux)" \
    --with-package-origin="http://www.chakraos.org/" 
  make
  sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd $pkgname-$pkgver
 
  make check
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR="${pkgdir}" install
}

