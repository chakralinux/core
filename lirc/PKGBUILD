# maintainer almack@chakraos.org

pkgname=lirc
_pkgver=0.9.4d
[[ $_pkgver =~ [a-z]$ ]] && pkgver="${_pkgver:0:-1}.${_pkgver: -1}" || pkgver="$_pkgver"
pkgrel=1
pkgdesc="Linux Infrared Remote Control utils"
arch=('x86_64')
url="http://www.lirc.org/"
license=('GPL')
depends=('alsa-lib' 'libx11' 'libftdi' 'libirman')
makedepends=('help2man' 'python3' 'libxml2' 'python3-yaml' 'doxygen' 'man2html' 'libxslt')
optdepends=('python3: for lirc-setup, irdb-get and pronto2lirc')
provides=('lirc-utils')
conflicts=('lirc-utils')
replaces=('lirc-utils' 'lirc+pctv')
backup=('etc/lirc/lirc_options.conf' 'etc/lirc/lircd.conf' 'etc/lirc/lircmd.conf')
install=lirc.install
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.bz2"
        lirc.logrotate
        lirc.tmpfiles)
md5sums=('0d11679cbdd94a5a6da00a8e7231b4bf'
         '3deb02604b37811d41816e9b4385fcc3'
         'febf25c154a7d36f01159e84f26c2d9a')

build() {
  cd "${srcdir}/lirc-${_pkgver}"

  HAVE_UINPUT=1 ./configure --prefix=/usr --sbindir=/usr/sbin --sysconfdir=/etc --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/lirc-${_pkgver}"

  make DESTDIR="${pkgdir}" -j1 install

  install -Dm644 "${srcdir}"/lirc.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lirc.conf
  install -Dm644 "${srcdir}"/lirc.logrotate "${pkgdir}"/etc/logrotate.d/lirc

  rmdir "${pkgdir}"/var/{run/lirc/,run/}
}
