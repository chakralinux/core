# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abeveritas[at]chakra-project[dot]org>

pkgname=libjpeg-turbo
pkgver=1.2.1
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
makedepends=('nasm')
provides=('libjpeg=8.0.2' 'libjpeg6')
replaces=('libjpeg' 'libjpeg6')
conflicts=('libjpeg' 'libjpeg6')
options=('!libtool')
source=("http://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('f61e60ff01381ece4d2fe65eeb52669e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # create libjpeg6
  ./configure --prefix=/usr --with-jpeg6 --mandir=/usr/share/man
  make
  mkdir -p $srcdir/libjpeg6
  cp -d .libs/libjpeg.so.6* $srcdir/libjpeg6
  rm -R .libs
  # create libjpeg8
  ./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install libjpeg6
  mkdir -p $pkgdir/usr/lib
  cp -vd $srcdir/libjpeg6/libjpeg.so.6* $pkgdir/usr/lib
  
  # install libjpeg8
  make DESTDIR="$pkgdir/" install

  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h $pkgdir/usr/include/

  install -Dm644 README-turbo.txt $pkgdir/usr/share/licenses/libjpeg-turbo/README-turbo.txt
  install -Dm644 README $pkgdir/usr/share/licenses/libjpeg-turbo/README
}
