# Maintainer:
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Anke Boersma (abveritas) <abveritas@chakra-project.org>
#               Gaetan Bisson <bisson@archlinux.org>
#               Allan McRae <allan@archlinux.org>
#               Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libjpeg-turbo
pkgver=1.3.0
pkgrel=1
pkgdesc='JPEG image codec with accelerated baseline compression and decompression'
arch=('x86_64')
url='http://libjpeg-turbo.virtualgl.org/'
license=('GPL'
         'custom')
makedepends=('nasm')
provides=('libjpeg=8.0.2'
          'libjpeg6')
replaces=('libjpeg'
          'libjpeg6')
conflicts=('libjpeg'
           'libjpeg6')
options=('!libtool')
source=("http://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-$pkgver.tar.gz"
        'cve-2013-6629.patch')
sha512sums=('4d34c3c5f2cdd70b2a3d1b55eeb4ce59cb3d4b8d22bb6d43c2ec844b7eb5685b55a9b1b46ad2bc5f2756b5f5535ccad032791c3b932af9c1efc502aa5e701053'
            'c5f063f87305ab48cb40c243df9e7a307efedc02eef8eee65e9ca5006676d5257a6d1dc7a32ff77a2486c8be8792b4f5431d18e46759ad63c182b8332a736099')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -i ../cve-2013-6629.patch # FS#38094
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Create libjpeg6
    ./configure \
        --prefix=/usr \
        --with-jpeg6 \
        --mandir=/usr/share/man
    make
    mkdir -p ${srcdir}/libjpeg6
    cp -d .libs/libjpeg.so.6* ${srcdir}/libjpeg6
    rm -R .libs

    # Create libjpeg8
    ./configure \
        --prefix=/usr \
        --with-jpeg8 \
        --mandir=/usr/share/man
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Install libjpeg6
    mkdir -p $pkgdir/usr/lib
    cp -vd $srcdir/libjpeg6/libjpeg.so.6* $pkgdir/usr/lib

    # Install libjpeg8
    make DESTDIR="$pkgdir/" install

    # Provide jpegint.h as it is required by various software
    install -m644 jpegint.h $pkgdir/usr/include/

    install -Dm644 README-turbo.txt $pkgdir/usr/share/licenses/libjpeg-turbo/README-turbo.txt
    install -Dm644 README $pkgdir/usr/share/licenses/libjpeg-turbo/README
}
