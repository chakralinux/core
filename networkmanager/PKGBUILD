#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# contributor: Radu Andries <admiral0[at]tuxfamily[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=networkmanager
pkgver=0.9.2.0
pkgrel=1
pkgdesc="Network Management daemon"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('wireless_tools' 'iproute2>=2.6.35' 'libnl>=1.1' 'ppp' 'dhcpcd>=5.2.5' 'wpa_supplicant>=0.6.10' 'iptables>=1.4.9' 'nss>=3.12.6' 'polkit>=0.96' 'udev>=164' 'dbus-glib')
makedepends=('intltool')
optdepends=('modemmanager: for modem management service')
options=('!libtool')
backup=('etc/NetworkManager/nm-system-settings.conf')
source=(http://ftp.gnome.org/pub/gnome/sources/NetworkManager/0.9/NetworkManager-${pkgver}.tar.bz2
        nm-system-settings.conf disable_set_hostname.patch)
sha256sums=('a178ed2f0b5a1045ec47b217ea531d0feba9208f6bcfe64b701174a5c1479816'
            '44b048804c7c0b8b3b0c29b8632b6ad613c397d0a1635ec918e10c0fbcdadf21'
            '25056837ea92e559f09563ed817e3e0cd9333be861b8914e45f62ceaae2e0460')
build() {
  cd "${srcdir}/NetworkManager-${pkgver}"

  patch -Np1 -i "${srcdir}/disable_set_hostname.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-distro=arch \
    --with-dhclient=/usr/sbin/dhclient \
    --with-dhcpcd=/sbin/dhcpcd \
    --with-iptables=/usr/sbin/iptables \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax

  make
}

package() {
  cd "${srcdir}/NetworkManager-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m644 "${srcdir}/nm-system-settings.conf" "${pkgdir}/etc/NetworkManager/"

  rm -rf "${pkgdir}/var/run/"
}
