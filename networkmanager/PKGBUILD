pkgname=networkmanager
_pkgname=NetworkManager
pkgver=1.4.4
pkgrel=4
pkgdesc="Network Management daemon"
arch=('x86_64')
license=('GPL')
_pppver=2.4.7
url="http://www.gnome.org/projects/NetworkManager/"
depends=(iproute2 polkit wpa_supplicant libsoup modemmanager
         libnewt libndp libteam "ppp=${_pppver}"
         libgudev nss dbus-glib util-linux jansson systemd)
makedepends=(intltool dhclient iptables gobject-introspection gtk-doc
             "ppp=${_pppver}" modemmanager dbus-glib iproute2 nss polkit
             wpa_supplicant libsoup systemd libgudev rp-pppoe
             libnewt libndp libteam vala perl-yaml python3-gobject git)
checkdepends=(libx11 python3-dbus)
optdepends=('dnsmasq: connection sharing'
            'bluez: Bluetooth support'
            'openresolv: resolvconf support'
            'ppp: dialup connection support'
            'rp-pppoe: ADSL support'
            'dhclient: External DHCP client'
            'modemmanager: cellular network support')
backup=('etc/NetworkManager/NetworkManager.conf')
install=networkmanager.install
_commit=3c70a03cf5be420afaaf44f3a0bab5d00cc4e993  # tags/1.4.4^0
source=("git+https://anongit.freedesktop.org/git/NetworkManager/NetworkManager#commit=$_commit"
        NetworkManager.conf
        20-connectivity.conf)
sha256sums=('SKIP'
            '67f112c1ac8ee3726eb229f5cd783de19f09cc252af49e157343d82b324b923f'
            '569c312647723c39c356a9ba746ce674f9c69595b4764990b1e8254dd141e0b1')

prepare() {
  cd NetworkManager

  2to3 -w libnm src tools

  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd NetworkManager
  git describe | sed 's/-dev/dev/;s/-/+/g'
}

build() {
  cd NetworkManager
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-dhclient=/usr/bin/dhclient \
    --without-dhcpcd \
    --with-dnsmasq=/usr/bin/dnsmasq \
    --with-iptables=/usr/bin/iptables \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/bin/resolvconf \
    --with-pppd=/usr/bin/pppd \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --with-pppoe=/usr/bin/pppoe \
    --with-kernel-firmware-dir=/usr/lib/firmware \
    --with-session-tracking=systemd \
    --with-modem-manager-1 \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system \
    --enable-doc \
    --disable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install

  install -dm700 "$pkgdir/etc/NetworkManager/system-connections"
  install -d "$pkgdir"/etc/NetworkManager/{conf,dnsmasq}.d
  install -m644 ../NetworkManager.conf "$pkgdir/etc/NetworkManager/"
  install -Dm644 ../20-connectivity.conf \
    "$pkgdir/usr/lib/NetworkManager/conf.d/20-connectivity.conf"
 
  rm -r "$pkgdir/var/run"  
}
