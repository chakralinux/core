#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: Martín González kote[at]chakra-project[dot]org>

pkgname=networkmanager
_pkgname=NetworkManager
pkgver=0.9.10.0
pkgrel=3
pkgdesc="Network Management daemon"
arch=('x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
_pppver=2.4.7
depends=('dbus-glib' 'iproute2' 'libndp' 'libnl' 'nss' 'polkit' 'systemd' 'wpa_supplicant' "ppp=${_pppver}" 'dhclient'
         'libsoup' 'modemmanager' 'libnewt' 'libteam')
makedepends=('intltool' 'dhclient' 'iptables' 'gobject-introspection' 'rp-pppoe')
optdepends=('dhcpcd: Alternative dhcp support'
            'dnsmasq: connection sharing'
            'bluez: Bluetooth support'
            'openresolv: resolvconf support'
            'ppp: dialup connection support'
            'rp-pppoe: ADSL support'
            'modemmanager: cellular network support')
options=('!libtool')
backup=('etc/NetworkManager/NetworkManager.conf')
install=networkmanager.install

source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver:0:3}/$_pkgname-$pkgver.tar.xz"
        'NetworkManager.conf' 
        'disable_set_hostname.patch' 
        'git-fixes.patch' '0002-bluez-split-out-errors.patch' '0003-bluez-track-adapter-address-in-NMBluezDevice.patch' '0004-bluez-re-add-DUN-support-for-Bluez5.patch')
sha256sums=('66a88346bb04d4f402540281181340313b2ec433e75aa9d9ea13f31697f9487e'
            '44b048804c7c0b8b3b0c29b8632b6ad613c397d0a1635ec918e10c0fbcdadf21'
            '25056837ea92e559f09563ed817e3e0cd9333be861b8914e45f62ceaae2e0460'
            'cc1cadb35c21327bebdb222517a8872d612c8e60583f2f9f040162d74a293b1b'
            '5b9901d4fdbad72ec40893517691be8e87e065a611f08d1fb9a17a6c32619539'
            'f523b8df3b7a105eb94f58eb241216032660db5feb7a2a1f07b4a617a357058f'
            '7e6807635d4b245d2702ae1fc68505411527f9d50af20e0907b3ee452e138d98')


build() {
  cd $_pkgname-$pkgver

  patch -p1 -i ../disable_set_hostname.patch
  patch -p1 -i ../git-fixes.patch
  patch -Np1 -i ../0002-bluez-split-out-errors.patch
  patch -Np1 -i ../0003-bluez-track-adapter-address-in-NMBluezDevice.patch
  patch -Np1 -i ../0004-bluez-re-add-DUN-support-for-Bluez5.patch
  NOCONFIGURE=1 ./autogen.sh

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-dhclient=/usr/bin/dhclient \
    --with-dhcpcd=/usr/bin/dhcpcd \
    --with-dnsmasq=/usr/bin/dnsmasq \
    --with-iptables=/usr/bin/iptables \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/bin/resolvconf \
    --with-pppd=/usr/bin/pppd \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --with-pppoe=/usr/bin/pppoe \
    --with-kernel-firmware-dir=/usr/lib/firmware \
    --with-session-tracking=systemd \
    --with-modem-manager-1 \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system 

  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -m644 ../NetworkManager.conf "$pkgdir/etc/NetworkManager/"
  install -m755 -d "$pkgdir/etc/NetworkManager/dnsmasq.d"
 
  rm -r "$pkgdir/var/run"  
}
