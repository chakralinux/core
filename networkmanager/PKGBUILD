#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=networkmanager
pkgver=0.8.2
pkgrel=2
pkgdesc="Network Management daemon"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('wireless_tools' 'iproute2>=2.6.35' 'libnl>=1.1' 'ppp' 'dhcpcd>=5.2.5' 'wpa_supplicant>=0.6.10' 'iptables>=1.4.9' 'nss>=3.12.6' 'polkit>=0.96' 'udev>=164')
makedepends=('intltool')
optdepends=('modemmanager: for modem management service')
options=('!libtool')
backup=('etc/NetworkManager/nm-system-settings.conf')
source=(http://ftp.gnome.org/pub/gnome/sources/NetworkManager/0.8/NetworkManager-${pkgver}.tar.bz2
        nm-system-settings.conf create_dirs.patch)
sha256sums=('58e49dcd83cb641a9dcaad4fd566a08196c862479ad3086c00f28f03768eb4f1'
            '44b048804c7c0b8b3b0c29b8632b6ad613c397d0a1635ec918e10c0fbcdadf21'
            '2961ce4cf3079edbf27927a67573ce85dad2268f610fd123dd781eb5ba764c12')

build() {
  cd "${srcdir}/NetworkManager-${pkgver}"

  patch -Np1 -i "${srcdir}/create_dirs.patch"
  autoreconf -fi

  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-distro=arch --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-static --with-dhcpcd=/sbin/dhcpcd \
      --with-crypto=nss --with-iptables=/usr/sbin/iptables \
      --enable-more-warnings=no

  make
  make DESTDIR="${pkgdir}" install
  install -m644 "${srcdir}/nm-system-settings.conf" "${pkgdir}/etc/NetworkManager/"
  install -m 0755 test/.libs/nm-online "${pkgdir}/usr/bin/"
}