# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kinfocenter
source ../plasma.conf

pkgname=kinfocenter
pkgver=${PVersion}
pkgrel=1
pkgdesc='KInfocenter'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kinfocenter'
license=('LGPL')
depends=(kcmutils pciutils glu libraw1394 kwayland)
makedepends=(extra-cmake-modules python3 plasma-framework kdoctools)
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PSubVersion}.tar.xz"{,.sig}
        'kcm-about-distrorc'
        'chakra-label.png')
sha256sums=( $(getSum ${pkgname})
            'SKIP'
            '7705eef707bbc5238c61bac4bbc749c4ba257d752b2b67e0720e56f9e287fa73'
            'b06c781319509e0b082508a69f26f511acc40f169e79be5bc2cad8508ba25513')
validpgpkeys=(${Pvalidpgpkeys[@]})

prepare() {
  mkdir -p build
  
  cd "${pkgname}-${pkgver}/Modules/about-distro/src"
  # applay a custom patch to include only the chakra logo
  # disable this for now
  # patch -Np0 -i ${srcdir}/Module_Chakra.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver} 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir/kcm-about-distrorc" "$pkgdir/etc/xdg/kcm-about-distrorc"
  install -Dm644 "$srcdir/chakra-label.png" "$pkgdir/usr/share/apps/$pkgname/chakra-label.png"
}
