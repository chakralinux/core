# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kinfocenter

pkgname=kinfocenter
pkgver=5.14.1
pkgrel=1
pkgdesc='KInfocenter'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kinfocenter'
license=('LGPL')
depends=(kcmutils pciutils glu libraw1394 kwayland)
makedepends=(extra-cmake-modules python3 plasma-framework kdoctools)
groups=('plasma')
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        'kcm-about-distrorc'
        'chakra-label.png')
sha256sums=('b79ae50383b080326868f5db43d2452cef1162bf0506354c84af770ad7a5fad9'
            'SKIP'
            '7705eef707bbc5238c61bac4bbc749c4ba257d752b2b67e0720e56f9e287fa73'
            'b06c781319509e0b082508a69f26f511acc40f169e79be5bc2cad8508ba25513')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd "${pkgname}-${pkgver}/Modules/about-distro/src"
  # applay a custom patch to include only the chakra logo
  # disable this for now
  # patch -Np0 -i ${srcdir}/Module_Chakra.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir/kcm-about-distrorc" "$pkgdir/etc/xdg/kcm-about-distrorc"
  install -Dm644 "$srcdir/chakra-label.png" "$pkgdir/usr/share/apps/$pkgname/chakra-label.png"
}
