#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=ftgl
_pkgver=2.1.3-rc5
pkgver=${_pkgver/-/}
pkgrel=2
pkgdesc="OpenGL library to use arbitrary fonts"
arch=('i686' 'x86_64')
url="http://ftgl.wiki.sourceforge.net/"
license=('MIT')
depends=('freetype2' 'glut')
makedepends=('doxygen')
options=('!libtool')
source=(http://downloads.sourceforge.net/ftgl/$pkgname-2.1.3-rc5.tar.bz2)
md5sums=('c7879018cde844059495b3029b0b6503')

build() {
  cd "$srcdir/$pkgname-${_pkgver/-/~}"
  ./configure --prefix=/usr --with-pic
  make
}

package() {
  cd "$srcdir/$pkgname-${_pkgver/-/~}"
  make DESTDIR="$pkgdir" install

  # FS#23283: install missing headers for OpenCascade
  install -m644 src/FT{Face,Library,Size}.h "$pkgdir/usr/include/FTGL/"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
