# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kcompletion
pkgver=5.50.0
pkgrel=1
pkgdesc='Text completion helpers and widgets'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kcompletion'
license=('LGPL')
depends=('kwidgetsaddons' 'kconfig')
makedepends=("extra-cmake-modules>=5.50.0" 'qt5-tools')
checkdepends=('xorg-server-xvfb')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('5fe538fc18c57f167cac95262f75d4c638b16408c0e9cc3250c6c12be6cc0ed8'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   xvfb-run make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
