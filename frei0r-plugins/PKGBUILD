# Contributor: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# Maintainer: abveritas[at]chakra-project[dot]org>

pkgname=frei0r-plugins
pkgver=1.4
pkgrel=5
pkgdesc="frei0r is a minimalistic plugin API for video sources and filters."
arch=('x86_64')
url="http://frei0r.dyne.org/"
license=('GPL')
depends=('gavl')
makedepends=('doxygen')
source=(https://distfiles.macports.org/frei0r-plugins/frei0r-plugins-1.4.tar.gz
        as-needed.patch)
md5sums=('202375d1bcb545c1b6eb8f34e0260ec5'
         '49bab3a8700da8d8ffb6369423fa356d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i ../as-needed.patch
  [[ -e build ]] && rm -rf build
  mkdir build
}

build() {
  cd "$srcdir/$pkgname-$pkgver"/build
  
  #./configure --prefix=/usr 
  export CXXFLAGS="${CXXFLAGS} -std=gnu++11"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DWITHOUT_OPENCV=ON ..
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"/build
  
  make DESTDIR="$pkgdir" install 
}

