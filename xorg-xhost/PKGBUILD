# Maintainer: Drake Justice <djustice@chakraos.org>

pkgname=xorg-xhost
pkgver=1.0.6
pkgrel=1
pkgdesc="Server access control program for X"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xhost-${pkgver}.tar.bz2)
sha1sums=('6224c8bcd909bf205c7e92dbdc349356890c4428')

build() {
  cd "${srcdir}/xhost-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xhost-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

