# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/oxygen

pkgbase=oxygen
pkgname=('oxygen' 'oxygen-cursors' 'oxygen-kde4')
pkgver=5.13.0
pkgrel=2
pkgdesc="KDE Oxygen style"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/oxygen'
license=('LGPL')
makedepends=(frameworkintegration kcmutils kdecoration extra-cmake-modules kdoctools kdelibs automoc4 python3)
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3e7f8044f308f42969d5a0622022ead799deefaf6f3a9cb9be77238158a3fd97'
             'SKIP' )
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build{,-kde4}
}

build() {
  cd build
  cmake_kf5 ../$pkgname-$PSubVersion 
  make
  cd ..

  cd build-kde4
  cmake ../$pkgname-$PSubVersion \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DUSE_KDE4=ON
  make
}

package_oxygen() {
  pkgdesc='KDE Oxygen style'
  depends=('frameworkintegration' 'kdecoration')
  optdepends=('oxygen-cursors: cursor themes for Plasma 5')
  groups=('plasma')

  cd build
  make DESTDIR="${pkgdir}" install

  # Cursors conflicts with kde-workspace
  rm -r "${pkgdir}"/usr/share/icons/
}

package_oxygen-cursors() {
  pkgdesc='KDE Oxygen cursor themes for Plasma 5'
  groups=('plasma')
  conflicts=('kde-workspace')

  cd build/cursors
  make DESTDIR="${pkgdir}" install
}

package_oxygen-kde4() {
  pkgdesc='KDE Oxygen style for KDE4 applications'
  depends=('kdelibs')
  conflicts=('kde-workspace')

  cd build-kde4
  make DESTDIR="${pkgdir}" install

  # needed so that pure Qt4 apps are correctly themed
  install -d -m755 "$pkgdir"/usr/lib/qt/plugins/styles
  ln -s /usr/lib/kde4/plugins/styles/oxygen.so "$pkgdir"/usr/lib/qt/plugins/styles
}
