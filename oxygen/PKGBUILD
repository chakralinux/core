# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/oxygen

pkgbase=oxygen
pkgname=('oxygen' 'oxygen-cursors')
pkgver=5.14.4
pkgrel=2
pkgdesc="KDE Oxygen style"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/oxygen'
license=('LGPL')
makedepends=(frameworkintegration kcmutils kdecoration extra-cmake-modules kdoctools python3)
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1f4c1e7c94204af3d146670df4a6a271b433b23b4543b6ad5adcfb9aad8ebde5'
             'SKIP' )
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build{,-kde4}
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
  cd ..
}

package_oxygen() {
  pkgdesc='KDE Oxygen style'
  depends=('frameworkintegration' 'kdecoration')
  optdepends=('oxygen-cursors: cursor themes for Plasma 5')
  groups=('plasma')

  cd build
  make DESTDIR="${pkgdir}" install

  # Cursors conflicts with kde-workspace
  rm -r "${pkgdir}"/usr/share/icons/
}

package_oxygen-cursors() {
  pkgdesc='KDE Oxygen cursor themes for Plasma 5'
  groups=('plasma')
  conflicts=('kde-workspace')

  cd build/cursors
  make DESTDIR="${pkgdir}" install
}
