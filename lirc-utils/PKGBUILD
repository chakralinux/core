# maintainer almack@chakra-project.org

_extramodules=extramodules-3.15.5-CHAKRA
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=lirc-utils
pkgver=0.9.1
pkgrel=2
pkgdesc="Linux Infrared Remote Control utils"
arch=('x86_64')
url="http://www.lirc.org/"
license=('GPL')
depends=('alsa-lib' 'libx11' 'libftdi' 'linux')
makedepends=('help2man' 'linux-headers>=3.15' 'linux-headers<3.16' 'python2' 'libtool')
optdepends=('python2: pronto2lirc utility')
replaces=('lirc+pctv')
backup=('etc/conf.d/lircd.conf' 'etc/conf.d/irexec.conf')
options=('!makeflags')
install=lirc-utils.install
source=(http://sourceforge.net/projects/lirc/files/LIRC/$pkgver/lirc-$pkgver.tar.bz2
        kernel-2.6.39.patch
        linux-3.8.patch
        lirc.logrotate lircd.conf irexec.conf 
        lirc.tmpfiles)
md5sums=('3b78c3cc872d5e2fa78b796c2efd46db'
         '087a7d102e1c96bf1179f38db2b0b237'
         '9ee196bd03ea44af5a752fb0cc6ca96a'
         '3deb02604b37811d41816e9b4385fcc3'
         '5b1f8c9cd788a39a6283f93302ce5c6e'
         'f0c0ac930326168035f0c8e24357ae55'
         'f367f3dfecd10b9cfa035f9a6c2d2ea6'
         'e2a9a403c2f5107bb9ef1eb70a623876'
         '07131d117fcfe9dcd50c453c3a5e9531'
         'febf25c154a7d36f01159e84f26c2d9a')

prepare() {
        # configure
        cd "${srcdir}/lirc-$pkgver" 
	patch -Np1 -i "${srcdir}/kernel-2.6.39.patch"
	patch -Np1 -i "${srcdir}/linux-3.8.patch"

	sed -i '/AC_PATH_XTRA/d' configure.ac
	sed -e 's/@X_CFLAGS@//g' \
         -e 's/@X_LIBS@//g' \
         -e 's/@X_PRE_LIBS@//g' \
         -e 's/@X_EXTRA_LIBS@//g' -i Makefile.am tools/Makefile.am

        # fix for new automake #33497
	sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac

	libtoolize 
	autoreconf 
}

build() {
	cd "${srcdir}/lirc-${pkgver}"
	./configure --enable-sandboxed --prefix=/usr \
	--with-driver=all --with-kerneldir=/usr/src/linux-${_kver} \
	--with-moduledir=/lib/modules/${_kver}/kernel/drivers/misc \
	--with-transmitter
		
        # Remove drivers already in kernel
        sed -e "s:lirc_dev::" -e "s:lirc_bt829::" -e "s:lirc_igorplugusb::" \
            -e "s:lirc_imon::" -e "s:lirc_parallel::" -e "s:lirc_sasem::" \
            -e "s:lirc_serial::" -e "s:lirc_sir::" -e "s:lirc_ttusbir::" \
            -i Makefile drivers/Makefile drivers/*/Makefile tools/Makefile 
                
  	# build
	make 
}

package() {
        cd "${srcdir}/lirc-${pkgver}"
	make DESTDIR=""${pkgdir}"" install 
	
	install -D -m644 "${srcdir}"/lirc.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lirc.conf
		
	cp -rp remotes "${pkgdir}"/usr/share/lirc 
	chmod -R go-w "${pkgdir}"/usr/share/lirc/ 


	# install the logrotate config
    	install -D -m644 "${srcdir}"/lirc.logrotate "${pkgdir}"/etc/logrotate.d/lirc 
    
	# install conf.d file
	install -D -m644 "${srcdir}"/lircd.conf "${pkgdir}"/etc/conf.d/lircd.conf 

	# install conf.d file
        install -D -m644 "${srcdir}"/irexec.conf "${pkgdir}"/etc/conf.d/irexec.conf 

	install -d -m755 "${pkgdir}"/etc/lirc
	
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/lirc-utils.install"
}
