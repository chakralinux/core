# maintainer almack@chakraos.org

_extramodules=extramodules-3.15.5-CHAKRA
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=lirc-utils
pkgver=0.9.1
pkgrel=3
pkgdesc="Linux Infrared Remote Control utils"
arch=('x86_64')
url="http://www.lirc.org/"
license=('GPL')
depends=('alsa-lib' 'libx11' 'libftdi' 'linux')
makedepends=('help2man' 'linux-headers>=3.15' 'linux-headers<3.16' 'python2' 'libtool')
optdepends=('python2: pronto2lirc utility')
replaces=('lirc+pctv')
backup=('etc/lirc/lirc_options.conf' 'etc/lirc/lircd.conf' 'etc/lirc/lircmd.conf')
options=('!makeflags')
install=lirc-utils.install
source=(http://sourceforge.net/projects/lirc/files/LIRC/$pkgver/lirc-$pkgver.tar.bz2
        kernel-2.6.39.patch
        linux-3.8.patch
        irexec.service
        lirc.logrotate
        lirc.tmpfiles)
md5sums=('3b78c3cc872d5e2fa78b796c2efd46db'
         '087a7d102e1c96bf1179f38db2b0b237'
         '9ee196bd03ea44af5a752fb0cc6ca96a'
         '32df3b9bc859565d6acf5f0e5b747083'
         '3deb02604b37811d41816e9b4385fcc3'
         'febf25c154a7d36f01159e84f26c2d9a')

prepare() {
        # configure
        cd "${srcdir}/lirc-$pkgver" 
	patch -Np1 -i "${srcdir}/kernel-2.6.39.patch"
	patch -Np1 -i "${srcdir}/linux-3.8.patch"

	sed -i '/AC_PATH_XTRA/d' configure.ac
	sed -e 's/@X_CFLAGS@//g' \
         -e 's/@X_LIBS@//g' \
         -e 's/@X_PRE_LIBS@//g' \
         -e 's/@X_EXTRA_LIBS@//g' -i Makefile.am tools/Makefile.am

        # fix for new automake #33497
	sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac

	libtoolize 
	autoreconf 
}

build() {
	cd "${srcdir}/lirc-${pkgver}"
	./configure --enable-sandboxed --prefix=/usr \
	--with-driver=all --with-kerneldir=/usr/src/linux-${_kver} \
	--with-moduledir=/lib/modules/${_kver}/kernel/drivers/misc \
	--with-transmitter
		
        # Remove drivers already in kernel
        sed -e "s:lirc_dev::" -e "s:lirc_bt829::" -e "s:lirc_igorplugusb::" \
            -e "s:lirc_imon::" -e "s:lirc_parallel::" -e "s:lirc_sasem::" \
            -e "s:lirc_serial::" -e "s:lirc_sir::" -e "s:lirc_ttusbir::" \
            -i Makefile drivers/Makefile drivers/*/Makefile tools/Makefile 
                
  	# build
	make 
}

package() {
        cd "${srcdir}/lirc-${pkgver}"
	make DESTDIR=""${pkgdir}"" install 
	
	install -D -m644 "${srcdir}"/lirc.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lirc.conf
	install -Dm644 "${srcdir}"/irexec.service "${pkgdir}"/usr/lib/systemd/system/irexec.service
		
	cp -rp remotes "${pkgdir}"/usr/share/lirc 
	chmod -R go-w "${pkgdir}"/usr/share/lirc/ 

	# install the logrotate config
    	install -D -m644 "${srcdir}"/lirc.logrotate "${pkgdir}"/etc/logrotate.d/lirc 

	install -d -m755 "${pkgdir}"/etc/lirc
	
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/lirc-utils.install"
}
