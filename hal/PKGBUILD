
pkgname=hal
pkgver=0.5.14
pkgrel=4
pkgdesc="Hardware Abstraction Layer"
arch=(i686 x86_64)
license=('GPL' 'custom')
url="http://www.freedesktop.org/wiki/Software/hal"
depends=('dbus-glib>=0.82' 'libusb-compat' 'udev>=146' 'filesystem>=0.7.1-5' 'hal-info>=0.20090716' 'eject' 'dmidecode' 'pciutils>=3.0.2' 'usbutils>=0.73-5' 'pm-utils>=1.2.5' 'consolekit>=0.4.1' 'util-linux-ng>=2.16' 'v4l-utils')
makedepends=('pkgconfig' 'gperf')
options=('!libtool' '!makeflags')
install=hal.install
source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
         hald
         hal.patch
         udev-update.patch
         hal-glib-2.3-compile-fix.patch)
md5sums=('e9163df591a6f38f59fdbfe33e73bf20'
         '4cdfc673ad65ddb51919f5a757f62145'
         '185dd5d6ffc703ec8395b4eb3a1ae498'
         'cad3564de1708603c7a1e0facf6b1921'
         'c126a66766118f83a171511832d1b619')

build() {
  cd $srcdir
  patch -p1 -d ${srcdir} < hal.patch
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/udev-update.patch"
  patch -p1 -i "${srcdir}/hal-glib-2.3-compile-fix.patch"
  
  libtoolize --force
  aclocal
  autoconf
  automake

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/hal --with-udev-prefix=/etc \
      --enable-static=no --disable-acpi-ibm \
      --disable-docbook-docs --disable-console-kit \
      --disable-policy-kit --disable-acl-management \
      --enable-umount-helper --disable-smbios \
      --with-hal-user=hal --with-hal-group=hal \
      --with-pid-file=/var/run/hald.pid \
      --disable-gtk-doc
  make

}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 -d "${pkgdir}/media"
  install -m755 "${srcdir}/hald" "${pkgdir}/etc/rc.d/hal"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  
  # Fix udev rule location
  install -d -m755 "${pkgdir}/usr/lib"
  mv "${pkgdir}/etc/udev" "${pkgdir}/usr/lib/"
}
