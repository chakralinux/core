# arch contributor: https://projects.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/graphene
 
pkgname=graphene
pkgver=1.2.10
pkgrel=1
pkgdesc="A thin layer of graphic data types"
arch=(i686 x86_64)
license=(MIT)
url="https://github.com/ebassi/graphene"
depends=(glib2)
makedepends=(git gtk-doc gobject-introspection)
source=("git+$url#tag=$pkgver")
md5sums=('SKIP')

_confargs=
[[ $CARCH == i686 ]] && _confargs=--disable-sse2

prepare() {
  cd $pkgname
  sed -i '1s/python$/&2/' build/identfilter.py
  NOCONFIGURE=1 ./autogen.sh
}
 
build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc $_confargs
  make
}

check() {
  cd $pkgname
  make check
}
 
package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
