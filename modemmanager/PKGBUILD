# Maintainer: Francesco Marinucci <franzmari[at]chakra-project[dot]it>

pkgname=modemmanager
_realname=ModemManager
pkgver=1.4.0
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="http://cgit.freedesktop.org/ModemManager/ModemManager/"
license=('GPL2')
depends=('dbus-glib>=0.100' 'systemd' 'ppp>=2.4.5' 'polkit')
makedepends=('intltool')
source=("http://www.freedesktop.org/software/${_realname}/${_realname}-${pkgver}.tar.xz")
optdepends=('usb-modeswitch: install if your modem shows up as a storage drive')
md5sums=('4f78b31c23828dc1037352d6dbb608db')

build() {
  cd "$srcdir/${_realname}-${pkgver}"
   CFLAGS="${CFLAGS} -Wno-error -w" CPPFLAGS="${CPPFLAGS} -Wno-error -w" ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
	--disable-static \
        --with-polkit \
	--without-qmi \
	--without-mbim
  sed -i 's/-Werror/-w/g' Makefile
  CFLAGS="${CFLAGS} -Wno-error -w" CPPFLAGS="${CPPFLAGS} -Wno-error -w" make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
