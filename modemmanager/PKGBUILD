pkgname=modemmanager
_realname=ModemManager
pkgver=1.4.10
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/ModemManager/"
license=('GPL2' 'LGPL2.1')
depends=('systemd' 'libgudev' 'polkit' 'ppp' 'python2' 'libqmi' 'libmbim' 'hicolor-icon-theme')
makedepends=('intltool' 'gobject-introspection' 'vala')
source=("http://www.freedesktop.org/software/${_realname}/${_realname}-${pkgver}.tar.xz"
        "bus-signals.patch")
optdepends=('usb-modeswitch: install if your modem shows up as a storage drive')
md5sums=('1e46a148e2af0e9f503660fcd2d8957d'
         '02aac68b3ef2ec1971f05300760ba4e5')

prepare() {
  cd ModemManager-$pkgver
  patch -Np1 -i ../bus-signals.patch
}

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
        --with-polkit=permissive \
        --disable-gtk-doc \
        --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
