#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainerabveritas[at]chakra-project[dot]org>

pkgname=modemmanager
_realname=ModemManager
pkgver=0.5.2.0
pkgrel=3
pkgdesc="Mobile broadband modem management service"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/ModemManager/ModemManager/"
license=('GPL2')
depends=('dbus-glib>=0.100' 'systemd' 'ppp>=2.4.5')
makedepends=('intltool')
options=('!libtool')
source=(http://download.gnome.org/sources/${_realname}/0.5/${_realname}-${pkgver}.tar.xz
	modem-manager.service)
optdepends=('usb-modeswitch: install if your modem shows up as a storage drive')
sha256sums=('7f8b3e1996a24e031df7534e226a0f0c7c2dd8b30322e6c5f4611dc1a848e6c8'
            '66c9947972066dd5494c3a18b0e833f236e0866b455675368b5994900cbaf384')

build() {
  cd "$srcdir/${_realname}-${pkgver}"
   ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
	--disable-static
  make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  
  install -Dm644 "$srcdir/modem-manager.service" "$pkgdir/usr/lib/systemd/system/modem-manager.service"
  echo "SystemdService=modem-manager.service" \
    >> "$pkgdir/usr/share/dbus-1/system-services/org.freedesktop.ModemManager.service"
}
