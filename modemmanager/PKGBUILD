pkgname=modemmanager
_realname=ModemManager
pkgver=1.6.4
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/ModemManager/"
license=(GPL2 LGPL2.1)
depends=(systemd libgudev polkit ppp libqmi libmbim)
makedepends=(intltool gtk-doc gobject-introspection vala autoconf-archive git)
_commit=ed41d18ab6b0db386b30ed8ef712ab51a865c7cf  # tags/1.6.4
source=("git://anongit.freedesktop.org/ModemManager/ModemManager#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ModemManager
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd ModemManager
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ModemManager
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --with-dbus-sys-dir=/usr/share/dbus-1/system.d \
        --with-udev-base-dir=/usr/lib/udev \
        --with-polkit=permissive \
        --with-suspend-resume=systemd \
        --enable-gtk-doc \
        --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd ModemManager
  make DESTDIR="$pkgdir" install
}
