#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=modemmanager
_realname=ModemManager
pkgver=0.6.2.0
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="http://cgit.freedesktop.org/ModemManager/ModemManager/"
license=('GPL2')
depends=('dbus-glib>=0.100' 'systemd' 'ppp>=2.4.5' 'polkit')
makedepends=('intltool')
options=('!libtool')
source=("http://download.gnome.org/sources/${_realname}/0.6/${_realname}-${pkgver}.tar.xz"
	'modem-manager.service')
optdepends=('usb-modeswitch: install if your modem shows up as a storage drive')

build() {
  cd "$srcdir/${_realname}-${pkgver}"
   CFLAGS="${CFLAGS} -Wno-error -w" CPPFLAGS="${CPPFLAGS} -Wno-error -w" ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
	--disable-static \
        --with-polkit
  sed -i 's/-Werror/-w/g' Makefile
  CFLAGS="${CFLAGS} -Wno-error -w" CPPFLAGS="${CPPFLAGS} -Wno-error -w" make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  
  install -Dm644 "$srcdir/modem-manager.service" "$pkgdir/usr/lib/systemd/system/modem-manager.service"
  echo "SystemdService=modem-manager.service" \
    >> "$pkgdir/usr/share/dbus-1/system-services/org.freedesktop.ModemManager.service"
}
md5sums=('29f97defbf9429f648c888a42732a955'
         'e098d96691f38eb4c91c6a5e6acae307')
