#
# Platform Packages for Chakra, part of www.chakraos.org
#
# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>

pkgbase=sip
pkgname=('python-sip-common' 'python2-sip' 'python3-sip')
pkgver=4.16.6
pkgrel=1
pkgdesc="A tool for creating Python bindings for C and C++ libraries"
arch=('x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
makedepends=('python2' 'python3')
source=("http://downloads.sourceforge.net/pyqt/${pkgbase}-${pkgver}.tar.gz")
md5sums=('44e87c4c98206f465d3f07f4caa0c903')

build() {
    cp -r ${pkgbase}-${pkgver} python2-${pkgbase}-${pkgver}

    # Python 3 version
    cd ${srcdir}/${pkgbase}-${pkgver}
    msg "Configuring the Python 3 version"
    python3 configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
    msg "Making the Python 3 version"
    make

    # Python 2 version
    cd ${srcdir}/python2-${pkgbase}-${pkgver}
    msg "Configuring the Python 2 version"
    python2 configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
    msg "Making the Python 2 version"
    make
}

package_python-sip-common() {
    pkgdesc=("A tool for creating Python bindings for C and C++ libraries")
    depends=('glibc')
    conflicts=('sip-common' 'sip')
    replaces=('sip-common' 'sip')
    provides=('sip-common=${pkgver}')

    cd ${srcdir}/${pkgbase}-${pkgver}
    msg "Packaging python-sip-common"
    make DESTDIR=${pkgdir} install -C sipgen
    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_python3-sip() {
    pkgdesc=("Python 3 SIP bindings for C and C++ libraries")
    depends=('python3' 'python-sip-common')
    conflicts=('sip-python3')
    replaces=('sip-python3')

    cd ${srcdir}/${pkgbase}-${pkgver}
    msg "Packaging python3-sip"
    make DESTDIR=${pkgdir} install -C siplib
    install -Dm644 sipconfig.py "${pkgdir}"/usr/lib/python3.4/site-packages/sipconfig.py
    install -Dm644 sipdistutils.py "${pkgdir}"/usr/lib/python3.4/site-packages/sipdistutils.py
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-sip() {
    pkgdesc=("Python 2 SIP bindings for C and C++ libraries")
    depends=('python2' 'python-sip-common')
    conflicts=('sip-python2')
    replaces=('sip-python2')

    cd ${srcdir}/python2-${pkgbase}-${pkgver}
    msg "Packaging python2-sip"
    make DESTDIR=${pkgdir} install -C siplib
    install -Dm644 sipconfig.py "${pkgdir}"/usr/lib/python2.7/site-packages/sipconfig.py
    install -Dm644 sipdistutils.py "${pkgdir}"/usr/lib/python2.7/site-packages/sipdistutils.py
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
