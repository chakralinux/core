#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=initscripts
pkgver=2011.11.2
pkgrel=1
pkgdesc="System initialization/bootup scripts"
arch=('i686' 'x86_64')
url="http://www.chakra-project.org"
license=('GPL2')
groups=('base')
backup=(etc/inittab etc/rc.conf etc/rc.local etc/rc.local.shutdown etc/conf.d/wireless etc/locale.conf)
depends=('glibc' 'bash' 'grep' 'coreutils' 'udev>=174' 'iproute2'
         'ncurses' 'kbd' 'findutils' 'sysvinit')
optdepends=('net-tools: legacy networking support'
            'bridge-utils: Network bridging support'
            'dhcpcd: DHCP network configuration'
            'wireless_tools: Wireless networking')
makedepends=(asciidoc)
install=initscripts.install
source=("http://chakra.sourceforge.net/sources/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "http://chakra.sourceforge.net/sources/initscripts/wireless.conf.d")

# create tarball: source PKGBUILD && mksource

mksource() {
  rm -vRf chakra-${pkgname}
  git clone git://gitorious.org/chakra/chakra-${pkgname}.git
  rm -vRf chakra-${pkgname}/.git
  pushd chakra-${pkgname}
  popd
  tar -cvJf ${pkgname}-${pkgver}.tar.xz chakra-${pkgname}/*
  sha256sum ${pkgname}-${pkgver}.tar.xz
}

package() {
  cd ${srcdir}/chakra-${pkgname}
  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/wireless.conf.d ${pkgdir}/etc/conf.d/wireless
 
  # Avoid conflicts
  mv ${pkgdir}/etc/profile.d/locale.sh ${pkgdir}/etc/profile.d/locale.sh.new
}

sha256sums=('1a69b69cfd09b2f5295340a136b45fc20dde303785f4793cefa748f3f36e9f3a'
            '0eba4250b07e0a727ef64772f5ee9027b123cbd4bb0894ba2842f545035c827f')
