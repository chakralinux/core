# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kpackage
pkgver=5.48.0
pkgrel=1
pkgdesc='Framework that lets applications manage user installable packages of non-binary assets'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kpackage'
license=('LGPL')
depends=('karchive' 'ki18n' 'kcoreaddons' 'kconfig')
makedepends=("extra-cmake-modules>=5.48.0" 'qt5-tools' 'python3' 'kdoctools')
checkdepends=("cmake")
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('995db29fa77466b3b4b914a7021a5be4e1d55265f5c801508b5efefac27d06a3'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

check() {
  cd build
  make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
