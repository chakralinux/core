pkgname=libndp
pkgver=1.6
pkgrel=1
pkgdesc="Library for Neighbor Discovery Protocol"
arch=('x86_64')
url="http://libndp.org/"
license=('LGPL2.1')
depends=('glibc')
source=("$url/files/$pkgname-$pkgver.tar.gz")
sha256sums=('0c7dfa84e013bd5e569ef2c6292a6f72cfaf14f4ff77a77425e52edc33ffac0e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
