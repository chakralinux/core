# $Id: PKGBUILD 80074 2010-05-10 21:13:12Z thomas $
# Maintainer: judd <jvinet@zeroflux.org>

pkgname=libgpg-error
pkgver=1.10
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('glibc' 'sh')
options=(!libtool)
source=("ftp://ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('95b324359627fbcb762487ab6091afbe59823b29')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  # keep static lib for crypsetup
  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install || return 1

  # Move dynamic libraries to /lib
  install -d -m755 "${pkgdir}"/lib/ || return 1
  mv "${pkgdir}"/usr/lib/libgpg-error.so* "${pkgdir}"/lib/ || return 1
  ln -sf /lib/libgpg-error.so "${pkgdir}/usr/lib/libgpg-error.so" || return 1
}

# vim:set ts=2 sw=2 et:
