pkgname=libgpg-error
pkgver=1.31
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('glibc' 'sh')
options=(!libtool)
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig})
install=${pkgname}.install
sha1sums=('2bafad316d4e3e12bae4822b14ed9020090e6acf'
          'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'  # Werner Koch
              '031EC2536E580D8EA286A9F22071B08A33BD3F06') # NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make 
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install 
}
