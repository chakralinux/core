# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kross
pkgver=5.54.0
pkgrel=1
pkgdesc='Multi-language application scripting'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kross'
license=('LGPL')
depends=('kparts')
makedepends=("extra-cmake-modules>=5.54.0" 'kdoctools' 'qt5-tools')
checkdepends=('xorg-server-xvfb')
groups=('kf5-aids')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('6d8673c764d7aab41a1573151afc29a15dd28d05d4299089a253c399c64832a2'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   xvfb-run make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
