#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=libcap
pkgver=2.22
pkgrel=1
pkgdesc="POSIX 1003.1e capabilities"
arch=('i686' 'x86_64')
url="http://www.kernel.org/pub/linux/libs/security/linux-privs/"
license=('GPL')
depends=('glibc' 'attr')
source=(http://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/${pkgname}-${pkgver}.tar.gz)
md5sums=('b4896816b626bea445f0b3849bdd4077')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make prefix=/usr DESTDIR=${pkgdir} RAISE_SETFCAP=no install

  chmod 755 ${pkgdir}/usr/lib/libcap.so.${pkgver}
  rm ${pkgdir}/usr/lib/libcap.a

  install -Dm644 pam_cap/capability.conf \
    $pkgdir/usr/share/doc/$pkgname/capability.conf.example
}
