# maintainer abveritas@chakra-project.org

pkgname=libcap
pkgver=2.25
pkgrel=1
pkgdesc="POSIX 1003.1e capabilities"
arch=('i686' 'x86_64')
url="http://www.kernel.org/pub/linux/libs/security/linux-privs/"
license=('GPL')
depends=('glibc' 'attr')
source=(http://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/${pkgname}-${pkgver}.tar{.gz,.sign})
sha256sums=('4ca80dc6f9f23d14747e4b619fd9784434c570e24a7346f326c692784ed83a86'
            'SKIP')
validpgpkeys=('38A644698C69787344E954CE29EE848AE2CCF3F4')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # disable static library
  sed -i '/install.*STALIBNAME/ s/^/#/' libcap/Makefile
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # libcap installs to /usr/lib64 by default, which is rather bad when everything looks for the .sos under /usr/lib
  make prefix=/usr lib=/lib DESTDIR=${pkgdir} RAISE_SETFCAP=no install

  install -Dm644 pam_cap/capability.conf \
    $pkgdir/usr/share/doc/$pkgname/capability.conf.example
}
