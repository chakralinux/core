
pkgname=qt5-virtualkeyboard
pkgver=5.11.3
pkgrel=1
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='Virtual keyboard framework'
depends=('qt5-declarative' 'qt5-svg')
makedepends=()
groups=('qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${pkgver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/submodules/${_pkgfqn}.tar.xz"
"https://github.com/qt/qtvirtualkeyboard/commit/c7a94110987672308ed04b40fd8ed207d1c8d93b.diff")
md5sums=('66b03225f71549ea63f351c781ffba94'
         'e49c186eccddb816bd6a38a65babfdff')

prepare() {
  mkdir -p build

  cd ${_pkgfqn}
 # Security warning KDE ml
 patch -p1 -i ${srcdir}/c7a94110987672308ed04b40fd8ed207d1c8d93b.diff
}

build() {
  cd build

  qmake-qt5 ../${_pkgfqn}  
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
