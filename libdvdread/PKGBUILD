pkgname=libdvdread
pkgver=5.0.3
pkgrel=1
pkgdesc="Provides a simple foundation for reading DVD video disks"
arch=('x86_64')
url="http://dvdnav.mplayerhq.hu"
license=('GPL')
depends=('glibc' 'libdvdcss')
source=(http://download.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('321cdf2dbdc83c96572bc583cd27d8c660ddb540ff16672ecb28607d018ed82b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-static=no \
              --sharedstatedir=/var \
              --localstatedir=/var \
              --sysconfdir=/etc \
              --with-libdvdcss=yes
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
