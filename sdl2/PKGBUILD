pkgname=sdl2
pkgver=2.0.3
pkgrel=2
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2.0). (Mercurial Version)"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('glibc' 'libxext' 'libxrender' 'libx11' 'libgl' 'libxcursor')
makedepends=('alsa-lib' 'mesa' 'libpulse' 'libxrandr' 'libxinerama' 'wayland' 'libxkbcommon')
optdepends=('alsa-lib: ALSA audio driver'
            'libpulse: PulseAudio audio driver')
source=("http://www.libsdl.org/release/SDL2-${pkgver}.tar.gz"
        "FindSDL2.cmake")
md5sums=('fe6c61d2e9df9ef570e7e80c6e822537'
         'f7f356b30a6984dafa353f0c476e6557')

build() {
  cd "${srcdir}/SDL2-${pkgver}"

  mkdir build && cd build
  
  ../configure --prefix=/usr \
	       --enable-sdl-dlopen \
               --disable-arts \
               --disable-esd \
               --disable-nas \
	       --enable-alsa \
	       --enable-pulseaudio-shared \
	       --enable-video-wayland \
	       --disable-video-mir \
	       --disable-mir-shared \
	       --disable-rpath
  make
}

package() {
  cd "${srcdir}/SDL2-${pkgver}/build"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 ../COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  # we can remove the following when provided directly by cmake
  install -Dm644 $srcdir/FindSDL2.cmake ${pkgdir}/usr/share/cmake-3.0/Modules/FindSDL2.cmake
}
