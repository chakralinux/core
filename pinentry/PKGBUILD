# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=pinentry
pkgver=0.9.0
pkgrel=1
pkgdesc="a collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol"
arch=('i686' 'x86_64')
license=('GPL')
url="http://gnupg.org/related_software/pinentry/"
depends=('ncurses' 'libcap>=2.16')
makedepends=('qt')
optdepends=('gtk2: for gtk2 backend'
            'qt: for qt4 backend')  
source=(ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2)
install=pinentry.install
md5sums=('40a05856cb3accf6679987b7899b0f5a')

build() {
  cd $srcdir/$pkgname-$pkgver

  for file in qt4/*.moc; do
    moc ${file/.moc/.h} > ${file}
  done
  ./configure --prefix=/usr \
	--enable-pinentry-curses \
        --disable-pinentry-gtk \
        --disable-pinentry-qt \
	--disable-pinentry-gtk2 \
        --enable-pinentry-qt4 \
	--enable-fallback-curses
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install 
}
