#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
pkgname=systemd
pkgver=37
pkgrel=2
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-core' 'kbd' 'libcap' 'util-linux>=2.19' 'udev>=172')
makedepends=('docbook-xsl' 'gperf' 'libxslt' 'cryptsetup' 'intltool' 'libnotify')
optdepends=('cryptsetup: required for encrypted block devices'
            'dbus-python: systemd-analyze'
            'initscripts: legacy support for hostname and vconsole setup'
            'initscripts-systemd: native boot and initialization scripts'
            'libnotify: systemadm'
            'python2-cairo: systemd-analyze'
            'systemd-units: collection of native unit files for Chakra daemon/init scripts')
groups=('systemd')
options=('!libtool')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/systemd-logind.conf)
install=systemd.install
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.bz2"
        "os-release" "systemd-add-chakra.patch" )
md5sums=('1435f23be79c8c38d1121c6b150510f3'
         '68cf3e12b0e98a51efde9811e5fc713c'
         'd7cbae2627bb60360a6b1c03dd0fd90a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
    # Don't unset locale in getty
    # https://bugzilla.redhat.com/show_bug.cgi?id=663900
      sed -i -e '/^Environ.*LANG/s/^/#/' \
             -e '/^ExecStart/s/agetty/& -8/' units/getty@.service.m4
  patch -Np1 -i ${srcdir}/systemd-add-chakra.patch

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib/systemd \
              --with-distro=chakra \
              --libdir=/usr/lib \
              --localstatedir=/var \
              --with-rootdir= \
              --with-rootlibdir=/lib \
              --disable-audit \
              --disable-tcpwrap \
              --disable-gtk
  make

  # fix .so links in manpages
  sed -i 's|\.so halt\.8|.so systemd.halt.8|' man/{halt,poweroff}.8
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
  printf "d /run/console 755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  # fix systemd-analyze for python2
  sed -i '1s/python$/python2/' "$pkgdir/usr/bin/systemd-analyze"

  # rename man pages to avoid conflicts with sysvinit and initscripts
  cd "$pkgdir/usr/share/man"

  manpages=(man8/{telinit,halt,reboot,poweroff,runlevel,shutdown}.8
            man5/{hostname,{vconsole,locale}.conf}.5)

  for manpage in "${manpages[@]}"; do
    IFS='/' read section page <<< "$manpage"
    mv "$manpage" "$section/systemd.$page"
  done
}



