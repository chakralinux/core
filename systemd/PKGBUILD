#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=systemd
pkgver=29
pkgrel=2
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-systemd' 'initscripts' 'sysvinit' 'rsyslog' 'util-linux>=2.19.1')
makedepends=('docbook-xsl' 'libxslt' 'cryptsetup' )
optdepends=('cryptsetup: required for encrypted block devices'
            'dbus-python: systemd-analyze'
            'initscripts-systemd: native boot and initialization scripts'
            'libnotify: systemadm'
            'python2-cairo: systemd-analyze'
            'systemd-units: collection of native unit files for Chakra daemon/init scripts')
groups=('systemd')
options=('!libtool')
backup=(etc/systemd/system.conf
        etc/tmpfiles.d/systemd.conf
        etc/tmpfiles.d/x11.conf)
install=systemd.install
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.bz2"
        "os-release" "systemd-add-chakra.patch")
md5sums=('e91ffd208c5f03e5c11966eee7b684b0'
         '68cf3e12b0e98a51efde9811e5fc713c'
         'd7cbae2627bb60360a6b1c03dd0fd90a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ${srcdir}/systemd-add-chakra.patch

  ./configure --prefix=/usr \
              --with-rootdir= \
              --with-distro=chakra \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var \
              --disable-audit \
              --disable-tcpwrap \
              --disable-gtk
echo "test"
  make
}

package() {
  mkdir -p ${pkgdir}/run
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
#  install -dm755 "$pkgdir/etc/modules-load.d"
  printf "d /run/console 755 root root\n" > "$pkgdir/etc/tmpfiles.d/console.conf"

  # fix systemd-analyze for python2
  sed -i '1s/python$/python2/' "$pkgdir/usr/bin/systemd-analyze"

  # install target doesn't bring in plymouth units
  #  install -m644 units/plymouth-* "$pkgdir/lib/systemd/system"

  # rename man pages to avoid conflicts with sysvinit
  cd "$pkgdir/usr/share/man/man8"
  for manpage in telinit halt reboot poweroff runlevel shutdown; do
    mv {,systemd.}"$manpage.8"
  done
}



