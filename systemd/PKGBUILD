#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# maintainer (x86_64:) Mateusz Krawczuk <willingmagic[at]gmail[dot]com>

pkgname=systemd
pkgver=34
pkgrel=2
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus' 'initscripts' 'sysvinit' 'intltool' 'rsyslog' 'util-linux>=2.19.1' 'gperf')
makedepends=('docbook-xsl' 'libxslt' 'cryptsetup' )
optdepends=('cryptsetup: required for encrypted block devices'
            'dbus-python: systemd-analyze'
            'initscripts-systemd: native boot and initialization scripts'
            'libnotify: systemadm'
            'python2-cairo: systemd-analyze'
            'systemd-units: collection of native unit files for Chakra daemon/init scripts')
groups=('systemd')
options=('!libtool')
backup=(etc/systemd/system.conf
        etc/tmpfiles.d/systemd.conf
        etc/tmpfiles.d/x11.conf)
install=systemd.install
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.bz2"
        "os-release" "systemd-add-chakra.patch" )
md5sums=('35761c50d5fe8fa8d15a3c651ab8bace'
         '68cf3e12b0e98a51efde9811e5fc713c'
         'd7cbae2627bb60360a6b1c03dd0fd90a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
    # Don't unset locale in getty
    # https://bugzilla.redhat.com/show_bug.cgi?id=663900
      sed -i -e '/^Environ.*LANG/s/^/#/' \
             -e '/^ExecStart/s/agetty/& -8/' units/getty@.service.m4
  patch -Np1 -i ${srcdir}/systemd-add-chakra.patch
#  patch -uN ${srcdir}/$pkgname-$pkgver/src/load-fragment-gperf.gperf.m4 ${srcdir}/v33.diff
  ./configure --prefix=/usr \
              --with-rootdir= \
              --with-distro=chakra \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var \
              --disable-audit \
              --disable-tcpwrap \
              --disable-gtk
echo "test"
  make
}

package() {
  mkdir -p ${pkgdir}/run
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
#  install -dm755 "$pkgdir/etc/modules-load.d"
  printf "d /run/console 755 root root\n" > "$pkgdir/etc/tmpfiles.d/console.conf"

  # fix systemd-analyze for python2
  sed -i '1s/python$/python2/' "$pkgdir/usr/bin/systemd-analyze"

  # install target doesn't bring in plymouth units
  #  install -m644 units/plymouth-* "$pkgdir/lib/systemd/system"

  # rename man pages to avoid conflicts with sysvinit
  cd "$pkgdir/usr/share/man/man8"
  for manpage in telinit halt reboot poweroff runlevel shutdown; do
    mv {,systemd.}"$manpage.8"
  done
}



