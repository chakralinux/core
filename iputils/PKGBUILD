#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=iputils
pkgver=20100214
_traceroutever=1.4a12
pkgrel=5
pkgdesc="IP Configuration Utilities (and Ping)."
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.linuxfoundation.org/en/Net:Iputils"
groups=('base')
depends=('glibc')
makedepends=('libxslt')
conflicts=('netkit-base' 'arping' 'netkit-tftpd' 'traceroute')
replaces=('netkit-base' 'traceroute')
source=("http://www.skbuff.net/iputils/${pkgname}-s${pkgver}.tar.bz2"
        "ftp://ftp.ee.lbl.gov/traceroute-${_traceroutever}.tar.gz"
	      'permission-fix.patch')
options=(!makeflags)
md5sums=('37596c8b3eed537294e77464b9804726'
         '964d599ef696efccdeebe7721cd4828d'
         '66109966cad06e425bb12a78bab85b70')

build()
{
  #build iptuils
  cd "${srcdir}/${pkgname}-s${pkgver}"
  make KERNEL_INCLUDE=/usr/include || return 1   # for i686

  # build traceroute
  # this is combined in this package because it's awkward as a separate package
  cd "${srcdir}/traceroute-${_traceroutever}"
  patch -Np1 -i "${srcdir}/permission-fix.patch" || return 1
  ./configure --prefix=/usr --sbindir=/bin --mandir=/usr/share/man
  make CFLAGS="${CFLAGS} -Ilinux-include/" || return 1
}

package() {
  cd "${srcdir}/${pkgname}-s${pkgver}"
  for i in arping clockdiff rarpd rdisc tftpd tracepath tracepath6; do
    install -D -m755 "${i}" "${pkgdir}/usr/sbin/${i}"
  done
  for i in ping ping6 traceroute6; do
    install -D -m4755 "${i}" "${pkgdir}/bin/${i}"
  done

  cd "${srcdir}/traceroute-${_traceroutever}"
  install -d -m755 "${pkgdir}/usr/share/man/man8"
  make DESTDIR="${pkgdir}" install install-man
}

# vim:set ts=2 sw=2 et:
