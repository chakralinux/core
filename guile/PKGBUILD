pkgname=guile
pkgver=2.0.13
pkgrel=3
pkgdesc="Guile is a portable, embeddable Scheme implementation written in C"
url="http://www.gnu.org/software/guile/"
arch=('x86_64')
license=('GPL')
depends=('gmp' 'libltdl' 'ncurses' 'texinfo' 'libunistring' 'gc' 'libffi')
install=guile.install
source=(https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('3CE464558A84FDC69DB40CFB090B11993D9AEBB5') # Ludovic Court√®s
md5sums=('dac267e76330176d48583258aeaa7016'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static \
    --disable-silent-rules \
    --disable-error-on-warning \
    --with-threads
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  install -D -dm755 "$pkgdir"/usr/share/gdb/auto-load/usr/lib
  mv "$pkgdir"/usr/lib/libguile-2.0.so.*-gdb.scm "$pkgdir"/usr/share/gdb/auto-load/usr/lib
}
