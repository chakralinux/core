#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer  <inkane@chakra-project.org>
# contributor <abveritas[at]chakra-project[dot]org>

pkgname=wpa_supplicant
pkgver=2.3
pkgrel=1
pkgdesc="A utility providing key negotiation for WPA wireless networks"
arch=('x86_64')
depends=('openssl' 'dbus-core' 'readline' 'libnl')
license=('GPL')
groups=('base')
backup=('etc/wpa_supplicant/wpa_supplicant.conf')
url="http://hostap.epitest.fi/wpa_supplicant"
source=("http://hostap.epitest.fi/releases/wpa_supplicant-$pkgver.tar.gz"
        'config')
md5sums=('f2ed8fef72cf63d8d446a2d0a6da630a'
         'd621945fe43f04f8fdea612ae6d1251e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}/"
  cp "${srcdir}/config" ./.config
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"

  # The Makefile does not pick up our CPPFLAGS
  export CFLAGS="$CPPFLAGS $CFLAGS"
  make LIBDIR=/usr/lib BINDIR=/usr/bin
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"
  make LIBDIR=/usr/lib BINDIR=/usr/bin DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/etc/wpa_supplicant"
  install -m644 wpa_supplicant.conf "${pkgdir}/etc/wpa_supplicant/wpa_supplicant.conf"
  
  install -d -m755 "${pkgdir}/usr/share/man/man"{5,8}
  install -m644 doc/docbook/*.5 "${pkgdir}/usr/share/man/man5/"
  install -m644 doc/docbook/*.8 "${pkgdir}/usr/share/man/man8/"
  rm -f "${pkgdir}/usr/share/man/man8/wpa_"{priv,gui}.8

  install -m755 -d "${pkgdir}/usr/share/dbus-1/system-services"
  install -m644 dbus/{fi.epitest.hostap.WPASupplicant.service,fi.w1.wpa_supplicant1.service} "${pkgdir}/usr/share/dbus-1/system-services/"

  install -m755 -d "${pkgdir}/etc/dbus-1/system.d"
  install -m644 dbus/dbus-wpa_supplicant.conf "${pkgdir}/etc/dbus-1/system.d/wpa_supplicant.conf"

  install -d -m755 "${pkgdir}/usr/lib/systemd/system"
  install -m644 systemd/*.service "${pkgdir}/usr/lib/systemd/system/"
}
