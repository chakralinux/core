# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=wicd
pkgver=1.7.2.4
pkgrel=8
pkgdesc="New and alternative wireless/wired network management utility"
arch=('x86_64')
url="http://wicd.sourceforge.net/"
license=('GPL2')
depends=('python2' 'dhcpcd' 'wpa_supplicant' 'wireless_tools' 'python-babel'
           'net-tools' 'ethtool' 'shared-mime-info' 'python2-urwid' 'python2-gobject' 
           'python2-dbus' 'gettext')
optdepends=('python-wpactrl:    needed if you want to use the new experimental ioctrl backend'
            'python-iwscan:     needed if you want to use the new experimental ioctrl backend'
            'wicd-client-kde:   needed if you want to use kde ui')
conflicts=('wicd-svn')
install=wicd.install
source=(https://launchpad.net/wicd/1.7/$pkgver/+download/$pkgname-$pkgver.tar.gz     
        wicd.desktop
        dbus_string_fix.patch
        http://bazaar.launchpad.net/~wicd-devel/wicd/experimental/diff/831
        http://bazaar.launchpad.net/~wicd-devel/wicd/experimental/diff/835)
categories=('network')
options=('emptydirs')
backup=('etc/wicd/encryption/templates/active')
md5sums=('c2435ddfdef0b9898852d72a85a45f0f'
         '326df163a5732d38741371baa4fce9e5'
         '744b3c12fe901ed435351e884dc8cb1d'
         '0d2355bc2eb7234a537f42f1773edfe0'
         '1630d7fe8f09911302d33b26286ff9b8')

prepare() {
  cd $pkgbase-$pkgver

  find . -type f -exec sed -i 's@#!/usr.*python@#!/usr/bin/python2@' {} \;
  export PYTHON=python2
  
  sed -i 's|/usr/sbin/|/usr/bin/|' other/wicd.service

  patch -p0 < "$srcdir/dbus_string_fix.patch"
  
  # Fix problem with new urwid FS#33378 LP#1075399
  patch -Np0 -i "$srcdir/831"
  patch -Np0 -i "$srcdir/835"
}

build() {
  cd $srcdir/$pkgname-$pkgver

  python2 setup.py configure --no-install-init \
                             --resume=/usr/share/wicd/scripts/ \
                             --suspend=/usr/share/wicd/scripts/ \
                             --verbose \
                             --python=/usr/bin/python2 \
                             --lib=/usr/lib \
                             --sbin=/usr/bin \
                             --systemd=/usr/lib/systemd/system

  mkdir -p translations/ast/LC_MESSAGES
  msgfmt po/ast.po -o translations/ast/LC_MESSAGES/wicd.mo
}

package() {
  cd $srcdir/$pkgname-$pkgver
  python2 setup.py install --optimize=1 --root=$pkgdir
  
  cd build/lib/wicd
  for i in *.py; do
    install -Dm 755 $i $pkgdir/usr/lib/wicd/$i
  done
  
  rm -rf $pkgdir/usr/share/autostart

  #deleting the GTK stuff
  rm -rf $pkgdir/etc/xdg
  rm -f $pkgdir/usr/bin/{wicd-client,wicd-gtk}
  rm -rf $pkgdir/usr/share/{applications,icons,pixmaps}
  rm -rf $pkgdir/usr/share/wicd/gtk  
}
