pkgname=wicd
pkgver=1.7.4
pkgrel=1
pkgdesc="New and alternative wireless/wired network management utility"
arch=('x86_64')
url="http://wicd.sourceforge.net/"
license=('GPL2')
depends=('python2' 'python2-dbus' 'dhcpcd' 'wpa_supplicant' 'wireless_tools' 'pm-utils'
         'inetutils' 'net-tools' 'ethtool' 'shared-mime-info' 'python2-urwid' 'python2-gobject2' 'dbus-glib' 'rfkill')
makedepends=('python2-babel' 'python2-setuptools' 'gettext')
optdepends=('wicd-client-kde: needed if you want to use kde ui')
conflicts=('wicd-svn')
install=wicd.install
source=(https://launchpad.net/wicd/1.7/$pkgver/+download/$pkgname-$pkgver.tar.gz     
        wicd.desktop
        wicd-1.7.3-urwid-1.3.0.patch)
categories=('network')
options=('emptydirs')
backup=('etc/wicd/encryption/templates/active')
md5sums=('aaa2e9f5320849e0b5d053099a6f0007'
         '326df163a5732d38741371baa4fce9e5'
         'f7e6085e482f37f2b8529326c21cbbbf')

prepare() {
  cd $pkgbase-$pkgver

  find . -type f -exec sed -i 's@#!/usr.*python@#!/usr/bin/python2@' {} \;
  export PYTHON=python2
  
  sed -i 's|/usr/sbin/|/usr/bin/|' other/wicd.service
  sed -i 's|pybabel|pybabel2|' setup.py

  patch -Np0 -i "$srcdir/wicd-1.7.3-urwid-1.3.0.patch"
}

build() {
  cd $srcdir/$pkgname-$pkgver

  python2 setup.py configure --no-install-init \
			     --no-install-gtk \
			     --no-install-gnome-shell-extensions \
			     --distro=arch \
                             --resume=/usr/share/wicd/scripts/ \
                             --suspend=/usr/share/wicd/scripts/ \
                             --verbose \
                             --python=/usr/bin/python2 \
                             --lib=/usr/lib \
                             --sbin=/usr/bin \
                             --systemd=/usr/lib/systemd/system
  
  python2 setup.py build
  python2 setup.py compile_translations
  
  #HACK for https://bugs.launchpad.net/wicd/+bug/928589
  mkdir -p translations/ast/LC_MESSAGES
  msgfmt po/ast.po -o translations/ast/LC_MESSAGES/wicd.mo
}

package() {
  cd $srcdir/$pkgname-$pkgver
  python2 setup.py install --skip-build --optimize=1 --root=$pkgdir
  
  cd build/lib/wicd
  for i in *.py; do
    install -Dm 755 $i $pkgdir/usr/lib/wicd/$i
  done
  
  rm -rf $pkgdir/usr/share/autostart  
}
