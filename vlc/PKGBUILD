#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=vlc
pkgname=('vlc'
         'vlc-plugin')
pkgver=1.1.4
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
makedepends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'lirc-utils'
             'libdca' 'qt' 'libproxy' 'sdl_image' 'libdvdnav' 'mesa'
             'lua' 'libv4l' 'libcddb' 'smbclient' 'libmatroska' 'zvbi'
             'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2'
             'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug'
             'avahi' 'ttf-freefont' 'libxv' 'libass' 'xdg-utils' 'libxpm'
             'xulrunner' 'pkgconfig' 'live-media' 'libnotify' 'libcdio'
             'flac' 'libtheora' 'alsa-lib' 'jack' 'udev'
             'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'chrpath')

source=("http://download.videolan.org/pub/videolan/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('b71aacaa242f8050a64c6ffe09d6f067')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  sed -i -e 's:truetype/freefont:TTF:g' modules/misc/freetype.c
  ./configure --prefix=/usr \
              --disable-rpath \
              --enable-faad \
              --enable-v4l \
              --enable-snapshot \
              --enable-dbus-control \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --enable-mozilla \
              --with-live555-tree=/usr/lib/live \
              --enable-realrtsp \
              --disable-pulse
  CFLAGS="-fPIC"
  make
}

package_vlc() {
  pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player"
  depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libcdio'
           'libdca' 'qt' 'libproxy' 'sdl_image' 'libdvdnav' 'mesa'
           'lua' 'libv4l' 'libcddb' 'smbclient' 'libmatroska' 'zvbi'
           'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2'
           'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug'
           'avahi' 'ttf-freefont' 'libxv' 'libass' 'xdg-utils')
  optdepends=('libnotify: for notification plugin'
              'ncurses: for ncurses interface support'
              'libdvdcss: for decoding encrypted DVDs'
              'lirc-utils: for lirc plugin'
              'libavc1394: for devices using the 1394ta AV/C'
              'libdc1394: for IEEE 1394 plugin'
              'vdpau-video: VDPAU backend for VA API (for GPU acceleration on Nvidia cards)')
  options=('!libtool')
  install=vlc.install
  cd "${srcdir}/${pkgbase}-${pkgver}"

  make DESTDIR=${pkgdir}/ install

  for res in 16 32 48 128; do
    install -D -m644 ${srcdir}/vlc-${pkgver}/share/icons/${res}x${res}/vlc.png \
        ${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png
  done

  rm -rf ${pkgdir}/usr/lib/mozilla
}

package_vlc-plugin() {
  pkgdesc="VLC mozilla browser plugin"
  depends=("${pkgbase}=${pkgver}" 'nspr' 'xulrunner' 'libxpm')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -Dm755 projects/mozilla/.libs/libvlcplugin.so ${pkgdir}/usr/lib/mozilla/plugins/libvlcplugin.so
  chrpath -d ${pkgdir}/usr/lib/mozilla/plugins/libvlcplugin.so
}
