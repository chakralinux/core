#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname='vlc'
pkgver=1.1.13
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libxpm' 'libcdio'
         'libdca' 'qt' 'libproxy' 'sdl_image' 'libdvdnav' 'mesa'
         'lua' 'v4l-utils' 'libcddb' 'smbclient' 'libmatroska' 'zvbi'
         'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp'
         'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug' 
         'ttf-freefont' 'libxv' 'libass' 'xdg-utils' 'desktop-file-utils')
makedepends=('avahi' 'pkgconfig' 'live-media' 'libnotify'
             'flac' 'libtheora' 'alsa-lib' 'jack' 'kdelibs' 'udev'
             'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'libpulse'
             'lirc-utils' 'gnutls' 'libcaca')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin'
            'ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'libpulse: PulseAudio support'
            'vdpau-video: vdpau back-end for nvidia'
            'libva-driver-intel: back-end for intel cards')
conflicts=('vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/http/.hosts'
        'usr/share/vlc/http/dialogs/.hosts'
        'usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool')
install="vlc.install"
source=("http://download.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('9678fb7a04808b6e0de63746d35e4bb1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e 's:truetype/freefont:TTF:g' modules/misc/freetype.c

  ./configure --prefix=/usr \
              --disable-rpath \
              --enable-faad \
              --enable-v4l \
              --enable-snapshot \
              --enable-dbus-control \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --with-live555-tree=/usr/lib/live \
              --enable-upnp \
              --enable-realrtsp
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for res in 16 32 48 128; do
    install -D -m644 "${srcdir}/vlc-${pkgver}/share/icons/${res}x${res}/vlc.png" \
        "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done
}

# vim:set ts=2 sw=2 et:
