pkgname=sdl2_image
pkgver=2.0.1
pkgrel=1
pkgdesc="SDL2 image libraries"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('sdl2' 'libpng' 'libtiff' 'libjpeg' 'libwebp')
makedepends=('cmake')
source=("http://www.libsdl.org/projects/SDL_image/release/SDL2_image-${pkgver}.tar.gz"
        "FindSDL2_image.cmake")
sha256sums=('3a3eafbceea5125c04be585373bfd8b3a18f259bd7eae3efc4e6d8e60e0d7f64'
            '177afccde2a7e6c112f8d2fa75d7ad72e0e4827f307f1e4dac67f7756c0b8b19')

build() {
  cd "${srcdir}/SDL2_image-${pkgver}/"
  ./configure --disable-static --prefix=/usr
  make
}

package() {
  cd "${srcdir}/SDL2_image-${pkgver}/"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  # we can remove the following when provided directly by cmake
  install -Dm644 $srcdir/FindSDL2_image.cmake ${pkgdir}/usr/share/cmake-3.0/Modules/FindSDL2_image.cmake
}

