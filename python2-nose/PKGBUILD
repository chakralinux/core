#
# Platform packages for Chakra, part of chakra-project.org
#
# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Adrian Chaves Fernandez (Gallaecio) <adriyetichaves@gmail.com>
#               Douglas Soares de Andrade <douglas@archlinux.org>
#               Cilyan Olowen <gaknar@gmail.com>

pkgbase=python-nose
pkgname=('python3-nose' 'python2-nose' 'python-nose-doc')
pkgver=1.3.0
pkgrel=1
pkgdesc="A discovery-based unittest extension"
arch=('x86_64')
url="http://nose.readthedocs.org/"
license=('LGPL2')
makedepends=('python' 'python3-distribute' 'python2' 'python2-distribute' 'python2-sphinx')
source=("http://pypi.python.org/packages/source/n/nose/nose-${pkgver}.tar.gz")
sha512sums=('a3b8060e415560a868599224449a3af636d24a060f1381990b175dcd12f30249edd181179d23aea06b0c755ff3dc821b7a15ed8840f7855530479587d4d814f4')

build() {
    cd "$srcdir/nose-$pkgver"
    sed -i -e "s:man/man1:share/man/man1:g" setup.py
    cp -a "$srcdir/nose-$pkgver" "$srcdir/nose-python2-$pkgver"
}

package_python3-nose() {
    depends=('python3' 'python3-distribute')

    cd "$srcdir/nose-$pkgver"
    python3 setup.py install --prefix=/usr --root="${pkgdir}"
    mv "$pkgdir/usr/bin/nosetests" "$pkgdir/usr/bin/nosetests3"
}

package_python2-nose() {
    depends=('python2' 'python2-distribute')

    cd "$srcdir/nose-python2-$pkgver"
    python2 setup.py install --prefix=/usr --root="${pkgdir}"
    mv "$pkgdir/usr/bin/nosetests" "$pkgdir/usr/bin/nosetests2"
    rm -rf "$pkgdir/usr/share"
}

package_python-nose-doc(){
    pkgdesc="Nose documentation and examples"

    cd "$srcdir/nose-$pkgver/doc"
    make SPHINXBUILD=sphinx-build2 html
    mkdir -p "$pkgdir/usr/share/doc/python-nose"
    cp -r .build/html "$pkgdir/usr/share/doc/python-nose"
    cp -r ../examples "$pkgdir/usr/share/doc/python-nose"
}
