# maintainer almack@chakraos.org
# contributor Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer
pkgver=1.2.4
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz)
md5sums=('74d8f674187a5f0ddf38da594c3998c3')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib \
      --with-package-name="GStreamer (Chakra GNU/Linux)" \
      --with-package-origin="http://www.chakra-project.org/" \
      --disable-static
  make
}

check() {
  cd "${srcdir}/gstreamer-${pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}
