# maintainer almack@chakraos.org
# contributor Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer
pkgver=1.12.3
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz{,.asc})
sha256sums=('d388f492440897f02b01eebb033ca2d41078a3d85c0eddc030cdea5a337a216e'
            'SKIP')
validpgpkeys=('7F4BC7CC3CA06F97336BBFEB0668CC1486C2D7B5')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib \
      --with-package-name="GStreamer (Chakra GNU/Linux)" \
      --with-package-origin="https://chakralinux.org/" \
      --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd "${srcdir}/gstreamer-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
