# Platform packages for Chakra

pkgname=razor
pkgver=2.84
pkgrel=3
pkgdesc="A distributed, collaborative, spam detection and filtering network"
arch=('i686' 'x86_64')
url="http://razor.sourceforge.net"
license=('PerlArtistic')
depends=('perl-net-dns' 'perl-digest-sha1' 'perl-uri' 'perl-digest-nilsimsa' 'perl>=5.14.2')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-agents-${pkgver}.tar.bz2")
md5sums=('8b9a11a6ce020383c32c45d1530d77c2')
options=('!emptydirs')

build() {
  cd "${srcdir}"/${pkgname}-agents-${pkgver}

  # skip install_razor_agents (we'll do the linking later)
  # /bin/sed -i "s|install :: all pure_install doc_install install_razor_agents|install :: all pure_install doc_install|g" Makefile

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}"/${pkgname}-agents-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${pkgname}-agents-${pkgver}
  make DESTDIR=${pkgdir} install

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete

  # cd ${pkgdir}/usr/bin
  # for i in razor-check razor-report razor-revoke razor-admin; do 
  #  /bin/ln -sf razor-client $i; 
  # done
}
