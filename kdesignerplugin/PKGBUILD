# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kdesignerplugin
pkgver=5.50.0
pkgrel=1
pkgdesc='Integration of Frameworks widgets in Qt Designer/Creator'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kdesignerplugin'
license=('LGPL')
depends=('kplotting' 'kdewebkit')
makedepends=("extra-cmake-modules>=5.50.0" 'qt5-tools' 'kdoctools')
checkdepends=('cmake' "xorg-server-xvfb")
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('a587516dec5b850745f5d711e4fd97f1176d19d75ea4f4b5fc67a9a3b382b001'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   xvfb-run make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
