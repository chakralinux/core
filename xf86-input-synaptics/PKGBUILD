#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=xf86-input-synaptics
pkgver=1.7.1
pkgrel=2
pkgdesc="Synaptics driver for notebook touchpads"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst' 'mtdev')
makedepends=('xorg-server>=1.12.3' 'libxi' 'libx11' 'xf86driproto' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.12.3')
replaces=('synaptics')
provides=('synaptics')
conflicts=('synaptics')
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=("http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"
        '10-synaptics.conf'
        'multitouch.patch')
sha1sums=('85018b51dfa0c2dcf5c257cf283298836273700d'
          '68e1f4ef5e1038231d210eb422fa4d18c5922f0f'
          '18d2508d5fcfd14951bcd316094187af367c720b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # bug http://chakra-project.org/bugs/index.php?do=details&task_id=488
  patch -p1 -i "${srcdir}"/multitouch.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}
