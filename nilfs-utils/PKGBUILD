pkgname=nilfs-utils
pkgver=2.2.6
pkgrel=3
pkgdesc="A log-structured file system supporting continuous snapshotting (userspace tools)"
arch=('x86_64')
url="http://nilfs.sourceforge.net/"
license=('GPL2' 'LGPL2.1')
depends=('util-linux')
source=(https://github.com/nilfs-dev/${pkgname}/archive/v${pkgver}.tar.gz)
md5sums=('614bd602caa83546acf0383160b94add')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh

  # TODO: there is a change to configure to apply here.
  # root_sbindir needs to change from /sbin to /usr/bin
  # or there are file conflicts later.
  ./configure --sbindir=/usr/bin --with-libmount
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" sbindir=/usr/bin install LDCONFIG=/bin/true
}
