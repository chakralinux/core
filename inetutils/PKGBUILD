#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=inetutils
pkgver=1.9.1
pkgrel=1
pkgdesc="A collection of common network programs."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/inetutils/"
license=('GPL')
depends=('readline' 'ncurses' 'pam')
backup=('etc/conf.d/ftpd' 'etc/xinetd.d/telnet' 'etc/xinetd.d/talk')
options=('!emptydirs')
install="inetutils.install"
source=("http://ftp.gnu.org/gnu/inetutils/${pkgname}-${pkgver}.tar.gz"
        ftpd.rc ftpd.conf telnet.xinetd talk.xinetd rlogin.xinetd rsh.xinetd)
md5sums=('944f7196a2b3dba2d400e9088576000c' 
         'c167200f6faeb64659e69ced2a9d672a'
         'e6e6e5990a2e8159a276fef8fbf54c04' 
         '8596208d119045543c5fb644e1d713d5'
         '2c3655985cf122591d71d031af84ff90' 
         'c25d8382a994de84d9c6962d804f689b'
         'c3aa508a9d1293a787341108cd49c1b7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --libexec=/usr/sbin --localstatedir=/var \
    --mandir=/usr/share/man --infodir=/usr/share/info \
    --without-wrap --with-pam \
    --enable-ftp --enable-ftpd \
    --enable-telnet --enable-telnetd \
    --enable-talk --enable-talkd \
    --enable-rlogin --enable-rlogind \
    --enable-rsh --enable-rshd \
    --disable-rexec --disable-rexecd \
    --enable-rcp \
    --disable-tftp --disable-tftpd \
    --disable-ping --disable-ping6 \
    --disable-logger --disable-syslogd \
    --disable-inetd --disable-whois \
    --disable-uucpd --disable-hostname \
    --disable-ifconfig --disable-traceroute

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m755 "${srcdir}/ftpd.rc" "${pkgdir}/etc/rc.d/ftpd"
  install -D -m644 "${srcdir}/ftpd.conf" "${pkgdir}/etc/conf.d/ftpd"
  install -D -m644 "${srcdir}/telnet.xinetd" "${pkgdir}/etc/xinetd.d/telnet"
  install -D -m644 "${srcdir}/talk.xinetd" "${pkgdir}/etc/xinetd.d/talk"
  install -D -m644 "${srcdir}/rlogin.xinetd" "${pkgdir}/etc/xinetd.d/rlogin"
  install -D -m644 "${srcdir}/rsh.xinetd" "${pkgdir}/etc/xinetd.d/rsh"
}

# vim:set ts=2 sw=2 et:
