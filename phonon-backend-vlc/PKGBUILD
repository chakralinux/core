#
# Platform packages of Chakra, part of chakra-project.org
#
# Contributor: abveritas@chakra-project.org
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=('phonon-backend-vlc')
pkgver=0.7.0
pkgrel=2
pkgdesc='Phonon VLC backend'
arch=('x86_64')
url='http://phonon.kde.org'
license=('LGPL')
depends=('vlc' "phonon")
makedepends=('qt' 'pkgconfig' 'cmake' 'automoc4' 'mesa')
groups=("kde-complete" "kde-uninstall")
replaces=('phonon-vlc-svn' 'phonon-vlc')
conflicts=('phonon-vlc-svn' 'phonon-vlc')
source=("http://download.kde.org/stable/phonon/${pkgname}/$pkgver/src/${pkgname}-$pkgver.tar.xz")
md5sums=('1a172744eb0fd9cb0dbe0ea8b69e50f1')

build() {
	cd "${srcdir}"
	mkdir -p build
	cd build
	cmake "../${pkgname}-$pkgver" \
		 -DCMAKE_BUILD_TYPE=Release \
		 -DCMAKE_INSTALL_PREFIX=/usr \
		 -DCMAKE_INSTALL_LIBDIR=/usr/lib
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install
} 
