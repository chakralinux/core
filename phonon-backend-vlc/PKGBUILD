# Contribution from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/phonon-vlc

pkgbase=phonon-backend-vlc
pkgname=('phonon-backend-vlc' 'phonon-qt5-backend-vlc')
pkgver=0.9.0
pkgrel=1
arch=('x86_64')
url='http://phonon.kde.org'
license=('LGPL')
makedepends=('cmake' 'extra-cmake-modules' 'vlc' 'phonon' 'phonon-qt5')
groups=("kde-complete" "kde-uninstall")
source=("http://download.kde.org/stable/phonon/phonon-backend-vlc/${pkgver}/phonon-backend-vlc-${pkgver}.tar.xz")
sha256sums=('c0ced7ca571acc22211eecf5158241714fa9ccdb82d4fe0a970ad702860ccdbe')

prepare() {
  mkdir build-qt4
  mkdir build-qt5
}

build() {
  cd build-qt4
  cmake ../phonon-vlc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DPLUGIN_INSTALL_DIR=/usr/lib/kde4
  make

  cd ../build-qt5
  cmake ../phonon-vlc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DPHONON_BUILD_PHONON4QT5=ON
  make
}

package_phonon-backend-vlc() {
  pkgdesc="Phonon VLC backend for Qt4"
  depends=('vlc')
  provides=('phonon-backend')

  cd build-qt4
  make DESTDIR="${pkgdir}" install
}

package_phonon-qt5-backend-vlc() {
  pkgdesc="Phonon VLC backend for Qt5"
  depends=('vlc')
  provides=('phonon-qt5-backend')

  cd build-qt5
  make DESTDIR="${pkgdir}" install
}
