# Contributor : Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kconfigwidgets
pkgver=${KFVersion}
pkgrel=1
pkgdesc='Widgets for KConfig'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kconfigwidgets'
license=('LGPL')
depends=('kauth' 'kcodecs' 'kconfig' 'kguiaddons' 'ki18n' 'kwidgetsaddons')
makedepends=("extra-cmake-modules>=${KFECMVersion}" "kdoctools" "python3")
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgname}-${pkgver}.tar.xz")
sha256sums=( $(getSum ${pkgname}) )

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

check() {
  cd build
  xvfb-run -s "-screen 0 640x480x24" make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
