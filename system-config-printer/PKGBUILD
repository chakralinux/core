#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=system-config-printer
pkgver=1.3.4
pkgrel=1
pkgdesc="A CUPS printer configuration tool and status applet"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('any')
license=('GPL')
depends=('pycups' 'dbus-python' 'pysmbc')
makedepends=('intltool' 'python2' 'xmlto' 'docbook-xsl' 'desktop-file-utils' 'libcups' 'libxml2')
optdepends=('kdeadmin-system-config-printer-kde: for the administration tool in KDE System Settings')
replaces=('system-config-printer')
provides=("system-config-printer=$pkgver" 'system-config-printer-common')
conflicts=('system-config-printer')
source=("http://cyberelk.net/tim/data/${pkgname}/1.3/${pkgname}-${pkgver}.tar.xz")
md5sums=('8e4b1b1fd078c09769708f09b77cb3ed')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  sed -i "s|#!/usr/bin/python|#!/usr/bin/python2|" udev/udev-add-printer 

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udev-rules

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -d ${pkgdir}/usr/share/${pkgname}
  install -m644 config.py \
    debug.py \
    installpackage.py \
    monitor.py \
    PhysicalDevice.py \
    ppdippstr.py \
    probe_printer.py \
    SearchCriterion.py \
    smburi.py \
    statereason.py \
    ${pkgdir}/usr/share/${pkgname}/

  install -m755 pysmb.py \
    ${pkgdir}/usr/share/${pkgname}/

  install -d ${pkgdir}/etc/dbus-1/system.d/
  install -m644 dbus/com.redhat.NewPrinterNotification.conf \
    dbus/com.redhat.PrinterDriversInstaller.conf \
    ${pkgdir}/etc/dbus-1/system.d/
  install -d ${pkgdir}/etc/cupshelpers/
  install -m644 xml/preferreddrivers.xml ${pkgdir}/etc/cupshelpers/

  install -d ${pkgdir}/lib/udev/rules.d
  install -m755 udev/{udev-add-printer,udev-configure-printer} \
    ${pkgdir}/lib/udev/
  install -m644 udev/70-printers.rules \
    ${pkgdir}/lib/udev/rules.d

  for file in build/lib/cupshelpers/*.py; do
    install -Dm644 $file \
      ${pkgdir}/usr/lib/python2.7/site-packages/cupshelpers/$(basename $file)
  done
}
