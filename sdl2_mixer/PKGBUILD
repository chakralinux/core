pkgname=sdl2_mixer
pkgver=2.0.1
pkgrel=1
pkgdesc="A simple multi-channel audio mixer (Version 2)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('sdl2' 'libvorbis' 'libmodplug' 'smpeg2' 'flac')
makedepends=('fluidsynth')
optdepends=('fluidsynth: MIDI software synth, replaces built-in timidity')
source=("http://www.libsdl.org/projects/SDL_mixer/release/SDL2_mixer-${pkgver}.tar.gz"
        "FindSDL2_mixer.cmake")
sha256sums=('5a24f62a610249d744cbd8d28ee399d8905db7222bf3bdbc8a8b4a76e597695f'
            '3910aa85e55ac64c8eb6da69fdd7ed3fab704119339e991f956f1cea19c11973')

prepare() {
  cd "${srcdir}/SDL2_mixer-${pkgver}/"

  sed -i "s|/etc/timidity|/etc/timidity++|g" timidity/config.h
  sed -i "s|/etc/timidity++.cfg|/etc/timidity++/timidity.cfg|g" timidity/config.h
}            
            
build() {
  cd "${srcdir}/SDL2_mixer-${pkgver}/"
  export CFLAGS+=" ${CFLAGS} -I/usr/include/libmodplug"
  ./configure --disable-static --prefix=/usr
  make
}

package() {
  cd "${srcdir}/SDL2_mixer-${pkgver}/"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  # we can remove the following when provided directly by cmake
  install -Dm644 $srcdir/FindSDL2_mixer.cmake ${pkgdir}/usr/share/cmake-3.0/Modules/FindSDL2_mixer.cmake
}

