# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=mtools
pkgver=4.0.17
pkgrel=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/mtools/"
license=('GPL')
depends=('sh')
makedepends=('texinfo' 'libx11' 'libsm')
optdepends=('libx11: required by floppyd'
	    'libsm:  required by floppyd')
backup=('etc/mtools.conf')
install=mtools.install
source=(http://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2)
md5sums=('15571c615d8f75f5f6d294272f80c7fa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '/^SAMPLE FILE$/s:^:# :' mtools.conf
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
	      --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make -j 1 DESTDIR="${pkgdir}" install
  install -Dm644 mtools.conf "${pkgdir}/etc/mtools.conf"
}
