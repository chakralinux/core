# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kdesu
pkgver=5.50.0
pkgrel=1
pkgdesc='Integration with su for elevated privileges'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kdesu'
license=('LGPL')
depends=('kservice' 'kpty')
makedepends=("extra-cmake-modules>=5.50.0" 'python3')
checkdepends=('cmake')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('6e3c32fd47a65a6592dba36767999ecb50b83edc8c3d5f33f413310a616bbb24'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

# check() {
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
