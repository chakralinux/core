# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kdesu
pkgver=5.53.0
pkgrel=1
pkgdesc='Integration with su for elevated privileges'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kdesu'
license=('LGPL')
depends=('kservice' 'kpty')
makedepends=("extra-cmake-modules>=5.53.0" 'python3')
checkdepends=('cmake')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('be0bc2d77e38cc9c13e24daa66543c06a735343207542ab3f294be927c0a6da2'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

# check() {
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
