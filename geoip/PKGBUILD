#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Martín González <kote[at]chakra-project[dot]org>

pkgname=geoip
pkgver=1.4.8
pkgrel=2
pkgdesc="Non-DNS IP-to-country resolver C library & utils"
arch=(i686 x86_64)
license=('GPL')
url="http://www.maxmind.com/app/c"
depends=('zlib' 'geoip-database')
backup=(etc/geoip/GeoIP.conf)
options=('!libtool' '!emptydirs')
source=(http://www.maxmind.com/download/geoip/api/c/GeoIP-${pkgver}.tar.gz)
sha256sums=('cf0f6b2bac1153e34d6ef55ee3851479b347d2b5c191fda8ff6a51fab5291ff4')

build() {
  cd "${srcdir}/GeoIP-${pkgver}"

  autoreconf -vi
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc/geoip

  make
}

check() {
  cd "${srcdir}/GeoIP-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/GeoIP-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}/usr/share/GeoIP/GeoIP.dat"
}

