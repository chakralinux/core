# Maintainer: Samir Benmendil <ram-z@chakraos.org>
# Contributor: ahveritas@chakra-project.org
# Contributor: https://www.archlinux.org/packages/extra/x86_64/smartmontools/

pkgname=smartmontools
pkgver=6.4
pkgrel=1
pkgdesc="Control and monitor S.M.A.R.T. enabled ATA and SCSI Hard Drives"
url="http://smartmontools.sourceforge.net"
license=('GPL')
arch=('x86_64')
depends=('gcc-libs' 'libcap-ng' 'bash')
backup=('etc/smartd.conf'
        'etc/conf.d/smartd')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'smartd.conf')
md5sums=('56812c8312fd123ed40ef65afde1049e'
         '28c1b80c41dac52d433961dcfb4f70e0')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --enable-drivedb \
              --with-libcap-ng=yes \
              --with-systemdsystemunitdir=/usr/lib/systemd/system \
              --with-smartdscriptdir=/usr/share/smartmontools \
              --with-smartdplugindir=/usr/share/smartmontools/smartd_warning.d

  make
}

package() {
  cd ${pkgname}-${pkgver}

  sed -i -e "s:sysconfig/smartmontools:conf.d/smartd:g" smartd.service
  sed -i -e "s:smartd_opts:SMARTD_ARGS:g" smartd.service

  make DESTDIR="${pkgdir}" install

  rm -rf ${pkgdir}/etc/rc.d
  install -Dm644 ${srcdir}/smartd.conf "${pkgdir}/etc/conf.d/smartd"
}
