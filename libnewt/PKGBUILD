pkgname=libnewt
pkgver=0.52.19
pkgrel=1
_tclver=8.6
pkgdesc="Not Erik's Windowing Toolkit - text mode windowing with slang"
arch=('x86_64')
url='https://fedorahosted.org/newt/'
license=('GPL')
depends=('slang' 'popt' 'gpm')
makedepends=("tcl>=$_tclver" 'python3' 'python2')
optdepends=('tcl: whiptcl support'
            'python3: libnewt support with the _snack module'
            'python2: libnewt support with the _snack module')
options=('!makeflags')
source=("https://fedorahosted.org/releases/n/e/newt/newt-$pkgver.tar.gz")
sha256sums=('08c0db56c21996af6a7cbab99491b774c6c09cef91cd9b03903c84634bff2e80')

prepare() {
  cd "newt-$pkgver"
  echo '#define USE_INTERP_RESULT 1' >> config.h
}

build() {
  cd "newt-$pkgver"

  ./configure --prefix=/usr --with-gpm-support
  make
}

package() {
  make -C "newt-$pkgver" prefix="$pkgdir/usr" install
}
