#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=libxcb
pkgver=1.8
pkgrel=1
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.6' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python')
conflicts=('libx11<1.1.99.2')
options=('!libtool')
license=('custom')
source=(${url}/dist/${pkgname}-${pkgver}.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch)
sha1sums=('18b76759d5bbb863777f37bf3aec23ebaa31d5be'
          '3455e84642283bc91c8313af319002a20bbcbdf4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"
  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing
  PYTHON=python2 ./configure --prefix=/usr --enable-xinput
  make
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
