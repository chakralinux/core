pkgname=libssh2
pkgver=1.8.0
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="http://www.libssh2.org/"
arch=('x86_64')
license=('BSD')
depends=('openssl')
makedepends=('zlib')
provides=('libssh2.so')
source=("https://www.libssh2.org/download/$pkgname-$pkgver.tar.gz"{,.asc})
md5sums=('3d1147cae66e2959ea5441b183de1b1c'
         'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2')   # Daniel Stenberg

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
