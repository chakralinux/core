pkgname=libao
pkgver=1.2.2
pkgrel=1
pkgdesc="A cross-platform audio output library and plugins"
url="http://www.xiph.org/ao"
arch=('x86_64')
license=('GPL')
depends=('glibc' 'alsa-lib')
makedepends=('libpulse' 'git')
backup=('etc/libao.conf')
source=("git+https://git.xiph.org/libao.git#commit=3f4b02f046550aca54d7f11a5d9c56ac421f0b66"
            'libao.conf')
md5sums=('SKIP'
         '5c29f3197ccaac2264c89450b4a298d5')

build() {
  cd "${srcdir}/${pkgname}"

  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-alsa-mmap \
              --enable-pulse
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install 

  # Add conf file
  install -Dm0644 "${srcdir}/libao.conf" "${pkgdir}/etc/libao.conf"
}
