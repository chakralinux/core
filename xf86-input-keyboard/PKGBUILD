# Part of X.org group
# maintainer AlmAck almack@chakraos.org

pkgname=xf86-input-keyboard
pkgver=1.8.0
pkgrel=4
pkgdesc="X.Org keyboard input driver"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-server' 'X-ABI-XINPUT_VERSION=20' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.15.0' 'X-ABI-XINPUT_VERSION<20' 'X-ABI-XINPUT_VERSION>=21')
groups=('xorg-input-drivers')
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('7728d733ef16d664a201868ecae5db3705cf8ef1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
