# Part of X.org group
# maintainer AlmAck almack@chakraos.org

source ../xorg.conf

pkgname=xf86-input-keyboard
pkgver=1.9.0
pkgrel=1
pkgdesc="X.Org keyboard input driver"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-server-devel' "X-ABI-XINPUT_VERSION=${X_ABI_XINPUT_VERSION}" 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.16' "X-ABI-XINPUT_VERSION<${X_ABI_XINPUT_MAJOR}" "X-ABI-XINPUT_VERSION>=$((X_ABI_XINPUT_MAJOR+1))")
groups=('xorg-input-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('f7c900f21752683402992b288d5a2826de7a6c0c0abac2aadd7e8a409e170388')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
