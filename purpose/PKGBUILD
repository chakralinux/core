# Contribution from Arch Linux:
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=purpose
pkgver=1.2.1
pkgrel=1
pkgdesc="Framework for providing abstractions to get the developer's purposes fulfilled"
arch=('x86_64')
url='https://projects.kde.org/purpose'
license=('LGPL')
depends=('kaccounts-integration' 'accounts-qml-module' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'intltool' 'python3')
optdepends=('kdeconnect: sharing to smartphone via KDE Connect')
source=("http://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('09a76125fd24323755b6d655f1cb038f')

build() {
  mkdir -p build && cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
