pkgname=arch-install-scripts
pkgver=20
pkgrel=1
pkgdesc="Scripts to aid in installing Arch Linux (with modifications for Chakra)"
arch=('any')
url="https://projects.archlinux.org/arch-install-scripts.git"
license=('GPL')
depends=('awk' 'bash' 'coreutils' 'pacman' 'util-linux')
makedepends=('git')
source=("git+https://git.archlinux.org/arch-install-scripts.git#tag=v$pkgver")
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53')  # Dave Reisner
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe | sed s,^v,,
}

prepare() {

  # for some reasons the iso build script throws an error without -yy
  # --needed is a time saver
  sed -i 's/pacman -r "$newroot" -Sy/pacman -r "$newroot" -Syy --needed/g'   "$pkgname"/pacstrap.in
}

build() {
  make -C "$pkgname"
}

check() {
  make -C "$pkgname" check
}

package() {
  make -C "$pkgname" PREFIX=/usr DESTDIR="$pkgdir" install
}
