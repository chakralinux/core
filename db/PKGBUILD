# maintainer abveritas[at]chakra-project[dot]org>

pkgname=db
pkgver=5.3.28
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('x86_64')
url="http://www.oracle.com/technetwork/database/database-technologies/berkeleydb/downloads/index.html"
license=('custom')
depends=('gcc-libs' 'sh')
install=db.install
source=("http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz")
sha1sums=('fa3f8a41ad5101f43d08bc0efb6241c9b6fc1ae9')

build() {
  cd ${srcdir}/$pkgname-${pkgver}/build_unix
  ../dist/configure --prefix=/usr --enable-compat185 \
    --enable-shared --enable-static --enable-cxx --enable-dbm \
    --enable-stl
  make LIBSO_LIBS=-lpthread
}

package() {
  cd ${srcdir}/$pkgname-${pkgver}/build_unix
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/docs
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
