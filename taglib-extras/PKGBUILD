# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/taglib-extras

pkgname=taglib-extras
pkgver=1.0.1
pkgrel=4
pkgdesc="Additional taglib plugins for KDE"
arch=('x86_64')
url="http://developer.kde.org/~wheeler/taglib.html"
license=('LGPL')
depends=('taglib')
makedepends=('pkgconfig' 'cmake' 'automoc4')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.gz"
        'taglib-1.10.patch')
md5sums=('e973ca609b18e2c03c147ff9fd9e6eb8'
         'b24f1ac96f3eca2db0d75188ea3a659b')

prepare() {
  mkdir -p build

# Fix taglib 1.10 detection
  cd $pkgname-$pkgver
  patch -p1 -i ../taglib-1.10.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}