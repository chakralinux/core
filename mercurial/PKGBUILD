#
# Chakra Packages for Chakra, part of chakra-project.org
#

pkgname=mercurial
pkgver=2.6
pkgrel=1
pkgdesc="A scalable distributed SCM tool"
arch=('x86_64')
url="http://www.selenic.com/mercurial"
license=('GPL')
depends=('python2')
optdepends=('tk: for the hgk GUI')
source=("http://www.selenic.com/mercurial/release/${pkgname}-${pkgver}.tar.gz"
        'mercurial.profile')
md5sums=('d012d8bb5f85369d3b6a630a80667170'
         '43e1d36564d4c7fbe9a091d3ea370a44')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1 

  sed -i -e 's#env python#env python2#' \
    "${pkgdir}"/usr/lib/python2.7/site-packages/mercurial/lsprof.py
    
  install -d ${pkgdir}/usr/share/man/{man1,man5}
  install -m644 doc/hg.1 "${pkgdir}/usr/share/man/man1" 
  install -m644 doc/{hgrc.5,hgignore.5} "${pkgdir}/usr/share/man/man5" 
  install -m755 contrib/hgk "${pkgdir}/usr/bin" 
  install -m644 -D contrib/zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_hg" 
  install -m644 -D contrib/bash_completion "${pkgdir}/etc/bash_completion.d/hg" 
  install -d "${pkgdir}/usr/share/emacs/site-lisp"
  install -m644 contrib/{mq.el,mercurial.el} "${pkgdir}/usr/share/emacs/site-lisp" 

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm644 contrib/vim/HGAnnotate.vim "${vimpath}/syntax/HGAnnotate.vim" 

  # set some variables
  install -m755 -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/mercurial.profile "${pkgdir}/etc/profile.d/mercurial.sh" 
  
  # install configuration file
  install -m755 -d ${pkgdir}/etc/mercurial
  install -m644 contrib/sample.hgrc "${pkgdir}/etc/mercurial/hgrc"
}
