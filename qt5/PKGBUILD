#
# QT Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgbase=qt5
pkgname=('qt5' 'qt5-private-headers')
pkgver=5.0.0
_pkgver=5.0.0
pkgrel=2
arch=('x86_64')
url='http://qt.nokia.com/'
license=('GPL3' 'LGPL')
makedepends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
    'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib' 'icu'
    'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'postgresql-libs'
    'mysql' 'unixodbc' 'cups' 'libxinerama' 'libxcursor' 'python2' 'gperf' 'ruby'
    'xcb-util-image' 'xcb-util-keysyms' 'xcb-util-renderutil' 'xcb-util-wm' 'pulseaudio'
    'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins'
    'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg' 'gstreamer0.10-ugly-plugins')
options=('!libtool')
_pkgfqn="qt-everywhere-opensource-src-${_pkgver}"
source=("http://origin.releases.qt-project.org/qt5/${_pkgver}/single/qt-everywhere-opensource-src-${_pkgver}.tar.xz"
        'assistant.desktop' 'designer.desktop' 'linguist.desktop'
        'qtconfig.desktop')
md5sums=('6b4f2f3f1e6252fbce3a57ac5165baff'
         'fc211414130ab2764132e7370f8e5caa'
         '85179f5e0437514f8639957e1d8baf62'
         'f11852b97583610f3dbb669ebc3e21bc'
         '6b771c8a81dd90b45e8a79afa0e5bbfd')

build() {
	cd $srcdir/$_pkgfqn

        unset QTDIR
        export PATH="$PWD/qtbase/bin:$PWD/qtrepotools/bin:$PATH"

	./configure -confirm-license -opensource \
		-prefix /opt/qt5 \
		-system-sqlite \
                 -openssl-linked \
                 -nomake demos \
                 -nomake examples \
                 -nomake docs \
                 -optimized-qmake \
                 -reduce-relocations \
                 -dbus-linked \
                 -no-openvg \
                 -no-separate-debug-info \
                 -no-gtkstyle \
                 -opengl \
                 -feature-menu \
                 -feature-textdate \
                 -feature-ftp \
                 -xcursor
	 make
	 #./configure -prefix $PWD/qtbase -opensource
         #make module-qtwebkit
}

package_qt5() {
    pkgdesc='A cross-platform application and UI framework'
    depends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
             'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib' 'ruby'
             'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'libxinerama' 'libxcursor'
             'pulseaudio' 'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins'
             'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg'
             'gstreamer0.10-ugly-plugins' 'xcb-util-image' 'xcb-util-keysyms'
             'xcb-util-renderutil' 'xcb-util-wm')
    optdepends=('postgresql-libs: PostgreSQL driver'
	  'libmysqlclient: MySQL driver'
	  'unixodbc: ODBC driver'
	  'libxinerama: Xinerama support'
	  'libxfixes: Xfixes support')
	
    cd $srcdir/$_pkgfqn
	make INSTALL_ROOT=$pkgdir install
}

package_qt5-private-headers() {
    pkgdesc="Qt private headers for development"
    depends=("qt5=${pkgver}")

    install -d ${pkgdir}/opt/include/{QtCore,QtDeclarative,QtGui,QtScript}
    install -d ${pkgdir}/opt/src/{corelib,declarative,gui,script}
    
    cp -r ${srcdir}/$_pkgfqn/qtbase/include/QtCore/${pkgver}/QtCore/private/ ${pkgdir}/opt/include/QtCore/
    cp -r ${srcdir}/$_pkgfqn/qtbase/include/QtGui/${pkgver}/QtGui/private/ ${pkgdir}/opt/include/QtGui/
    cp -r ${srcdir}/$_pkgfqn/qtdeclarative/include/QtQml/${pkgver}/QtQml/private/ ${pkgdir}/opt/include/QtQml/
    cp -r ${srcdir}/$_pkgfqn/qtscript/include/QtScript/${pkgver}/QtScript/private/ ${pkgdir}/opt/include/QtScript/

    for i in corelib gui; do
      cp -r ${srcdir}/$_pkgfqn/qtbase/src/${i} ${pkgdir}/opt/src/
    done
    cp -r ${srcdir}/$_pkgfqn/qtscript/src/script ${pkgdir}/opt/src/
    cp -r ${srcdir}/$_pkgfqn/qtdeclarative/src/qml ${pkgdir}/opt/src/
}

