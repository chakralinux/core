#
# QT Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgbase=qt5
pkgname=('qt5' )
pkgname=('qt5-base'
         'qt5-declarative'
         'qt5-graphicaleffects'
         'qt5-imageformats'
         'qt5-jsbackend'
         'qt5-multimedia'
         'qt5-quick1'
         'qt5-quickcontrols'
         'qt5-script'
         'qt5-sensors'
         'qt5-serialport'
         'qt5-svg'
         'qt5-tools'
         'qt5-translations'
         'qt5-webkit'
         'qt5-x11extras'
         'qt5-xmlpatterns'
         'qt5-private-headers')
pkgver=5.1.1
_pkgver=5.1.1
pkgrel=3
arch=('x86_64')
url='http://qt.nokia.com/'
license=('GPL3' 'LGPL')
makedepends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
    'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib' 'icu'
    'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'postgresql-libs'
    'mariadb' 'unixodbc' 'cups' 'libxinerama' 'libxcursor' 'python2' 'gperf' 'ruby'
    'xcb-util-image' 'xcb-util-keysyms' 'xcb-util-renderutil' 'xcb-util-wm' 'pulseaudio'
    'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins'
    'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg' 'gstreamer0.10-ugly-plugins')
options=('!libtool')
_pkgfqn="qt-everywhere-opensource-src-${_pkgver}"
source=(#"${pkgname}.tar.xz"
        "http://download.qt-project.org/official_releases/qt/5.1/${pkgver}/single/qt-everywhere-opensource-src-${_pkgver}.tar.xz"
        'assistant-qt5.desktop' 'designer-qt5.desktop' 'linguist-qt5.desktop' 'qtconfig-qt5.desktop'
        'youtubeview.desktop' 'flickrview.desktop' 'browser.desktop' 'fancybrowser.desktop' 'qmlviewer.desktop'
        'qdbusviewer-qt5.desktop'  'defaultbookmarks_new.diff')
sha1sums=('e09a34ef5a80b98eda4e07dcf810d1ce0ef62346'
          '981cf376da66c8be7c7ec99866da0299114192f6'
          'e131a17beda4625f21022981a519b5e8d8c8e6f7'
          '62806b6696696d4ef69e4c0733fc9f1d761f08cd'
          '2e0b6245c40c91fbb0a965724450b6d6883f2927'
          'bd7d161a013f7cf09bd6638a6ff26bfe91ddf7d5'
          'f7a01d24dd011d1d7ee92ac64e9fb9cf8c99fe47'
          '78f65f580c14fdc24d0052702d216ed37ee6e01d'
          'b4ddedb067154a959c1b4577de8829dffd710f7a'
          'b0e6c61b8364eb3f7af4dae41fa7738c3181dd96'
          '6c845b5879d2f443078e25856cf60cf53644c41b'
          'c0c37469bbecfbc3d15eb21020e1078f4ed47784')

_prlfix() {
  # Fix wrong path in prl files
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
}

build() {
	cd $srcdir/$_pkgfqn
        # reenable patch later
	#patch -p1 -i "$srcdir/defaultbookmarks_new.diff"

        unset QTDIR
        export PATH="$PWD/qtbase/bin:$PWD/qtrepotools/bin:$PATH"

	./configure -confirm-license -opensource \
		-prefix /usr \
                -bindir /usr/lib/qt5/bin \
                -headerdir /usr/include/qt5 \
                -archdatadir /usr/lib/qt5 \
                -docdir /usr/share/doc/qt5 \
                -plugindir /usr/lib/qt5/plugins \
                -importdir /usr/lib/qt5/imports \
                -qmldir /usr/lib/qt5/qml \
                -datadir /usr/share/qt5 \
                -testsdir /usr/share/qt5/tests \
                -translationdir /usr/share/qt5/translations \
                -sysconfdir /etc/xdg/qt5 \
                -examplesdir /usr/share/qt5/examples \
		-system-sqlite \
                -openssl-linked \
                -nomake tests \
                -optimized-qmake \
                -reduce-relocations \
                -dbus-linked \
                -no-openvg \
                -no-separate-debug-info \
                -no-gtkstyle \
                -opengl desktop \
                -feature-menu \
                -feature-textdate \
                -feature-ftp \
                -xcursor
	 make
         #make module-qtwebkit
}

package_qt5-base() {
    pkgdesc='A cross-platform application and UI framework'
    conflicts=('qt5')
    depends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
             'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib' 'ruby'
             'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'libxinerama' 'libxcursor'
             'pulseaudio' 'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins'
             'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg'
             'gstreamer0.10-ugly-plugins' 'xcb-util-image' 'xcb-util-keysyms'
             'xcb-util-renderutil' 'xcb-util-wm')
    optdepends=('postgresql-libs: PostgreSQL driver'
	  'libmysqlclient: MySQL driver'
	  'unixodbc: ODBC driver'
	  'libxinerama: Xinerama support'
	  'libxfixes: Xfixes support')
	
    cd $srcdir/$_pkgfqn/qtbase
	make INSTALL_ROOT=$pkgdir install
	
    _prlfix

    # Fix wrong qmake path in pri file
    sed -i "s|${srcdir}/${_pkgfqn}/qtbase|/usr|" \
       "${pkgdir}"/usr/lib/qt5/mkspecs/modules/qt_lib_bootstrap.pri
    

    # consider moving the following stuff into a new packgage, like qt5-examples
    install -d ${pkgdir}/usr/share/applications
    install -m644 ${srcdir}/youtubeview.desktop ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/flickrview.desktop ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/qmlviewer.desktop ${pkgdir}/usr/share/applications/
    
    # create some symlinks in /usr/bin, postfixed with -qt5
    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
        ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
    done


   # install license
   cd "${srcdir}"
   install -D -m644 ${_pkgfqn}/qtbase/LGPL_EXCEPTION.txt \
     ${pkgdir}/usr/share/licenses/${pkgbase}/LGPL_EXCEPTION.txt

}

package_qt5-declarative() {
    pkgdesc='A cross-platform application and UI framework (QtQml, QtQuick)'
    depends=('qt5-jsbackend' 'qt5-xmlpatterns')
    conflicts=('qt5')

    cd $srcdir/$_pkgfqn/qtdeclarative
	make INSTALL_ROOT=$pkgdir install

    _prlfix

    # create some symlinks in /usr/bin, postfixed with -qt5
    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
        ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
    done
}

package_qt5-jsbackend() {
    pkgdesc='A cross-platform application and UI framework (QtV8)'
    depends=('qt5-base')
    conflicts=('qt5')

    cd $srcdir/$_pkgfqn/qtjsbackend
	make INSTALL_ROOT=$pkgdir install

    _prlfix
}

package_qt5-xmlpatterns() {
    pkgdesc='A cross-platform application and UI framework (QtXmlPatterns)'
    depends=('qt5-base')
    conflicts=('qt5')

    cd $srcdir/$_pkgfqn/qtxmlpatterns
	make INSTALL_ROOT=$pkgdir install

    _prlfix

    # create some symlinks in /usr/bin, postfixed with -qt5
    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
        ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
    done
}

package_qt5-translations() {
  pkgdesc='A cross-platform application and UI framework (Translations)'
  depends=('qt5-base')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qttranslations
       make INSTALL_ROOT="${pkgdir}" install
}


package_qt5-multimedia() {
  pkgdesc='A cross-platform application and UI framework (QtMultimedia)'
  depends=('qt5-declarative' 'libpulse' 'gstreamer0.10-base' 'openal')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qtmultimedia
      make INSTALL_ROOT="${pkgdir}" install

   _prlfix
}

package_qt5-graphicaleffects() {
  pkgdesc='A cross-platform application and UI framework (QtGraphicalEffects)'
  depends=('qt5-declarative')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qtgraphicaleffects
      make INSTALL_ROOT="${pkgdir}" install
}

package_qt5-imageformats() {
  pkgdesc='A cross-platform application and UI framework (Images plugins)'
  depends=('qt5-base' 'libtiff' 'libmng')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qtimageformats
      make INSTALL_ROOT="${pkgdir}" install
}

package_qt5-quick1() {
  pkgdesc='A cross-platform application and UI framework (QtDeclarative)'
  depends=('qt5-webkit' 'qt5-script')
  conflicts=('qt5')
 
  cd ${srcdir}/${_pkgfqn}/qtquick1
  make INSTALL_ROOT="${pkgdir}" install

  _prlfix 
  
  
  # create some symlinks in /usr/bin, postfixed with -qt5
  mkdir -p ${pkgdir}/usr/bin
  for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
      ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
  done
}

package_qt5-quickcontrols() {
  pkgdesc='A cross-platform application and UI framework (QtQuick)'
  depends=('qt5-declarative')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qtquickcontrols
  make INSTALL_ROOT="${pkgdir}" install
}

package_qt5-script() {
  pkgdesc='A cross-platform application and UI framework (QtScript)'
  depends=('qt5-base')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qtscript
  make INSTALL_ROOT="${pkgdir}" install

  _prlfix
}

package_qt5-sensors() {
  pkgdesc='A cross-platform application and UI framework (QtSensors)'
  depends=('qt5-declarative')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qtsensors
  make INSTALL_ROOT="${pkgdir}" install

  _prlfix
}

package_qt5-serialport() {
  pkgdesc='A cross-platform application and UI framework (QtSerialPort)'
  depends=('qt5-base')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qtserialport
  make INSTALL_ROOT="${pkgdir}" install

  _prlfix
}

package_qt5-svg() {
  pkgdesc='A cross-platform application and UI framework (QtSvg)'
  depends=('qt5-base')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qtsvg
  make INSTALL_ROOT="${pkgdir}" install

  _prlfix
}
  
package_qt5-tools() {
  pkgdesc='A cross-platform application and UI framework (Development Tools, QtHelp)'
  depends=('qt5-webkit' 'desktop-file-utils' 'qt5-translations' 'hicolor-icon-theme'
           'xdg-utils')
  optdepends=('qt5-doc: documentation')
  conflicts=('qt5')

  cd ${srcdir}/${_pkgfqn}/qttools
  make INSTALL_ROOT="${pkgdir}" install
  
  # install missing icons and desktop files
  for icon in src/linguist/linguist/images/icons/linguist-*-32.png ; do
    size=$(echo $(basename ${icon}) | cut -d- -f2)
    install -p -D -m644 ${icon} \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/linguist-qt5.png"
  done

  install -D -m644 src/assistant/assistant/images/assistant.png \
    "${pkgdir}/usr/share/icons/hicolor/32x32/apps/assistant-qt5.png"
  install -D -m644 src/assistant/assistant/images/assistant-128.png \
    "${pkgdir}/usr/share/icons/hicolor/128x128/apps/assistant-qt5.png"
  install -D -m644 src/designer/src/designer/images/designer.png \
    "${pkgdir}/usr/share/icons/hicolor/128x128/apps/designer-qt5.png"
  install -D -m644 src/qdbus/qdbusviewer/images/qdbusviewer.png \
    "${pkgdir}/usr/share/icons/hicolor/32x32/apps/qdbusviewer-qt5.png"
  install -D -m644 src/qdbus/qdbusviewer/images/qdbusviewer-128.png \
    "${pkgdir}/usr/share/icons/hicolor/128x128/apps/qdbusviewer-qt5.png"
  install -d "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}"/{linguist-qt5,designer-qt5,assistant-qt5,qdbusviewer-qt5}.desktop \
    "${pkgdir}/usr/share/applications/"

  _prlfix
  
  # create some symlinks in /usr/bin, postfixed with -qt5
  mkdir -p ${pkgdir}/usr/bin
  for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
      ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
  done
}

package_qt5-webkit() {
  pkgdesc='A cross-platform application and UI framework (QtWebKit)'
  depends=('qt5-declarative' 'gstreamer0.10-base' 'libxslt' 'libxcomposite' 'qt5-sensors')
  conflicts=('qt5') 

  cd ${srcdir}/${_pkgfqn}/qtwebkit
  make INSTALL_ROOT="${pkgdir}" install

  _prlfix
  
  # Fix wrong path in pc file
  perl -pi -e "s, -L${srcdir}/?\S+,,g" "${pkgdir}"/usr/lib/pkgconfig/Qt5WebKit.pc
}

package_qt5-x11extras() {
  pkgdesc='A cross-platform application and UI framework (QtX11Extras)'
  depends=('qt5-base')
  conflicts=('qt5') 
  
  cd ${srcdir}/${_pkgfqn}/qtx11extras
  make INSTALL_ROOT="${pkgdir}" install

  _prlfix
}


package_qt5-private-headers() {
    pkgdesc="Qt private headers for development"
    depends=("qt5-base=${pkgver}")

    install -d ${pkgdir}/usr/include/qt5/{QtCore,QtDeclarative,QtGui,QtScript}
    install -d ${pkgdir}/usr/src/qt5/{corelib,declarative,gui,script}
    
    cp -r ${srcdir}/$_pkgfqn/qtbase/include/QtCore/${pkgver}/QtCore/private/ ${pkgdir}/usr/include/qt5/QtCore/
    cp -r ${srcdir}/$_pkgfqn/qtbase/include/QtGui/${pkgver}/QtGui/private/ ${pkgdir}/usr/include/qt5/QtGui/
    cp -r ${srcdir}/$_pkgfqn/qtdeclarative/include/QtQml/${pkgver}/QtQml/private/ ${pkgdir}/usr/include/qt5/QtQml/
    cp -r ${srcdir}/$_pkgfqn/qtscript/include/QtScript/${pkgver}/QtScript/private/ ${pkgdir}/usr/include/qt5/QtScript/

    for i in corelib gui; do
      cp -r ${srcdir}/$_pkgfqn/qtbase/src/${i} ${pkgdir}/usr/src/qt5/
    done
    cp -r ${srcdir}/$_pkgfqn/qtscript/src/script ${pkgdir}/usr/src/qt5/
    cp -r ${srcdir}/$_pkgfqn/qtdeclarative/src/qml ${pkgdir}/usr/src/qt5/
}
