#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>

pkgbase=dhcp
pkgname=('dhcp' 
	 'dhclient')
# separate patch levels with a period to maintain proper versioning.
pkgver=4.2.4.2
_pkgver=4.2.4-P2
pkgrel=1
arch=('i686' 'x86_64')
license=('custom:isc-dhcp')
url="https://www.isc.org/software/dhcp"
source=(ftp://ftp.isc.org/isc/${pkgbase}/${_pkgver}/${pkgbase}-${_pkgver}.tar.gz
        dhcp4
        dhcp6 
        dhcp.conf.d 
        dhcpd4.service
        dhcpd6.service
        dhcp-4.2.4-P2-missing-ipv6-not-fatal.patch
        dhclient-script-pathFixes.patch)
sha256sums=('0f75170e323cd9573e6e09a5d9236725f3e56e3cac5a70a01fe2a9d76b436499'
            'e857d57c860fef26eabe0142fd6e4fc19453dff2cc1ee4cc9f611368b351229f'
            '6211936c07986437f6ff982653ad4f26324859c7873cfea41b0f0ed32027d18c'
            '83bfd784c8496f7543d81c145ccfb31bcbbfbce01c0410d0f6337a59eba53f47'
            '02ba5fb78c242f6ff57da83b3d4499cf57d54bc95473b182bed1ea7eb47876e8'
            '8a4a92b0cbd423294dc19d56df753a26f7634494540ef89cca3aff7335b7297d'
            '96d85d4cdc92a90c54c420fdea993cd02f9339af2220ac5c82e10853ff2c06a4'
            'c4ec67c0bb06f8113e871e20a7784fea2dd9baa14435b1624b963370fef29136')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  # Define _GNU_SOURCE to fix IPV6.
  sed '/^CFLAGS="$CFLAGS/ s/INGS"/INGS -D_GNU_SOURCE"/' -i configure

  # Make not having ipv6 non-fatal.
  patch -p1 -i "${srcdir}/dhcp-4.2.4-P2-missing-ipv6-not-fatal.patch"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-srv-lease-file=/var/state/dhcp/dhcpd.leases \
              --with-srv6-lease-file=/var/state/dhcp/dhcpd6.leases \
              --with-cli-lease-file=/var/state/dhclient/dhclient.leases \
              --with-cli6-lease-file=/var/state/dhclient/dhclient6.leases

  make

  patch -i "${srcdir}/dhclient-script-pathFixes.patch" client/scripts/linux
}

package_dhcp() {
  pkgdesc="A DHCP server, client, and relay agent"
  depends=('openssl>=0.9.8a')
  backup=('etc/dhcpd.conf' 'etc/conf.d/dhcp')
  install=dhcp.install
  
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m0755 "${srcdir}/dhcp4" "${pkgdir}/etc/rc.d/dhcp4"
  install -D -m0755 "${srcdir}/dhcp6" "${pkgdir}/etc/rc.d/dhcp6"
  install -D -m0644 "${srcdir}/dhcp.conf.d" "${pkgdir}/etc/conf.d/${pkgbase}"
  mkdir -p "${pkgdir}/var/state/dhcp"

  # Install systemd service files
  install -D -m0644 "${srcdir}/dhcpd4.service" "${pkgdir}/usr/lib/systemd/system/dhcpd4.service"
  install -D -m0644 "${srcdir}/dhcpd6.service" "${pkgdir}/usr/lib/systemd/system/dhcpd6.service"
  ln -s dhcpd4.service "${pkgdir}/usr/lib/systemd/system/dhcp4.service"
  ln -s dhcpd6.service "${pkgdir}/usr/lib/systemd/system/dhcp6.service"
  
  # Remove dhclient
  make -C client DESTDIR="${pkgdir}" uninstall 

  # install licenses
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/dhcp/LICENSE"
}

package_dhclient() {
  _pkgname=dhcp
  pkgdesc="dhclient is standalone client from the dhcp package"
  depends=('bash' 'iproute2' 'net-tools')
  
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  make -C client DESTDIR="${pkgdir}" install 

  #move dhclient.conf to dhclient.conf.example
  mv "${pkgdir}"/etc/dhclient.conf{,.example}

  install -d "${pkgdir}/var/state/dhclient"

  # install dhclient linux script
  install -Dm0755 client/scripts/linux "${pkgdir}/sbin/dhclient-script"

  # install licenses
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/dhclient/LICENSE"
}
