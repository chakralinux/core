pkgname=libvorbis
pkgver=1.3.5
pkgrel=1
pkgdesc="Vorbis codec library"
arch=('x86_64')
license=('custom')
url="http://www.xiph.org/ogg/vorbis/"
depends=('libogg')
source=("http://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.gz")
md5sums=('7220e089f3be3412a2317d6fde9e3944')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #-march=i686 optimizes too much, strip it out
  CFLAGS=${CFLAGS/-march=$CARCH} LIBS+="-lm" ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
