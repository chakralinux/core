# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributors from Arch: Thayer Williams <thayer@archlinux.org>
#  			  Hugo Doria <hugo@archlinux.org>
#                         TuxSpirit<tuxspirit@archlinux.fr>

pkgname=p7zip
pkgver=9.20.1
pkgrel=1
pkgdesc="A command-line port of the 7zip compression utility"
arch=('x86_64')
license=('GPL')
url="http://p7zip.sourceforge.net"
depends=('gcc-libs' 'bash')
makedepends=('yasm' 'nasm')
options=('!makeflags')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}_${pkgver}_src_all.tar.bz2"
        7z.desktop)
md5sums=('bd6caaea567dc0d995c990c5cc883c89'
         '4a21a2c956840140a0ee859440968751')

build() {
	cd "${srcdir}/${pkgname}_${pkgver}"

	#[[ $CARCH = x86_64 ]] \
	#&& cp makefile.linux_amd64_asm makefile.machine \
	#|| cp makefile.linux_x86_asm_gcc_4.X makefile.machine
        cp makefile.linux_amd64_asm makefile.machine

	make all3 OPTFLAGS="${CXXFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}_${pkgver}"

	make install \
		DEST_DIR="${pkgdir}" \
		DEST_HOME="/usr" \
		DEST_MAN="/usr/share/man"

	# Licenses
	install -d "${pkgdir}"/usr/share/licenses/p7zip
	ln -s -t "${pkgdir}"/usr/share/licenses/p7zip \
		/usr/share/doc/p7zip/DOCS/License.txt \
		/usr/share/doc/p7zip/DOCS/unRarLicense.txt

	# Integration with stuff...
	install -D GUI/p7zip_32.png "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/p7zip.png
	install -d "${pkgdir}"/usr/share/{applications,kde4/services/ServiceMenus}
	cp GUI/kde4/* "${pkgdir}"/usr/share/kde4/services/ServiceMenus/
	cp ../7z.desktop "${pkgdir}"/usr/share/applications/
	ln -s 7zCon.sfx "${pkgdir}"/usr/lib/p7zip/7z.sfx

	find GUI/help -type d -exec chmod 755 {} \;
	cp -r GUI/help "${pkgdir}"/usr/lib/p7zip/

	chmod -R u+w "${pkgdir}/usr"
}
