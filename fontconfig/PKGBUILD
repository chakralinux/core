pkgname=fontconfig
pkgver=2.13.1
pkgrel=1
_ubuntu_diff_version=${pkgver}-2ubuntu2
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat' 'freetype2')
makedepends=('gperf')
install='fontconfig.install'
source=("http://www.fontconfig.org/release/$pkgname-$pkgver.tar.bz2"
        "http://archive.ubuntu.com/ubuntu/pool/main/f/fontconfig/fontconfig_${_ubuntu_diff_version}.debian.tar.xz"
        '29-replace-bitmap-fonts.conf'
        '70-yes-bitmaps.conf'
        '20-no-embeded.conf'
        fontconfig.hook)

conflicts=('fontconfig-ubuntu')
provides=('fontconfig-ubuntu')
replaces=('fontconfig-ubuntu')
md5sums=('36cdea1058ef13cbbfdabe6cb019dc1c'
         '0ea15dcc7eb03f7e3a0c82bd60e92f4a'
         '39654873d8ff74d418f533e559b57fb9'
         '91c414090c7d8bfe557785fe845cb6bd'
         '9b991efd2795149fac2e1452b97aeb74'
         'ba07f578513842a325e397aa0f696319')

prepare() {
  cd "${pkgname}-${pkgver}"
  
  # apply ubuntu patches
  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  libtoolize -f
  autoreconf -fi
}


build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-templatedir=/etc/fonts/conf.avail \
              --with-xmldir=/etc/fonts \
              --localstatedir=/var \
              --disable-static \
              --with-default-fonts=/usr/share/fonts \
              --with-add-fonts=/usr/share/fonts

  make
}

check() {
  cd "${pkgname}-${pkgver}"
  # fail with ubuntu patches at 53-monospace-lcd-filter.conf
  make -k check | true
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 ../fontconfig.hook "$pkgdir/usr/share/libalpm/hooks/fontconfig.hook"
  
  # use ubuntu 70-yes-bitmaps.conf
  mv ${pkgdir}/etc/fonts/conf.avail/70-yes-bitmaps.conf ${pkgdir}/etc/fonts/conf.avail/70-force-bitmaps.conf
  install -m644 "${srcdir}/70-yes-bitmaps.conf" "${pkgdir}/etc/fonts/conf.avail"

  install -m644 "${srcdir}/29-replace-bitmap-fonts.conf" \
      "${pkgdir}/etc/fonts/conf.avail"

  ln -s ../conf.avail/29-replace-bitmap-fonts.conf \
      "$pkgdir/etc/fonts/conf.d/29-replace-bitmap-fonts.conf"

  # disable embedded bitmap for all fonts
  install -m644 "${srcdir}/20-no-embeded.conf" \
      "${pkgdir}/etc/fonts/conf.avail"

  ln -s ../conf.avail/20-no-embeded.conf \
      "$pkgdir/etc/fonts/conf.d/20-no-embeded.conf"

  # Install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}"
  
  # disable 70-no-bitmaps.conf
  rm "$pkgdir/etc/fonts/conf.d/70-no-bitmaps.conf"
}

