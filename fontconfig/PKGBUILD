#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): AlmAck <almack[at]chakraos[dot]org>

pkgname=fontconfig
pkgver=2.11.0
pkgrel=1
_ubuntu_diff_version=${pkgver}-0ubuntu4
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat' 'freetype2')
install='fontconfig.install'
source=("http://www.fontconfig.org/release/$pkgname-$pkgver.tar.bz2"
        "http://archive.ubuntu.com/ubuntu/pool/main/f/fontconfig/fontconfig_${_ubuntu_diff_version}.debian.tar.gz"
        '29-replace-bitmap-fonts.conf'
        '70-yes-bitmaps.conf'
        '20-no-embeded.conf')

conflicts=('fontconfig-ubuntu')
provides=('fontconfig-ubuntu')
replaces=('fontconfig-ubuntu')
md5sums=('000bd4baf7aefa828e03414d0c8c7dc5'
         '50682ac34c77af146b97c2bcc01c3ffb'
         '39654873d8ff74d418f533e559b57fb9'
         '91c414090c7d8bfe557785fe845cb6bd'
         '9b991efd2795149fac2e1452b97aeb74')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # apply ubuntu patches
  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  libtoolize -f
  autoreconf -fi

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-templatedir=/etc/fonts/conf.avail \
              --with-xmldir=/etc/fonts \
              --localstatedir=/var \
              --disable-static \
              --with-default-fonts=/usr/share/fonts \
              --with-add-fonts=/usr/share/fonts

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  # use ubuntu 70-yes-bitmaps.conf
  mv ${pkgdir}/etc/fonts/conf.avail/70-yes-bitmaps.conf ${pkgdir}/etc/fonts/conf.avail/70-force-bitmaps.conf
  install -m644 "${srcdir}/70-yes-bitmaps.conf" "${pkgdir}/etc/fonts/conf.avail"

  install -m644 "${srcdir}/29-replace-bitmap-fonts.conf" \
      "${pkgdir}/etc/fonts/conf.avail"

  ln -s ../conf.avail/29-replace-bitmap-fonts.conf \
      "$pkgdir/etc/fonts/conf.d/29-replace-bitmap-fonts.conf"

  # disable embedded bitmap for all fonts
  install -m644 "${srcdir}/20-no-embeded.conf" \
      "${pkgdir}/etc/fonts/conf.avail"

  ln -s ../conf.avail/20-no-embeded.conf \
      "$pkgdir/etc/fonts/conf.d/20-no-embeded.conf"

  # Install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}"
  
  # disable 70-no-bitmaps.conf
  rm "$pkgdir/etc/fonts/conf.d/70-no-bitmaps.conf"
}

