#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=fontconfig
pkgver=2.10.93
pkgrel=4
_ubuntu_diff_version=${pkgver}-0ubuntu1
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat>=2.0.1' 'freetype2>=2.3.11')
options=('!libtool')
install='fontconfig.install'
source=("http://www.fontconfig.org/release/${pkgname}-${pkgver}.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/main/f/fontconfig/fontconfig_${_ubuntu_diff_version}.debian.tar.gz"
        '29-replace-bitmap-fonts.conf'
        '70-yes-bitmaps.conf'
        '20-no-embeded.conf')

conflicts=('fontconfig-ubuntu')
provides=('fontconfig-ubuntu')
replaces=('fontconfig-ubuntu')
sha256sums=('381541e4d1a150dac46af1d9ad64bdc3a131d6b2707d192209b2ed26e3e64147'
            'bd02a8858786e30a70b145494168c0c99a0ad4c8606d3bbaed99f9f3d3ea690f'
            '2325c478783659e8fce79f5754119934d6ce21f81552f878b740c7b18f9fe0d6'
            '87228113c2e8ffbf5565ffaa80d5e5fde33b14f0725accf69e1e31c80df398c0'
            'df3cc26ac29b0e16b81ec60f93e67995c65c438f0a55cf480b3b15cf82adc51c')

backup=(etc/fonts/conf.d/10-antialias.conf
        etc/fonts/conf.d/10-hinting.conf
        etc/fonts/conf.d/10-hinting-slight.conf
        etc/fonts/conf.d/11-lcdfilter-default.conf
        etc/fonts/conf.d/20-unhint-small-vera.conf
        etc/fonts/conf.d/30-metric-aliases.conf
        etc/fonts/conf.d/30-urw-aliases.conf
        etc/fonts/conf.d/40-nonlatin.conf
        etc/fonts/conf.d/45-latin.conf
        etc/fonts/conf.d/49-sansserif.conf
        etc/fonts/conf.d/50-user.conf
        etc/fonts/conf.d/51-local.conf
        etc/fonts/conf.d/60-latin.conf
        etc/fonts/conf.d/65-fonts-persian.conf
        etc/fonts/conf.d/65-khmer.conf
        etc/fonts/conf.d/65-nonlatin.conf
        etc/fonts/conf.d/69-unifont.conf
        etc/fonts/conf.d/70-no-bitmaps.conf
        etc/fonts/conf.d/80-delicious.conf
        etc/fonts/conf.d/90-synthetic.conf
)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # apply ubuntu patches
  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  libtoolize -f
  autoreconf -fi

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-templatedir=/etc/fonts/conf.avail \
              --with-xmldir=/etc/fonts \
              --localstatedir=/var \
              --disable-static \
              --with-default-fonts=/usr/share/fonts \
              --with-add-fonts=/usr/share/fonts

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  # use ubuntu 70-yes-bitmaps.conf
  mv ${pkgdir}/etc/fonts/conf.avail/70-yes-bitmaps.conf ${pkgdir}/etc/fonts/conf.avail/70-force-bitmaps.conf
  install -m644 "${srcdir}/70-yes-bitmaps.conf" "${pkgdir}/etc/fonts/conf.avail"

  install -m644 "${srcdir}/29-replace-bitmap-fonts.conf" \
      "${pkgdir}/etc/fonts/conf.avail"

  ln -s ../conf.avail/29-replace-bitmap-fonts.conf \
      "$pkgdir/etc/fonts/conf.d/29-replace-bitmap-fonts.conf"

  # disable embedded bitmap for all fonts
  install -m644 "${srcdir}/20-no-embeded.conf" \
      "${pkgdir}/etc/fonts/conf.avail"

  ln -s ../conf.avail/20-no-embeded.conf \
      "$pkgdir/etc/fonts/conf.d/20-no-embeded.conf"

  # Install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}"
}

