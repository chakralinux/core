#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=dmraid
pkgver=1.0.0.rc16.3
pkgrel=4
pkgdesc="Device mapper RAID interface"
url="http://people.redhat.com/~heinzm/sw/dmraid/"
depends=('device-mapper>=2.0.54')
arch=('i686' 'x86_64')
license=('GPL')
source=(#ftp://ftp.archlinux.org/other/dmraid/$pkgname-$pkgver.tar.bz2
        http://people.redhat.com/~heinzm/sw/dmraid/src/$pkgname-1.0.0.rc16-3.tar.bz2
        dmraid_install
        dmraid_hook
        dmraid_tmpfiles
        dmraid.service)
install=dmraid.install
md5sums=('819338fcef98e8e25819f0516722beeb'
         '7a040ebcba305aba1e47dfe6ca8323b5'
         'faec669dc85f87187b45b5d3968efe2c'
         '56a8bb0ece8d206cd8efb504ee072ddd'
         'ea6d280fc6f63fb799abcd882bca53c8')

build() {
  cd "$srcdir"/$pkgname/1.0.0.rc16-3/$pkgname
  ./configure --enable-led --enable-intel_led
  make
}

package() {
  cd "$srcdir"/$pkgname/1.0.0.rc16-3/$pkgname
  make DESTDIR="$pkgdir" sbindir=/usr/bin prefix=/usr libdir=/usr/lib mandir=/usr/share/man includedir=/usr/include install
  install -D -m644 "$srcdir"/dmraid_install "$pkgdir"/usr/lib/initcpio/install/dmraid
  install -D -m644 "$srcdir"/dmraid_hook "$pkgdir"/usr/lib/initcpio/hooks/dmraid
  install -D -m644 "$srcdir"/dmraid_tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/dmraid.conf

  # fix permissions
  chmod 644 "$pkgdir"/usr/lib/libdmraid.a
  chmod 644 "$pkgdir"/usr/include/dmraid/*
  
  # install systemd unit
  install -Dm644 "$srcdir/dmraid.service" "$pkgdir/usr/lib/systemd/system/dmraid.service"
}
