#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=taglib
pkgver=1.7
pkgrel=1
pkgdesc="A Library for reading and editing the meta-data of several popular audio formats"
arch=('i686' 'x86_64')
url="http://developer.kde.org/~wheeler/taglib.html"
license=('GPL2')
depends=('sh' 'zlib' 'gcc-libs')
makedepends=('cmake' 'pkgconfig')
source=("http://developer.kde.org/~wheeler/files/src/$pkgname-$pkgver.tar.gz"
        'ape-idev2.patch')
options=('!libtool')
md5sums=('6a7e312668f153fa905a81714aebc257'
         'c7c165d99acf01456351ba39ff7056cb')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/ape-idev2.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_MP4=ON \
    -DWITH_ASF=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
