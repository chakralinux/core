pkgname=libarchive
pkgver=3.2.1
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats"
arch=('x86_64')
url="http://libarchive.org/"
license=('BSD')
depends=('acl' 'attr' 'bzip2' 'expat' 'lzo2>=2.06' 'nettle' 'xz' 'zlib')
#options=('libtool')
provides=('libarchive.so')
source=("http://libarchive.org/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('afa257047d1941a565216edbf0171e72')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --without-xml2
  make
}

check() {
  cd "$pkgname-$pkgver"

  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
