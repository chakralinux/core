#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=glew
pkgver=1.9.0
pkgrel=1
pkgdesc="A cross-platform C/C++ extension loading library"
arch=('x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('libxmu' 'libxi' 'mesa')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tgz")
sha1sums=('9291f5c5afefd482c7f3e91ffb3cd4716c6c9ffe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|lib64|lib|' config/Makefile.linux
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GLEW_DEST="${pkgdir}/usr" install.all
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm "${pkgdir}"/usr/lib/{libGLEW,libGLEWmx}.a
  chmod 0755 "${pkgdir}"/usr/lib/libGLEW*.so.${pkgver}
}
