# Platform Packages for Chakra, part of chakra-project.org
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=netcdf
pkgver=4.2.1
pkgrel=1
pkgdesc="Network Common Data Form interface for array-oriented data access and corresponding library."
arch=("x86_64")
url="http://www.unidata.ucar.edu/downloads/netcdf/index.jsp"
depends=('gcc-libs' 'hdf5' 'gcc-fortran')
options=('!libtool' '!makeflags')
license=('custom')
source=(http://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-${pkgver}.tar.gz)
sha1sums=('dfb6b10ef8dd20e785efa5e29b448383090f144d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared --enable-netcdf-4
  make
#  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/share/man
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYRIGHT \
        ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT
}
