#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=readline
_basever=6.2
_patchlevel=004 #prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="GNU readline library"
arch=('x86_64')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('glibc' 'ncurses')
backup=('etc/inputrc')
options=('!emptydirs')
install='readline.install'
source=("http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz"
        'inputrc')
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p))
    done
fi
md5sums=('67948acb2ca081f23359d0256e9a271c'
         '58d54966c1191db45973cb3191ac621a'
         '83287d52a482f790dfb30ec0a8746669'
         '0665020ea118e8434bd145fb71f452cc'
         'c9d5d79718856e711667dede87cb7622'
         'c08e787f50579ce301075c523fa660a4')

build() {
  cd ${srcdir}/${pkgname}-$_basever
  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -Np0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
  done

  # Remove RPATH from shared objects
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  # build with -fPIC for x86_64 (FS#15634)
  [ $CARCH == "x86_64" ] && CFLAGS="$CFLAGS -fPIC"

  ./configure --prefix=/usr 
  make SHLIB_LIBS=-lncurses
}

package() {
  cd ${srcdir}/${pkgname}-$_basever
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ${srcdir}/inputrc ${pkgdir}/etc/inputrc
}

