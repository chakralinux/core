#
# Platform Packages for Chakra, part of www.chakra-project.org
#
# Maintainer:   F Kosmale (Inkane) <inkane@chakra-project.org>
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               A Boersma (abveritas) <abveritas@chakra-project.org>
#               Mateusz Krawczuk <willingmagic@gmail.com>

pkgname=python3
pkgver=3.4.1
pkgrel=1
_pybasever=3.4
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64')
license=('custom')
url="http://www.python.org"
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('tk' 'sqlite3' 'valgrind' 'bluez')
optdepends=('python3-setuptools'
            'python3-pip'
            'sqlite3'
            'xz: for lzma'
            'tk: for tkinter')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz)
md5sums=('6cafc183b4106476dd73d5738d7f616a')

prepare(){
    cd Python-${pkgver}

    # FS#23997
    sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

    # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
    # rather than copies shipped in the tarball
    rm -r Modules/expat
    rm -r Modules/zlib
    rm -r Modules/_ctypes/{darwin,libffi}*
}
build() {
    cd Python-${pkgver}

    ./configure --prefix=/usr \
                --enable-shared \
                --with-threads \
                --with-computed-gotos \
                --enable-ipv6 \
                --with-valgrind \
                --with-system-expat \
                --with-dbmliborder=gdbm:ndbm \
                --with-system-ffi \
                --without-ensurepip

    make
}

check() {
  cd Python-${pkgver}
  LD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${LD_LIBRARY_PATH} \
        "${srcdir}/Python-${pkgver}/python" -m test.regrtest -uall -x test_posixpath test_uuid test_site test_urllib2_localnet
}

package() {
    cd Python-${pkgver}
    make DESTDIR="${pkgdir}" install maninstall

    # Fix conflicts with python2 
    rm ${pkgdir}/usr/bin/2to3
    #rm ${pkgdir}/usr/bin/python
    #rm ${pkgdir}/usr/bin/python-config
    #rm ${pkgdir}/usr/bin/idle
    #rm ${pkgdir}/usr/bin/pydoc


    # Fix FS#22552
    ln -sf ../../libpython${_pybasever}mu.so \
        "${pkgdir}/usr/lib/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so"

    # Clean-up reference to build directory
    sed -i "s|$srcdir/Python-${pkgver}:||" "$pkgdir/usr/lib/python${_pybasever}/config-${_pybasever}m/Makefile"

    # License
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
