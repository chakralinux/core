#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=sdl2_ttf
pkgver=2.0.12
pkgrel=1
pkgdesc="A library that allows you to use TrueType fonts in your SDL applications (Version 2)"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('sdl2' 'freetype2')
makedepends=('cmake')
options=(!libtool)
source=("http://www.libsdl.org/projects/SDL_ttf/release/SDL2_ttf-${pkgver}.tar.gz"
        "FindSDL2_ttf.cmake")
sha256sums=('8728605443ea1cca5cad501dc34dc0cb15135d1e575551da6d151d213d356f6e'
            '457852758f2009caf337afe4f1068375e5a661cece4808acb465a10fd6952046')

build() {
  cd "${srcdir}/SDL2_ttf-${pkgver}/"
  
  ./autogen.sh
  ./configure --disable-static --prefix=/usr
  make
}

package() {
  cd "${srcdir}/SDL2_ttf-${pkgver}/"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  # we can remove the following when provided directly by cmake
  install -Dm644 $srcdir/FindSDL2_ttf.cmake ${pkgdir}/usr/share/cmake-2.8/Modules/FindSDL2_ttf.cmake
}

