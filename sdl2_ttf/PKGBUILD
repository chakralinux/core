pkgname=sdl2_ttf
pkgver=2.0.14
pkgrel=1
pkgdesc="A library that allows you to use TrueType fonts in your SDL applications (Version 2)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('sdl2' 'freetype2')
makedepends=('cmake')
source=("http://www.libsdl.org/projects/SDL_ttf/release/SDL2_ttf-${pkgver}.tar.gz"
        "FindSDL2_ttf.cmake")
sha256sums=('34db5e20bcf64e7071fe9ae25acaa7d72bdc4f11ab3ce59acc768ab62fe39276'
            '457852758f2009caf337afe4f1068375e5a661cece4808acb465a10fd6952046')

build() {
  cd "${srcdir}/SDL2_ttf-${pkgver}/"
  
  ./configure --disable-static --prefix=/usr
  make
}

package() {
  cd "${srcdir}/SDL2_ttf-${pkgver}/"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  # we can remove the following when provided directly by cmake
  install -Dm644 $srcdir/FindSDL2_ttf.cmake ${pkgdir}/usr/share/cmake-3.0/Modules/FindSDL2_ttf.cmake
}

