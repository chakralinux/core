pkgname=libtiff
pkgver=4.0.10
pkgrel=1
pkgdesc='Library for manipulation of TIFF images'
arch=('x86_64')
url="http://www.simplesystems.org/libtiff/"
license=('custom')
depends=('libjpeg-turbo' 'zlib' 'xz' 'zstd')
makedepends=('freeglut' 'glu' 'mesa' 'jbigkit')
optdepends=('freeglut: for using tiffgt')
source=(https://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz)
sha1sums=('c783b80f05cdacf282aa022dc5f5b0ede5e021ae')

build() {
    cd tiff-${pkgver}
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --with-docdir=/usr/share/doc/${pkgname}
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

check() {
    cd tiff-${pkgver}
    make check
}

package() {
    cd tiff-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
