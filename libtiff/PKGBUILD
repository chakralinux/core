pkgname=libtiff
pkgver=4.0.6
pkgrel=2
pkgdesc='Library for manipulation of TIFF images'
arch=('x86_64')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('libjpeg-turbo' 'zlib' 'xz')
makedepends=('freeglut' 'glu' 'mesa' 'jbigkit')
optdepends=('freeglut: for using tiffgt')
source=("ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz"
        'tiff-4.0.6-buffer-overflow.patch')
sha1sums=('280e27704eaca5f592b82e71ac0c78b87395e2de'
          '3fecfb52f78e1798439707210bdc352b2b710860')

prepare() {
   cd tiff-${pkgver}
   patch -Np1 -i ${srcdir}/tiff-4.0.6-buffer-overflow.patch
}

build() {
    cd tiff-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd tiff-${pkgver}
    make check
}

package() {
    cd tiff-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
