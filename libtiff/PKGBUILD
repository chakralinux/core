pkgname=libtiff
pkgver=4.0.9
pkgrel=1
pkgdesc='Library for manipulation of TIFF images'
arch=('x86_64')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('libjpeg-turbo' 'zlib' 'xz')
makedepends=('freeglut' 'glu' 'mesa' 'jbigkit')
optdepends=('freeglut: for using tiffgt')
source=(http://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz)
sha1sums=('87d4543579176cc568668617c22baceccd568296')


build() {
    cd tiff-${pkgver}
    ./configure --prefix=/usr
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

check() {
    cd tiff-${pkgver}
    make check
}

package() {
    cd tiff-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
