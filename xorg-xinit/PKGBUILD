# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributions from: https://www.archlinux.org/packages/extra/x86_64/xorg-xinit/

pkgname=xorg-xinit
pkgver=1.3.3
pkgrel=1
pkgdesc="X.Org initialisation program "
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth' 'inetutils')
makedepends=('xorg-util-macros')
optdepends=('xorg-twm' 'xterm')
backup=('etc/skel/.xinitrc'
        'etc/skel/.xsession'
        'etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2
        06_move_serverauthfile_into_tmp.diff
        fs25361.patch
        xinitrc
        xsession
        xserverrc
        cpp-4.8-compat.patch
        fix-globals-shadowing.patch)
options=(!emptydirs)
sha256sums=('74b2878de6e3d5b5948f1562a77e7f53b4e10621e505ddb278050c7f3cae00af'
            'a5c6ced5822bbb3145e0f30a4b12995782ca6d80ff2d927851ec91b5e41212cb'
            '45ddda10515475f981525604d674696b55dfb6e702b5bb43723a02b42a9fef93'
            'e3a7329a13b26b45ec39fc604b61f3d80657746e94de1c706ab47b560fdb0c26'
            'fcf483e84af7fb3445d7526bc9f1fc3843791b5ff4ed7a4e12a82fd30539192b'
            'dddb9fddafc836db7a00f78cdebc61ac86998895c1039288f41709c71ccc9c2b'
            '194104efba9bcbf8ec0bd9ac1b145017d459b734afbc8bbfc37e361efd467a96'
            '6491c96dd0e98b6f5a45c4f0183eabcf2fb54728c81e793910c19d4107606bb1')

prepare() {
  cd xinit-${pkgver}
  patch -Np1 -i ../06_move_serverauthfile_into_tmp.diff
  patch -Np1 -i ../fs25361.patch
  patch -Np1 -i ../cpp-4.8-compat.patch
  patch -Np1 -i ../fix-globals-shadowing.patch
  sed -i -e 's/XSLASHGLOB.sh/XSLASHGLOB/' xinitrc.cpp
}

build() {
  cd xinit-${pkgver}
  autoreconf -fi
  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd xinit-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/skel"
  install -m644 "${srcdir}/xinitrc" "${pkgdir}/etc/skel/.xinitrc"
  install -m644 "${srcdir}/xsession" "${pkgdir}/etc/skel/.xsession"
  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
