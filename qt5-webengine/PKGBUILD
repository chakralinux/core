
pkgname=qt5-webengine
pkgver=5.11.1
pkgrel=1
arch=('x86_64')
url='http://qt-project.org/'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss'
         'libevent' 'snappy' 'nss' 'protobuf' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx')
makedepends=('python2' 'git' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools')
groups=('qt5')
options=('debug')
_pkgfqn="${pkgname/5-/}-everywhere-src-${pkgver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/submodules/${_pkgfqn}.tar.xz"
         qtwebengine-harmony.patch qtwebengine-ffmpeg4.patch
         qtwebengine-wayland.patch::"http://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=a66d4cd8")
md5sums=('75d2ff31addba4ec41981b0f459cc587'
         '254061cb349cff714230b7cca83b0a4c'
         '2835f9fe8097ff3d6098812c70f48044'
         '04360a0896a267ee6b32df946db8202f')

prepare() {
  mkdir -p build

  # Hack to force using python2
  mkdir -p bin
  ln -s /usr/bin/python2 bin/python

  cd ${_pkgfqn}

  # FreeType 2.8.1
  patch -Np1 -i ../qtwebengine-harmony.patch
  # Fix openGL detection on Wayland
  patch -p1 -i ../qtwebengine-wayland.patch

  cd src/3rdparty/chromium
  # Fix build with ffmpeg 4
  patch -p2 -i "$srcdir"/qtwebengine-ffmpeg4.patch
}

build() {
  cd build

  export PATH="$srcdir/bin:$PATH"
  qmake-qt5 ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name *.prl \
    -exec sed -i -e /^QMAKE_PRL_BUILD_DIR/d {} ;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
