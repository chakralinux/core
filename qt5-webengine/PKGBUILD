source ../qt5.conf

pkgname=qt5-webengine
pkgver=${QSubVersion}
pkgrel=2
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 'libvpx' 
         'libevent' 'libsrtp' 'snappy' 'nss' 'protobuf' 'libxslt' 'libxdamage' 'minizip' 'ffmpeg')
         # namcap note: libxdamage is needed for nvidia users
makedepends=('python2' 'git' 'gperf' 'jsoncpp' 'ninja')
groups=('qt5')
options=('debug')
source=( $(getSrc ${pkgname}))
md5sums=( $(getSum ${pkgname}) )

prepare() {
  mkdir -p build

  # Hack to force using python2
  mkdir -p bin
  ln -s /usr/bin/python2 bin/python

  cd $(getPkgName ${pkgname})

}

build() {
  cd build

  export PATH="$srcdir/bin:$PATH"
  qmake-qt5 ../$(getPkgName ${pkgname}) -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker
#   -webengine-icu
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  qtprlfix

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
