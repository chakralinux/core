source ../qt5.conf

pkgname=qt5-webengine
pkgver=${QSubVersion}
pkgrel=1
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'libxtst' 'libxcursor' 'libpulse' 'pciutils' 'libxss' 'libvpx' 'opus'
         'libevent' 'libsrtp' 'jsoncpp' 'libwebp' 'snappy' 'nss' 'libxml2' 'libxslt' 'protobuf' 'libproxy') # minizip
makedepends=('python2' 'git' 'gperf')
groups=('qt5')
options=('debug')
source=( $(getSrc ${pkgname}) 
        qtwebengine-5.7.0-icu58.patch
        qtbug-58488.patch::"https://github.com/qt/qtwebengine/commit/7e7dd262.patch")
md5sums=( $(getSum ${pkgname}) 
         '9d225d1bf83ea45dbf6556d30d35fcb8'
         '3762cbdbc6a752e4d876e048e5e16de6')

prepare() {
  mkdir -p build

  # Hack to force using python2
  mkdir -p bin
  ln -s /usr/bin/python2 bin/python

  cd $(getPkgName ${pkgname})

  # Fix build with ICU 58 (gentoo)
  #patch -p1 -i "$srcdir"/qtwebengine-5.7.0-icu58.patch

  # Prevent drop-down popups form stealing focus https://bugreports.qt.io/browse/QTBUG-58488
  patch -p1 -i ../qtbug-58488.patch
}

build() {
  cd build

  export PATH="$srcdir/bin:$PATH"
  qmake-qt5 WEBENGINE_CONFIG+=use_proprietary_codecs ../$(getPkgName ${pkgname})
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  qtprlfix

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
