#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=tar
pkgver=1.28
pkgrel=1
pkgdesc='Utility used to store, backup, and transport files'
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/tar/tar.html"
license=('GPL3')
groups=('base')
depends=('glibc' 'acl' 'attr')
options=('!emptydirs')
install=tar.install
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('49b6306167724fe48f419a33a5beb857'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/tar
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
