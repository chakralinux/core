pkgname=sord
pkgver=0.12.2
pkgrel=1
pkgdesc="A lightweight C library for storing RDF data in memory."
arch=('x86_64')
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('serd' 'pcre')
makedepends=('python2')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('55d5866650b92ab64eda08fcc73aa425')
 
build() {
  cd "$srcdir/$pkgname-$pkgver"
 
  # remove ldconfig
  sed -i "/ldconfig/d" wscript
 
  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man
  python2 ./waf
}
 
package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir" python2 ./waf install
 
  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
 
