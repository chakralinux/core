# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/sord

pkgname=sord
pkgver=0.14.0
pkgrel=1
pkgdesc="A lightweight C library for storing RDF data in memory."
arch=('x86_64')
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('serd' 'pcre')
makedepends=('python2')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('7ffda283ad0a5716f196f650edd25713')
 
build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove ldconfig --speps
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
