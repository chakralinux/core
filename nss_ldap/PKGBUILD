# Contribution from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/nss_ldap
pkgname=nss_ldap
pkgver=265
pkgrel=6
pkgdesc="The nss_ldap module provides the means for Linux and Solaris workstations to resolve the entities defined in RFC 2307 from LDAP directories."
arch=('x86_64')
url="http://www.padl.com/OSS/nss_ldap.html"
license=('LGPL')
depends=('libldap>=2.4.18' 'krb5')
backup=("etc/nss_ldap.conf")
source=("http://www.padl.com/download/${pkgname}-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/6a27f55c8370f8b49fd3c10d46214de9c9c37932/extra/nss_ldap/nss_ldap-265-glibc-2.16.patch"
        "https://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/6a27f55c8370f8b49fd3c10d46214de9c9c37932/extra/nss_ldap/nss_ldap-265-pthread.patch")
md5sums=('c1cb02d1a85538cf16bca6f6a562abe4'
         '49df78dc482f90f5c3d850a664308216'
         '38c881edcb8df6b426f8a9d8f269d567')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#41068 
  patch -p0 -i ../nss_ldap-265-glibc-2.16.patch
  patch -p0 -i ../nss_ldap-265-pthread.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
       --prefix=/usr \
       --with-ldap-conf-file=/etc/nss_ldap.conf \
       --with-ldap=openldap \
       --sysconfdir=/etc \
       --mandir=/usr/share/man \
       --enable-schema-mapping \
       --enable-rfc2307bis \
       --enable-configurable-krb5-ccname-gssapi 
  env PATH=`pwd`:"$PATH" make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # issue with /usr/lib move
  rm -rf "${pkgdir}/usr/usr"
}
