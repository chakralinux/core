#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Pulphix <crimea.v@libero.it>

pkgname=gamin
pkgver=0.1.10
pkgrel=3
pkgdesc='File and directory monitoring system defined to be a subset of the FAM (File Alteration Monitor).'
url='http://www.gnome.org/~veillard/gamin'
license=('GPL2')
arch=('x86_64')
depends=('glib2')
makedepends=('python2')
optdepends=('python2: for the Python module.')
provides=('fam')
conflicts=('fam')
source=("http://www.gnome.org/~veillard/$pkgname/sources/$pkgname-$pkgver.tar.gz"
        'fix-deprecated-const.patch'
        '18_gam_server_deadlocks.patch'
        '0001-Poll-files-on-nfs4.patch')
md5sums=('b4ec549e57da470c04edd5ec2876a028'
         'f679aeb48fe9dd376c8828cc9b6941ab'
         '4784359a3206bfa3c0dce1c23468f87f'
         '6711cd0b1e8d2ad038c5761715d084a5')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  # https://bugs.archlinux.org/task/33642
  patch -Np1 -i ../18_gam_server_deadlocks.patch

  patch -Np1 -i ../fix-deprecated-const.patch
  patch -Np1 -i ../0001-Poll-files-on-nfs4.patch

  # Python 2.
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' python/gamin.py
}

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure \
    --disable-debug \
    --disable-debug-api \
    --disable-static \
    --libexecdir=/usr/lib/gamin \
    --prefix=/usr \
    --with-python=/usr/bin/python2 \
    --with-threads
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
