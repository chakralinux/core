# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: abveritas@chakraproject.org
# contributor: David Sotelo <dvsotelo at gmail.com>
# contributor: Nuno Araujo <nuno.araujo@russo79.com>

pkgname=qpdf
pkgver=5.1.1
pkgrel=2
pkgdesc="QPDF: A Content-Preserving PDF Transformation System"
arch=('x86_64')
url="http://qpdf.sourceforge.net/"
license=('custom:Artistic-2.0')
depends=('pcre' 'perl')
makedepends=('make')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('378bfd1c2ee15d7815f2f4b1a50fa6e0')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check # passes all
}

package() {
  cd "${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}/" install

  mkdir -m755 -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 Artistic-2.0 ${pkgdir}/usr/share/licenses/${pkgname}/
}
