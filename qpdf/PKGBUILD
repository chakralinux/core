# maintainer: almack@chakraos.org

pkgname=qpdf
pkgver=5.1.2
pkgrel=1
pkgdesc="QPDF: A Content-Preserving PDF Transformation System"
arch=('x86_64')
url="http://qpdf.sourceforge.net/"
license=('custom:Artistic-2.0')
depends=('pcre' 'perl')
makedepends=('make')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('0bd15ef5eea5f628951ab456c84e78ec')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check # passes all
}

package() {
  cd "${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}/" install

  mkdir -m755 -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 Artistic-2.0 ${pkgdir}/usr/share/licenses/${pkgname}/
}