pkgname=qpdf
pkgver=8.2.1
pkgrel=1
pkgdesc="QPDF: A Content-Preserving PDF Transformation System"
arch=('x86_64')
url="https://qpdf.sourceforge.net/"
license=('custom:Artistic-2.0')
depends=('libtiff')
optdepends=('perl: required for fix-qdf')
checkdepends=('perl')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a11c1f99753934be7d60c0ab43821bbc')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}/" install

  mkdir -m755 -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 {Artistic-2.0,LICENSE.txt} ${pkgdir}/usr/share/licenses/${pkgname}/
}
