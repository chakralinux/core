#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=gnutls
pkgver=2.12.6.1
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=(i686 x86_64)
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
install=gnutls.install
options=('libtool' 'zipman')
depends=('gcc-libs>=4.4.3' 'libtasn1>=2.5' 'readline>=6.0' 'zlib' 'libgcrypt>=1.4.5')
source=(ftp://ftp.gnu.org/gnu/gnutls/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e724e37f96984f3b0a215c11f03729bd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr \
	--with-zlib \
	--with-libgcrypt \
	--disable-static \
	--disable-guile
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #make check # passes all  || /bin/true
  make check || /bin/true # see https://lists.gnu.org/archive/html/gnutls-devel/2011-06/msg00007.html
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
