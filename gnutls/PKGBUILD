# Maintainer: almack[at]chakraos[dot]org>
 
pkgname=gnutls
pkgver=3.2.20
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('x86_64')
license=('GPL3' 'LGPL2.1')
url="http://www.gnu.org/software/gnutls/"
install=gnutls.install
depends=('gcc-libs' 'libtasn1' 'readline' 'zlib' 'libgcrypt' 'texinfo' 'nettle')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.2/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('637ef52191bf87a597240a49cc533972'
         'SKIP')
 
build() {
  cd ${pkgname}-${pkgver}
 
  ./configure --prefix=/usr --with-zlib --disable-guile --disable-valgrind-tests --disable-static
  make
}
 
check() {
  cd ${pkgname}-${pkgver}
  make -j1 check
}
 
package() {
  cd ${pkgname}-${pkgver}
 
  make DESTDIR="${pkgdir}" install
}
