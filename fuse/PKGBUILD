#
# Platform Packages for Chakra
#
# Drake Justice <djustice[at]chakra-linux[dot]org>
# Contributors from Arch:
# Ronald van Haren <ronald.archlinux.org>
# Tom Gundersen <teg@jklm.no>
#  Mark Rosenstand <mark@archlinux.org>

pkgname=fuse
pkgver=2.9.2
pkgrel=2
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i686' 'x86_64')
url="http://fuse.sourceforge.net/"
license="GPL2"
depends=('glibc')
makedepends=('pkgconfig')
backup=(etc/fuse.conf)
install=fuse.install
options=(!libtool)
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'fuse.conf')
md5sums=('7d80d0dc9cc2b9199a0c53787c151205'
         'c9457cf5b2196da67d5ac816d1c86a4f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix building with glibc-2.14
  sed -i '1i#define _GNU_SOURCE' util/fusermount.c

  ./configure --prefix=/usr --libdir=/usr/lib --enable-lib \
              --enable-util --bindir=/bin

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make install DESTDIR="${pkgdir}"

  # static device nodes will cause collision.
  rm -rf "${pkgdir}/dev"

  # Remove init script in wrong path
  rm -rf "${pkgdir}/etc/init.d"

  # install sample config file
  install -D -m644 "${srcdir}/fuse.conf" "${pkgdir}/etc/fuse.conf"

  # remove udev rules (is in the udev package}
  rm -rf ${pkgdir}/etc/udev
}

