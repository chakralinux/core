#
# Platform Packages for Chakra
#
# Drake Justice <djustice[at]chakra-linux[dot]org>
# Contributors from Arch:
# Ronald van Haren <ronald.archlinux.org>
# Tom Gundersen <teg@jklm.no>
#  Mark Rosenstand <mark@archlinux.org>

pkgname=fuse
pkgver=2.9.3
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i686' 'x86_64')
url="http://fuse.sourceforge.net/"
license=('GPL2')
depends=('glibc')
makedepends=('pkgconfig')
backup=(etc/fuse.conf)
install=fuse.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'fuse.conf')
sha1sums=('94bd1974a9f2173ac3c2cf122f9fa3c35996b88e'
          '3b42e37a741d4651099225987dc40e7f02a716ad')

prepare() {
  cd "$pkgname-$pkgver"

  # fix building with glibc-2.14
  sed -i '1i#define _GNU_SOURCE' util/fusermount.c 
  
  sed -i "/MOUNT_FUSE_PATH=/s#/sbin#/usr/bin#" configure
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --libdir=/usr/lib \
              --enable-lib --enable-util --disable-example
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR=${pkgdir} install

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -rf ${pkgdir}/etc/init.d

  # install sample config file
  install -Dm644 ${srcdir}/fuse.conf ${pkgdir}/etc/fuse.conf

  # remove udev rules (is in the udev package}
  rm -rf ${pkgdir}/etc/udev

  # static device nodes are handled by udev
  rm -rf ${pkgdir}/dev
}
