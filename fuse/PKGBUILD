#
# Chakra Package
#
# Drake Justice <djustice[at]chakra-linux[dot]org>

pkgname=fuse
pkgver=2.9.1
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i686' 'x86_64')
url="http://fuse.sourceforge.net/"
license="GPL2"
depends=('glibc')
makedepends=('pkgconfig')
backup=(etc/fuse.conf etc/udev/rules.d/99-fuse.rules)
install=fuse.install
options=(!libtool)
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'fuse.conf'
        'fuse.rc.d')
md5sums=('c646133c7bbf8ad9d2ced0888dc4a319'
         'c9457cf5b2196da67d5ac816d1c86a4f'
         'c3c81ea3da48860529d547b6ceffce9c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-lib \
              --enable-util \
  || return 1

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make install DESTDIR="${pkgdir}"

  # static device nodes will cause collision.
  rm -rf "${pkgdir}/dev"
 
  # Remove init script in wrong path
  rm -rf "${pkgdir}/etc/init.d"
  install -D -m755 "${srcdir}/fuse.rc.d" \
                   "${pkgdir}/etc/rc.d/fuse"

  # install sample config file
  install -D -m644 "${srcdir}/fuse.conf" \
                   "${pkgdir}/etc/fuse.conf"
}

# vim:set ts=2 sw=2 et:
