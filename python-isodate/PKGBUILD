# $Id$
# Maintainer:

pkgbase=isodate
pkgname=('python2-isodate' 'python3-isodate')
pkgver=0.6.0
pkgrel=1
pkgdesc="An ISO 8601 date/time/duration parser and formater"
arch=('any')
url="http://pypi.python.org/pypi/isodate"
license=('BSD')
makedepends=('python2' 'python3')
checkdepends=('python2-setuptools' 'python3-setuptools')
source=("https://files.pythonhosted.org/packages/source/i/isodate/isodate-$pkgver.tar.gz")
sha1sums=('2c69e08c9894a9cd96c627146463260f5731df62')

build() {
  cp -r "$pkgbase-$pkgver" "$srcdir/python2"
}

check() {
  cd "$srcdir/python2"
  python2 setup.py test
  cd "$srcdir/$pkgbase-$pkgver"
  python3 setup.py test
}

package_python2-isodate() {
  depends=('python2')

  cd "$srcdir/python2"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  head -26 src/isodate/isodates.py > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python3-isodate() {
  depends=('python3')

  cd "$srcdir/$pkgbase-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
  find "$pkgdir/" -type d -empty -delete
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  head -26 src/isodate/isodates.py > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
