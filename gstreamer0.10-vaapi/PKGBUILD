pkgname=gstreamer0.10-vaapi
_pkgname=gstreamer-vaapi
epoch=2
pkgver=0.5.9
pkgrel=1
pkgdesc="GStreamer Multimedia Framework VA Plugins"
arch=(x86_64)
license=(LGPL)
url="http://www.freedesktop.org/software/vaapi/releases/gstreamer-vaapi/"
depends=(gstreamer0.10-base-plugins gstreamer0.10-bad-plugins libva libxrandr libvpx)
makedepends=(libva libxrandr libvpx)
options=(!emptydirs)
source=($url/$_pkgname-$pkgver.tar.bz2)
sha1sums=('14f476f834a15422ecfa6a472df1b18ac2a0e50f')

prepare() {
  mkdir build
}

build() (
  cd build
  ../$_pkgname-$pkgver/configure \
    --prefix=/usr \
    --disable-static \
    --disable-builtin-libvpx \
    --disable-wayland \
    --with-gstreamer-api=0.10
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
)

check() {
  make -C build -k check
}

package_gstreamer0.10-vaapi() {
  make -C build DESTDIR="$pkgdir" install
}
