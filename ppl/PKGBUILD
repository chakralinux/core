#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=ppl
pkgver=1.0
pkgrel=3
pkgdesc="A modern library for convex polyhedra and other numerical abstractions."
arch=('i686' 'x86_64')
url="http://bugseng.com/products/ppl"
license=('GPL3')
depends=('gmp>=5.1')
options=('!docs' '!libtool')
source=(http://bugseng.com/products/ppl/download/ftp/releases/$pkgver/ppl-$pkgver.tar.gz{,.sign}
        "ppl-fix-gmp-5.1.0.patch")
md5sums=('c4566928c6b7bb63e95f96d1a7b5cb4c'
         'SKIP'
         '781098eb6da66a4567a9f6fc758fffc8')

prepare() {
  cd ${srcdir}/ppl-${pkgver}
  patch -p0 < ${srcdir}/ppl-fix-gmp-5.1.0.patch
}

build() {
  cd $srcdir/ppl-$pkgver
  ./configure --prefix=/usr --enable-interfaces="c,cxx"
  make
}

check() {
  cd $srcdir/ppl-$pkgver
  make check
}

package() {
  cd $srcdir/ppl-$pkgver
  make DESTDIR=$pkgdir install
}
