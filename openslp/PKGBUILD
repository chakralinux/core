#
# Platform Packages for Chakra, part of chakra-project.org
# maintainer <inkane@chakra-project.org>
# contributor <abveritas[at]chakra-project[dot]org

pkgname=openslp
pkgver=2.0.0
pkgrel=1
pkgdesc="Open-source implementation of Service Location Protocol"
arch=('x86_64')
url="http://www.openslp.org"
license=('BSD')
depends=('glibc' 'bash' 'openssl')
backup=('etc/slp.conf' 'etc/slp.reg' 'etc/slp.spi')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "makefile.patch"
        'openslp.service')
md5sums=('18cf7940bcc444e32592cf34e84f833f'
         '996b9f8371290b7a8c6b1c470734bdd2'
         '296dc3c8f75e7a1823fcb9dd97ea0971')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
   
   # fix package bug, tries to create director /usr/var/log
   patch -p1 -i ${srcdir}/makefile.patch

  ./configure --prefix=/usr \
    --disable-static \
    --enable-slpv1 \
    --enable-slpv2-security
  make 
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} DOC_DIR=/usr/share/doc/openslp-${pkgver} install 
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -D -m644 $srcdir/openslp.service ${pkgdir}/usr/lib/systemd/system/openslp.service
}
