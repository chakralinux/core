#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=perl-digest-hmac
_realname=Digest-HMAC
pkgver=1.02
pkgrel=2
pkgdesc="Perl Module: Keyed-Hashing for Message Authentication."
arch=(any)
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-digest-sha1' 'perl>=5.10.0')
options=(!emptydirs)
replaces=('digest-hmac')
provides=('digest-hmac')
source=(http://www.cpan.org/authors/id/G/GA/GAAS/${_realname}-${pkgver}.tar.gz)
md5sums=('64c4b247d83cd64ec32aa22bf58a709b')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}

  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}