# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Armin K. <krejzi at email dot com>

pkgname=ocaml-ctypes
pkgver=0.11.2
pkgrel=1
pkgdesc="Library for binding to C libraries using pure OCaml"
arch=('x86_64')
url="https://github.com/ocamllabs/ocaml-ctypes"
license=('MIT')
depends=('ocaml' 'libffi')
makedepends=('ocaml-findlib')
options=('!strip' '!makeflags')
source=($pkgname-$pkgver::https://github.com/ocamllabs/ocaml-ctypes/archive/$pkgver.tar.gz)
sha256sums=('708e2c5501ba00b3ed6726a7da1c2b58f607859e8e1cc0381eefd222510488d6')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  local _destdir="$pkgdir$(ocamlfind printconf destdir)"
  install -d "$_destdir/stublibs"

  make OCAMLFIND_DESTDIR="$_destdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
