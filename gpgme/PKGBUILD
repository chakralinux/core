
pkgname=gpgme
pkgver=1.8.0
pkgrel=1
pkgdesc="A C wrapper library for GnuPG"
arch=('x86_64')
url="http://www.gnupg.org/related_software/gpgme/"
license=('LGPL')
makedepends=('libgpg-error' 'gnupg' 'qt5-base')
depends=('libgpg-error' 'gnupg>=2')
options=('!emptydirs')
source=(https://www.gnupg.org/ftp/gcrypt/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2{,.sig}
libsuffix.patch::"https://git.gnupg.org/cgi-bin/gitweb.cgi?p=gpgme.git;a=patch;h=572c1aac107125ce62230251713349348373db5a")
md5sums=('722a4153904b9b5dc15485a22d29263b'
         'SKIP'
         'e7f9eec6d2dde8d9cf03cd8596a1f6aa')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

prepare() {
 cd ${pkgbase}-${pkgver}
 patch -Np1 -i ${srcdir}/libsuffix.patch
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --disable-fd-passing --disable-static \
              --disable-gpgsm-test
  make
}

check() {
  cd ${pkgbase}-${pkgver}
  make check
}

package() {
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
