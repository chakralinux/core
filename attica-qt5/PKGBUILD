# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=attica-qt5
_pkgname=attica
pkgver=5.48.0
pkgrel=1
arch=('x86_64')
url='https://projects.kde.org/projects/kdesupport/${_pkgname}'
pkgdesc='A Qt5 library that implements the Open Collaboration Services API'
license=('LGPL')
depends=("qt5-base")
makedepends=("extra-cmake-modules>=5.48.0")
checkdepends=("cmake")
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('087fef3a77b5193eca081730f9bed181e1ffbd36a5a09352e3233a58df1841e0'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON

  make
}

# check() {
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
