pkgname=gst-vaapi
_pkgname=gstreamer-vaapi
pkgver=0.7.0
pkgrel=2
pkgdesc="GStreamer Multimedia Framework VA Plugins"
arch=(x86_64)
license=(LGPL)
url="http://www.freedesktop.org/software/vaapi/releases/gstreamer-vaapi/"
depends=(gst-plugins-base gst-plugins-bad libva libxrandr libvpx)
makedepends=(libva libxrandr libvpx)
options=(!emptydirs)
source=($url/$_pkgname-$pkgver.tar.bz2)
sha1sums=('053a7af120c72fda0b46450c4a94a752abf64a74')

prepare() {
  mkdir build
}

build() (
  cd build
  ../$_pkgname-$pkgver/configure \
    --prefix=/usr \
    --disable-static \
    --disable-builtin-libvpx
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
)

check() {
  make -C build -k check
}

package() {
  make -C build DESTDIR="$pkgdir" install
}
