pkgbase=gstreamer-vaapi
pkgname=(gst-vaapi gstreamer0.10-vaapi)
pkgver=0.7.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework VA Plugins"
arch=(x86_64)
license=(LGPL)
url="http://www.freedesktop.org/software/vaapi/releases/gstreamer-vaapi/"
makedepends=(gst-plugins-base gst-plugins-bad gstreamer0.10-base-plugins gstreamer0.10-bad-plugins
             libva libxrandr libvpx)
options=(!emptydirs)

source=($url/$pkgbase-$pkgver.tar.bz2)
sha1sums=('053a7af120c72fda0b46450c4a94a752abf64a74')

prepare() {
  mkdir build build-0.10
}

_build() (
  cd "$1"; shift
  ../$pkgbase-$pkgver/configure --prefix=/usr \
    --disable-static \
    --disable-builtin-libvpx \
    "$@"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
)

build() {
  _build build
  _build build-0.10 --with-gstreamer-api=0.10 --disable-wayland
}

check() {
  make -C build -k check
  make -C build-0.10 -k check
}

package_gst-vaapi() {
  depends=(gst-plugins-base gst-plugins-bad libva libxrandr libvpx)
  make -C build DESTDIR="$pkgdir" install
}

package_gstreamer0.10-vaapi() {
  depends=(gstreamer0.10-base-plugins gstreamer0.10-bad-plugins libva libxrandr libvpx)
  make -C build-0.10 DESTDIR="$pkgdir" install
}
