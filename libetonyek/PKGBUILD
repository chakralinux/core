pkgname=libetonyek
pkgver=0.1.9
pkgrel=1
pkgdesc='Library and a set of tools for reading and converting Apple Keynote presentations'
arch=('x86_64')
url='https://wiki.documentfoundation.org/DLP/Libraries/libetonyek'
license=('MPL')
depends=('libxml2' 'zlib' 'libwpd' 'boost-libs' 'librevenge' 'liblangtag')
makedepends=('boost' 'cppunit' 'gperf' 'doxygen' 'glm' 'mdds')
source=(https://dev-www.libreoffice.org/src/${pkgname}/${pkgname}-${pkgver}.tar.xz)
md5sums=('b891c14c5233f93fb7db052b5762a692')


build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-mdds=1.4
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
