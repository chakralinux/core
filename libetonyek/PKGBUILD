pkgname=libetonyek
pkgver=0.1.6
pkgrel=4
pkgdesc='Library and a set of tools for reading and converting Apple Keynote presentations'
arch=('x86_64')
url='https://wiki.documentfoundation.org/DLP/Libraries/libetonyek'
license=('MPL')
depends=('libxml2' 'zlib' 'libwpd' 'boost-libs' 'librevenge' 'liblangtag')
makedepends=('boost' 'cppunit' 'gperf' 'doxygen' 'glm' 'mdds')
source=(http://dev-www.libreoffice.org/src/${pkgname}/${pkgname}-${pkgver}.tar.xz
        0001-Allow-building-with-mdds-1.2.patch
        0001-drop-test-that-violates-Unicode-tr-35.patch)
md5sums=('dfe25b0c7739283a5c61a930ea1f9bf8'
         '0635b9c27717596828ac32d9e0521eb3'
         'aa201a5d1058122f523685c3f47bb5ae')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../0001-Allow-building-with-mdds-1.2.patch
  patch -Np1 -i ../0001-drop-test-that-violates-Unicode-tr-35.patch
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-mdds=1.2
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
