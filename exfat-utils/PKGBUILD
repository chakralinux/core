pkgname=exfat-utils
pkgver=1.2.8
pkgrel=1
pkgdesc="Utilities for exFAT file system"
arch=('x86_64')
url='https://github.com/relan/exfat'
license=('GPL2')
depends=('glibc' 'fuse2')
conflicts=('fuse-exfat')
provides=('fuse-exfat')
replaces=('fuse-exfat')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/relan/exfat/archive/v${pkgver}.tar.gz)
sha256sums=('778474bfbcd92e35bba5b88bd7ad3f6400328256636abb9031bfcc1ce15ed887')
 
prepare() {
  cd exfat-${pkgver}
  autoreconf -fiv
}

build() {
  cd exfat-$pkgver

  ./configure --prefix=/usr \
              --sbindir=/usr/bin
 
  make CCFLAGS="${CFLAGS} ${CPPFLAGS} -std=c99" LINKFLAGS="${LDFLAGS}"
}
 
package() {
  cd exfat-$pkgver
 
  make DESTDIR=$pkgdir install

  install -Dm 644 */*.8 -t "${pkgdir}/usr/share/man/man8"
}
