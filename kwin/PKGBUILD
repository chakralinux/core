# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kwin
source ../plasma.conf

pkgname=kwin
pkgver=${PSubVersion}
pkgrel=2
pkgdesc='KDE Window manager'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kwin'
license=('LGPL')
depends=('qt5-multimedia' 'plasma-framework' 'knewstuff' 'libxcursor' 'kinit'
         'hicolor-icon-theme' 'libepoxy' 'kwayland' 'kdecoration' 'libinput' 'xcb-util-cursor' 'kscreenlocker' 'libxkbcommon')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools')
groups=('plasma')
install=${pkgname}.install
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PVersion}.tar.xz"{,.sig})
sha256sums=( $(getSum ${pkgname}) 
             'SKIP' )
source+=(cmake3.10.patch::https://github.com/KDE/kwin/commit/cd544890ced4192d07467c89e23adbb62d8cea5c.patch)
sha256sums+=('42105c4cd898237851c1631ecea27e0ef96f230ad6f55e9defcf2e499d836ac4')
validpgpkeys=(${Pvalidpgpkeys[@]})

prepare() {
  mkdir -p build
  
  cd ${pkgname}-${pkgver}
  # fix build with cmake 3.10 (https://github.com/KDE/kwin/commit/cd544890ced4192d07467c89e23adbb62d8cea5c)
  patch -p1 -i ${srcdir}/cmake3.10.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
