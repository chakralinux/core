

pkgname=kxmlrpcclient
pkgver=5.51.0
pkgrel=1
pkgdesc='Interaction with XMLRPC services'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/pim/kxmlrpcclient'
license=('LGPL')
depends=('kio')
makedepends=("extra-cmake-modules>=5.51.0")
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('87b4974fc87f40bf5c51e81c22f372b855b0c0eecc32908f1817cc563abd4e3b'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
 
