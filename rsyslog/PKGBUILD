#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: abveritas[at]chakra-project[dot]org>
# maintainer: Mateusz Krawczuk <willingmagic[at]gmail[dot]com>

pkgname=rsyslog
pkgver=6.4.2
pkgrel=1
pkgdesc="An enhanced multi-threaded syslogd with a focus on security and reliability"
url="http://www.rsyslog.com/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('zlib' 'libee')
makedepends=('postgresql-libs>=8.4.1' 'libmysqlclient' 'net-snmp' 'gnutls')
optdepends=('postgresql-libs: PostgreSQL Database Support'
            'libmysqlclient: MySQL Database Support'
            'gnutls'
            'systemd')
backup=('etc/rsyslog.conf' 
        'etc/logrotate.d/rsyslog'
        'etc/conf.d/rsyslog')
options=('strip' 'zipman' '!libtool')
source=("http://www.rsyslog.com/files/download/rsyslog/rsyslog-$pkgver.tar.gz"
        'rsyslog'
        'rsyslog.logrotate'
        'rsyslog.conf.d')
sha256sums=('ca92434c87f7390a5ea7ce6aea94c6cb05474bf778ec5d8ebc0b9a50f321749c'
            'ca6ab11c33f6b11236a9d04f8277d101384b003d09612349734ba1a66e8d0d9f'
            '12f400a97a2a46ccbc068fa5b9bafc70bddefa30b10dcc41df5c04c9c82f851d'
            '64fccf3ea60fce51749d154b5ac91134d0bc7d062ef83ca8ced96f7b7dd43522')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-mysql \
              --enable-pgsql \
              --enable-mail \
              --enable-imfile \
              --enable-snmp \
              --enable-gnutls \
              --enable-inet \
              --with-systemdsystemunitdir=/usr/lib/systemd/system
  echo "rsyslogd_LDADD += \$(LIBESTR_LIBS) -lm" >>tools/Makefile
  make
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # Install Daemons and Configuration Files
  install -D -m0755 "${srcdir}/${pkgname}" "${pkgdir}/etc/rc.d/${pkgname}d"
  install -D -m0644 "${pkgname}.conf" "${pkgdir}/etc/${pkgname}.conf"
  install -D -m0644 "$srcdir/${pkgname}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgname}"
  install -D -m0644 "${srcdir}/${pkgname}.conf.d" "${pkgdir}/etc/conf.d/${pkgname}"
  
  # fix location of systemctl and remove start precondition
  sed -i "${pkgdir}/usr/lib/systemd/system/rsyslog.service" \
    -e 's@/bin/systemctl@/usr&@' \
    -e '/^ExecStartPre/d'
}

