# $Id: PKGBUILD 79835 2010-05-08 11:38:48Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgbase=libgsf
pkgname=('libgsf')
pkgver=1.14.24
pkgrel=1
arch=(i686 x86_64)
url="http://www.gnome.org/"
license=('GPL' 'LGPL')
makedepends=('libxml2>=2.7.7' 'glib2>=2.24.1' 'bzip2' 'intltool' 'pkgconfig' 'python2' 'libbonobo>=2.24.3' 'gconf>=2.28.1' 'pygtk')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/1.14/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('975fc00a1332167e2baf649f8e2637884f798db1c8bb04abece2d5141663abff')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --mandir=/usr/share/man --disable-static || return 1
  make || return 1
}

package_libgsf() {
  pkgdesc="The GNOME Structured File Library is a utility library for reading and writing structured file formats"
  depends=('libxml2>=2.7.7' 'glib2>=2.24.1' 'bzip2')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1
  #All those removes will strip gnome specific stuff
  rm -f "${pkgdir}/usr/share/man/man1/gsf-office-thumbnailer.1"
  rm -f "${pkgdir}/usr/lib/libgsf-gnome"*
  rm -rf "${pkgdir}/usr/include/libgsf-1/gsf-gnome/"
  rm -rf "${pkgdir}/etc/gconf"
  rm -f "${pkgdir}/usr/lib/pkgconfig/libgsf-gnome-1.pc"
  rm -f "${pkgdir}/usr/bin/gsf-office-thumbnailer"
  rm -f "${pkgdir}"/usr/lib/python*/site-packages/gsf/gnomemodule.so
}
