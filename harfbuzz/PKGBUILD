# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# contributor Jan de Groot <jgc@archlinux.org>

pkgname=harfbuzz
pkgver=0.9.21
pkgrel=1
pkgdesc="OpenType text shaping engine."
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('icu' 'glib2' 'freetype2' 'graphite')
makedepends=('icu' 'cairo')
optdepends=('cairo: hb-view program')
options=(!libtool)
source=("http://www.freedesktop.org/software/harfbuzz/release/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('a0d36f883d98f2375f9b2a03c8a6c361b161a76d4b58ea9d08be59e39e73dae8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
	--with-glib \
	--with-freetype \
	--with-cairo \
	--with-icu \
	--with-graphite2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/harfbuzz/COPYING"
}
