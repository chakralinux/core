#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=libdrm
pkgver=2.4.33
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        no-pthread-stubs.patch
        COPYING)
md5sums=('eb2a76720af5051b1687328a2240daed'
         '304b2790d2098711d138a0b8c19df20c'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch"
  
#  libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr \
      --enable-udev \
      --enable-intel \
      --enable-radeon \
      --enable-vmwgfx-experimental-api \
      --enable-nouveau-experimental-api
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
