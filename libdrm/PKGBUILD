#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=libdrm
pkgver=2.4.37
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686' 'x86_64')
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind')
replaces=('libdrm-nouveau' 'libdrm-nouveau-lts')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=("http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'no-pthread-stubs.patch'
        'COPYING')
md5sums=('9765919c28d4a54887576db3680137cc'
         '71db2ef5000b61a8f37672d084ae509f'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/no-pthread-stubs.patch"
  
#  libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr \
      --enable-udev \
      --enable-vmwgfx-experimental-api 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
