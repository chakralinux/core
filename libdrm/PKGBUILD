# maintainer almack@chakraos.org

pkgname=libdrm
pkgver=2.4.54
pkgrel=3
pkgdesc="Userspace interface to kernel DRM services"
arch=('x86_64')
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('valgrind')
checkdepends=('cairo')
replaces=('libdrm-nouveau' 'libdrm-nouveau-lts')
url="http://dri.freedesktop.org/"

source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2
        COPYING)
md5sums=('56e98a9c2073c3fab7f95e003b657f46'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')

build() {
  cd $pkgname-$pkgver

  # pthread is useless in Linux
  sed -i "/pthread-stubs/d" configure.ac
  autoreconf --force --install
  
  ./configure --prefix=/usr --enable-udev --disable-cairo-tests
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}

