# Contributions from Arch:
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=libdvbpsi
pkgver=1.3.2
pkgrel=1
pkgdesc='Library designed for decoding and generation of MPEG TS and DVB PSI tables'
url='http://developers.videolan.org/libdvbpsi/'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
provides=('libdvbpsi.so')
source=(${pkgname}-${pkgver}.tar.bz2::https://code.videolan.org/videolan/libdvbpsi/repository/${pkgver}/archive.tar.bz2)
sha256sums=('573c29359354a38fa8e958c26cffe2a574a4aa73dcaf2215737326c9f95922fc')

prepare() {
  cd ${pkgname}-${pkgver}-*
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}-*
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}-*
  make DESTDIR="${pkgdir}" install
}
