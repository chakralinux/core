#
# Platform Packages for Chakra, part of chakra-project.org
#
# Maintainer: Francesco Marinucci < franzmari[at]chakra-project[dot]it>
# contributor Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-ugly
pkgver=1.4.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Ugly Plugins"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'libdvdread' 'lame' 'libmpeg2' 'a52dec'
         'libmad' 'libsidplay' 'libcdio' 'x264' 'opencore-amr')
options=('!emptydirs')
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('558146cb5ec8b313afe2113aafc3da85')

build() {
  cd $pkgname-$pkgver
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (Chakra GNU/Linux)" \
    --with-package-origin="http://www.chakraos.org/"
  make
}

check() {
  cd $pkgname-$pkgver
  
  make check
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR="${pkgdir}" install
}
