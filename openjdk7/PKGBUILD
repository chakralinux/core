# Maintainer: Daniele Cocca <jmc@chakra-project.org>

pkgname=openjdk7
_pkgdate="02_may_2012"
_pkgver="7u4"
_pkgbld="b22"
pkgver="${_pkgver}.${_pkgbld}"
pkgrel=1
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform."
arch=('i686' 'x86_64')
url="http://openjdk.java.net/projects/jdk7/"
license=('GPL')
depends=( 'alsa-lib' 'libpulse' 'giflib' 'libpng' 'freetype2')
makedepends=('openjdk6' 'apache-ant' 'zip' 'unzip' 'fastjar'
             'cpio' 'cups' 'orbit2')

# Source and MD5 sum can be found at http://jdk7.java.net/source.html
source=("http://www.java.net/download/openjdk/jdk${_pkgver}/promoted/${_pkgbld}/openjdk-${_pkgver}-fcs-src-${_pkgbld}-${_pkgdate}.zip")
md5sums=('37699588f783c07a71fbae00623889fa')

# CPU-dependant values
[ "${CARCH}" = 'i686' ] \
  && _cpuname=i586 \
  || _cpuname=amd64

build() {
  cd "${srcdir}/openjdk"

  . /etc/profile.d/apache-ant.sh
  . /etc/profile.d/openjdk6.sh

  # Fix options for parallel builds
  export ALT_PARALLEL_COMPILE_JOBS="${MAKEFLAGS/-j}"
  export HOTSPOT_BUILD_JOBS="${ALT_PARALLEL_COMPILE_JOBS}"

  # Fix the value of some environmental variables
  export LANG="C"
  export LC_ALL="C"
  export USER="chakra-buildbot"
  export MILESTONE="fcs"
  export SKIP_DEBUG_BUILD="true"
  export ALT_BOOTDIR="${JAVA_HOME}"
  export BUILD_NUMBER="${_pkgbld}"

  if [ "${CARCH}" != "$(uname -m)" ]; then
    export CROSS_COMPILE_ARCH="${_cpuname}"
    export ALT_COMPILER_PATH="$(dirname "$(which gcc)")"
  fi

  # Unset some harmful variables
  unset LD_PRELOAD MAKEFLAGS JAVA_HOME JDK_HOME

  # Apply some fixes, needed for a correct build in a Chakra environment
  sed -e 's,\$(UTILS_COMMAND_PATH)touch,touch,g' \
      -e 's,\$(USRBIN_PATH)gawk,gawk,g' \
      -e 's,SHARED_LIBRARY_FLAG = -shared -mimpure-text,SHARED_LIBRARY_FLAG = -shared,g' \
      -i jdk/make/common/shared/Defs-utils.gmk \
      -i jdk/make/common/shared/Compiler-gcc.gmk \
      -i corba/make/common/shared/Defs-utils.gmk \
      -i corba/make/common/shared/Platform.gmk

  # Test the environment and start the build
  . jdk/make/jdk_generic_profile.sh
  make sanity && make ALLOW_DOWNLOADS=true
}

package() {
  cd "${srcdir}/openjdk"

  install -d -m755 "${pkgdir}/opt"

  cp -r "build/linux-${_cpuname}/j2sdk-image" \
        "${pkgdir}/opt/openjdk7"
}

# vim:set ts=2 sw=2 et:
