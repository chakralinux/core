#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=vi
pkgver=050325
pkgrel=4
pkgdesc='The original ex/vi text editor.'
arch=('i686' 'x86_64')
url='http://ex-vi.sourceforge.net/'
license=('custom:ex')
depends=('ncurses')
groups=('base')
options=(force strip !libtool emptydirs)
source=(http://downloads.sourceforge.net/sourceforge/ex-vi/ex-${pkgver}.tar.bz2
        navkeys.patch increase-tube.patch exrc.sample preserve_dir.patch)
md5sums=('e668595254233e4d96811083a3e4e2f3' 'aac133930047eafa4f28736cfaf976b3'
         'e596e05a00a24187b576e389fa1de45d' 'd3b483c994d859232ce369b82a4b5668'
         '419a8755e0dd16e95542bc107e6d2b24')

build() {
  cd "${srcdir}/ex-${pkgver}"

  # apply patches
  patch -Np1 -i  ../navkeys.patch
  patch -Np0 -i ../increase-tube.patch
  # FS#20653
  patch -Np1 -i ../preserve_dir.patch

  make PREFIX=/usr LIBEXECDIR=/usr/lib/ex PRESERVEDIR=/var/lib/ex \
      'FEATURES=-DCHDIR -DFASTTAG -DUCVISUAL -DMB -DBIT8' TERMLIB=ncurses
}

package() {
  cd "${srcdir}/ex-${pkgver}"

  make PREFIX=/usr LIBEXECDIR=/usr/lib/ex PRESERVEDIR=/var/lib/ex INSTALL=/bin/install \
      DESTDIR="$pkgdir" install

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
