pkgname=qoauth-qt5
_pkgname=qoauth
pkgver=1.0.1
pkgrel=1
pkgdesc="Support with OAuth-powered network services"
url="http://github.com/ayoy/qoauth"
license=('GPL')
arch=('x86_64')
depends=('qca-qt5')
provides=('qoauth' 'qoauth-qt5')
conflicts=('qoauth')
replaces=('qoauth')
source=($_pkgname-$pkgver::"https://github.com/ayoy/qoauth/archive/v$pkgver.tar.gz"
        "qtstring-fix.patch")
md5sums=('255121fc0070a1fbed83703e39111ce4'
         '986bd219364bb44a17f7fe7c618ea37d')

prepare() {
  cd $_pkgname-$pkgver
  # Move libraries from /usr/lib64 to /usr/lib
  sed -i 's|lib64|lib|' src/src.pro
  patch -p1 -i ../qtstring-fix.patch
}

build() {
  cd $_pkgname-$pkgver  
  qmake-qt5
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install
 
}
