pkgname=gypsy
pkgver=0.9
pkgrel=1
pkgdesc="GPS multiplexing daemon"
arch=(x86_64)
url="http://gypsy.freedesktop.org/"
license=(GPL)
depends=(dbus-glib bluez bluez-libs libgudev)
makedepends=(libxslt)
source=("http://gypsy.freedesktop.org/releases/$pkgname-$pkgver.tar.gz"
        g_type_init_deprecation.patch)
md5sums=('e2d186df9c2cc3b70a027043e22acf1a'
         'f42f0bb01f8a278fce81d6f05305b9fb')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../g_type_init_deprecation.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
