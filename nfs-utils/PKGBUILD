#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer Samir Benmendil <ram-z[at]chakra-project[dot]org>
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=nfs-utils
pkgver=1.3.0
pkgrel=1
pkgdesc="Support programs for Network File Systems"
arch=('x86_64')
url='http://nfs.sourceforge.net'
license=('GPL')
backup=(etc/{exports,idmapd.conf,nfsmount.conf} etc/conf.d/{nfs-common.conf,nfs-server.conf})
depends=('glibc' 'e2fsprogs' 'rpcbind' 'libtirpc' 'librpcsecgss' 'nfsidmap'
         'libevent' 'libgssglue' 'device-mapper')
makedepends=('pkg-config' 'autoconf' 'automake' 'keyutils' 'sqlite3')
source=(http://downloads.sourceforge.net/project/nfs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2
	nfs-common.conf
	nfs-server.conf
	exports
	idmapd.conf
	nfs-utils-1.1.4-mtab-sym.patch
	nfs-utils.conf
        id_resolver.conf
        nfs-utils_env.sh
        nfs-utils-1.3.1-rc2.patch)
install=nfs-utils.install
md5sums=('3ac3726eda563946d1f44ac3e5b61d56'
         '31ca748f692c01d403dfe526c9527413'
         'a8974a59b375f6e6f883ddc9089a857f'
         'b777554461bd67c6029dab18c0a9115e'
         'eb4f4027fab6fc1201f1ca04f5954c76'
         '7674106eaaa4c149bccd4f05fe3604e9'
         '8ac484023d786766d287ccbe878ae4ba'
         '3139890830c3f6bee42d1757282be9c1'
         '517868c7475ff086f25c783eaceaf61c'
         '92423a1e833ba90fdc446aa2296ac7f0')

prepare() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -Np1 -i ../nfs-utils-1.1.4-mtab-sym.patch
  # fix segfaults, from fedora git
  patch -Np1 -i ../nfs-utils-1.3.1-rc2.patch
  ./autogen.sh
}


build() {
  cd $srcdir/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sbindir=/usr/sbin --enable-nfsv4 --enable-nfsv41 --enable-gss \
              --without-tcp-wrappers --with-statedir=/var/lib/nfs \
              --enable-ipv6 --sysconfdir=/etc --enable-libmount-mount \
              --with-gssglue \
              --enable-mountconfig --with-start-statd=/usr/bin/start-statd
  # move mount helpers to /usr/bin
  #sed -i -e 's#sbindir = /sbin#sbindir = /usr/bin#g' utils/mount/Makefile
  # move osd_login to /usr/bin
  #sed -i -e 's#sbindir = /sbin#sbindir = /usr/bin#g' utils/osd_login/Makefile
  # move nfsdcltrack to /usr/bin
  #sed -i -e 's#sbindir = /sbin#sbindir = /usr/bin#g' utils/nfsdcltrack/Makefile
  make 
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make DESTDIR=$pkgdir install

  #sed -i '1s/python$/python2/' "$pkgdir"/usr/bin/{nfsiostat,mountstats}
  install -D -m 644 utils/mount/nfsmount.conf "$pkgdir"/etc/nfsmount.conf

  # Configuration
  install -D -m 644 ../exports          "$pkgdir/"etc/exports
  install -D -m 644 ../idmapd.conf      "$pkgdir/"etc/idmapd.conf
  install -D -m 644 ../nfs-server.conf  "$pkgdir/"etc/conf.d/nfs-server.conf
  install -D -m 644 ../nfs-common.conf  "$pkgdir/"etc/conf.d/nfs-common.conf
  install -D -m 644 ../id_resolver.conf "$pkgdir"/etc/request-key.d/id_resolver.conf
  install -D -m 644 ../nfs-utils.conf   "$pkgdir/"usr/lib/modules-load.d/nfs-utils.conf
  install -D -m 755 ../nfs-utils_env.sh "$pkgdir"/usr/lib/systemd/scripts/nfs-utils_env.sh
  
  # systemd files
  for i in systemd/{*.service,*.mount,*.target}; do
    install -D -m 644 $i "$pkgdir/usr/lib/systemd/system/${i##*/}"
  done
  
  # directories
  mkdir "$pkgdir/"etc/exports.d
  mkdir -m 555 "$pkgdir/"var/lib/nfs/rpc_pipefs
  mkdir "$pkgdir/"var/lib/nfs/v4recovery
}
