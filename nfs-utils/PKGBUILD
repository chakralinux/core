#
# Core Packages for Chakra, part of chakraos.org
#
# maintainer Samir Benmendil <ram-z[at]chakraos[dot]org>


pkgname=nfs-utils
pkgver=1.3.3
pkgrel=1
pkgdesc="Support programs for Network File Systems"
arch=('x86_64')
url='http://nfs.sourceforge.net'
license=('GPL2')
backup=(etc/{exports,nfsmount.conf} etc/conf.d/{nfs-common.conf,nfs-server.conf})
depends=('glibc' 'e2fsprogs' 'rpcbind' 'librpcsecgss>=0.19-2' 'nfsidmap' 'libevent' 'device-mapper')
makedepends=('pkg-config' 'autoconf' 'automake' 'sqlite3')
source=(http://downloads.sourceforge.net/project/nfs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.{bz2,sign}
        nfs-common.conf
        nfs-server.conf
        exports
        nfs-utils.conf
        id_resolver.conf
        nfs-utils_env.sh
        ignore_unsupported_address_types_in_nfssvc_setfds.diff)
install=nfs-utils.install
optdepends=('sqlite3: for nfsdcltrack usage'
            'python2: for nfsiostat and mountstats usage')
sha1sums=('7c561e6a22a626aed93766bdb0c34e9a4e77b9e7'
          'SKIP'
          '5a2274542285330051dd9df8cdda23d68ee93c69'
          'a48b2e6f1b75fdcb84c929343e5068931a17c01c'
          '170a929d9c0f22edb13b656648cadf372efea841'
          'c109f2300f2d841104adb09fe76a5f2583015c72'
          '24b3c10b47dc120b2d252cf1e5001effa8f76a62'
          '96734598a0b851da10cb9e02afb52ec55fed6273'
          '0fa72f3e3485abf71b1199caa1e6089f9f129a77')
validpgpkeys=('E1B71E339E20A10A676F7CB69AFB1D681A125177') # Steve Dickson

prepare() {
  cd ${pkgname}-${pkgver}

  # fix IPv6-less kernels
  patch -Np1 -i "$srcdir"/ignore_unsupported_address_types_in_nfssvc_setfds.diff

  # fix hardcoded sbin path to our needs
  sed -i "s|sbindir = /sbin|sbindir = /usr/bin|g" utils/*/Makefile.am
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-gss \
    --without-tcp-wrappers \
    --with-statedir=/var/lib/nfs \
    --enable-ipv6 \
    --enable-libmount-mount \
    --enable-mountconfig \
    --with-start-statd=/usr/bin/start-statd
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" install

  sed -i '1s/python$/python2/' "$pkgdir"/usr/bin/{nfsiostat,mountstats}

  install -D -m 644 utils/mount/nfsmount.conf "$pkgdir"/etc/nfsmount.conf

  for i in systemd/{*.service,*.mount,*.target}; do
    install -D -m 644 $i "$pkgdir"/usr/lib/systemd/system/$(basename $i)
  done

  cd ..
  install -D -m 644 nfs-common.conf  "$pkgdir"/etc/conf.d/nfs-common.conf
  install -D -m 644 nfs-server.conf  "$pkgdir"/etc/conf.d/nfs-server.conf
  install -D -m 755 nfs-utils_env.sh "$pkgdir"/usr/lib/systemd/scripts/nfs-utils_env.sh
  install -D -m 644 exports          "$pkgdir"/etc/exports
  install -D -m 644 id_resolver.conf "$pkgdir"/etc/request-key.d/id_resolver.conf
  install -D -m 644 nfs-utils.conf   "$pkgdir"/usr/lib/modules-load.d/nfs-utils.conf
  mkdir "$pkgdir"/etc/exports.d
  mkdir -m 555 "$pkgdir"/var/lib/nfs/rpc_pipefs
  mkdir "$pkgdir"/var/lib/nfs/v4recovery
}
