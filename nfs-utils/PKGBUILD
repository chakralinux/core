#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer Samir Benmendil <ram-z[at]chakra-project[dot]org>
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=nfs-utils
pkgver=1.2.8
pkgrel=1
pkgdesc="Support programs for Network File Systems"
arch=('x86_64')
url='http://nfs.sourceforge.net'
license=('GPL')
backup=(etc/{exports,idmapd.conf,nfsmount.conf} etc/conf.d/{nfs-common.conf,nfs-server.conf})
depends=('glibc' 'e2fsprogs' 'rpcbind' 'libtirpc>=0.2.1' 'librpcsecgss>=0.19-2' 'nfsidmap'
         'libevent>=2.0.10' 'libgssglue' 'device-mapper')
makedepends=('pkg-config' 'autoconf' 'automake' 'keyutils' 'sqlite3')
source=("http://downloads.sourceforge.net/project/nfs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
	'exports'
	'idmapd.conf'
	'start-statd.patch'
	'nfs-utils-1.1.4-mtab-sym.patch'
	'nfs-common.conf'
	'nfs-server.conf'
	'rpc-gssd.service'
	'rpc-mountd.service'
	'rpc-svcgssd.service'
	'rpc-idmapd.service'
	'rpc-statd.service'
	'nfsd.service'
	'var-lib-nfs-rpc_pipefs.mount'
	'proc-fs-nfsd.mount'
	'blkmapd.service'
	'nfs-utils.conf')
md5sums=('6e7d97de51e428a0b8698c16ca23db77'
         'ff585faf410a62c4333a027c50b56bae'
         'eb4f4027fab6fc1201f1ca04f5954c76'
         'e9144277a89a620d9bc80413158a7d27'
         '7674106eaaa4c149bccd4f05fe3604e9'
         'f73f197a16b02c3e248488ec35c4cf43'
         '2bf71def3263325643a09458635520f0'
         '3694619813cebc5b4502b58f1992b442'
         '965311784d49a7d126d512cadbe91deb'
         'e05705d9ccccaeaeb1ecaee20adc05bc'
         '359d84a85f8781dc3f45f4cf1dbe4b53'
         'beff8a426b3b14c46e3e78a2a283e2ab'
         '5d33d2e754fd37280365b287603bac90'
         '1cd65909fa0983047f3f06a3ab352401'
         '987ffb1b3065100b41ff1f1d6996eb0c'
         '8ffc2ebe932d29efe17d6f3f23d5b975'
         '8ac484023d786766d287ccbe878ae4ba')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -p1 -i ../nfs-utils-1.1.4-mtab-sym.patch
  # Chakra (?) specific patch, according to Arch fixes /usr/bin in start-statd shell script
  # our patch adds a few more lines
  patch -p0 -i $srcdir/start-statd.patch

  ./configure --prefix=/usr --enable-nfsv4 --enable-nfsv41 --enable-gss \
              --without-tcp-wrappers --with-statedir=/var/lib/nfs \
              --enable-ipv6 --sysconfdir=/etc \
	      --enable-libmount-mount --enable-mountconfig \
              --with-gssglue \
              --enable-mountconfig

  make 
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  install -dm755 "${pkgdir}/sbin"
  make DESTDIR=$pkgdir install

  sed -i '1s/python$/python2/' "$pkgdir"/usr/sbin/{nfsiostat,mountstats}

  # Configuration
  install -D -m 644 ../exports "$pkgdir/"etc/exports
  install -D -m 644 ../idmapd.conf "$pkgdir/"etc/idmapd.conf
  install -D -m 644 ../nfs-server.conf "$pkgdir/"etc/conf.d/nfs-server.conf
  install -D -m 644 ../nfs-common.conf "$pkgdir/"etc/conf.d/nfs-common.conf
  install -D -m 644 utils/mount/nfsmount.conf "$pkgdir/"etc/nfsmount.conf
  # systemd files
  for i in ${srcdir}/*.{service,mount}; do
    install -D -m 644 $i "$pkgdir/usr/lib/systemd/system/${i##*/}"
  done
  install -D -m 644 ../nfs-utils.conf "$pkgdir/"usr/lib/modules-load.d/nfs-utils.conf
  # directories
  mkdir "$pkgdir/"etc/exports.d
  mkdir -m 555 "$pkgdir/"var/lib/nfs/rpc_pipefs
  mkdir "$pkgdir/"var/lib/nfs/v4recovery
}
