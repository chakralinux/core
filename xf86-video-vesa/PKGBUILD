# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributor: abveritas@chakra-project.org

pkgname=xf86-video-vesa
pkgver=2.3.2
pkgrel=1
pkgdesc="X.org vesa video driver"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('pkg-config' 'xorg-server>=1.12.3' 'xf86driproto' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.12.3')
groups=('xorg' 'xorg-video-drivers')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        revert-kernelcheck.patch
        git-fixes.patch)
sha256sums=('144a17ffae3c86603ddc4ae33521a52813498ee1f8213faa662dc4a8d6490ee3'
            'ef591a342cea65f1b08e84771ae0de84395c98ac8a71739dbf5c0a7f7a36543c'
            'beba7e8499b1d0b457762e28e7e413b223cbe87a84a39a3566e96c44da8544a1')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -R -i "${srcdir}/revert-kernelcheck.patch"
  patch -Np1 -i ${srcdir}/git-fixes.patch
}


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install 
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" 
}
