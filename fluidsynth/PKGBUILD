# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/fluidsynth

pkgname=fluidsynth
# Caution: This package has a lib32 alternative, please update that as well
pkgver=1.1.6
pkgrel=7
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="http://www.fluidsynth.org/"
depends=('jack' 'pulseaudio')
makedepends=('cmake' 'ladspa' 'doxygen')
license=('LGPL')
install=$pkgname.install
backup=('etc/conf.d/fluidsynth')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
	'fluidsynth.conf' 
	'fluidsynth.service')
md5sums=('ae5aca6de824b4173667cbd3a310b263'
         '16c5f4d4cbdddc6c5fcbd4fd4cc142f1'
         'e0406748a154409907df62292be32e8a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -Denable-ladspa=ON \
          -DLIB_SUFFIX=""
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir/fluidsynth.conf" \
    "$pkgdir/etc/conf.d/fluidsynth"

  install -Dm644 "$srcdir/fluidsynth.service" \
    "$pkgdir/usr/lib/systemd/system/fluidsynth.service"
}
