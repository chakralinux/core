
pkgname=libedit
_pkgver=20160903-3.1
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
arch=('x86_64')
url='http://www.thrysoee.dk/editline/'
license=('BSD')
depends=('ncurses')
source=("http://www.thrysoee.dk/editline/libedit-${pkgver/_/-}.tar.gz")
sha1sums=('55e327ee4661b13d20ebb411d790f2bb258271cf')

build() {
	cd "${srcdir}/${pkgname}-${_pkgver/_/-}"
	./configure --prefix=/usr --enable-widec
	make
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver/_/-}"
	make prefix="${pkgdir}"/usr install

        rm "${pkgdir}"/usr/share/man/man3/history.3 # conflicts with readline
	cp "${pkgdir}"/usr/share/man/man3/editline.3 "${pkgdir}"/usr/share/man/man3/el.3
	install -D -m0644 COPYING "${pkgdir}"/usr/share/licenses/libedit/LICENSE
}
