#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer <inkane@chakra-project.org>
# contributor <abveritas[at]chakra-project[dot]org>

pkgname=libssh
pkgver=0.6.3
pkgrel=1
pkgdesc="Library for accessing ssh client services through C libraries"
url="http://www.libssh.org/"
license=('LGPL')
arch=('x86_64')
depends=('openssl')
makedepends=('cmake' 'doxygen')
source=("https://red.libssh.org/attachments/download/87/${pkgname}-${pkgver}.tar.xz")
md5sums=('66cf16e77f60913b4d54f18c92cdbf71')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package(){
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
