pkgname=libssh
pkgver=0.7.6
pkgrel=1
pkgdesc="Library for accessing ssh client services through C libraries"
url="https://www.libssh.org/"
license=('LGPL')
arch=('x86_64')
depends=('zlib' 'openssl')
makedepends=('cmake' 'cmocka' 'doxygen' 'python3')
source=(http://www.libssh.org/files/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('1d607d3859274f755942324afb0f887ee22edd157f9596a2e69e3a28ec6d1092'
            'SKIP')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>

prepare() {
  # disable the test. It is confused by our clean container setup.
  # 'extra-x86-build' uses user 'nobody' that has a record in /etc/passwd file
  # but $HOME envvar is set to '/build'. The test expects that $HOME corresponds to passwd file.
  sed 's/cmocka_unit_test(torture_path_expand_tilde_unix),//' -i libssh-${pkgver}/tests/unittests/torture_misc.c

  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_GSSAPI=OFF \
    -DUNIT_TESTING=ON
  make
}

package(){
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
