pkgname=libssh
pkgver=0.7.2
pkgrel=1
pkgdesc="Library for accessing ssh client services through C libraries"
url="http://www.libssh.org/"
license=('LGPL')
arch=('x86_64')
depends=('openssl')
makedepends=('cmake' 'doxygen')
source=(https://red.libssh.org/attachments/download/177/${pkgname}-${pkgver}.tar.xz
        https://red.libssh.org/attachments/download/168/${pkgname}-${pkgver}.tar.asc)
md5sums=('5d7d468937649a6dfc6186edfff083db'
         'SKIP')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>

build() {
  mkdir -p build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_GSSAPI=Off
  make
}

package(){
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
