#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>


pkgname=coreutils
pkgver=8.14
pkgrel=1
pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('base')
depends=('glibc' 'shadow' 'pam' 'acl' 'gmp>=5.0' 'libcap')
provides=('mktemp')
conflicts=('mktemp')
replaces=('sh-utils' 'fileutils' 'textutils' 'mktemp')
backup=('etc/pam.d/su')
install=${pkgname}.install
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz
	coreutils-uname.patch
	coreutils-pam.patch
        su.pam)
md5sums=('bcb135ce553493a45aba01b39eb3920a'
         'c4fcca138b6abf6d443d48a6f0cd8833'
         'aad79a2aa6d566c375d7bdd1b0767278'
         'fa85e5cce5d723275b14365ba71a8aad')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # added pam patch and i18n patch from fedora cvs
  patch -Np1 -i $srcdir/coreutils-pam.patch || return 1

  # from gentoo portage
  patch -Np1 -i $srcdir/coreutils-uname.patch || return 1

  autoreconf -v
  ./configure --prefix=/usr \
              --enable-install-program=su \
              --enable-no-install-program=groups,hostname,kill,uptime \
              --enable-pam ac_cv_func_openat=no || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  cd ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/{bin,usr/sbin}
  
  # binaries required by FHS
  _fhs="cat chgrp chmod chown cp date dd df echo false ln ls \
        mkdir mknod mv pwd rm rmdir stty su sync true uname"
  mv ${_fhs} $pkgdir/bin

  # binaries required by various scripts
  _bin="cut dir dircolors du install mkfifo readlink shred \
        sleep touch tr vdir"
  mv ${_bin} $pkgdir/bin
  ln -sf /bin/sleep ${pkgdir}/usr/bin/sleep
    
  mv chroot $pkgdir/usr/sbin
  install -Dm644 $srcdir/su.pam ${pkgdir}/etc/pam.d/su
}

