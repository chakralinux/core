#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=coreutils
pkgver=8.17
pkgrel=2
pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('base')
depends=('glibc' 'pam' 'acl' 'gmp' 'libcap')
replaces=('mktemp')
backup=('etc/pam.d/su')
install="${pkgname}.install"
options=('!emptydirs')
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"
  "ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz.sig"
  'coreutils-pam.patch'
  'su.pam')
md5sums=('bbda656ce8ca2c6903948f9faa204ba3'
         'ebecd29b095aa21b0b2f833f1ec20d70'
         'aad79a2aa6d566c375d7bdd1b0767278'
         'fa85e5cce5d723275b14365ba71a8aad')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # added su wheel group pam patch (from fedora git)
  patch -Np1 -i "${srcdir}/coreutils-pam.patch"

  autoreconf -v
  ./configure --prefix=/usr --libexecdir=/usr/lib/coreutils \
              --enable-install-program=su \
              --enable-no-install-program=groups,hostname,kill,uptime \
              --enable-pam
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make RUN_EXPENSIVE_TESTS=yes check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # makepkg uses the full path to this...
  install -d -m755 "${pkgdir}/bin"
  ln -s /usr/bin/du "${pkgdir}/bin/du"

  install -d -m755 "${pkgdir}/usr/sbin"
  mv "${pkgdir}/usr/bin/chroot" "${pkgdir}/usr/sbin"

  install -D -m644 "${srcdir}/su.pam" "${pkgdir}/etc/pam.d/su"
}

# vim:set ts=2 sw=2 et:
