#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=ntfs-3g
pkgver=2011.1.15
pkgrel=1
pkgdesc="Stable read and write NTFS driver"
url="http://www.tuxera.com"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://www.tuxera.com/opensource/${pkgname}-${pkgver}.tgz
        25-ntfs-config-write-policy.fdi)
sha1sums=('d7cf0c5e8730568f5fa8802869d6fe8cdd4a0213'
          '200029f2999a2c284fd30ae25734abf6459c3501')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ac_cv_path_LDCONFIG=/bin/true ./configure --prefix=/usr \
    --with-fuse=internal --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  ln -s /bin/ntfs-3g "${pkgdir}/sbin/mount.ntfs"
  install -m755 -d "${pkgdir}/usr/share/hal/fdi/policy/10osvendor"
  install -m644 "${srcdir}/25-ntfs-config-write-policy.fdi" "${pkgdir}/usr/share/hal/fdi/policy/10osvendor/"
}
