#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=ntfs-3g
_realname=ntfs-3g_ntfsprogs
pkgver=2012.1.15
pkgrel=3
pkgdesc="Stable read and write NTFS driver"
url="http://www.tuxera.com"
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'fuse' 'util-linux')
makedepends=('pkgconfig')
options=('!libtool')
conflicts=('ntfsprogs')
provides=("ntfsprogs=${pkgver}")
source=("http://www.tuxera.com/opensource/${_realname}-${pkgver}.tgz")
sha1sums=('8d55cf49afde172fefa369a0a85289e09c4d7bbb')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ./configure --prefix=/usr \
    --with-fuse=external --disable-static \
    --enable-posix-acls --disable-ldconfig \
    --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  mv "$pkgdir"/bin/* "$pkgdir"/usr/bin/
  rm -rf "$pkgdir"/{s,}bin

  cd "$pkgdir"/usr/bin
  ln -s ntfs-3g mount.ntfs
  ln -s ntfs-3g mount.ntfs-3g
  ln -s lowntfs-3g mount.lowntfs-3g
  
  mv "$pkgdir"/lib/* "$pkgdir"/usr/lib/
  rm -rf "$pkgdir"/lib
}

