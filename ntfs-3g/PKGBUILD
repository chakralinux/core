pkgname=ntfs-3g
_pkgname=ntfs-3g_ntfsprogs
pkgver=2016.2.22
pkgrel=1
pkgdesc='NTFS filesystem driver and utilities'
url='http://www.tuxera.com/community/ntfs-3g-download/'
arch=('x86_64')
license=('GPL2')
depends=('util-linux' 'fuse' 'hwinfo')
source=("http://tuxera.com/opensource/${_pkgname}-${pkgver}.tgz")
sha1sums=('382df40c366711003cf24d2342033c23e2580b42')

conflicts=('ntfsprogs')
provides=("ntfsprogs=${pkgver}")

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	sed 's|$(DESTDIR)/sbin|$(DESTDIR)/usr/bin|' -i {ntfsprogs,src}/Makefile.in
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sbin=/usr/bin \
		--mandir=/usr/share/man \
		--disable-ldconfig \
		--disable-static \
		--with-fuse=external \
		--enable-posix-acls \
		--enable-extras \

	make
}

package_ntfs-3g() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" rootbindir=/usr/bin rootsbindir=/usr/bin rootlibdir=/usr/lib install
	ln -s /usr/bin/ntfs-3g "${pkgdir}/usr/bin/mount.ntfs"
}
