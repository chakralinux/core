# $Id: PKGBUILD 59171 2009-11-21 01:08:10Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libmikmod
pkgver=3.2.0
pkgrel=2
pkgdesc="A portable sound library"
license=('GPL' 'LGPL')
url="http://sourceforge.net/projects/mikmod/"
arch=('i686' 'x86_64')
depends=('glibc')
options=('!libtool')
install=$pkgname.install
source=(http://mikmod.shlomifish.org/files/${pkgname}-${pkgver}.tar.gz
        libmikmod-3.2.0-64bit-fix.diff
        libmikmod-3.1.12-loopingvolume-fix.diff)
md5sums=('96e9820d72a41fe27ff304071739696c'
         '0bb09aac6e83e0a7fd6535961b3cff4c'
         'a837fd876cbd2ac27419b802504489db')

build() {
  cd $srcdir/$pkgname-$pkgver

  # patches from sdl_mixer-1.2.11 source
  patch -Np1 -i $srcdir/libmikmod-3.2.0-64bit-fix.diff
  patch -Np1 -i $srcdir/libmikmod-3.1.12-loopingvolume-fix.diff
  
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make || return 1
  make DESTDIR=$pkgdir install
}

