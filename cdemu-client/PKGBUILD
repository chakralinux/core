# Platform Packages for Chakra, part of chakra-project.org
# Contributor: abveritas@chakra-project.org
# Maintainer: BrLi <rainman59118[at]gmail.com>

pkgname=cdemu-client
pkgver=2.1.1
pkgrel=2
pkgdesc="Simple command-line client for controlling cdemu-daemon"
arch=('x86_64')
url=("http://cdemu.sourceforge.net/")
license=('GPL')
depends=('python2' 'dbus-python' 'cdemu-daemon')
makedepends=('intltool' 'cmake')
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2")
md5sums=('e3062659481b804d12fac58ad64316a0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' src/cdemu

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install
  rm -f "${pkgdir}"/usr/share/applications/cdemu-client.desktop
}
