#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=rtkit
pkgver=0.8
pkgrel=1
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=('i686' 'x86_64')
url="http://git.0pointer.de/?p=rtkit.git"
license=('GPL' 'custom:BSD')
depends=('dbus' 'polkit')
makedepends=('pkgconfig')
install=rtkit.install
source=(http://0pointer.de/public/$pkgname-$pkgver.tar.gz)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --libexecdir=/usr/lib --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # DBus configuration
  mkdir -p "$pkgdir/usr/share/dbus-1/interfaces"
  ./rtkit-daemon --introspect >"$pkgdir/usr/share/dbus-1/interfaces/org.freedesktop.RealtimeKit1.xml"

  # Client license
  mkdir -p "$pkgdir/usr/share/licenses/rtkit"
  sed -ne '4,25p' rtkit.c >"$pkgdir/usr/share/licenses/rtkit/COPYING"
}

md5sums=('5db8e9c266c695b64b7e5b0d26bd5685')
