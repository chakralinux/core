# Contributor: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# Maintainer: abveritas[at]chakra-project[dot]org>

_pkgname=frei0r-plugins
pkgname=frei0r-plugins-qt5
pkgver=1.4
pkgrel=1
pkgdesc="frei0r is a minimalistic plugin API for video sources and filters."
arch=('x86_64')
url="http://www.piksel.org/frei0r"
license=('GPL')
depends=('gcc-libs' 'gavl' 'opencv-qt5')
makedepends=('doxygen')
options=('!emptydirs')
source=("https://distfiles.macports.org/frei0r-plugins/frei0r-plugins-1.4.tar.gz"
        "opencv.patch")
md5sums=('202375d1bcb545c1b6eb8f34e0260ec5'
         '5f254918dfd1dd1523ad70771048032b')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i ../opencv.patch
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  export PKG_CONFIG_PATH=/opt/opencv-qt5/lib/pkgconfig  
  ./autogen.sh
  ./configure --prefix=/opt/opencv-qt5
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  make DESTDIR="$pkgdir" install 
}

