# Part of X. org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributions from: https://www.archlinux.org/packages/extra/any/xorg-fonts-cyrillic/

pkgname=xorg-fonts-cyrillic
pkgver=1.0.1
pkgrel=2
pkgdesc="X.org cyrillic fonts"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(xorg-fonts-encodings xorg-fonts-alias xorg-font-utils fontconfig)
install=xfonts.install
source=(${url}/releases/individual/font/font-cronyx-cyrillic-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-misc-cyrillic-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-screen-cyrillic-1.0.4.tar.bz2
        ${url}/releases/individual/font/font-winitzki-cyrillic-${pkgver}.tar.bz2)
md5sums=('15f86f5e572df21218617982eb4b3bfc'
         'c79d7921d95b2c4f10fad464bb121090'
         '6f3fdcf2454bf08128a651914b7948ca'
         '92bf4ecb373faac545cef2dcbe40be35')

build() {
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
		      --with-fontdir=/usr/share/fonts/cyrillic
      make
      popd
    fi
  done
}

package() {
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-*}"
      popd
    fi    
  done
  rm -f "${pkgdir}"/usr/share/fonts/*/fonts.*
}
