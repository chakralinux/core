# Contrubutions from Arch:
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=libplacebo
pkgver=0.6.0
pkgrel=1
pkgdesc='Reusable library for GPU-accelerated video/image rendering primitives'
url='https://github.com/haasn/libplacebo'
arch=('x86_64')
license=('LGPL2.1')
depends=('vulkan-icd-loader' 'glslang' 'lcms2' 'shaderc' 'libshaderc_shared.so')
makedepends=('meson' 'ninja' 'vulkan-headers')
provides=('libplacebo.so')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/haasn/libplacebo/archive/v${pkgver}.tar.gz)
sha256sums=('199b0b76be2c20d1ef9c78bea1e2409781026398c9689187e5e5d69a775e65ee')

build() {
  cd ${pkgbase}-${pkgver}
  chakra-meson build \
    -D tests=true \
    -D vulkan=enabled \
    -D glslang=enabled \
    -D shaderc=enabled \
    -D lcms=enabled
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}
  ninja -C build test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
