#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# contributor Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-core
pkgver=2.6.3
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus-core' 'glib2' 'libxtst')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
source=("http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
md5sums=('eb1d3ca3332af8862791d3ff7dcfd20a')

build() {
  cd $pkgname-$pkgver

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/at-spi2-core --disable-xevie
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

