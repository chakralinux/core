# Maintainer:  
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Anke Boersma (abveritas) <abveritas@chakra-project.org>
#               Damir Perisa <damir.perisa@bluewin.ch>
#               Eduardo Romero <eduardo@archlinux.org>
#               Jan de Groot <jgc@archlinux.org>
#               Tom Gundersen <teg@jklm.no>

pkgname=libgphoto2
pkgver=2.5.2
pkgrel=1
pkgdesc='The core library of gphoto2, designed to allow access to digital camera by external programs'
arch=('x86_64')
url='http://www.gphoto.org'
license=('LGPL')
depends=('libexif>=0.6.21'
         'libjpeg-turbo>=1.3.0'
         'gd'
         'libtool>=2.4.2'
         'libusb-compat')
conflicts=('libgphoto2-nolimit')
replaces=('libgphoto2-nolimit')
provides=('libgphoto2-nolimit')
install=libgphoto2.install
options=('libtool')
source=(http://downloads.sourceforge.net/gphoto/${pkgname}-${pkgver}.tar.gz)
sha512sums=('ee5b6957b1356adbef4deb134bd93a297077d744dc8e665ed93cac21dfc03795faeb7acf995c776354b045a8e2d010df6b10c21234b1fea69c90b73e61f9d683')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Change the limit which cause "Fixed limit exceeded"
    sed -i -e "s|MAX_ENTRIES 1024|MAX_ENTRIES 8192|" libgphoto2/gphoto2-list.c

    udevscriptdir=/lib/udev ./configure \
        --prefix=/usr \
        --with-drivers=all \
        --disable-static \
        --disable-rpath
    LD_PRELOAD="" make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # Remove what?
    rm -fv ${pkgdir}/usr/lib/libgphoto2/${pkgver}/*.a

    install -m755 -d "${pkgdir}/usr/share/hal/fdi/information/20thirdparty"
    LD_LIBRARY_PATH="${pkgdir}/usr/lib${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH" \
    CAMLIBS="${pkgdir}/usr/lib/libgphoto2/${pkgver}" \
        "${pkgdir}/usr/lib/libgphoto2/print-camera-list" hal-fdi > \
        "${pkgdir}/usr/share/hal/fdi/information/20thirdparty/10-camera-libgphoto2.fdi"

    install -m755 -d "${pkgdir}/lib/udev/rules.d"
    LD_LIBRARY_PATH="${pkgdir}/usr/lib${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH" \
    CAMLIBS="${pkgdir}/usr/lib/libgphoto2/${pkgver}" \
        "${pkgdir}/usr/lib/libgphoto2/print-camera-list" udev-rules version 175 > \
        "${pkgdir}/lib/udev/rules.d/54-gphoto.rules"

    # Remove recursive symlink
    rm -fv "${pkgdir}/usr/include/gphoto2/gphoto2"
}
