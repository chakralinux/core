pkgname=baloo
pkgver=5.55.0
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/baloo'
license=('LGPL')
depends=('xapian-core' 'kfilemetadata' 'kidletime' 'kcmutils' 'kdelibs4support' 'lmdb')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('frameworks')
options=("debug")
replaces=('baloo5' 'kf5-baloo' 'baloo-kf5')
provides=('baloo5' 'kf5-baloo' 'baloo_file' 'baloo-kf5')
conflicts=('baloo5' 'kf5-baloo' 'baloo<=4.14.3-1' 'baloo4' 'baloo-kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        'baloo-5.16.0-fix-cjk-search.patch')
sha256sums=('b0676a76fe9bda625de335877e2ec4ffbb4dfdd7ec8c2fe1d4370d130ed01519'
            'SKIP'
            '65fca7ee01a6de3e749aabed3fd8d945bd41db41bb20005255094984b70e3a9d')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../baloo-5.16.0-fix-cjk-search.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
