# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/pstoedit

pkgname=pstoedit
pkgver=3.73
pkgrel=1
pkgdesc="Translates PostScript and PDF graphics into other vector formats"
arch=('x86_64')
url="http://www.pstoedit.net/"
license=('GPL')
depends=('gcc-libs' 'plotutils' 'gd' 'imagemagick')
makedepends=('ghostscript')
options=('!makeflags')
source=("http://downloads.sourceforge.net/sourceforge/pstoedit/pstoedit-${pkgver}.tar.gz")
sha1sums=('0737c78b3b84394e0acf514c43ea21f5d66a1c00')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/-pedantic//' configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
