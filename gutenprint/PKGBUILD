#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=gutenprint
pkgver=5.2.7
pkgrel=3
pkgdesc="Top quality printer drivers for POSIX systems."
arch=('i686' 'x86_64')
license=('GPL')
install=gutenprint.install
depends=('readline' 'gnutls>=2.8.5') # needs to be checked. build log says -Lgnutls but namcap doesn't detect it
makedepends=('cups>=1.4.2' 'foomatic-db-engine' 'ghostscript>=8.71')
optdepends=('cups:		to use cups printer spooler(recommended)'
            'foomatic-db-engine:	to use foomatic spooler'
            'ghostscript:	adds postscript support for ijsgutenprint')
source=(http://downloads.sourceforge.net/gimp-print/$pkgname-$pkgver.tar.bz2
        buildfix.patch)
url="http://gimp-print.sourceforge.net/"
replaces=('gimp-print' 'gutenprint')
options=('!libtool' '!emptydirs')
md5sums=('b19029972bf28f6efd693270daf8f5de'
         '3d5e93734b403dbc5b067856be1cbd65')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/buildfix.patch
  ./configure --prefix=/usr --disable-samples --with-cups \
	--disable-translated-cups-ppds \
	--disable-libgutenprintui  --disable-libgutenprintui2 --disable-gtktest \
	--enable-cups-ppds --enable-cups-ppds-at-top-level \
	--with-foomatic --with-foomatic3 --with-ghostscript \
	--disable-static --disable-static-genppd --with-ijs \
	--disable-globalized-cups-ppds
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}

