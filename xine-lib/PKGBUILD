#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=xine-lib
pkgver=1.1.19
pkgrel=7
pkgdesc="A multimedia playback engine"
arch=('i686' 'x86_64')
url="http://www.xine-project.org"
license=('LGPL' 'GPL')
depends=('libgl' 'libxvmc' 'flac' 'ffmpeg' 'libxinerama' 'libmodplug')
makedepends=('wavpack' 'faad2' 'libmng' 'imagemagick' 'smbclient' 'mesa'
             'v4l-utils' 'vcdimager' 'jack' 'libpulse' 'speex' 'aalib')
optdepends=('imagemagick: for using the imagemagick plugin' \
            'smbclient: for using the smb plugin' \
            'jack: for using the jack plugin' \
            'vcdimager: for using the vcd plugin' \
            'mesa: for using the opengl plugin' \
            'libpulse: for using the pulseaudio plugin' \
            'speex: for using the speex plugin' \
            'v4l-utils: for using the v4l plugin' \
            'wavpack: for using the wavpack plugin' \
            'faad2: for using the faad plugin' \
            'libmng: for using the mng plugin' \
            'aalib: for using the aalib plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.bz2
        xine-lib-1.1.1-configure-no-mcpu-march.patch xine-lib-1.1.19-xvmc.patch
        xine-lib-1.1.19-ffmpeg.patch)
sha1sums=('5afcc28c5cf2bdaab99d951960f6587797e1e5a0'
          '121a8358d7919b2e51067412373f52848290338a'
          '920bf27e6e3523dfe4cc9c802ff713e9688a3f34'
          '5c72832408877be2fee22cccaf4d630b4c0843fe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < ../xine-lib-1.1.1-configure-no-mcpu-march.patch
  patch -p0 < ../xine-lib-1.1.19-xvmc.patch
  patch -p1 -i "${srcdir}/xine-lib-1.1.19-ffmpeg.patch"
  sed -i 's|linux/videodev.h|libv4l1-videodev.h|' \
    configure.ac src/input/input_v4l.c src/video_out/video_out_syncfb.h

  libtoolize --force --copy
  aclocal -I m4
  autoconf
  automake --add-missing

  ./configure --prefix=/usr --with-wavpack --with-external-ffmpeg --with-external-libfaad --disable-gnomevfs
    
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

