pkgname=xine-lib
pkgver=1.2.6
pkgrel=4
pkgdesc="A multimedia playback engine"
arch=('x86_64')
url="http://www.xine-project.org"
license=('LGPL' 'GPL')
depends=('libxvmc' 'ffmpeg' 'libxinerama' 'pulseaudio')
makedepends=('wavpack' 'faad2' 'libmng' 'imagemagick' 'glu' 'libmodplug'
             'vcdimager' 'jack' 'aalib' 'libdca' 'a52dec' 'libmad'
             'libmpcdec' 'libcaca' 'libbluray' 'libvdpau' 'libdvdnav')
optdepends=('imagemagick: for using the imagemagick plugin'
            'jack: for using the jack plugin'
            'vcdimager: for using the vcd plugin'
            'glu: for using the opengl plugin'
            'wavpack: for using the wavpack plugin'
            'faad2: for using the faad plugin'
            'libmng: for using the mng plugin'
            'aalib: for using the aalib plugin'
            'libmodplug: for using the modplug plugin'
            'libdca: for using the dca plugin'
            'a52dec: for using the a52 plugin'
            'libmad: for using the mp3 plugin'
            'libdvdnav: for using the dvd plugin'
            'libmpcdec: for using the musepack plugin'
            'libcaca: for using the caca plugin'
            'libbluray: for using the bluray plugin'
            'libvdpau: for using the VDPAU plugin'
            'smbclient: for using the samba plugin' )
source=("http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.xz"
	xine-lib-xcb-1.12.patch)
sha1sums=('ac929eef2b7bf5c27699bbed612b953a01fccba5'
          '890c8f044c0080bfb7d4b163061dd42621fc4df1')

prepare() {
  cd $pkgname-$pkgver
# Fix build with xcb 1.12 (Gentoo)
  patch -p1 -i ../xine-lib-xcb-1.12.patch
  autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-wavpack \
              --with-external-dvdnav \
              --enable-vdpau \
              --disable-optimizations \
              --disable-gnomevfs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
