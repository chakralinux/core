# Maintainer: abveritas@chakra-project.org

pkgname=xine-lib
pkgver=1.2.4
pkgrel=1
pkgdesc="A multimedia playback engine"
arch=('x86_64')
url="http://www.xine-project.org"
license=('LGPL' 'GPL')
depends=('libxvmc' 'ffmpeg' 'libxinerama' 'pulseaudio')
makedepends=('wavpack' 'faad2' 'libmng' 'imagemagick' 'mesa' 'libmodplug'
             'vcdimager' 'jack' 'aalib' 'libdca' 'a52dec' 'libmad'
             'libmpcdec' 'libcaca' 'libbluray' 'libvdpau')
optdepends=('imagemagick: for using the imagemagick plugin' \
            'jack: for using the jack plugin' \
            'vcdimager: for using the vcd plugin' \
            'mesa: for using the opengl plugin' \
            'wavpack: for using the wavpack plugin' \
            'faad2: for using the faad plugin' \
            'libmng: for using the mng plugin' \
            'aalib: for using the aalib plugin' \
            'libmodplug: for using the modplug plugin' \
            'libdca: for using the dca plugin' \
            'a52dec: for using the a52 plugin' \
            'libmad: for using the mp3 plugin' \
            'libmpcdec: for using the musepack plugin' \
            'libcaca: for using the caca plugin' \
            'libbluray: for using the bluray plugin' \
            'libvdpau: for using the VDPAU plugin')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.xz")
sha1sums=('32267c5fcaa1439a5fbf7606d27dc4fafba9e504')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-wavpack \
              --enable-vdpau \
              --disable-optimizations \
              --disable-gnomevfs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
