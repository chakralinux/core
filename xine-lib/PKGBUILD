#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=xine-lib
pkgver=1.1.20.1
pkgrel=1
pkgdesc="A multimedia playback engine"
arch=('i686' 'x86_64')
url="http://www.xine-project.org"
license=('LGPL' 'GPL')
depends=('libgl' 'libxvmc' 'flac' 'ffmpeg' 'libxinerama' 'libmodplug')
makedepends=('wavpack' 'faad2' 'libmng' 'imagemagick' 'smbclient' 'mesa'
             'v4l-utils' 'vcdimager' 'jack' 'libpulse' 'speex' 'aalib')
optdepends=('imagemagick: for using the imagemagick plugin' \
            'smbclient: for using the smb plugin' \
            'jack: for using the jack plugin' \
            'vcdimager: for using the vcd plugin' \
            'mesa: for using the opengl plugin' \
            'libpulse: for using the pulseaudio plugin' \
            'speex: for using the speex plugin' \
            'v4l-utils: for using the v4l plugin' \
            'wavpack: for using the wavpack plugin' \
            'faad2: for using the faad plugin' \
            'libmng: for using the mng plugin' \
            'aalib: for using the aalib plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.xz)
sha1sums=('577537bf3aa50a708c7e487e4804ae3c1f803796')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|linux/videodev.h|libv4l1-videodev.h|' \
    configure.ac src/input/input_v4l.c src/video_out/video_out_syncfb.h
   ./configure --prefix=/usr --with-wavpack --with-external-ffmpeg --with-external-libfaad \
   --disable-gnomevfs --disable-optimizations   
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

