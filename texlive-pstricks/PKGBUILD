# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/texlive-pstricks

pkgname=texlive-pstricks
pkgver=2018.47400
_revnr=${pkgver#*.}
pkgrel=1
pkgdesc="TeX Live - Additional PSTricks packages."
license=('GPL3')
arch=(x86_64)
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
install=texlive.install
options=('!emptydirs')
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
md5sums=('cd1fe9a41c8613f6905684d087778dc2')

build() {
  for p in *.tar.xz; do
    bsdtar -xf $p
  done
  rm -rf {tlpkg,doc,source} || true
}

package() {
  install -m755 -d $pkgdir/var/lib/texmf/chakra/installedpkgs
  sed -i '/^#/d' CONTENTS
  install -m644 CONTENTS $pkgdir/var/lib/texmf/chakra/installedpkgs/${pkgname}_$_pkgver.pkgs
  install -m755 -d $pkgdir/usr/share
  wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
  for dir in $wanteddirs; do
    find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
    find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
  done
  if [[ -d texmf-dist ]]; then
    find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
    find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
  fi
  if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
    find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
  fi
   rm "$pkgdir"/usr/share/texmf-dist/scripts/pst-pdf/ps4pdf.bat*
   install -m755 -d $pkgdir/usr/bin
   ln -s /usr/share/texmf-dist/scripts/pedigree-perl/pedigree.pl $pkgdir/usr/bin/pedigree
   ln -s /usr/share/texmf-dist/scripts/pst2pdf/pst2pdf.pl $pkgdir/usr/bin/pst2pdf
   ln -s /usr/share/texmf-dist/scripts/pst-pdf/ps4pdf $pkgdir/usr/bin/ps4pdf
}
