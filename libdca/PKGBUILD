# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/libdca

pkgname=libdca
pkgver=0.0.5
pkgrel=3
pkgdesc="Free library for decoding DTS Coherent Acoustics streams"
arch=(x86_64)
license=('GPL')
url="http://www.videolan.org/developers/libdca.html"
source=(http://download.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
provides=('libdts')
replaces=('libdts')
options=('!libtool' '!emptydirs')
md5sums=('dab6b2795c66a82a6fcd4f8343343021')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/share/man/man1"/{extract_dts,dtsdec}.1
  ln -s dcadec.1.gz "${pkgdir}/usr/share/man/man1/dtsdec.1.gz"
  ln -s extract_dca.1.gz "${pkgdir}/usr/share/man/man1/extract_dts.1.gz"
}
