#
# Chakra Packages for Chakra, part of chakra-project.org
#

_extramodules=extramodules-4.0-CHAKRA
_kver="$(cat /usr/lib/modules/${_extramodules}/version)"
_kernelver=4.0.5

pkgname=(catalyst chd-catalyst)
pkgver=15.5
pkgrel=2
pkgdesc="AMD/ATI Catalyst drivers for linux. fglrx kernel module only. Radeons HD 2 3 4 xxx ARE NOT SUPPORTED"
screenshot="http://linuxtation3.files.wordpress.com/2007/03/pantallazo.jpg"
arch=('x86_64')
url="http://www.amd.com"
license=('custom')
makedepends=('gcc-libs' 'gcc>4.0.0' 'make' "linux-headers=${_kernelver}")

url_ref="http://support.amd.com/en-us/download/desktop?os=Linux+x86"
DLAGENTS="http::/usr/bin/curl --referer ${url_ref} -o %o %u"

source=(
        http://www2.ati.com/drivers/linux/amd-catalyst-omega-${pkgver}-linux-run-installers.zip
#        http://archive.ubuntu.com/ubuntu/pool/restricted/f/fglrx-installer/fglrx-installer_14.201.orig.tar.gz
        ati_make.sh
        makefile_compat.patch
        lano1106_fglrx_intel_iommu.patch
        lano1106_kcl_agp_13_4.patch
        lano1106_fglrx-13.8_proc.patch
        cold-fglrx-3.14-current_euid.patch
        fglrx_gpl_symbol.patch
        fglrx_3.17rc6-no_hotplug.patch
        kolasa-3.19-get_cpu_var.patch
        kolasa_4.0-cr4-strn.patch
        kolasa_4.1_remove-IRQF_DISABLED.patch
        fglrx_supported)
md5sums=('979f9f2e0948fa6e92ff0125f5c6b575'
         'fd98b7e486d7fd4cad8de7b95b5b031e'
         '3e1b82bd69774ea808da69c983d6a43b'
         '5184b94a2a40216a67996999481dd9ee'
         'c5156eddf81c8a1719b160d05a2e8d67'
         '2ab4837233de42332753882445373d7b'
         'ba33b6ef10896d3e1b5e4cd96390b771'
         'ef97fc080ce7e5a275fe0c372bc2a418'
         '67a22f624bae95a76638ce269392cb01'
         '3aa45013515b724a71bbd8e01f98ad99'
         'dee3df1c5d3ed87363f4304da917fc00'
         '81a9e38dee025151cccb7e5db2362cfb'
         '8bd182de340d6ed8b2dfcedfd3e5a389')


build() {   
    ## Unpack archive
    /bin/sh ./amd-catalyst-omega-${pkgver}-linux-run-installers.run --extract archive_files
    
    msg "Setting compilation variables ..."
    # include ati_make.sh and use _ati_check function from it
    . ati_make.sh
    _ati_check

    BUILDARCH=x86_64
   _archdir=x86_64

    cd "${srcdir}/archive_files/"

    msg "Applying patch(es) ..."
    # makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
    # works only in combination with ati_make.sh script
    # patches partially token from https://aur.archlinux.org/packages/ca/catalyst/PKGBUILD
    patch -Np1 -i ../makefile_compat.patch
    patch -Np1 -i ../lano1106_fglrx_intel_iommu.patch
    patch -Np1 -i ../lano1106_kcl_agp_13_4.patch
    patch -Np1 -i ../lano1106_fglrx-13.8_proc.patch
    patch -Np1 -i ../cold-fglrx-3.14-current_euid.patch
    patch -Np1 -i ../fglrx_3.17rc6-no_hotplug.patch
    patch -Np1 -i ../fglrx_gpl_symbol.patch
    patch -Np1 -i ../kolasa-3.19-get_cpu_var.patch
    patch -Np1 -i ../kolasa_4.0-cr4-strn.patch
    #patch -Np1 -i ../kolasa_4.1_remove-IRQF_DISABLED.patch
    
    cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
    cp "${srcdir}/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" . 
    cp 2.6.x/Makefile . 

    msg "Compiling fglrx module ..."
    # Compile module using _ati_check variables
    CFLAGS_MODULE="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX $def_smp $def_modversions"
    
    make -C /usr/lib/modules/${_kver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} \
    MODFLAGS="$CFLAGS_MODULE" \
    CFLAGS_MODULE="$CFLAGS_MODULE" \
    PAGE_ATTR_FIX=$PAGE_ATTR_FIX modules 
}

package_catalyst() {
    pkgdesc="AMD/ATI Catalyst drivers for linux. fglrx kernel module only"
    screenshot="http://linuxtation3.files.wordpress.com/2007/03/pantallazo.jpg"
    depends=("linux=${_kernelver}" 'catalyst-utils')
    conflicts=('nvidia' 'xf86-video-ati' 'xf86-video-radeonhd' 'catalyst-legacy')
    install=catalyst.install

    # here we are checking kernel's extramodules dir
    for r in /usr/lib/modules/*; do
      s=${r:17}
      if [[ ${s:0:3} = "ext" ]]; then
       if [[ `cat ${r}/version | grep -c ${_kver}` != 0 ]]; then
         _destidir=${s}
         sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${s}/" $startdir/catalyst.install
       fi
       elif [[ ${s} = ${_kver} ]] && [[ ! -e ${r}/extramodules ]]; then
         _destidir=${_kver}/video
         sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=null/" $startdir/catalyst.install
      fi
    done

    cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
    install -m755 -d "${pkgdir}/usr/lib/modules/${_destidir}/" 
    install -m644 fglrx.ko "${pkgdir}/usr/lib/modules/${_destidir}/" 
    find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
    sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kver}/" $startdir/catalyst.install

    # License
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
#    install -m644 "${srcdir}/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -m644 ${srcdir}/archive_files/common/usr/share/doc/fglrx/LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}
}

package_chd-catalyst() {
    pkgdesc="CHD ids for catalyst $pkgver"
    arch=('any')

    cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod/"

    install -dm755 "${pkgdir}/opt/chakra/hwdb"
    sh -e "${srcdir}/fglrx_supported" fglrxko_pci_ids.h \
        > "${pkgdir}/opt/chakra/hwdb/hw_ati_hwdb"
}
