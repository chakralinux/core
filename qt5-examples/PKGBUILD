
pkgname=qt5-examples
pkgver=5.11.3
pkgrel=2
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='Examples and demos from qt5 documentation'
depends=('qt5-doc')
makedepends=()
groups=('qt5')
_pkgfqn="qt-everywhere-src-${pkgver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/single/${_pkgfqn}.tar.xz")
md5sums=('02b353bfe7a40a8dc4274e1d17226d2b')

package() {
  _base="$pkgdir"/usr/share/doc/qt5/examples

  # The various example dirs have conflicting .pro files, but
  # QtCreator requires them to be in the same top-level directory.
  # Matching the Qt5 installer, only the qtbase project is kept.
  mkdir -p $_base
  cp ${_pkgfqn}/qtbase/examples/examples.pro $_base

  _fdirs=$(find "${_pkgfqn}" -maxdepth 2 -type d -name examples)
  for _dir in $_fdirs; do
      _mod=$(basename ${_dir%/examples})

      if [ -e "$_dir/README" ]; then
        cp $_dir/README $_dir/README.$_mod
      fi

    # Don't overwrite existing examples.pro file
      mkdir $_base/$_mod
      cp -rn $_dir/* $_base/$_mod
  done
}
