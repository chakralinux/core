# Platform Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor: FigoDaPaura <ffigoDaPaura>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=virtualgl
_srcname=VirtualGL
pkgver=2.2.90
_srcbranch='2.2.90 (2.3beta1)'
pkgrel=4
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card."
arch=('i686' 'x86_64')
url="http://virtualgl.org"
license=('LGPL')
depends=('mesa' 'libxv' 'libjpeg-turbo')
makedepends=('cmake')
provides=('virtualgl')
source=("http://downloads.sourceforge.net/project/virtualgl/VirtualGL/$_srcbranch/VirtualGL-$pkgver.tar.gz")
md5sums=('4153b8ffd8d6087c6ec945f687eae332')

build() {
  cd "$srcdir/VirtualGL-$pkgver"

  mkdir -p build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/opt/VirtualGL -DTJPEG_INCLUDE_DIR=/usr/include -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so \
    -DVGL_LIBDIR=/usr/lib -DVGL_BINDIR=/usr/bin
  make
}

package() {
  cd "$srcdir/VirtualGL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  mkdir -p "$pkgdir/opt/VirtualGL/bin"
  mv "$pkgdir"/usr/bin/glxinfo "$pkgdir/opt/VirtualGL/bin"
}

