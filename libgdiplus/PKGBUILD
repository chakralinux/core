#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=libgdiplus
pkgver=2.10.9
pkgrel=1
pkgdesc="An Open Source Implementation of the GDI+ API"
arch=('x86_64')
license=('MPL' 'LGPL')
url="http://www.mono-project.com"
depends=('libtiff' 'cairo' 'giflib' 'glib2' 'libexif')
makedepends=('pkgconfig')
source=("http://download.mono-project.com/sources/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        libpng15.patch
        libgdiplus0-giflib5.patch
        libgdiplus-2.10.9-gold.patch)
md5sums=('b4615c14584b5d73cbb9757c28887654'
         'a2d143676bbaceeb88b4c34069e93986'
         'ad97558c721106eea03c7808b501814b'
         '95fb92750c131ce9287419c3ac5fcb76')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p0 -i "${srcdir}/libpng15.patch"
  patch -Np1 -i ../libgdiplus0-giflib5.patch
  patch -Np1 -i ../libgdiplus-2.10.9-gold.patch

  sed -i -e 's/-L\${libjpeg_prefix}\/lib -ljpeg/-ljpeg/' configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr --with-cairo=system
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install 
}
