pkgname=libgdiplus
pkgver=3.12
pkgrel=1
pkgdesc="An Open Source Implementation of the GDI+ API"
arch=(x86_64)
license=('MPL' 'LGPL')
url="http://www.mono-project.com"
depends=('libtiff' 'cairo' 'giflib' 'glib2' 'libexif')
source=(http://download.mono-project.com/sources/${pkgname}/${pkgname}-${pkgver}.tar.gz
	libgdiplus0-giflib5.patch)
sha1sums=('7f7d2b82c0d755bb854fd76d0d62120cdde35d8c'
          '78a0b9ad5815b37fb1d2b4ddf01628b13aab33a4')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../libgdiplus0-giflib5.patch

  sed -i -e 's/-L\${libjpeg_prefix}\/lib -ljpeg/-ljpeg/' configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-cairo=system
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
