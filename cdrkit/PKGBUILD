# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=cdrkit
pkgver=1.1.11
pkgrel=2
pkgdesc="Suite of programs for CD/DVD recording, ISO image creation, and audio CD extraction"
arch=('i686' 'x86_64')
url="http://cdrkit.org/"
license=('GPL2')
depends=('file' 'bzip2' 'perl')
makedepends=('cmake')
provides=('cdrtools')
conflicts=('cdrtools')
# cdrkit.org, the old source, is now an empty domain. This is
# the only one carrying the latest version.
source=("http://pkgs.fedoraproject.org/repo/pkgs/${pkgname}/${pkgname}-${pkgver}.tar.gz/efe08e2f3ca478486037b053acd512e9/${pkgname}-${pkgver}.tar.gz")
md5sums=('efe08e2f3ca478486037b053acd512e9')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" install

  # Make symlinks for cdrtools compatibility
  cd "$pkgdir/usr/bin"
  ln -s wodim cdrecord
  ln -s readom readcd
  ln -s genisoimage mkisofs
  ln -s genisoimage mkhybrid
  ln -s icedax cdda2wav

  cd "$pkgdir/usr/share/man/man1"
  ln -s wodim.1 cdrecord.1
  ln -s readom.1 readcd.1
  ln -s genisoimage.1 mkisofs.1
  ln -s genisoimage.1 mkhybrid.1
  ln -s icedax.1 cdda2wav.1
}
