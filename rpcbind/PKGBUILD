
pkgname=rpcbind
pkgver=0.2.3
pkgrel=1
pkgdesc="portmap replacement which supports RPC over various protocols"
arch=('x86_64')
depends=('bash' 'glibc' 'libtirpc')
url="http://rpcbind.sourceforge.net"
license=('custom')
source=("http://downloads.sourceforge.net/sourceforge/rpcbind/rpcbind-${pkgver}.tar.bz2" 
	'rpcbind-sunrpc.patch'
	'rpcbind.service'
	'rpcbind.socket'
	'rpcbind.conf')
install=rpcbind.install
md5sums=('c8875246b2688a1adfbd6ad43480278d'
         '68f403f51b72619fecdc3d57550a2fdb'
         '0f493a590c250267034e230e7bf1f894'
         'e5a205cd7402fedaf74a744b2a22f0ad'
         'd4c74d1be9f98344af138a15ad3b6f8c')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../rpcbind-sunrpc.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --with-rpcuser=rpc \
    --enable-warmstarts \
    --with-statedir=/var/lib/rpcbind
  make 
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR=$pkgdir install

  # install rpcbind config file
  install -D -m644 $srcdir/rpcbind.conf $pkgdir/etc/conf.d/rpcbind
  # install missing man page - https://bugs.archlinux.org/task/21271
  install -m644 man/rpcinfo.8 $pkgdir/usr/share/man/man8/
  # install systemd service file
  install -D -m644 $srcdir/rpcbind.service $pkgdir/usr/lib/systemd/system/rpcbind.service
  # install system socket activation
  install -D -m644 $srcdir/rpcbind.socket $pkgdir/usr/lib/systemd/system/rpcbind.socket

  # add symlinks
  mkdir $pkgdir/usr/lib/systemd/system/sockets.target.wants
  mkdir $pkgdir/usr/lib/systemd/system/multi-user.target.wants
  ln -sf ../rpcbind.socket $pkgdir/usr/lib/systemd/system/sockets.target.wants/rpcbind.socket
  ln -sf ../rpcbind.service $pkgdir/usr/lib/systemd/system/multi-user.target.wants/rpcbind.service

  # add state directory
  install -d -m 700 -o 32 -g 32 $pkgdir/var/lib/rpcbind
  # install license
  install -D -m644 COPYING $pkgdir/usr/share/licenses/rpcbind/COPYING
}
