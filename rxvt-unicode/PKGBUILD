#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=rxvt-unicode
pkgver=9.18
pkgrel=1
pkgdesc="An unicode enabled rxvt-clone terminal emulator (urxvt)."
arch=('x86_64')
depends=('libxft' 'perl' 'startup-notification')
optdepends=('perl: lots of utilities'
            'gtk2: urxvt-tabbed'
            'gtk2-perl: to use the urxvt-tabbed')
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
license=("GPL")
source=("http://dist.schmorp.de/rxvt-unicode/${pkgname}-${pkgver}.tar.bz2"
        "urxvt.desktop"
        "urxvtc.desktop"
        "urxvt-tabbed.desktop"
        "01-fix-tabber-perl-ext.patch")
md5sums=('963ccc748fe5bca925e7b92c0404d68b'
         '4a041e3baa87991c415468e12bc6bcc9'
         'fac55f0a8404c86dad3e702146762332'
         '8a5599197568c63720e282b9722a7990'
         '0c26ae7022a128e68981577785358b63')

prepare() {
  patch -p1 -d $pkgname-$pkgver < 01-fix-tabber-perl-ext.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --with-terminfo=/usr/share/terminfo \
    --enable-256-color \
    --enable-combining \
    --enable-fading \
    --enable-font-styles \
    --enable-iso14755 \
    --enable-keepscrolling \
    --enable-lastlog \
    --enable-mousewheel \
    --enable-next-scroll \
    --enable-perl \
    --enable-pointer-blank \
    --enable-rxwt-scroll \
    --enable-selectionscrolling \
    --enable-slipwheeling \
    --enable-smart-resize \
    --enable-startup-notification \
    --enable-transparency \
    --enable-utmp \
    --enable-wtmp \
    --enable-xft \
    --enable-xim \
    --enable-xterm-scroll \
    --enable-pixbuf \
    --disable-frills
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export TERMINFO="${pkgdir}/usr/share/terminfo"
  install -d "$TERMINFO"
  make DESTDIR="${pkgdir}" install

  # install the tabbing wrapper ( requires gtk2-perl! )
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
  install -Dm 755 doc/rxvt-tabbed "${pkgdir}/usr/bin/urxvt-tabbed"

  cd "${srcdir}"
  # install freedesktop menu
  for _f in urxvt urxvtc urxvt-tabbed; do
    install -Dm644 $_f.desktop "$pkgdir/usr/share/applications/$_f.desktop"
  done

  # remove the terminfo files: we will provide them in the ncurses package,
  # so that, e.g., you can install ncurses on a remote server and correctly
  # connect to it via a remote shell login through rxvt-unicode, not needing
  # to install rxvt-unicode on the server as well just for that matter.
  rm -rf "${pkgdir}/usr/share/terminfo"
}

# vim:set ts=2 sw=2 et:
