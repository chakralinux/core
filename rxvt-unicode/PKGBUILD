#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=rxvt-unicode
pkgver=9.15
pkgrel=3
pkgdesc="An unicode enabled rxvt-clone terminal emulator (urxvt)."
arch=('i686' 'x86_64')
depends=('gcc-libs' 'libxft' 'libxpm')
makedepends=('ncurses' 'perl>=5.10.0' 'pkgconfig')
optdepends=('perl: lots of utilities'
            'gtk2: urxvt-tabbed')
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
license=("GPL")
source=("http://dist.schmorp.de/rxvt-unicode/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.desktop"
        "${pkgname}.png"
        'popup-menu-hang.diff')
md5sums=('15595aa326167ac5eb68c28d95432faf' '5bfefa1b41c2b81ca18f2ef847330543'\
         '84328cada91751df07324d95f8be4d1b' 'aead33ba3b08eeb251fb0c1427a4a024')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i "${srcdir}/popup-menu-hang.diff" || return 1

  ./configure --prefix=/usr \
              --with-terminfo=/usr/share/terminfo \
              --enable-font-styles \
              --enable-xim \
              --enable-keepscrolling \
              --enable-selectionscrolling \
              --enable-smart-resize \
              --enable-xpm-background \
              --enable-transparency \
              --enable-utmp \
              --enable-wtmp \
              --enable-lastlog \
              --enable-xft \
              --enable-startup-notification \
              --enable-text-blink \
              --enable-pointer-blank \
              --disable-frills || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}/usr/share/terminfo"
  export TERMINFO="${pkgdir}/usr/share/terminfo"
  make DESTDIR="${pkgdir}" install || return 1

  # install the tabbing wrapper ( requires gtk2-perl! )
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed || return 1
  install -Dm 755 doc/rxvt-tabbed "${pkgdir}/usr/bin/urxvt-tabbed" || return 1

  # install freedesktop menu and icon ( icon from cvs checkout )
  install -Dm644 "${srcdir}/${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${srcdir}/${pkgname}.png" \
                 "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # remove the terminfo files: we will provide them in the ncurses package,
  # so that, e.g., you can install ncurses on a remote server and correctly
  # connect to it via a remote shell login through rxvt-unicode, not needing
  # to install rxvt-unicode on the server as well just for that matter.
  rm -rf "${pkgdir}/usr/share/terminfo"
}

# vim:set ts=2 sw=2 et:
