#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=perl-locale-gettext
_realname=gettext
pkgver=1.05
pkgrel=6
pkgdesc="Permits access from Perl to the gettext() family of functions"
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('gettext' 'perl>=5.10.0')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/P/PV/PVANDRY/${_realname}-${pkgver}.tar.gz
        compatibility-with-POSIX-module.patch)
md5sums=('f3d3f474a1458f37174c410dfef61a46'
         '854302f34d01a2f8ae739187788973dd')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i ${srcdir}/compatibility-with-POSIX-module.patch
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}