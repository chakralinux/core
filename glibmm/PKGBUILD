#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.38.1
pkgrel=2
arch=('x86_64')
license=('LGPL')
makedepends=('glib2' 'libsigc++2.0' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
url="http://gtkmm.sourceforge.net/"
md5sums=('50d86a77398cd473ff13c4c9382e39bd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++2.0')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install
}
