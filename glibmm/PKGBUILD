# $Id: PKGBUILD 79559 2010-05-04 13:57:18Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.25.5
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('glib2>=2.24.1' 'libsigc++2.0>=2.2.5' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.25/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool')
url="http://gtkmm.sourceforge.net/"
sha256sums=('a808bc5872556697a5bb221a3f9f14de9e8224b90a1900e7b37dcd0b3fcb0051')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2>=2.24.0' 'libsigc++2.0>=2.2.5')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install || return 1
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install || return 1
}
