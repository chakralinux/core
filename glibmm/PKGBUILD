
# Platform Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.32.0
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('glib2' 'libsigc++2.0' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
options=('!libtool')
url="http://gtkmm.sourceforge.net/"
sha256sums=('e1806f884c6e9f904ee2af0d39dd8d4de3f91a039f897f54333b0890de06f94b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++2.0')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install
}
