# Platform Packages for Chakra, part of chakra-project.org
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributions from AUR: https://aur.archlinux.org/packages/bumblebee/

pkgname=bumblebee
pkgver=3.2.1
pkgrel=1
pkgdesc="Bumblebee brings Optimus Support for Linux Through VirtualGL. You need to install proper drivers separately. Can be used with Nouveau or Nvidia"
arch=('x86_64')
depends=('virtualgl' 'glib2')
makedepends=('help2man')
optdepends=('xf86-video-nouveau: Nouveau driver' 
            'nouveau-dri: 3D acceleration features fo Nouveau'
            'mesa: 3D acceleration features fo Nouveau'
            'bbswitch: switch on/off discrete card' 'nvidia-utils-bumblebee: Nvidia utils not breaking LibGL'
            'nvidia: Nvidia kernel driver'
            'lib32-virtualgl: run 32bit applications with optirun'
            'primus: alternative back-end for optirun'
            'lib32-primus: alternative back-end for optirun' )
url="http://www.Bumblebee-Project.org"
license=("GPL3")
install='bumblebee.install'
provides=('bumblebee')
backup=('etc/bumblebee/bumblebee.conf'
        'etc/bumblebee/xorg.conf.nouveau'
         'etc/bumblebee/xorg.conf.nvidia')
source=("http://www.bumblebee-project.org/${pkgname}-${pkgver}.tar.gz")
md5sums=('30974e677bb13e8a3825fd6f3e7d3b24')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia-bumblebee:/usr/lib32/nvidia-bumblebee \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia-bumblebee/xorg/,/usr/lib/xorg/modules \
        --prefix=/usr \
        --with-udev-rules=/usr/lib/udev/rules.d/ \
        --without-pidfile \
        --sysconfdir=/etc
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make install DESTDIR="$pkgdir"
  
    # Install systemd unit
    install -D -m644 "scripts/systemd/bumblebeed.service" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"

    # Make bash_completion work
    mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"
   }
