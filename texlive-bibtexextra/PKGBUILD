#
# Chakra 
#
# Maintainer: Drake Justice <djustice[at]chakra-linux[dot]org>
#


pkgname=texlive-bibtexextra
pkgver=2012.26868
_revnr=${pkgver#2012.}
pkgrel=1
pkgdesc="TeX Live - Additional BibTeX styles and bibliography databases"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
install=texlive.install
md5sums=('6781ae49bf64b298037028d627bb4870')

build() {
   cd "${srcdir}"

   for p in *.tar.xz; do
	   bsdtar -xf $p
   done

   rm -rf {tlpkg,doc,source} || true
}

package() {
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m755 -d $pkgdir/usr/share

   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done

   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi

   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi

   install -m755 -d $pkgdir/usr/bin

   ln -s /usr/share/texmf-dist/scripts/bibexport/bibexport.sh $pkgdir/usr/bin/bibexport
   ln -s /usr/share/texmf-dist/scripts/listbib/listbib $pkgdir/usr/bin/listbib
   ln -s /usr/share/texmf-dist/scripts/urlbst/urlbst $pkgdir/usr/bin/urlbst
}

