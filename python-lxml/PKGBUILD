#
# Platform packages for Chakra, part of chakra-project.org
#
# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>

pkgbase=python-lxml
pkgname=('python3-lxml' 'python2-lxml' 'python-lxml-docs')
pkgver=3.2.3
pkgrel=1
arch=('i686' 'x86_64')
license=('BSD' 'GPL' 'custom')
makedepends=('python3' 'python2' 'libxslt')
# makedepends=('python3' 'python2' 'libxslt' 'python3-cssselect' 'python2-cssselect')
source=(http://pypi.python.org/packages/source/l/lxml/lxml-${pkgver}.tar.gz)
sha512sums=('c3af0d94a93a289c47d60bd96b4f55529bb01da086c8e716e3b7d9fcaf89b7bd4b5357c01ca393d41379c7593e2ba48bb9cfbca1626dd33b60f7f73d621a1d5a')

package_python2-lxml() {
    pkgdesc="Python 2 binding for the libxml2 and libxslt libraries"
    depends=('python2' 'libxslt')
    optdepends=('python2-beautifulsoup: support for parsing not well formed HTML'
                'python-lxml-docs: offline docs')

    cd ${srcdir}/lxml-$pkgver
    python2 setup.py install --root="${pkgdir}" --optimize=1

    install -Dm644 LICENSES.txt \
        "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 doc/licenses/BSD.txt \
        "${pkgdir}"/usr/share/licenses/$pkgname/BSD.txt
    install -Dm644 doc/licenses/elementtree.txt \
        "${pkgdir}"/usr/share/licenses/$pkgname/elementtree.txt
}

package_python3-lxml() {
    pkgdesc="Python 3 binding for the libxml2 and libxslt libraries"
    depends=('python3' 'libxslt')
    optdepends=('python3-beautifulsoup: support for parsing not well formed HTML'
                'python-lxml-docs: offline docs')

    cd ${srcdir}/lxml-$pkgver
    python3 setup.py install --root="${pkgdir}" --optimize=1

    install -Dm644 LICENSES.txt \
        "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 doc/licenses/BSD.txt \
        "${pkgdir}"/usr/share/licenses/$pkgname/BSD.txt
    install -Dm644 doc/licenses/elementtree.txt \
        "${pkgdir}"/usr/share/licenses/$pkgname/elementtree.txt
}

package_python-lxml-docs() {
    pkgdesc="Documentation covering the Python binding for the libxml2 and libxslt libraries"
    options=('docs')

    cd ${srcdir}/lxml-$pkgver
    install -d "${pkgdir}"/usr/share/doc/${pkgbase}
    cp -r doc/html "${pkgdir}"/usr/share/doc/${pkgbase}

    install -Dm644 LICENSES.txt \
        "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 doc/licenses/BSD.txt \
        "${pkgdir}"/usr/share/licenses/$pkgname/BSD.txt
    install -Dm644 doc/licenses/elementtree.txt \
        "${pkgdir}"/usr/share/licenses/$pkgname/elementtree.txt
}
