# Contributor from Arch Linux: https://projects.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/numactl

pkgname=numactl
pkgver=2.0.11
pkgrel=1
pkgdesc="Simple NUMA policy support"
arch=('i686' 'x86_64')
url="http://oss.sgi.com/projects/libnuma/"
license=('LGPL2.1' 'GPL2')
depends=('perl')
source=(ftp://oss.sgi.com/www/projects/libnuma/download/$pkgname-${pkgver/_/-}.tar.gz)
md5sums=('d3bc88b7ddb9f06d60898f4816ae9127')
sha1sums=('0846670269824078a4eae8a977728175437b0da4')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install
  # move_pages(2) is provided by man-pages and is more up-to-date there anyway
  rm -rf "$pkgdir/usr/share/man/man2"
}
