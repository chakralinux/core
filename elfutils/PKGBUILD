
pkgbase=elfutils
pkgname=(elfutils libelf)
pkgver=0.175
pkgrel=1
pkgdesc="Utilities to handle ELF object files and DWARF debugging information"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
options=('staticlibs')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig})
sha1sums=('361f835640ecffddc6d4543fb044eb53f673026f'
          'SKIP')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A')  # Mark J. Wielaard <mark@klomp.org>

build() {
  cd ${pkgbase}-${pkgver}

  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-" --enable-deterministic-archives

  make
}

check() {
  cd ${pkgbase}-${pkgver}

  make check || (cat tests/test-suite.log; false)
}

package_elfutils() {
  depends=("libelf=$pkgver-$pkgrel")

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install

  mkdir "${srcdir}"/libelf
  mv "${pkgdir}"/usr/{lib,include} "${srcdir}/libelf"
}

package_libelf() {
  pkgdesc="Libraries to handle ELF object files and DWARF debugging information"
  mv libelf "${pkgdir}/usr"
}
