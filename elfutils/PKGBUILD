#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=elfutils
pkgver=0.156
pkgrel=1
pkgdesc="Collection of libraries and utilities for working with ELF object files and DWARF debugging information."
arch=('x86_64')
url="https://fedorahosted.org/elfutils/"
license=('GPL2')
depends=('glibc')
provides=('libelf')
replaces=('libelf')
conflicts=('libelf')
source=("https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2")
md5sums=('f3b5cbb63b0757f571c6cc109df4c55b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # make check requires that we have dwarf information; it should be stripped away later
  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-"
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
