# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Yichao Yu <yyc1992@gmail.com>
#               Douglas Soares de Andrade <douglas@archlinux.org>
#               riai <riai@bigfoot.com> Ben <ben@benmazer.net>
#               Gerardo

pkgbase=pyqt5
pkgname=('python-pyqt5-common' 'python3-pyqt5' 'python2-pyqt5')
pkgver=5.3.1
pkgrel=2
arch=('x86_64')
url='http://riverbankcomputing.co.uk/software/pyqt/intro'
license=('GPL')
makedepends=('python2-dbus'
             'python2-opengl'
             'python2-sip'
             'python3-dbus'
             'python3-opengl'
             'python3-sip'
             'qt5-multimedia'
             'qt5-serialport'
             'qt5-svg'
             'qt5-tools')
source=("http://downloads.sourceforge.net/project/pyqt/PyQt5/PyQt-${pkgver}/PyQt-gpl-${pkgver}.tar.gz")
md5sums=('c24b1a4040292fcfdbf128a9a5a38e66')

build() {
    cp -r ${srcdir}/PyQt-gpl-${pkgver} ${srcdir}/python2-Qt-gpl-${pkgver}

    # Python 3 version
    cd ${srcdir}/PyQt-gpl-${pkgver}
    msg "Configuring the Python 3 version"
    python3 configure.py \
        --confirm-license \
        -v /usr/share/sip/PyQt5 \
        --qsci-api \
        -q /usr/bin/qmake-qt5
    find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
    msg "Making the Python 3 version"
    make

    # Python 2 version
    cd ${srcdir}/python2-Qt-gpl-${pkgver}
    msg "Configuring the Python 2 version"
    python2 configure.py \
        --confirm-license \
        -v /usr/share/sip/Py2-PyQt5 \
        --qsci-api \
        -q /usr/bin/qmake-qt5
    find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'
    msg "Making the Python 2 version"
    make
}

package_python-pyqt5-common(){
    pkgdesc="Common PyQt files shared between python3-pyqt5 and python2-pyqt5"
    depends=('qt5-base')
    conflicts=('pyqt5-common')

    cd ${srcdir}/PyQt-gpl-${pkgver}
    msg "Packaging python-pyqt5-common"
    make -C pyrcc DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
    make -C pylupdate DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
    install -Dm644 PyQt5.api "${pkgdir}"/usr/share/qt5/qsci/api/python/PyQt5.api
}

package_python3-pyqt5(){
    pkgdesc="A set of Python 3 bindings for the Qt5 toolkit"
    depends=('python3'
             'python-pyqt5-common'
             'python3-sip')
    optdepends=('python3-dbus: for python-dbus mainloop support'
                'python3-opengl: enable OpenGL 3D graphics in PyQt applications'
                'qt5-declarative: QtQml, qmlplugin'
                'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
                'qt5-serialport: QtSerialPort'
                'qt5-svg: QtSvg'
                'qt5-tools: QtHelp, QtDesigner'
                'qt5-webkit: QtWebKit, QtWebKitWidgets'
                'qt5-xmlpatterns: QtXmlPatterns')

    cd ${srcdir}/PyQt-gpl-${pkgver}
    msg "Packaging python3-pyqt5"

    # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    # Fix naming conflict with python2-pyqt5
    mv "${pkgdir}"/usr/bin/pyuic5{,-3.3}

    # Default /usr/bin/pyuic5
    ln -s /usr/bin/pyuic5-3.3 "${pkgdir}"/usr/bin/pyuic5

    # Provided by python-pyqt5-common
    rm "${pkgdir}"/usr/bin/{pylupdate5,pyrcc5}
    rm -r "${pkgdir}"/usr/share/qt5
}

package_python2-pyqt5(){
    pkgdesc="A set of Python 2 bindings for the Qt5 toolkit"
    depends=('python-pyqt5-common'
             'python2'
             'python2-sip')
    optdepends=('python2-dbus: for python-dbus mainloop support'
                'python2-opengl: enable OpenGL 3D graphics in PyQt applications'
                'qt5-declarative: QtQml, qmlplugin'
                'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
                'qt5-serialport: QtSerialPort'
                'qt5-svg: QtSvg'
                'qt5-tools: QtHelp, QtDesigner'
                'qt5-webkit: QtWebKit, QtWebKitWidgets'
                'qt5-xmlpatterns: QtXmlPatterns')

    cd ${srcdir}/python2-Qt-gpl-${pkgver}
    msg "Packaging pyqt5-python2"

    # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    # Fix naming conflict with python3-pyqt5
    mv "${pkgdir}"/usr/bin/pyuic5{,-2.7}

    # Provided by python-pyqt5-common
    rm "${pkgdir}"/usr/bin/{pylupdate5,pyrcc5}
    rm "${pkgdir}"/usr/share/qt5/qsci/api/python/PyQt5.api
    
    # Provided by python3-pyqt5
    rm "${pkgdir}"/usr/lib/qt5/plugins/designer/libpyqt5.so
    rm "${pkgdir}"/usr/lib/qt5/plugins/PyQt5/libpyqt5qmlplugin.so
}
