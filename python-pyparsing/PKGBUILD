# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

pkgbase=python-pyparsing
pkgname=('python3-pyparsing' 'python2-pyparsing')
pkgver=2.0.3
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='http://pyparsing.wikispaces.com'
license=('MIT')
makedepends=('python3' 'python2')
source=("http://downloads.sourceforge.net/pyparsing/pyparsing-$pkgver.tar.gz")
sha256sums=('06e729e1cbf5274703b1f47b6135ed8335999d547f9d8cf048b210fb8ebf844f')

prepare() {
  cp -vr $srcdir/pyparsing-$pkgver $srcdir/pyparsing-$pkgver-py2
}
package_python3-pyparsing() {
  cd "$srcdir/pyparsing-$pkgver"

  rm -rf build
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyparsing() {
  cd "pyparsing-$pkgver-py2"

  rm -rf build
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-pyparsing/LICENSE"
}

# vim:set ts=2 sw=2 et:
