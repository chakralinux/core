#
# Platform Packages for Chakra, part of chakra-project.org
#
# Arch Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
#
# Contributor: abveritas@chakra-project.org
# Maintainer: BrLi <rainman59118[at]gmail.com>

pkgname=rdesktop
pkgver=1.8.3
pkgrel=1
depends=('libx11' 'openssl' 'libao' 'libsamplerate' 'libxrandr' 'pcsclite')
pkgdesc="An open source client for Windows Remote Desktop Services"
url="http://www.rdesktop.org/"
license=('GPLv3')
arch=('x86_64')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        rdesktop-send_physical_buttons.diff)
md5sums=('86e8b368a7c715e74ded92e0d7912dc5'
         'cbfb12729e7f28e497afb883cc42022b')

prepare() {
  cd ${pkgname}-${pkgver}
  # FS#15113
  patch -i "${srcdir}/rdesktop-send_physical_buttons.diff"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-smartcard \
    --with-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}