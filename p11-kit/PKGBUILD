pkgname=p11-kit
pkgver=0.23.1
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules"
arch=('x86_64')
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=('glibc' 'libtasn1' 'libffi')
source=($url/releases/$pkgname-$pkgver.tar.gz{,.sig}
        libnssckbi-compat.patch)
md5sums=('96f073270c489c9a594e1c9413f42db8'
         'SKIP'
         '8c3f119005908cf4a3e0ef0a0a310f14')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF')

prepare() {
  cd $pkgname-$pkgver

  # Build and install an additional library (libnssckbi-p11-kit.so) which
  # is a copy of p11-kit-trust.so but uses the same label for root certs as
  # libnssckbi.so ("Builtin Object Token" instead of "Default Trust")
  # https://bugs.freedesktop.org/show_bug.cgi?id=66161
  patch -Np1 -i ../libnssckbi-compat.patch

  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-module-path=/usr/lib/pkcs11 \
    --with-trust-paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  make
}

check() {
  cd $pkgname-$pkgver
  #not ok 6 /token/not-writable
  # assertion failed (!p11_token_is_writable (token))
  # in test_not_writable() at test-token.c:245
  make check | true
}

package() {
  cd  $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING

  ln -srf "$pkgdir/usr/bin/update-ca-trust" "$pkgdir/usr/lib/p11-kit/trust-extract-compat"
}