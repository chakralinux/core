pkgname=valgrind
pkgver=3.12.0
pkgrel=2
pkgdesc="A tool to help find memory-management problems in programs"
arch=('x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.25' 'glibc<2.26' 'perl')
makedepends=('gdb' 'openmpi')
optdepends=('openmpi: MPI support')
options=('!emptydirs' '!buildflags')
# patches taken from Arch
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('7a6878bf998c60d1e377a4f22ebece8d9305bda4')

prepare() {
  cd ${pkgname}-${pkgver}
}

build() {
  # valgrind does not like stack protector flags
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fstack-protector-strong/}
  CXXFLAGS=${CXXFLAGS/-fstack-protector-strong/}

  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
