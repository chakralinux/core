pkgbase=pangomm
pkgname=(pangomm pangomm-docs)
pkgver=2.40.1
pkgrel=1
pkgdesc="C++ bindings for Pango"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(pango glibmm cairomm)
makedepends=(mm-common glibmm-docs cairomm-docs pkgconfig)
source=(http://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
md5sums=('874eadd9434613dbacf0272c82c3ac23')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}
package_pangomm() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_pangomm-docs() {
  pkgdesc="Developer documentation for pangomm"
  depends=()

  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install
}
