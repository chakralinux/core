# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/telepathy-qt

pkgbase=telepathy-qt
pkgname=('telepathy-qt4' 'telepathy-qt5')
pkgver=0.9.6
pkgrel=1
arch=('x86_64')
url="http://telepathy.freedesktop.org/wiki/"
license=('LGPL')
makedepends=('qt4' 'telepathy-farstream' 'libxslt' 'python2' 'cmake' 'doxygen' 'qt5-base')
options=('staticlibs')
source=("http://telepathy.freedesktop.org/releases/${pkgbase}/${pkgbase}-${pkgver}.tar.gz"
  '0001-CMake-Fixed-deprecated-_BSD_SOURCE.patch'
  '0002-CMakeLists-Minimum-version-bumped-to-2.8.12.patch'
  '0021-Farstream-gst-gstconfig.h-can-be-in-LIBDIR-search-fo.patch')
md5sums=('1f18935f30dcf90282a2d6d65fcde752'
         '114e5f9747fe218f7e08864d67515b74'
         '2ce35b9e77326324a747e38a3f54a14a'
         '18ed8c6671e21c34e8cb354b40eb7944')

prepare() {
  [ -d build ] && rm -r build
  [ -d build-qt4 ] && rm -r build-qt4
  mkdir build{,-qt4}
  
  cd "$pkgbase-$pkgver"
  patch -Np1 -i ../0001-CMake-Fixed-deprecated-_BSD_SOURCE.patch
  patch -Np1 -i ../0002-CMakeLists-Minimum-version-bumped-to-2.8.12.patch
  patch -Np1 -i ../0021-Farstream-gst-gstconfig.h-can-be-in-LIBDIR-search-fo.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DENABLE_EXAMPLES=OFF \
    -DENABLE_TESTS=OFF \
    -DDESIRED_QT_VERSION=5 \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt5 
  make
  cd ..

  cd build-qt4
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DDESIRED_QT_VERSION=4 \
    -DENABLE_EXAMPLES=OFF \
    -DENABLE_TESTS=OFF
  make
}

package_telepathy-qt4() {
  pkgdesc="A library for Qt4-based Telepathy clients"
  depends=('qt4' 'telepathy-farstream')
  conflicts=('telepathy-qt')
  replaces=('telepathy-qt')
  provides=('telepathy-qt')

  cd build-qt4
  make DESTDIR="$pkgdir" install
}

package_telepathy-qt5() {
  pkgdesc="A library for Qt5-based Telepathy clients"
  depends=('qt5-base' 'telepathy-farstream')

  cd $srcdir/build
  make DESTDIR="$pkgdir" install
}
