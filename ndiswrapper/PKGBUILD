#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

_extramodules=extramodules-3.8-CHAKRA
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=ndiswrapper
pkgver=1.58
pkgrel=1
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors. For linux-testing."
license=('GPL')
arch=('x86_64')
url="http://ndiswrapper.sourceforge.net"
install="ndiswrapper.install"
depends=('linux>=3.8' 'linux<3.9')
provides=("ndiswrapper-utils=$pkgver")
replaces=('ndiswrapper-utils')
conflicts=('ndiswrapper-utils')
makedepends=('linux-headers')
source=("http://downloads.sourceforge.net/sourceforge/ndiswrapper/ndiswrapper-$pkgver.tar.gz")
md5sums=('ba6f57e5eb2f9e94cb07c7f151b2afcd')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i "/modinfo/s/s/usr\//" driver/Makefile
  sed -i "s|/include/linux/version.h|/include/generated/uapi/linux/version.h|g" driver/Makefile
  make KVERS=$_kver
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make INST_DIR="lib/modules/$_extramodules" \
    KVERS=$_kver DESTDIR="$pkgdir/" install

  gzip "$pkgdir/lib/modules/$_extramodules/$pkgname.ko"
}
