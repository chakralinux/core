# $Id: PKGBUILD 80074 2010-05-10 21:13:12Z thomas $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libgcrypt
pkgver=1.5.0
pkgrel=1
pkgdesc="a general purpose crypto library based on the code used"
arch=('i686' 'x86_64')
url="http://www.gnupg.org"
license=('LGPL')
depends=('libgpg-error>=1.8')
options=(!libtool)
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('3e776d44375dc1a710560b98ae8437d5da6e32cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # keep static library for , needed for cryptsetup
  ./configure --prefix=/usr --disable-padlock-support
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Move dynamic libraries to /lib
  install -d -m755 "${pkgdir}"/lib/ || return 1
  mv "${pkgdir}"/usr/lib/libgcrypt.so* "${pkgdir}"/lib/ || return 1
  ln -sf /lib/libgcrypt.so "${pkgdir}/usr/lib/libgcrypt.so" || return 1
}
