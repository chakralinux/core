#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=sudo
_sudover=1.8.11p2
pkgver=${_sudover/p/.p}
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root."
arch=('i686' 'x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
depends=('glibc' 'pam' 'libldap')
backup=('etc/sudoers' 'etc/pam.d/sudo')
source=("ftp://ftp.sudo.ws/pub/sudo/${pkgname}-${_sudover}.tar.gz"
        sudo.tmpfiles.conf
        'sudo.pam')
sha1sums=('1fb7cff2b7ed67ba98b1518f6cb0dd80dbcc2c48'
          'b2a8594916f6920568cf6d7eada5684109cf5510'
          '2388e362dc31cc3d7270fe1c86b6be9e6d28470d')

build() {
  cd "$srcdir/$pkgname-$_sudover"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-rundir=/run/sudo \
    --with-vardir=/var/db/sudo \
    --with-logfac=auth \
    --with-pam \
    --with-ldap \
    --with-ldap-conf-file=/etc/openldap/ldap.conf \
    --with-env-editor \
    --with-passprompt="[sudo] password for %p: " \
    --with-all-insults
  make
}

check() {
  cd "$srcdir/$pkgname-$_sudover"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_sudover}"

  make DESTDIR="${pkgdir}" install

  # Remove /run/sudo directory from the package; we create it using tmpfiles.d
  # see 'Major changes between version 1.8.10 and 1.8.9p5:' on http://www.sudo.ws/sudo/stable.html why the run dir is needed 
  rmdir "$pkgdir/run/sudo"
  rmdir "$pkgdir/run"

  install -Dm644 "$srcdir/sudo.tmpfiles.conf" \
    "$pkgdir/usr/lib/tmpfiles.d/sudo.conf"

  install -Dm644 "$srcdir/sudo.pam" "$pkgdir/etc/pam.d/sudo"

  install -Dm644 doc/LICENSE "$pkgdir/usr/share/licenses/sudo/LICENSE"
}

# vim:set ts=2 sw=2 et:
