# Platform Packages for Chakra, part of chakra-project.org

pkgname=perl-net-ssleay
_cpanname=Net-SSLeay
pkgver=1.48
pkgrel=1
pkgdesc="Perl extension for using OpenSSL"
arch=('i686' 'x86_64')
license=('custom:BSD')
url="http://search.cpan.org/dist/${_cpanname}/"
depends=('openssl')
options=(!emptydirs)
replaces=('net-ssleay')
provides=('net-ssleay')
source=("http://www.cpan.org/authors/id/M/MI/MIKEM/${_cpanname}-${pkgver}.tar.gz")
md5sums=('4e10d9da28f26732e37807820bf72af5')

build() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -- INSTALLDIRS=vendor 
  make
}

package() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  make install DESTDIR=${pkgdir} 

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete

  install -D -m644 README "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
