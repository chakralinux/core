#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=hwinfo
pkgver=19.0
pkgrel=4
pkgdesc="A simple program that lists results from the hardware detection library."
arch=('i686' 'x86_64')
url="http://www.opensuse.org"
license=('GPL2')
depends=('perl' 'libx86emu')
makedepends=('sysfsutils' 'rpmextract' 'flex')
options=('!emptydirs')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/${pkgname}-${pkgver}-4.2.src.rpm"
        "kbd.patch")

build() {
  cd "${srcdir}"

  msg2 "Extracting source rpm..."
  rpmextract.sh "${pkgname}-${pkgver}-4.2.src.rpm"
  tar -xjf ${pkgname}-${pkgver}.tar.bz2
  cd "${pkgname}-${pkgver}"
  msg2 "Patching..."
  patch -p0 < ../kbd.patch
  # Fix Makefile using /usr/lib64 instead of /usr/lib - it broke x86_64, possibly i686 too. 
  # Comment if this fix breaks it.
  sed -i 's_usr/lib64_usr/lib_' src/hd/Makefile 
  make -j1 CFLAGS+="-fPIC -I${srcdir}/${pkgname}-${pkgver}/src/hd" LIBDIR=/usr/lib
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
md5sums=('a9173a198cacd834fb596c47d163e0fd'
         '1500ecfa26f6aaf4d7967eea74e86e5d')
