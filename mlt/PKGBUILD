# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: AlmAck

pkgbase=mlt
pkgname=('mlt' 'mlt-python-bindings')
pkgver=0.9.2
pkgrel=3
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=('GPL')
depends=('sdl_image' 'libsamplerate' 'libdv' 'qt' 'sox' 'libxml2' 'ffmpeg'
         'frei0r-plugins' 'python2' 'vid.stab')
optdepends=('jack')
makedepends=('swig')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('f88ead8fcea628a65ebf329cc4470b6d')

build() {
  # mlt
  cd "${srcdir}/mlt-${pkgver}"

  ./configure --prefix=/usr \
              --enable-gpl \
              --enable-gpl3 \
              --qimage-libdir=/usr/lib/ \
              --qimage-includedir=/usr/include/Qt \
              --avformat-vdpau \
              --disable-gtk2 \
              --avformat-swscale
  make

  # mlt python bindings
  cd "${srcdir}/mlt-${pkgver}/src/swig/python"
  sed -i 's_path=`which python_path=`which python2_' build
  sed -i 's_`python -c_`python2 -c_' build
  sed -i 's#python-config#python2-config#' build
  ./build
}

package_mlt() {
  depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg>=0.11' 'frei0r-plugins' 'vid.stab')
  optdepends=('jack' 'libexif' 'qt' 'ladspa')

  cd "${srcdir}/mlt-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_mlt-python-bindings() {
  depends=('python2' 'mlt>=0.8')

  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "${pkgdir}/usr/lib/python2.7/"
  install -m0755 mlt.py "${pkgdir}/usr/lib/python2.7/"
  install -m0755 _mlt.so "${pkgdir}/usr/lib/python2.7/"
  install -m0755 mlt_wrap.o "${pkgdir}/usr/lib/python2.7/"
}
