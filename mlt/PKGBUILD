# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: AlmAck

pkgbase=mlt
pkgname=(mlt mlt-python-bindings)
pkgver=6.4.1
pkgrel=2
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=(GPL)
makedepends=(ladspa frei0r-plugins libdv sdl_image libsamplerate sox ffmpeg vid.stab qt5-svg jack libexif python2 swig movit eigen3)
source=("https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz"
        kdenlive-crash-on-exit.patch::"https://github.com/mltframework/mlt/commit/a3188e30.patch"
        mlt-glibc-2.26.patch
        mlt-multithread.patch::"https://github.com/mltframework/mlt/commit/0f8e807a.patch")
sha256sums=('87583af552695b2235f4ee3fc1e645d69e31702b109331d7e8785fb180cfa382'
            '53e1f7c343ac8b9eb1b1bfa4aad2fa11f2bdf166e8d8dc72880cc68b41354fd5'
            '1bb3d01264033e194887897c158931299d60100b030f2cfe4b8a69af8d3110fa'
            '8a240062fcb474b3796dea5a3dd6bcb098675e775c5714166b9031e90b9ae4b5')
options=('debug')

prepare() {
  cd $pkgname-$pkgver
# Fix kdenlive cash on exit
  patch -p1 -i ../kdenlive-crash-on-exit.patch
# Fix build with glibc 2.26
  patch -p1 -i ../mlt-glibc-2.26.patch
# Fix ffmpeg multi-threading
  patch -p1 -i ../mlt-multithread.patch
}

build() {
  # mlt
  cd "$srcdir/mlt-$pkgver"

   #--avformat-vdpau \ bug: https://sourceforge.net/p/mlt/bugs/240/
   #https://bugs.gentoo.org/show_bug.cgi?id=580630

  ./configure --prefix=/usr \
              --avformat-swscale \
              --enable-gpl --enable-gpl3 \
              --qt-includedir=/usr/include/qt5 \
              --qt-libdir=/usr/lib \
              --disable-gtk2
  make

  # mlt python bindings
  cd "$srcdir/mlt-$pkgver/src/swig/python"
  sed -i 's_path=`which python_path=`which python2_' build
  sed -i 's_`python -c_`python2 -c_' build
  sed -i 's#python-config#python2-config#' build
  ./build
}

package_mlt() {
  optdepends=('sdl_image: for SDL plugin'
            'libsamplerate: for libavresample plugin'
            'sox: for SOX (Audio Swiss Army Knife) plugin'
            'ffmpeg: for ffmpeg plugin'
            'vid.stab: for video stabilize plugin'
            'qt5-svg: for Qt5 plugins'
            'jack2: for JACK sound output plugin'
            'ladspa: for LADSPA plugins'
            'libexif: for auto rotate plugin'
            'frei0r-plugins: for additional effects'
            'movit: opengl plugin')
  conflicts=('mlt++<=0.3.8')

  cd "$srcdir/mlt-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_mlt-python-bindings() {
  depends=('python2' 'mlt')

  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt.py "$pkgdir/usr/lib/python2.7/"
  install -m755 _mlt.so "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt_wrap.o "$pkgdir/usr/lib/python2.7/"
}
