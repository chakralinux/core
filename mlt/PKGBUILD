# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: AlmAck

pkgbase=mlt
pkgname=(mlt mlt-python-bindings)
pkgver=6.8.0
pkgrel=1
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=(GPL)
makedepends=(ladspa frei0r-plugins libdv sdl_image libsamplerate sox ffmpeg vid.stab qt5-svg jack libexif python2 swig movit eigen3)
source=("https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz")
md5sums=('28926ce34e3afe054ebd08131ec3d98b')
options=('debug')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  # mlt
  cd "$srcdir/mlt-$pkgver"

   #--avformat-vdpau \ bug: https://sourceforge.net/p/mlt/bugs/240/
   #https://bugs.gentoo.org/show_bug.cgi?id=580630

  ./configure --prefix=/usr \
              --avformat-swscale \
              --enable-gpl --enable-gpl3 \
              --qt-includedir=/usr/include/qt5 \
              --qt-libdir=/usr/lib \
              --disable-gtk2
  make

  # mlt python bindings
  cd "$srcdir/mlt-$pkgver/src/swig/python"
  sed -i 's_path=`which python_path=`which python2_' build
  sed -i 's_`python -c_`python2 -c_' build
  sed -i 's#python-config#python2-config#' build
  ./build
}

package_mlt() {
  optdepends=('sdl_image: for SDL plugin'
            'libsamplerate: for libavresample plugin'
            'sox: for SOX (Audio Swiss Army Knife) plugin'
            'ffmpeg: for ffmpeg plugin'
            'vid.stab: for video stabilize plugin'
            'qt5-svg: for Qt5 plugins'
            'jack2: for JACK sound output plugin'
            'ladspa: for LADSPA plugins'
            'libexif: for auto rotate plugin'
            'frei0r-plugins: for additional effects'
            'movit: opengl plugin')

  cd "$srcdir/mlt-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_mlt-python-bindings() {
  depends=('python2' 'mlt')

  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt.py "$pkgdir/usr/lib/python2.7/"
  install -m755 _mlt.so "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt_wrap.o "$pkgdir/usr/lib/python2.7/"
}
