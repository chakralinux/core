#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

# NOTE: Disabled the gtk2 module so no gtk-preview

pkgbase=mlt
pkgname=('mlt' 'mlt-python-bindings')
pkgver=0.8.2
pkgrel=2
pkgdesc="An open source multimedia framework"
arch=('i686' 'x86_64')
url="http://www.mltframework.org"
license=('GPL')
depends=('sdl_image' 'libsamplerate' 'libdv' 'qt' 'sox' 'libxml2' 'ffmpeg>=0.11' 'frei0r-plugins' 'swig' 'python2')
optdepends=('jack')
makedepends=('subversion')
conflicts=('mlt++<=0.3.8')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('36c743d2abc68ce737ac526507fc2caf71b3d6a53a2bf31d22e8193a8b899577')

build() {
  # mlt
  cd "${srcdir}/mlt-${pkgver}"

#  [ $CARCH == "i686" ] && SSE2=--disable-sse2 || SSE2=
  SSE2=--disable-sse2
  SSE=--disable-sse

  msg "SSE2=$SSE2"
  msg "SSE=$SSE"
  ./configure --prefix=/usr \
              --enable-gpl \
              --qimage-libdir=/usr/lib/ \
              --qimage-includedir=/usr/include/Qt \
              --avformat-vdpau \
              --disable-gtk2 \
              --avformat-swscale $SSE2 $SSE
  make

  # mlt python bindings
  cd "${srcdir}/mlt-${pkgver}/src/swig/python"
  sed -i 's_path=`which python_path=`which python2_' build
  sed -i 's_`python -c_`python2 -c_' build
  sed -i 's#python-config#python2-config#' build
  ./build
}

package_mlt() {
  depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg>=0.11' 'frei0r-plugins')
  optdepends=('jack' 'libexif' 'qt' "ladspa")
  conflicts=('mlt++<=0.3.8')

  cd "${srcdir}/mlt-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_mlt-python-bindings() {
  depends=('python2' 'mlt>=0.8')

  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "${pkgdir}/usr/lib/python2.7/"
  install -m0755 mlt.py "${pkgdir}/usr/lib/python2.7/"
  install -m0755 _mlt.so "${pkgdir}/usr/lib/python2.7/"
  install -m0755 mlt_wrap.o "${pkgdir}/usr/lib/python2.7/"
}
