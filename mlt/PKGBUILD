# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: AlmAck

pkgbase=mlt
pkgname=(mlt mlt-python-bindings)
pkgver=0.9.6
pkgrel=4
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=(GPL)
makedepends=(ladspa frei0r-plugins-qt5 libdv sdl_image libsamplerate sox ffmpeg vid.stab qt5-svg jack libexif python2 swig)
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        "frei0r.patch")
md5sums=('92bcc95af44ff086df0a0ce17a201d1c'
         '7c3fdf6671f0595a59f8ed27bfa8c0e3')

prepare() {
  cd "$srcdir/mlt-$pkgver"
  sed -i 's|freetype/|freetype2/|' src/modules/gtk2/producer_pango.c
  patch -Np1 -i ../frei0r.patch
}

build() {
  # mlt
  cd "$srcdir/mlt-$pkgver"

  export PKG_CONFIG_PATH=/opt/opencv-qt5/lib/pkgconfig

  ./configure --prefix=/usr \
              --avformat-swscale \
              --enable-gpl --enable-gpl3 \
              --avformat-vdpau \
              --qt-includedir=/usr/include/qt5 \
              --qt-libdir=/usr/lib \
              --disable-gtk2
  make

  # mlt python bindings
  cd "$srcdir/mlt-$pkgver/src/swig/python"
  sed -i 's_path=`which python_path=`which python2_' build
  sed -i 's_`python -c_`python2 -c_' build
  sed -i 's#python-config#python2-config#' build
  ./build
}

package_mlt() {
  optdepends=('sdl_image: for SDL plugin'
            'libsamplerate: for libavresample plugin'
            'sox: for SOX (Audio Swiss Army Knife) plugin'
            'ffmpeg: for ffmpeg plugin'
            'vid.stab: for video stabilize plugin'
	    'gtk2: Gtk plugin'
            'qt5-svg: for Qt5 plugins'
            'jack2: for JACK sound output plugin'
            'libexif: for auto rotate plugin')
  conflicts=('mlt++<=0.3.8')

  cd "$srcdir/mlt-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_mlt-python-bindings() {
  depends=('python2' 'mlt>=0.8')

  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt.py "$pkgdir/usr/lib/python2.7/"
  install -m755 _mlt.so "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt_wrap.o "$pkgdir/usr/lib/python2.7/"
}
