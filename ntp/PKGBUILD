# maintainer almack[at]chakraos[dot]org>

pkgname=ntp
pkgver=4.2.8.p1
_realver=4.2.8p1
pkgrel=1
pkgdesc="NTP (Network Time Protocol) tries to keep servers in sync"
arch=('x86_64')
license=('custom')
url="http://www.ntp.org/"
depends=('openssl' 'libedit' 'libcap')
makedepends=('perl-html-parser')
backup=('etc/ntp.conf')
install=install
source=("http://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/ntp-${_realver}.tar.gz"
        'ntp.conf'
        'ntpdate.service'
        'ntpd.service')
md5sums=('65d8cdfae4722226fbe29863477641ed'
         'ac884ad959850731d8f27667681bbdf8'
         '11c7dea7e35034ffc370dafae49f1d45'
         '77d51b701d417aeb2e8911e24287496e')

build() {
  cd "${srcdir}/${pkgname}-${_realver}"
  
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --mandir=/usr/share/man \
    --enable-linuxcaps \
    --enable-ntp-signd
    
  make
}

package() {
  cd "${srcdir}/${pkgname}-$_realver"
  
  make DESTDIR="${pkgdir}" install
  
  install -d -o 87 "${pkgdir}/var/lib/ntp"
  install -Dm644 ../ntp.conf "${pkgdir}"/etc/ntp.conf
  install -Dm644 ../ntpd.service "${pkgdir}"/usr/lib/systemd/system/ntpd.service
  install -Dm644 ../ntpdate.service "${pkgdir}"/usr/lib/systemd/system/ntpdate.service
  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
