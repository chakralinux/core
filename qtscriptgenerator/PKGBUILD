#
# Apps Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qtscriptgenerator
pkgver=0.1.0
pkgrel=5
pkgdesc="Script generator for qt"
arch=("i686" "x86_64")
url="http://code.google.com/p/qtscriptgenerator/"
license=('GPL')
depends=('qt' 'phonon')
makedepends=('pkgconfig' 'mesa')
source=(https://qtscriptgenerator.googlecode.com/files/qtscriptgenerator-src-0.1.0.tar.gz
        phonon.patch
        no-phonon.patch
        qtscriptgenerator-gcc44.patch
        qt48.patch)
md5sums=('ca4046ad4bda36cd4e21649d4b98886d'
         'c0266892ecc645b3407f2203ee11a808'
         'e47b9c362bc7195a0e0c95faf6e88f23'
         'a3d48d6a3cfdd9be397bef01d3e4f638'
         '5802c189ee5d03323abc4c91e210795e')

build() {
  cd "${srcdir}"/$pkgname-src-$pkgver
  
  patch -p1 -i "${srcdir}"/phonon.patch 
  patch -p1 -i "${srcdir}"/no-phonon.patch
  patch -p0 -i "${srcdir}"/qtscriptgenerator-gcc44.patch
  patch -p1 -i "${srcdir}"/qt48.patch
  # prepare plugins
  cd generator
  qmake
  make
  ./generator --include-paths=/usr/include

  # build plugins
  cd "${srcdir}"/$pkgname-src-$pkgver/qtbindings
  qmake
  make
}

package() {
  cd "${srcdir}"/$pkgname-src-$pkgver/qtbindings
  # move plugins to correct directory
  install -d "${pkgdir}"/usr/lib/qt/plugins/script
  mv "${srcdir}"/$pkgname-src-$pkgver/plugins/script/* \
    "${pkgdir}"/usr/lib/qt/plugins/script/
}
