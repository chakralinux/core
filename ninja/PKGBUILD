# Maintainer: AlmAck

pkgname=ninja
pkgver=1.3.4
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=('x86_64')
url='http://martine.github.io/ninja/'
license=(Apache)
depends=('gcc-libs')
makedepends=('asciidoc' 'python2' 're2c' 'git')
optdepends=()
source=("git://github.com/martine/ninja.git#tag=v${pkgver}")
md5sums=('SKIP')

_python="python2"

build() {
    cd "${srcdir}/ninja"
    ${_python} bootstrap.py

    asciidoc doc/manual.asciidoc
}

package() {
    cd "${srcdir}/ninja"

    # Main binary
    install -m755 -D ninja \
                     "${pkgdir}/usr/bin/ninja"

    # Manual
    install -m644 -D doc/manual.asciidoc \
                     "${pkgdir}/usr/share/doc/ninja/manual.asciidoc"
    install -m644 -D doc/manual.html \
                     "${pkgdir}/usr/share/doc/ninja/manual.html"

    # Bash completion
    install -m644 -D misc/bash-completion ${pkgdir}/usr/share/bash-completion/completions/ninja

    # Zsh completion
    install -m644 -D misc/zsh-completion ${pkgdir}/usr/share/zsh/site-functions/_ninja
}
