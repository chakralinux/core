#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=binutils
pkgver=2.21
pkgrel=1
_date=20110416
# dash the next line if you want to skip the tests
_test=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base')
depends=('glibc>=2.12.1' 'zlib')
options=('!libtool' '!distcc' '!ccache' 'log')
install=binutils.install
source=(http://chakra-project.org/sources/${pkgname}/${pkgname}-${pkgver}_${_date}.tar.bz2)
md5sums=('2858722a3f3e0cc7f81a36e6920c17e3')

mksource() {
  rm -vRf ${pkgname}-${_date}
  mkdir ${pkgname}-${_date}
  cd ${pkgname}-${_date}
  export _TAG=binutils-2_21-branch
  export 'CVSROOT=:pserver:anoncvs@sourceware.org:/cvs/src'
  cvs -z9 co -r $_TAG binutils || return 1
  mv src binutils
  tar -cvjf ../binutils-${pkgver}_${_date}.tar.bz2 binutils/*
  cd ..
  md5sum binutils-${pkgver}_${_date}.tar.bz2
}

build() {
  cd ${srcdir}
  rm -vRf binutils-build
  mkdir binutils-build && cd binutils-build

  [[ $CARCH == "x86_64" ]] && CONFIGFLAG="--enable-64-bit-bfd --disable-multilib"

  ${srcdir}/binutils/configure --prefix=/usr \
    --enable-ld=default --enable-gold \
    --enable-plugins --enable-threads \
    --enable-shared $CONFIGFLAG

  # This checks the host environment and makes sure all the necessary tools are available to compile Binutils.
  make configure-host

  make tooldir=${pkgdir}/usr
  make tooldir=${pkgdir}/usr || return 1

  if [ -n "${_test}" ]; then
     echo "<<< STARTING TESTS >>>"
     # do not abort on errors - manually check log files
     make -k -j1 check || true
     echo "<<< ENDING TESTS >>>"
  fi
}

check() {
  cd ${srcdir}/binutils-build

  # do not abort on errors - manually check log files
  make -k -j1 check || true
}

package() {
  cd ${srcdir}/binutils-build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install
  
  # Add some useful headers
  install -m644 ${srcdir}/binutils/include/libiberty.h ${pkgdir}/usr/include
  install -m644 ${srcdir}/binutils/include/demangle.h ${pkgdir}/usr/include

  # Rebuild libiberty.a with -fPIC
  make -C libiberty clean
  make CFLAGS="$CFLAGS -fPIC" -C libiberty
  install -m644 libiberty/libiberty.a ${pkgdir}/usr/lib

  # Rebuild libbfd.a with -fPIC
  make -C bfd clean
  # hidden visability prevent 3rd party shared libraries exporting bfd non-stable API
  make CFLAGS="$CFLAGS -fPIC -fvisibility=hidden" -C bfd
  install -m644 bfd/libbfd.a ${pkgdir}/usr/lib

  # Remove Windows/Novell specific man pages
  rm -f ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # Remove these symlinks, they are not ABI stable.
  # Programs should compile static to the .a file.
  rm -f ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
  echo "INPUT ( /usr/lib/libbfd.a -liberty -lz )" >${pkgdir}/usr/lib/libbfd.so
  echo "INPUT ( /usr/lib/libopcodes.a -lbfd )" >${pkgdir}/usr/lib/libopcodes.so
}
