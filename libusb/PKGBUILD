# maintainer: Neophytos Kolokotronis <tetris4ATgmailDOTcom>
# Contributors: abveritas

pkgname=libusb
pkgver=1.0.19
pkgrel=1
depends=('glibc')
pkgdesc="Library that provides generic access to USB device"
arch=('x86_64')
url='http://libusb.info/'
license=('LGPL')
replaces=('libusb1' 'libusbx')
provides=("libusbx=$pkgver")
conflicts=('libusbx')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('f9e2bb5879968467e5ca756cb4e1fa7e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
