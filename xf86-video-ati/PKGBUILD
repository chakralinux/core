# maintainer abveritas@chakra-project.org

pkgname=xf86-video-ati
pkgver=7.2.0
pkgrel=2
pkgdesc="X.org ati video driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libpciaccess' 'libdrm>=2.4.41' 'systemd' 'pixman' 'mesa')
makedepends=('xorg-server' 'xf86driproto' 'resourceproto' 'scrnsaverproto' 'glamor-egl')
conflicts=('xorg-server<1.12.3' 'xf86-video-radeonhd')
provides=('xf86-video-radeonhd')
replaces=('xf86-video-radeonhd')
groups=('xorg-video-drivers')
options=('!libtool')
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('2b11e423506f1f967d4bb6be0ff1277c24332b68')

build() { 
    cd ${srcdir}/${pkgname}-${pkgver}
    
    ./configure \
            --prefix=/usr \
            --enable-maintainer-mode \
            --enable-dependency-tracking \
	    --enable-glamor
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install 
 
    msg "Copying license..."
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
