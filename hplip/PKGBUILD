
pkgname=hplip
pkgver=3.14.10
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet."
arch=('x86_64')
url="http://hplipopensource.com"
license=('GPL')
depends=('python2' 'ghostscript>=8.64-6' 'foomatic-db' 'foomatic-db-engine'
	'libcups' 'net-snmp>=5.5' 'libusb-compat')
makedepends=('pyqt' 'python2-gobject' 'pkgconfig' 'sane' 'rpcbind' 'dbus-python' 'cups')
optdepends=('cups: for printing support'
            'dbus-python: for dbus support'
            'sane: for scanner support'
            'rpcbind: for network support'
            'python2-pyqt4: for running hp-toolbox'
            'python2-gobject2: for running hp-toolbox')
install=hplip.install
source=(http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz{,.asc}
        disable_upgrade.patch)
md5sums=('7af44ee1c4312b00d2269c1b25008f73'
         'SKIP'
         '4ebb9377a989b4fb776e37f5968bb7b0')

build() {
 cd "$srcdir/$pkgname-$pkgver"
 
 export PYTHON=python2
 
 find . -type f -exec sed -i 's~^#.*env python~#!/usr/bin/env python2~' {} +
 sed -i '1s|#!/usr/bin/python|#!/usr/bin/python2|' doctor.py setup.py makeuri.py logcapture.py base/magic.py
 sed -i '1s|#!/usr/bin/python|#!/usr/bin/python2|' ui/upgradeform.py uninstall.py upgrade.py config_usb_printer.py
 sed -i 's|python ./print.py|python2 ./print.py|' scan.py
 sed -i 's|python ./testpage.py|python2 ./testpage.py|' setup.py
 sed -i 's|python ./setup.py|python2 ./setup.py|' ui4/devmgr5.py ui4/nodevicesdialog.py 
 sed -i 's|python %HOME%|python2 %HOME%|' base/utils.py
 sed -i 's|python ./plugin.py|python2 ./plugin.py|' base/pkit.py
 sed -i 's|/usr/bin/python|/usr/bin/python2|' data/rules/hplip-printer@.service
 
 # disable insecure update
 patch -Np0 -i ${srcdir}/disable_upgrade.patch
 
 # Use system foomatic-rip for hpijs driver instead of foomatic-rip-hplip
 # The hpcups driver does not use foomatic-rip
 local i
 for i in ppd/hpijs/*.ppd.gz ; do
         rm -f ${i}.temp
         gunzip -c ${i} | sed 's/foomatic-rip-hplip/foomatic-rip/g' | \
                 gzip > ${i}.temp || return 1
         mv ${i}.temp ${i}
 done
   
 export AUTOMAKE='automake --foreign'
 autoreconf --force --install

 ./configure --prefix=/usr \
             --enable-qt4 \
             --disable-foomatic-rip-hplip-install \
             --enable-foomatic-ppd-install \
             --enable-hpcups-install \
             --enable-new-hpcups \
             --enable-cups-ppd-install \
             --enable-cups-drv-install \
             --enable-hpijs-install \
             --enable-foomatic-drv-install \
             --enable-pp-build \
             --enable-udev-acl-rules #--help

 make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/.a/a}"
  make rulesdir=/usr/lib/udev/rules.d DESTDIR="$pkgdir/" install
  
  # remove config provided by sane and autostart of hp-daemon
  rm -rf "$pkgdir"/etc/{sane.d,xdg}
  
  # remove HAL .fdi file because HAL is no longer used
  rm -rf "$pkgdir"/usr/share/hal
 
  # remove rc script
  rm -rf "$pkgdir"/etc/init.d
}
