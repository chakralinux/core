# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=hplip
pkgver=3.11.5
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet."
arch=('i686' 'x86_64')
url="http://hplipopensource.com"
license=('GPL')
makedepends=('pyqt' 'pygobject' 'pkgconfig' 'sane' 'rpcbind' 'dbus-python' 'cups')
depends=('python2' 'ghostscript>=8.64-6' 'foomatic-db' 'foomatic-db-engine'
	'libcups>=1.3.10-3' 'net-snmp>=5.5' 'libusb-compat')
replaces=('hpijs')
options=('!libtool')
install=hplip.install
source=(http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz)
optdepends=('cups: for printing support'
            'dbus-python: for dbus support'
            'sane: for scanner support'
            'rpcbind: for network support'
            'pyqt: for running hp-toolbox'
            'pygobject: for running hp-toolbox')

build() {
 cd $srcdir/$pkgname-$pkgver
 ./configure --prefix=/usr \
             --enable-qt4 \
             --enable-foomatic-rip-hplip-install \
             --enable-foomatic-ppd-install \
             --enable-hpcups-install \
             --enable-cups-drv-install \
             --enable-hpijs-install \
             --enable-foomatic-drv-install \
             --enable-pp-build \
             --enable-udev-acl-rules

 make || return 1
 make DESTDIR=$pkgdir/ install

 rm -rf $pkgdir/etc/sane.d
 # remove autostart of hp-daemon
 rm -r $pkgdir/etc/xdg
 # move udev rules to /lib/udev/rules.d
 mkdir -p $pkgdir/lib/udev/rules.d
 mv $pkgdir/etc/udev/rules.d/* $pkgdir/lib/udev/rules.d/
 rm -r $pkgdir/etc/udev
 # remove lp user, it's not used on archlinux
 sed -i 's|OWNER="lp", ||g' $pkgdir/lib/udev/rules.d/40-hplip.rules || return 1
 # fix sysfs rules
 sed -i -e "s|SYSFS|ATTRS|g" -e "s|sysfs|attrs|g" $pkgdir/lib/udev/rules.d/56-hpmud_support.rules || return 1
}
md5sums=('0a3d0f46ec89857b4c0feea6923864f1')
