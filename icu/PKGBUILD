# Core Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=icu
pkgver=51.2
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=('x86_64')
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('gcc-libs' 'sh')
source=(http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz
        icu.8198.revert.icu5431.patch
	#http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver/./_}-src.tgz
)
md5sums=('072e501b87065f3a0ca888f1b5165709'
         'ebd5470fc969c75e52baf4af94a9ee82')

prepare() {
  cd ${srcdir}/icu/source
  # fix Malayalam encoding https://bugzilla.redhat.com/show_bug.cgi?id=654200
  patch -Rp3 -i ${srcdir}/icu.8198.revert.icu5431.patch
}

build() {
  cd ${srcdir}/icu/source
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make 
}

check() {
  cd "$srcdir/icu/source"
  make -k check # passes all
}

package() {
  cd ${srcdir}/icu/source
  make -j1 DESTDIR=${pkgdir} install 

  # Install license
  install -Dm644 ${srcdir}/icu/license.html ${pkgdir}/usr/share/licenses/icu/license.html
}
