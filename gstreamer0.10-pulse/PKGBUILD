#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=gstreamer0.10-pulse
pkgver=0.10.30
pkgrel=2
pkgdesc="A GStreamer 0.10 plugin for the PulseAudio sound server"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig')
url="http://gstreamer.freedesktop.org/"
depends=('gstreamer0.10-base' 'libpulse')
optdepends=('pulseaudio: pulseaudio support')
options=(!libtool !emptydirs)
groups=(pulseaudio)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.bz2)
md5sums=('62fd7a3ef187c4f99b3d7c352d58dae9')


build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Good Plugins (Chakra GNU/Linux)" \
    --with-package-origin="http://www.chakra-project.org/"

  cd ext/pulse && make
}

package() {
  cd "${srcdir}/gst-plugins-good-${pkgver}/ext/pulse"
  make DESTDIR="${pkgdir}" install
}

