#
# Platform packages of Chakra, part of chakra-project.org
#
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# Contributor: Samir Benmendil <ram-z[at]chakra-project[dot]org>

pkgname=mplayer2
pkgver=20130428
_commit=6c87a98
pkgrel=6
pkgdesc="A movie player"
arch=('x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
depends=('desktop-file-utils' 'ttf-dejavu' 'enca' 'libxss' 'a52dec' 'libvpx' 'lirc-utils'
         'x264' 'libmng' 'libdca' 'aalib' 'lame' 'fontconfig'  'libgl' 'libxinerama'
         'libvdpau' 'pulseaudio' 'smbclient' 'xvidcore' 'opencore-amr' 'jack' 'cdparanoia'
         'libmad' 'sdl' 'libtheora' 'libcaca' 'libxxf86dga' 'fribidi' 'libjpeg-turbo'
         'faac' 'faad2' 'libxvmc' 'schroedinger' 'mpg123' 'libass' 'libxxf86vm' 'libbluray'
         'ffmpeg' 'libbs2b' 'directfb' 'nas' 'libdvdread')    
makedepends=('mesa' 'libgl' 'unzip' 'yasm' 'python3' 'python3-docutils' 'git')
categories=('multimedia')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer')
conflicts=('mplayer')
install=$pkgname.install
source=("git://git.mplayer2.org/mplayer2.git#commit=${_commit}"
        "0001-demux_gif-declare-missing-PrintGifError.patch")
sha1sums=('SKIP'
          '02a18919668d1af8ef949215b8bbaa52c27147a4')

pkgver() {
  cd $pkgname
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

prepare() {
  cd $pkgname
  sed 's/gmplayer/mplayer/g' -i etc/mplayer.desktop
#  patch -p1 -i "$srcdir"/0001-demux_gif-declare-missing-PrintGifError.patch
}

build() {
  cd ${pkgname}

  ./configure --prefix=/usr \
              --enable-runtime-cpudetection \
              --disable-speex \
              --disable-openal \
              --disable-libdv \
              --disable-musepack \
              --enable-translation --language=all \
              --confdir=/etc/mplayer
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} "${pkgdir}/etc/mplayer"
  install -dm755 "${pkgdir}/usr/share/mplayer"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "${pkgdir}/usr/share/mplayer/subfont.ttf"

  install -Dm644 etc/mplayer.desktop "$pkgdir"/usr/share/applications/mplayer.desktop
}

# vim:set ts=2 sw=2 et:
