#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
 
pkgname=mplayer2
pkgver=2.0
pkgrel=1
pkgdesc="A media player for Linux, Uoti Urpala's fork"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
depends=('ffmpeg' 'fontconfig' 'gettext' 'libcss' 'libgl' 'libxvmc' 'libxss' 'libxxf86dga' 'sdl' 'ttf-dejavu')
makedepends=('live-media' 'mesa' 'unzip' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer')
conflicts=('mplayer' 'mplayer2-git')
source=(http://ftp.mplayer2.org/pub/release/${pkgname}-build-${pkgver/_/-}.tar.xz)
md5sums=('05b93784de995235e2758f182de15f73')

build() {
    unset CFLAGS LDFLAGS

    cd ${srcdir}/${pkgname}-build-${pkgver/_/-}/${pkgname/2/}

    ./configure --prefix=/usr \
                --enable-runtime-cpudetection \
                --disable-arts \
                --disable-speex \
                --disable-openal \
                --disable-libdv \
                --disable-musepack \
                --disable-esd \
                --disable-pulse \
                --disable-mga \
                --disable-fribidi \
                --enable-xvmc \
                --enable-translation \
                --language-doc=all \
                --language-man=all \
                --language-msg=all \
                --confdir=/etc/mplayer
    make
}

package() {
    cd ${srcdir}/${pkgname}-build-${pkgver/_/-}/${pkgname/2/}

    make DESTDIR="${pkgdir}" install
    install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
    install -dm755 ${pkgdir}/usr/share/mplayer/
    ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
    rm -rf ${pkgdir}/usr/share/mplayer/font
}