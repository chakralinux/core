#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
 
pkgname=mplayer2
pkgver=20121207
pkgrel=2
pkgdesc="A movie player"
arch=('x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
depends=('desktop-file-utils' 'ttf-dejavu' 'enca' 'libxss' 'a52dec' 'libvpx' 'lirc-utils'
         'x264' 'libmng' 'libdca' 'aalib' 'lame' 'fontconfig'  'libgl' 'libxinerama'
         'libvdpau' 'libpulse' 'smbclient' 'xvidcore' 'opencore-amr' 'jack' 'cdparanoia'
         'libmad' 'sdl' 'libtheora' 'libcaca' 'libxxf86dga' 'fribidi' 'libjpeg-turbo'
         'faac' 'faad2' 'libxvmc' 'schroedinger' 'mpg123' 'libass' 'libxxf86vm' 'libbluray'
         'ffmpeg' 'libbs2b' 'directfb' 'nas')    
makedepends=('live-media' 'mesa' 'unzip' 'yasm' 'python3' 'docutils')
categories=('multimedia')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer=99999')
conflicts=('mplayer')
source=("http://chakra-linux.org/sources/mplayer2/mplayer2-$pkgver.tar.xz")
sha1sums=('751334e473a95d23be9d92953568cfe8d7eac152')

# create tarball: source PKGBUILD && mksource

mksource() {
  echo "checkout ${pkgname}"
  git clone git://git.${pkgname}.org/${pkgname}.git
  echo "clean checkout"
  rm -vRf mplayer2/.git
  pushd mplayer2
  popd
  echo "pack checkout"
  tar -cvJf ${pkgname}-${pkgver}.tar.xz mplayer2/*
  echo -n "sha1sum: "
  sha1sum ${pkgname}-${pkgver}.tar.xz
}

build() {

  cd "${srcdir}/${pkgname}"

  ./configure --prefix=/usr \
              --enable-runtime-cpudetection \
              --disable-speex \
              --disable-openal \
              --disable-libdv \
              --disable-musepack \
              --language=all \
              --enable-translation \
              --confdir=/etc/mplayer
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} "${pkgdir}/etc/mplayer"
  install -dm755 "${pkgdir}/usr/share/mplayer"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "${pkgdir}/usr/share/mplayer/subfont.ttf"
}

# vim:set ts=2 sw=2 et:
