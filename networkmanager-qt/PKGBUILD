# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=networkmanager-qt
pkgver=5.49.0
pkgrel=1
pkgdesc='Qt wrapper for NetworkManager API'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/networkmanager-qt'
license=('LGPL')
depends=('networkmanager' 'qt5-base')
makedepends=("extra-cmake-modules>=5.49.0")
checkdepends=("cmake")
groups=('kf5')
replaces=('libnm-qt5')
conflicts=('libnm-qt5')
provides=("libnm-qt5=${pkgver}")
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('5f4c797c5bb2fc73711a91f904702e53f930539b83ed41ab582ceb544569f69a'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

check() {
  cd build
  make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
