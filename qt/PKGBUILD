## rebuild on icu update

pkgbase=qt
pkgname=('qt' 'qt-private-headers' 'qt-docs')
pkgver=4.8.7
pkgrel=9
arch=('x86_64')
url='http://www.qt.io'
license=('GPL3' 'LGPL')
makedepends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
    'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib'
    'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'postgresql-libs'
    'mysql' 'unixodbc' 'cups' 'libxinerama' 'libxcursor' 'freetds')
options=('staticlibs' 'debug')
_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://download.qt.io/archive/qt/4.8/${pkgver}/${_pkgfqn}.tar.gz"
        'assistant.desktop' 'designer.desktop' 'linguist.desktop'
        'qtconfig.desktop' 
        'qt_no_debug.patch'
        'add_nostrip_for_debug_packages.diff'
        'improve-cups-support.patch'
        'moc-boost-workaround.patch'
        'kubuntu_14_systemtrayicon.diff'
        'kde4-settings.patch'
        'glib-honor-ExcludeSocketNotifiers-flag.diff'
        'disable-sslv3.patch'
        'l-qclipboard_fix_recursive.patch'
        'l-qclipboard_delay.patch'
        'qt4-gcc6.patch' 'qt4-glibc-2.25.patch' 'qt4-icu59.patch' 'qt4-openssl-1.1.patch')
sha256sums=('e2882295097e47fe089f8ac741a95fef47e0a73a3f3cdf21b56990638f626ea0'
            '2cc5c2ed8c281a52e164e5552ebaa0002daa3b7fa043ab6349246666773bbafb'
            '41014db487da59208cc3f2ccf7ec4b3130220599715c8559633aef3dd634a10b'
            'e77b365f842b0a881630cb65dc7892c9962d14e8081357dc6160bf7d7e3bdef6'
            'ecab346b7e15e5de1d839456d16d26814e4507a23234d05654a55cb1ce75b23c'
            '8b2adbd707c896d7ad1b02ea2c0bdd978dbbd56d8f0b82c260e007005bd28312'
            '63cbca72141bcd4f6fb0ed6a86d8562cdd82eaab3b71297c3ab0f71c07603f53'
            '6a4e02d7719e20dacddeda175e612ba3dfc6f5e4f27660b76fc66f706f0277cb'
            '876c681ef8fbcc25f28cd4ad6c697abf8a4165d540bae37433bc40256dbf9d62'
            '9fad22674c5eec835613a7f16c11b865aa793b448e90974c0f804105284a548b'
            'ce97da195445f145d9f82df8f8e5d8716128e869ec6632db66c7125be663d813'
            'e7f8d1c906640b836454e8202a48602352609d8e44a33a3de05dc1d20f5b1a8a'
            '829b02ba10f208c2beba8e8a0110b6d10c63932612dabc08d536f099b9f66101'
            '5db36cbb0686b8a503941779c821febc4a0330dc260e51d603f7aa1e4d8860ad'
            'af3648ddb2372333b0e428788fd2ffbcfe571653fb46f898a55ae5a202f7e242'
            '51da49e41edac66559d3ec8dd0a152995a51a53e5d1f63f09fa089a8af7e3112'
            'e6555f4a681227447e94e9f14e11626d50b7e5108aad06088311e87063bc0347'
            '61d6bf45649c728dec5f8d22be5b496ed9d40f52c2c70102696d07133cd1750d'
            'ff3ddb5428cd2ff243558dc0c75b35f470077e9204bbc989ddcba04c866c1b68')

prepare() {
	cd ${_pkgfqn}

	patch -p1 -i "${srcdir}"/qt_no_debug.patch
	# (FS#28381) (KDEBUG#180051)
	patch -p1 -i "${srcdir}"/improve-cups-support.patch

	# QTBUG#22829
	patch -p1 -i "${srcdir}"/moc-boost-workaround.patch

	# http://blog.martin-graesslin.com/blog/2014/06/where-are-my-systray-icons/
	patch -p1 -i "${srcdir}"/kubuntu_14_systemtrayicon.diff

	# FS#45106
	patch -p0 -i "${srcdir}"/kde4-settings.patch

	# fixes for LibreOffice from the upstream Qt bug tracker FS#46436, FS#41648, FS#39819
	# https://bugreports.qt.io/browse/QTBUG-37380
	patch -p1 -i "${srcdir}"/glib-honor-ExcludeSocketNotifiers-flag.diff
	# https://bugreports.qt.io/browse/QTBUG-34614
	patch -p0 -i "${srcdir}"/l-qclipboard_fix_recursive.patch
	# https://bugreports.qt.io/browse/QTBUG-38585
	patch -p0 -i "${srcdir}"/l-qclipboard_delay.patch

	# React to OpenSSL's OPENSSL_NO_SSL3 define
	patch -p1 -i "${srcdir}"/disable-sslv3.patch

	patch -Np1 -i "${srcdir}"/add_nostrip_for_debug_packages.diff

        sed -i "s|-O2|${CXXFLAGS}|" mkspecs/common/{g++,gcc}-base.conf
        sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" mkspecs/common/gcc-base-unix.conf
        sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS}|g" mkspecs/common/gcc-base.conf

        cp mkspecs/common/linux{,32}.conf
        sed -i "/^QMAKE_LIBDIR\s/s|=|= /usr/lib32|g" mkspecs/common/linux32.conf
        sed -i "s|common/linux.conf|common/linux32.conf|" mkspecs/linux-g++-32/qmake.conf
  
	# Fix build with GCC6 (Fedora)
	patch -p1 -i "$srcdir"/qt4-gcc6.patch

	# Fix build of Qt4 applications with glibc 2.25 (Fedora)
	patch -p1 -i "$srcdir"/qt4-glibc-2.25.patch

	# Fix build with ICU 59 (pld-linux)
	patch -p1 -i "$srcdir"/qt4-icu59.patch

	# Fix build with OpenSSL 1.1 (Debian + OpenMandriva)
	#patch -p1 -i "$srcdir"/qt4-openssl-1.1.patch

	echo "QMAKE_CXXFLAGS += -std=gnu++98" >> src/3rdparty/javascriptcore/JavaScriptCore/JavaScriptCore.pri
        echo "QMAKE_CXXFLAGS += -std=gnu++98" >> src/plugins/accessible/qaccessiblebase.pri
}

build() {
	cd ${_pkgfqn}

        export QT4DIR="${srcdir}"/${_pkgfqn}
        export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}

	./configure -confirm-license -opensource \
                -release \
                -fast  \
		-prefix /usr \
		-docdir /usr/share/doc/qt \
		-plugindir /usr/lib/qt/plugins \
		-importdir /usr/lib/qt/imports \
		-datadir /usr/share/qt \
		-translationdir /usr/share/qt/translations \
		-sysconfdir /etc/xdg \
		-examplesdir /usr/share/doc/qt/examples \
		-demosdir /usr/share/doc/qt/demos \
		-plugin-sql-{psql,mysql,sqlite,odbc} \
		-system-sqlite \
		-no-phonon \
		-no-phonon-backend \
		-no-webkit \
		-graphicssystem raster \
                 -openssl-linked \
                 -nomake demos \
                 -nomake examples \
                 -nomake docs \
                 -silent \
                 -no-rpath \
                 -optimized-qmake \
                 -reduce-relocations \
                 -dbus-linked \
                 -no-openvg \
                 -xcursor
	 make
}

package_qt() {
    pkgdesc='A cross-platform application and UI framework'
    depends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
      'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib'
      'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'libxinerama' 'libxcursor')
    optdepends=('postgresql-libs: PostgreSQL driver'
	  'libmysqlclient: MySQL driver'
	  'unixodbc: ODBC driver'
	  'libxinerama: Xinerama support'
	  'libxfixes: Xfixes support'
	  'icu: Unicode support'
	  'freetds: Sybase & MS SQL driver')
    provides=('qt4')
    install='qt.install'
	
    cd $srcdir/$_pkgfqn
    make INSTALL_ROOT=$pkgdir install
   
    # install missing icons and desktop files
    for icon in tools/linguist/linguist/images/icons/linguist-*-32.png ; do
        size=$(echo $(basename ${icon}) | cut -d- -f2)
        install -p -D -m644 ${icon} ${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/linguist.png
    done
    install -p -D -m644 src/gui/dialogs/images/qtlogo-64.png ${pkgdir}/usr/share/icons/hicolor/64x64/apps/qtlogo.png
    install -p -D -m644 tools/assistant/tools/assistant/images/assistant.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/assistant.png
    install -p -D -m644 tools/designer/src/designer/images/designer.png ${pkgdir}/usr/share/icons/hicolor/128x128/apps/designer.png
    install -d ${pkgdir}/usr/share/applications
    install -m644 ${srcdir}/{linguist,designer,assistant,qtconfig}.desktop ${pkgdir}/usr/share/applications/
    
    # install license addition
    install -D -m644 LGPL_EXCEPTION.txt ${pkgdir}/usr/share/licenses/qt/LGPL_EXCEPTION.txt
    
    # Fix wrong path in pkgconfig files
    find ${pkgdir}/usr/lib/pkgconfig -type f -name '*.pc' \
        -exec perl -pi -e "s, -L${srcdir}/?\S+,,g" {} \;
    # Fix wrong path in prl files
    find ${pkgdir}/usr/lib -type f -name '*.prl' \
        -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
}

package_qt-private-headers() {
    pkgdesc="Qt private headers for development"
    provides=('qt4-private-headers')
    depends=("qt=${pkgver}")

    install -d ${pkgdir}/usr/include/{QtCore,QtDeclarative,QtGui,QtScript}
    install -d ${pkgdir}/usr/src/{corelib,declarative,gui,script}
    
    for i in QtCore QtDeclarative QtGui QtScript; do
      cp -r ${srcdir}/$_pkgfqn/include/${i}/private/ \
        ${pkgdir}/usr/include/${i}/
    done

    for i in corelib declarative gui script; do
      cp -r ${srcdir}/$_pkgfqn/src/${i} ${pkgdir}/usr/src/
    done

    find ${pkgdir} -name \*.o -type f -delete 
}

package_qt-docs() {
    pkgdesc='The Qt gui toolkit documentation'
    arch=('x86_64')
    options=('docs')
    depends=('qt')
    replaces=('qt-doc')
    conflicts=("qt-doc=${pkgver}")
    categories=('documentation')
    cd ${srcdir}/${_pkgfqn}
    install -d ${pkgdir}/usr/share/doc/qt
    cp -r doc/* ${pkgdir}/usr/share/doc/qt
    cp -r demos ${pkgdir}/usr/share/doc/qt
    cp -r examples ${pkgdir}/usr/share/doc/qt
}
