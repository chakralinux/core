#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# NOTE: requires rebuilt with each new gcc version
# https://projects.archlinux.org/svntogit/packages.git/commit/trunk?h=packages/libtool&id=7f6118409d3eae438a6d501cd7ae4e907582fbe9
# has some info about the libtool libltdl merge

pkgname=libtool
pkgver=2.4.6
pkgrel=4
_gccver=5.2.0
pkgdesc="A generic library support script"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
groups=('base-devel')
depends=('sh' 'tar' 'glibc')
makedepends=("gcc=$_gccver")
provides=("libltdl=$pkgver" "libtool-multilib=$pkgver")
conflicts=('libltdl' 'libtool-multilib')
replaces=('libltdl' 'libtool-multilib')
install=libtool.install
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('1bfb9b923f2c1339b4d2ce1807064aa5'
         'SKIP')
validpgpkeys=('CFE2BE707B538E8B26757D84151308092983D606')   # Gary Vaughan

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install
}

