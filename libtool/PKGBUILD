#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# NOTE: requires rebuilt with each new gcc version

pkgname=('libtool' 'libltdl')
pkgver=2.4.2
pkgrel=7
pkgdesc="A generic library support script"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
options=('!libtool')
depends=('sed' 'grep') # Needed by libtoolize
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('2ec8997e0c07249eb4cbd072417d70fe'
         '1e6ba57420c82c663c85e745d11c7eed')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make check
}

package_libtool() {
  depends=('sh' "libltdl=${pkgver}" 'tar' 'gcc=4.8.0')
  groups=('base-devel')
  install=libtool.install

  cd "${srcdir}/${pkgbase}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install-binSCRIPTS install-man install-info \
                           install-data-local
  rm -rf "${pkgdir}/usr/share/libtool/libltdl"
}

package_libltdl() {
  pkgdesc="A system independent dlopen wrapper for GNU libtool"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install-libLTLIBRARIES install-includeHEADERS \
                           install-ltdlincludeHEADERS install-data-local
  rm -rf "${pkgdir}/usr/share"/{aclocal,libtool/config}
}

# vim:set ts=2 sw=2 et:
