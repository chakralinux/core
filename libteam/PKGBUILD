# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/libteam

pkgname=libteam
pkgver=1.23
pkgrel=1
pkgdesc="Library for controlling team network device"
arch=('x86_64')
url="http://libteam.org/"
license=(LGPL2.1)
depends=(libnl libdaemon jansson dbus bash)
makedepends=(python2 python3 swig)
source=($url/files/$pkgname-$pkgver.tar.gz)
sha256sums=('d8cd132c425032cc3a7e9314c55485e32243875e0b1adbddd043bbadf1d28c5a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib
  make

  cp -aT binding/python binding/python2

  cd binding/python
  python3  ./setup.py build
  cd ../python2
  python2 ./setup.py build
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 teamd/redhat/systemd/teamd@.service \
    "$pkgdir/usr/lib/systemd/system/teamd@.service"
  install -Dm644 teamd/dbus/teamd.conf \
    "$pkgdir/etc/dbus-1/system.d/teamd.conf"

  cd binding/python
  python3  ./setup.py install --root "$pkgdir" -O1
  cd ../python2
  python2 ./setup.py install --root "$pkgdir" -O1
}
