# Maintainer:
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Anke Boersma (abveritas) <abveritas@chakra-project.org>

pkgname=libpng
pkgver=1.6.7
_apngver=1.6.7
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics files'
arch=('x86_64')
url='http://www.libpng.org/pub/png/libpng.html'
license=('custom')
depends=('zlib'
         'sh')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz")
sha512sums=('e76bae26b49be317b87dc1465a247498a40b0f25f3eac17ce22029487297b6c639f53f233e6fa74df84c3bbccc4cfba5d262857ae84572d588acfa8c9de42768'
            '41c95920b7bb67d741aec2f88247bb59d351a8de50548f41b10516c62cd8d8d0fbb9301365ba8f12f79871a758d65941792076aed43311902a4e88c6cd3f715a')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}
    # Add animated PNG (apng) support
    # see http://sourceforge.net/projects/libpng-apng/
    patch -p1 -i "${srcdir}/libpng-${_apngver}-apng.patch"
}
 
build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --disable-static
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    cd contrib/pngminus
    make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
    install -m755 png2pnm pnm2png "${pkgdir}/usr/bin/"
    install -D -m644 ../../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
