# Maintainer:
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Anke Boersma (abveritas) <abveritas@chakra-project.org>
# Note: don't forget lib32 part

pkgname=libpng
pkgver=1.6.19
_apngver=${pkgver}
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz{,.asc}
        http://downloads.sourceforge.net/sourceforge/apng/libpng-$_apngver-apng.patch.gz)
# Change checksums to SF-avalible format
sha1sums=('483d72ced11c9258f9d1119105273d9af9ff151c'
          'SKIP'
          '36f3c95f587cf9b01a7254051efb1e37e7629b9e')
validpgpkeys=('8048643BA2C840F4F92A195FF54984BFA16C640F')

prepare() {
  cd ${pkgname}-${pkgver}

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -Np0 -i "../libpng-${_apngver}-apng.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
      --prefix=/usr \
      --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "${pkgdir}/usr/bin/"
  install -D -m644 ../../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
