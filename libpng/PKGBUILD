# Maintainer:
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Anke Boersma (abveritas) <abveritas@chakra-project.org>

pkgname=libpng
pkgver=1.6.11
_apngver=1.6.11
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics files'
arch=('x86_64')
url='http://www.libpng.org/pub/png/libpng.html'
license=('custom')
depends=('zlib'
         'sh')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz")
# Change checksums to SF-avalible format
sha1sums=('4d832e419bd4aaa2bb416a7421b2d0279d0dc1d9'
          '611e2ca8d67a0d0b5c63f4c156c5bf63d6cf792f')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}
    # Add animated PNG (apng) support
    # see http://sourceforge.net/projects/libpng-apng/
    patch -p1 -i "${srcdir}/libpng-${_apngver}-apng.patch"
}
 
build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --disable-static
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    cd contrib/pngminus
    make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
    install -m755 png2pnm pnm2png "${pkgdir}/usr/bin/"
    install -D -m644 ../../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
