pkgname=libpng
pkgver=1.6.21
_apngver=${pkgver}
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz
        http://downloads.sourceforge.net/sourceforge/apng/libpng-$_apngver-apng.patch.gz)
sha1sums=('978b2f4e007eda56032001493ddb97d20f0ab291'
          'c34537bf0f5d599472ee4cd96a0af2147b724a10')
          
prepare() {
  cd ${pkgname}-${pkgver}

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -Np0 -i "../libpng-${_apngver}-apng.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
      --prefix=/usr \
      --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "${pkgdir}/usr/bin/"
  install -D -m644 ../../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
