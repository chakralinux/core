pkgname=libpng
pkgver=1.6.36
_apngver=${pkgver}
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
source=(https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz
        https://downloads.sourceforge.net/sourceforge/apng/libpng-$_apngver-apng.patch.gz)
sha1sums=('aec9548c8319104226cc4c31d1f5e524f1b55295'
          '31ed34fc74530d742d2d6cc3ae85e5fc02efb7ca')
          
prepare() {
  cd ${pkgname}-${pkgver}

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -Np0 -i "../libpng-${_apngver}-apng.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
      --prefix=/usr \
      --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "${pkgdir}/usr/bin/"
  install -Dm644 ../../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
