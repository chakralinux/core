#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=libogg
pkgver=1.3.0
pkgrel=3
pkgdesc="Ogg bitstream and framing library"
arch=('i686' 'x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
depends=('glibc')
options=('!libtool')
source=(http://downloads.xiph.org/releases/ogg/$pkgname-$pkgver.tar.xz
        config_types.h)
md5sums=('84a35715170f2cd4c77a4448772b95d5'
         'a4d786b1b1955cb85b64fd16cbbee3f9')

build() {
    cd $srcdir/${pkgname}-${pkgver}
    ./configure --prefix=/usr 
    make
}

package() {
    cd "$srcdir/${pkgname}-${pkgver}"
    make DESTDIR=$pkgdir install

    # install BSD license
    install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
	$pkgdir/usr/share/licenses/$pkgname/COPYING

    # Resolve multilib conflict
    if [[ $CARCH == "x86_64" ]]; then
      mv "$pkgdir"/usr/include/ogg/config_types{,-64}.h
      install -m 644 "$srcdir/config_types.h" \
        "${pkgdir}/usr/include/ogg/config_types.h"
    fi
}

