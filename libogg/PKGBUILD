pkgname=libogg
pkgver=1.3.3
pkgrel=1
pkgdesc="Ogg bitstream and framing library"
arch=('x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
depends=('glibc')
source=("http://downloads.xiph.org/releases/ogg/$pkgname-$pkgver.tar.xz")
md5sums=('87ed742047f065046eb6c36745d871b8')

build() {
    cd $srcdir/${pkgname}-${pkgver}
    ./configure --prefix=/usr 
    make
}

package() {
    cd "$srcdir/${pkgname}-${pkgver}"
    make DESTDIR=$pkgdir install

    # install BSD license
    install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
	$pkgdir/usr/share/licenses/$pkgname/COPYING
}
