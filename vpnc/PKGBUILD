#
# Core packages for Chakra, part of chakra-project.org
#
# maintainer : <inkane@chakra-project.org>
# contributor: <abveritas[at]chakra-project[dot]org>

pkgname=vpnc
pkgver=0.5.3.1
_svnver=527
_git=d2c5a77f3f0ea6ad80fc59158127d63ede81a6cb
pkgrel=2
pkgdesc="VPN client for cisco3000 VPN Concentrators"
url="http://www.unix-ag.uni-kl.de/~massar/vpnc/"
license=('GPL')
depends=('libgcrypt' 'openssl' 'iproute2')
optdepends=('openresolv: Let vpnc manage resolv.conf')
makedepends=('subversion')
arch=('x86_64')
source=("$pkgname::svn+http://svn.unix-ag.uni-kl.de/vpnc/trunk/#revision=$_svnver"
        "http://git.infradead.org/users/dwmw2/vpnc-scripts.git/blob_plain/$_git:/vpnc-script"
        'vpnc.conf'
        'vpnc@.service')
md5sums=('SKIP'
         '70198474faf36a84beaa11136f6c4d9c'
         'a3f4e0cc682f437e310a1c86ae198e45'
         '09cfded435c43dd2adb5a8863bd74cfc')
backup=(etc/vpnc/default.conf)

build() {
  cd $pkgname

  # Build hybrid support
  sed -i 's|^#OPENSSL|OPENSSL|g' Makefile

  # fix resolvconf location for anyone using openresolv
  sed -i 's|/sbin/resolvconf|/usr&|g' "$srcdir/vpnc-script"

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm644 "$srcdir"/vpnc.conf "$pkgdir"/etc/vpnc/default.conf
  install -Dm755 "$srcdir"/vpnc-script "$pkgdir"/etc/vpnc/vpnc-script

  install -Dm644 "$srcdir"/vpnc@.service "$pkgdir"/usr/lib/systemd/system/vpnc@.service
}
