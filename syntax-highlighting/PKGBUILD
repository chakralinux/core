pkgname=syntax-highlighting
pkgver=5.53.0
pkgrel=2
pkgdesc='Syntax highlighting Engine for Structured Text and Code.'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/syntax-highlighting'
license=('MIT')
depends=('qt5-base')
makedepends=('extra-cmake-modules' 'qt5-xmlpatterns' 'qt5-tools' 'doxygen' 'python3')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('aa31517936a6c19080c2a62519d7eecd4c369a881614f841028b5d82ac0225ab'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
        -DQRC_SYNTAX=OFF \
        -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
