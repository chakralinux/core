
pkgname=syntax-highlighting
pkgver=5.48.0
pkgrel=1
pkgdesc='Syntax highlighting Engine for Structured Text and Code.'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/syntax-highlighting'
license=('LGPL')
depends=('qt5-xmlpatterns' 'perl')
makedepends=("extra-cmake-modules>=5.48.0" 'python3' 'qt5-tools')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('652f24e1d3bf176d4a3f62ae143ff6a1901c78d80d38d36bbf5f47909ea48e0d'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  # syntax highlighting for PKGBUILD files
  sed -i -e 's|.bash_login;.profile|.bash_login;.profile;PKGBUILD|' data/syntax/bash.xml
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
