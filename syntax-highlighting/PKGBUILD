source ../frameworks.conf

pkgname=syntax-highlighting
pkgver=${KFVersion}
pkgrel=1
pkgdesc='Syntax highlighting Engine for Structured Text and Code.'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/syntax-highlighting'
license=('LGPL')
depends=('qt5-xmlpatterns' 'perl')
makedepends=("extra-cmake-modules>=${KFECMVersion}" 'python3' 'qt5-tools')
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgname}-${pkgver}.tar.xz")
sha256sums=( $(getSum ${pkgname}))

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  # syntax highlighting for PKGBUILD files
  sed -i -e 's|.bash_login;.profile|.bash_login;.profile;PKGBUILD|' data/syntax/bash.xml
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
