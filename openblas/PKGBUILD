# Maintainer: Fabian Kosmale
# Contributor (Arch original): Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

pkgname=openblas
_pkgname=OpenBLAS
pkgver=0.2.20
pkgrel=3
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD "
arch=('x86_64')
url="http://www.openblas.net/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran')
conflicts=('blas' 'cblas')
options=(!makeflags !emptydirs)
source=(${_pkgname}-v${pkgver}.tar.gz::http://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha256sums=('5ef38b15d9c652985774869efd548b8e3e972e1e99475c673b25537ed7bcf394')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  unset CFLAGS
  unset CXXFLAGS
  # using and OPENMP=1 should be the most sensible option
  # see https://github.com/xianyi/OpenBLAS/wiki/faq for  details
  # we use NO_LAPACK and NO_LAPACKE, as LAPACK should be provided by our lapack package
  make USE_OPENMP=1 NO_LAPACK=1 NO_LAPACKE=1 MAJOR_VERSION=3 DYNAMIC_ARCH=1
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make PREFIX="$pkgdir/usr" USE_OPENMP=1 NO_LAPACK=1 NO_LAPACKE=1 MAJOR_VERSION=3 DYNAMIC_ARCH=1 install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove reference to ${pkgdir}
  sed -i -e "s|${pkgdir}||" "${pkgdir}/usr/lib/cmake/openblas/OpenBLASConfig.cmake"
  sed -i -e "s|${pkgdir}||" "${pkgdir}/usr/lib/pkgconfig/openblas.pc"
  
  # create symlink as some packages just don't support arbitrary blas packages
  ln -s libopenblas.so $pkgdir/usr/lib/libblas.so.3
  ln -sf libblas.so.3 $pkgdir/usr/lib/libblas.so

}
