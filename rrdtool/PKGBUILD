# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributors from Arch: Eric Belanger <eric@archlinux.org>
#                         Tom K <tom@archlinux.org>

pkgname=rrdtool
pkgver=1.4.8
pkgrel=1
pkgdesc="Data logging and graphing application"
arch=('x86_64')
url="http://www.rrdtool.org"
license=('GPL')
depends=('libpng' 'libxml2' 'pango')
makedepends=('intltool' 'ruby' 'python2' 'tcl' 'lua' 'groff')
optdepends=('tcl: to use corresponding binding' \
            'python2: to use corresponding binding' \
            'ruby: to use corresponding binding' \
            'lua: to use corresponding binding')
options=('!libtool' '!emptydirs' '!makeflags')
source=("http://oss.oetiker.ch/rrdtool/pub/rrdtool-${pkgver}.tar.gz"
	"rrdtool-pangofont.patch")
sha1sums=('56d68857f39e70bfa32360947614d8220702ed02'
          '8c600285bdab7776c1d5301df7cf486d69eae048')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix FS#28521 make ruby install to vendor_ruby instead of site_ruby
  sed -e 's/$(RUBY) extconf.rb/& --vendor/' -i bindings/Makefile.in
  patch -p1 -i ../rrdtool-pangofont.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoconf
  ./configure --prefix=/usr \
    --localstatedir=/var \
    --disable-rpath \
    --enable-perl \
    --enable-perl-site-install \
    --with-perl-options='INSTALLDIRS=vendor' \
    --enable-ruby \
    --enable-ruby-site-install \
    --enable-python \
    --enable-lua \
    --enable-lua-site-install \
    --enable-tcl \
    --disable-libwrap
  make LIBS+="-lglib-2.0"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" includedir=/usr/include install
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
