# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributors from Arch: Eric Belanger <eric@archlinux.org>
#                         Tom K <tom@archlinux.org>

pkgname=rrdtool
pkgver=1.4.7
pkgrel=2
pkgdesc="Data logging and graphing application"
arch=('x86_64')
url="http://www.rrdtool.org"
license=('GPL')
depends=('libpng' 'libxml2' 'pango')
makedepends=('intltool' 'ruby' 'python2' 'tcl' 'lua' 'groff')
optdepends=('tcl: to use corresponding binding' \
            'python2: to use corresponding binding' \
            'ruby: to use corresponding binding' \
            'lua: to use corresponding binding')
options=('!libtool' '!emptydirs' '!makeflags')
source=("http://oss.oetiker.ch/rrdtool/pub/rrdtool-${pkgver}.tar.gz")
sha1sums=('faab7df7696b69f85d6f89dd9708d7cf0c9a273b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # make ruby install to vendor_ruby instead of site_ruby (reported: https://bugs.archlinux.org/task/28521)
  sed -e 's/$(RUBY) extconf.rb/& --vendor/' -i bindings/Makefile.in

  ./configure --prefix=/usr --localstatedir=/var --disable-rpath --enable-perl \
    --enable-perl-site-install --with-perl-options='INSTALLDIRS=vendor' \
    --enable-ruby --enable-ruby-site-install --enable-python \
    --enable-lua --enable-lua-site-install \
    --enable-tcl --disable-libwrap
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
