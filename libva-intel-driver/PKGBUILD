# Platform Packages for Chakra

pkgname=libva-intel-driver
pkgver=1.7.0
pkgrel=1
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family'
arch=('x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('libva')
replaces=('libva-driver-intel')
conflicts=('libva-driver-intel')
provides=('libva-driver-intel')
source=(http://freedesktop.org/software/vaapi/releases/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('00619c393e0c9c74b205d9adf69b68db')

prepare() {
  cd $pkgname-$pkgver

  # Only relevant if intel-gpu-tools is installed,
  # since then the shaders will be recompiled
  sed -i '1s/python$/&2/' src/shaders/gpp.py
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
