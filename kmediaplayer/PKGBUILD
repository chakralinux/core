# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kmediaplayer
pkgver=5.54.0
pkgrel=1
pkgdesc='Plugin interface for media player features'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kmediaplayer'
license=('LGPL')
depends=('kparts')
makedepends=("extra-cmake-modules>=5.54.0")
checkdepends=("cmake" "xorg-server-xvfb")
groups=('kf5-aids')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('cecf0a32a74ab9cecaf25bf767917d8bf7cb9619f51f5a487e7d7b6127c65461'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   xvfb-run make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
