#
# Platform Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=linphone
pkgver=3.5.2
pkgrel=1
pkgdesc="A Voice-over-IP phone. CLI version only."
arch=('i686' 'x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('alsa-lib' 'ffmpeg' 'libexosip2' 'speex' 'libv4l' 'v4l-utils' 'libpulse' 'libxv')
makedepends=('pkgconfig' 'perlxml' 'intltool')
optdepends=('pulseaudio')
conflicts=(ortp)
provides=(ortp)
options=('!libtool' '!emptydirs')
source=("http://download-mirror.savannah.gnu.org/releases/${pkgname}/${pkgver:0:3}.x/sources/${pkgname}-${pkgver}.tar.gz"
        "ffmpeg-0.11.patch")
sha256sums=('da37d5e539e3f8aaa44039aa52ed032ef372e7481d1b3ce68317bf79aa8a6020'
            'fff926ffb36fb0106e98cb4f00a28f87d3c539f426df6ee2879565977b4c7d4d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # ffmpeg-0.11 patch
  msg2 "Applying ffmpeg patch"
  patch -p1 -i "${srcdir}/ffmpeg-0.11.patch"

  export PKG_CONFIG=/usr/bin/pkg-config
  ./configure --prefix=/usr \
              --disable-static \
              --enable-ipv6 \
              --enable-video \
              --enable-gtk_ui=no \
              --enable-alsa \
              --enable-pulseaudio \
              --disable-artsc \
              --disable-strict \
              --libexecdir=/usr/lib/${pkgname}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
