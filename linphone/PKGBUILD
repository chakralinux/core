#
# Platform Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=linphone
pkgver=3.5.2
pkgrel=2
pkgdesc="A Voice-over-IP phone. CLI version only."
arch=('x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('alsa-lib' 'ffmpeg' 'libexosip2' 'speex' 'libv4l' 'v4l-utils' 'libpulse' 'libxv' 'mediastreamer' 'ortp')
makedepends=('pkgconfig' 'perlxml' 'intltool')
optdepends=('pulseaudio')
options=('!libtool' '!emptydirs')
source=("http://download-mirror.savannah.gnu.org/releases/${pkgname}/${pkgver:0:3}.x/sources/${pkgname}-${pkgver}.tar.gz")
sha256sums=('da37d5e539e3f8aaa44039aa52ed032ef372e7481d1b3ce68317bf79aa8a6020')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PKG_CONFIG=/usr/bin/pkg-config
  ./configure --prefix=/usr \
              --disable-static \
              --enable-ipv6 \
              --enable-video \
              --enable-gtk_ui=no \
              --enable-alsa \
              --enable-pulseaudio \
              --disable-artsc \
              --disable-strict \
              --libexecdir=/usr/lib/${pkgname} \
	      --enable-external-mediastreamer \
              --enable-external-ortp 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
