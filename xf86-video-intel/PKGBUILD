# Part of X.org group
# Maintainer AlmAck almack@chakraos.org
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=xf86-video-intel
pkgver=2.99.917
pkgrel=1
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
install=$pkgname.install
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
depends=('mesa-dri' 'libxvmc' 'pixman' 'xcb-util>=0.3.9')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=18' 'libx11' 'libxrender'
             # additional deps for intel-virtual-output
            'libxrandr' 'libxinerama' 'libxcursor' 'libxtst')
optdepends=('libxrandr: for intel-virtual-output'
            'libxinerama: for intel-virtual-output'
            'libxcursor: for intel-virtual-output'
            'libxtst: for intel-virtual-output')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<18' 'X-ABI-VIDEODRV_VERSION>=19')
groups=('xorg-drivers' 'xorg')
source=(${url}/archive/individual/driver/${pkgname}-${pkgver}.tar.bz2
        20-intel.conf)
sha256sums=('00b781eea055582820a123c47b62411bdf6aabf4f03dc0568faec55faf9667c9'
            'a1fdde7f081b74436a71addee42268af00c900156be1f696f661ee8f7e7646aa')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-glamor \
    --disable-dri3
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm644 ${srcdir}/20-intel.conf ${pkgdir}/etc/X11/xorg.conf.d/20-intel.conf
}

