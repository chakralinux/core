# Part of X.org group
# Maintainer AlmAck almack@chakraos.org
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=xf86-video-intel
pkgver=2.21.15 # WARINING, tried with version 2.99.912 but seems that with sna the rendering is corrupted (http://chakraos.org/forum/viewtopic.php?pid=81925#p81925)
pkgrel=2
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
install=$pkgname.install
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
depends=('intel-dri' 'libxvmc' 'pixman' 'xcb-util>=0.3.9' 'glamor-egl')
makedepends=('xorg-server' 'libx11' 'libxrender'
             # additional deps for intel-virtual-output
            'libxrandr' 'libxinerama' 'libxcursor' 'libxtst')
optdepends=('libxrandr: for intel-virtual-output'
            'libxinerama: for intel-virtual-output'
            'libxcursor: for intel-virtual-output'
            'libxtst: for intel-virtual-output')
replaces=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
provides=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16'
           'xf86-video-intel-sna' 
           'xf86-video-intel-uxa' 
           'xf86-video-i810' 
           'xf86-video-intel-legacy')
groups=('xorg-drivers' 'xorg')
source=(${url}/archive/individual/driver/${pkgname}-${pkgver}.tar.bz2
intel-2.21.15-xserver-1.15-compat.patch)
sha256sums=('7d5a140f82a72fd1cbc8a664d66c3d4eca47ee240ca4927b8a98d7af6f65d6fc'
            'fbf8fa35974dbf758b095c390d79f542b97993482ae3b63ec6542bffa87ba09f')

build() {
  cd ${pkgname}-${pkgver}
  # fix build with Xorg 1.15
  patch -Np1 -i ${srcdir}/intel-2.21.15-xserver-1.15-compat.patch
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-dri
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

