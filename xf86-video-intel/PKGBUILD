# Contributions of Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/xf86-video-intel
# Part of X.org group

source ../xorg.conf

pkgname=xf86-video-intel
_commit=cb6ba2da056f3298a765b4da5cd626343c00a533  # master
pkgver=2.99.917+770+gcb6ba2d
pkgrel=1
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
install=$pkgname.install
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
depends=('mesa-dri' 'libxvmc' 'pixman' 'xcb-util')
makedepends=('xorg-server-devel' "X-ABI-VIDEODRV_VERSION=${X_ABI_VIDEODRV_VERSION}" 'libx11' 'libxrender'
             # additional deps for intel-virtual-output
            'libxrandr' 'libxinerama' 'libxcursor' 'libxtst' 'libxss' 'git')
optdepends=('libxrandr: for intel-virtual-output'
            'libxinerama: for intel-virtual-output'
            'libxcursor: for intel-virtual-output'
            'libxtst: for intel-virtual-output')
conflicts=("X-ABI-VIDEODRV_VERSION<${X_ABI_VIDEODRV_MAJOR}" "X-ABI-VIDEODRV_VERSION>=$((X_ABI_VIDEODRV_MAJOR+1))")
groups=('xorg-drivers' 'xorg')
source=("git://anongit.freedesktop.org/xorg/driver/xf86-video-intel#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  # DRI3 is disabled by default and Glamor has been removed
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --with-default-dri=3
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

