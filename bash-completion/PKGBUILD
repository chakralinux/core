pkgname=bash-completion
pkgver=2.7
pkgrel=1
pkgdesc="Programmable completion for the bash shell"
arch=('any')
url="https://github.com/scop/bash-completion"
license=('GPL2')
depends=('bash')
options=('!emptydirs' '!makeflags')
#http://bash-completion.alioth.debian.org/files/${pkgname}-${pkgver}.tar.bz2
source=("https://github.com/scop/bash-completion/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('41ba892d3f427d4a686de32673f35401bc947a7801f684127120cdb13641441e')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # bash-completion is sourced in /etc/bash.bashrc so that non-bash shell don't source it
  rm "${pkgdir}/etc/profile.d/bash_completion.sh"

  # remove Slackware's makepkg completion
  rm "${pkgdir}/usr/share/bash-completion/completions/makepkg"

  # remove completions which overlap with util-linux
  rm "$pkgdir/usr/share/bash-completion/completions"/{{u,}mount,rfkill}
}
