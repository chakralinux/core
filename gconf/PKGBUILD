#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=gconf
pkgver=2.32.0
pkgrel=3
pkgdesc="A configuration database system"
arch=(i686 x86_64)
license=('LGPL')
depends=('orbit2>=2.14.18' 'gtk2>=2.22.0' 'libxml2>=2.7.7' 'polkit>=0.98' 'libldap>=2.4.23' 'dbus-glib>=0.86')
makedepends=('pkgconfig' 'intltool' 'gobject-introspection')
options=('!libtool')
install=gconf.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/GConf/2.32/GConf-${pkgver}.tar.bz2
        gconf-merge-schema
        gconfpkg
        gconf-reload.patch
        01_xml-gettext-domain.patch)
md5sums=('b161da6a1ab090b649aed3111003b0bb'
         '240b473fa17b748fc9020caf372d936c'
         'b745f04b4121d8724c002b14a4c110b6'
         'cfcc8e15be7b8a48de4aa34336ff6090'
         '1f9362976d71312bdb57c57fa031681d')

build() {
  cd "${srcdir}/GConf-${pkgver}"
  # Patch from fedora - reloads gconf after installing schemas
  patch -Np1 -i "${srcdir}/gconf-reload.patch"
  # http://bugzilla.gnome.org/show_bug.cgi?id=568845
  patch -Np1 -i "${srcdir}/01_xml-gettext-domain.patch"

  # Python2 fix
  sed -i '1s|#!/usr/bin/env python$|&2|' gsettings/gsettings-schema-convert

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/GConf \
      --disable-static --enable-defaults-service --with-gtk=3.0 \
      --disable-orbit

  make pkglibdir=/usr/lib/GConf
}

package() {
  cd "${srcdir}/GConf-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/gconf/gconf.xml.system"
  install -m755 "${srcdir}/gconf-merge-schema" "${pkgdir}/usr/bin/"
  install -Dm755 "${srcdir}/gconfpkg" "${pkgdir}/usr/sbin/gconfpkg"
}
