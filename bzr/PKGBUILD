#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# Contributer: Joshua L. Blocher <verbalshadow@gmail.com>

pkgname=bzr
pkgver=2.5.1
pkgrel=1
pkgdesc="Bazaar is a version control system that helps you track project history over time and to collaborate easily with others."
arch=('i686' 'x86_64')
url="http://bazaar.canonical.com/en/"
license=('GPL')
depends=('python2')
optdepends=('python-paramiko: for sftp support')
source=("http://launchpad.net/${pkgname}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"{,.sig})
md5sums=('ac5079858364a046071000d5cdccb67b'
         'c83dd08dd2c3e27edbab873500d7d6eb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|man/man1|share/man/man1|' setup.py
  sed -e 's|/usr/bin/env python|/usr/bin/env python2|' \
      -i bzrlib/{plugins/bash_completion/bashcomp.py,tests/ssl_certs/create_ssls.py,patiencediff.py,_patiencediff_py.py}
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  # bash-completion
  install -D -m644 contrib/bash/bzr "${pkgdir}/etc/bash_completion.d/bzr"
}

# vim:set ts=2 sw=2 et:
