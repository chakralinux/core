#
# Software packages for Chakra, part of chakraos.org
#

pkgbase=python-sphinx_rtd_theme
pkgname=('python3-sphinx_rtd_theme' 'python2-sphinx_rtd_theme')
pkgver=0.1.9
pkgrel=1
pkgdesc="Python Sphinx Read The Docs Theme"
url='https://github.com/snide/sphinx_rtd_theme'
arch=('any')
license=('MIT')
makedepends=('python3-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/s/sphinx_rtd_theme/sphinx_rtd_theme-$pkgver.tar.gz")
md5sums=('86a25c8d47147c872e42dc84cc66f97b')

build() {
  cp -r sphinx_rtd_theme-$pkgver sphinx_rtd_theme-$pkgver-py2

  cd sphinx_rtd_theme-$pkgver
  python3 setup.py build

  cd "$srcdir"/sphinx_rtd_theme-$pkgver-py2
  python2 setup.py build
}

package_python3-sphinx_rtd_theme() {
  cd sphinx_rtd_theme-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sphinx_rtd_theme() {
  cd sphinx_rtd_theme-${pkgver}-py2 
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

