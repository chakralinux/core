# Contribution form Arch:
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.29
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('x86_64')
license=('GPL' 'FDL')
depends=('docbook-xsl' 'perl' 'python3-six' 'python3-lxml' 'python3-pygments') 
makedepends=('jade' 'itstool')
optdepends=('jade: SGML support')
url="http://www.gtk.org/gtk-doc/"
_commit=7db567e83fe5d2d0659560c6dfb25ee27b338140  # tags/GTK_DOC_1_29^0
source=("git+https://git.gnome.org/browse/gtk-doc#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/GTK_DOC_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname
  make check | true
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
