#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=alsa-plugins
pkgver=1.0.25
pkgrel=5
pkgdesc="Extra alsa plugins"
arch=(i686 x86_64)
url="http://www.alsa-project.org"
license=(GPL)
depends=(alsa-lib)
makedepends=('libpulse' 'jack' 'ffmpeg')
optdepends=('libpulse: PulseAudio plugin'
            'jack: Jack plugin'
            'ffmpeg: libavcodec resampling plugin'
            'libsamplerate: libsamplerate resampling plugin'
            'speex: libspeexdsp resampling plugin')
options=('!libtool')
source=("ftp://ftp.alsa-project.org/pub/plugins/$pkgname-$pkgver.tar.bz2"
        "alsa-plugins-1.0.25-ffmpeg-0.11-obsolete-avcodec_init.patch"
        "alsa-plugins-1.0.25-ffmpeg-0.11-renamed-CH_LAYOUT-defs-v2.patch")
md5sums=('038c023eaa51171f018fbf7141255185'
         '9c52cbe8f1bc8c823d70a92b171c1055'
         '07d382f969d0514297e9dbc5f642bbdf')

# create tarball: source PKGBUILD && mksource

mksource() {
	  rm -vRf ${pkgname}-${pkgver}
	  git clone git://git.alsa-project.org/${pkgname}.git ${pkgname}-${pkgver}
	  rm -vRf ${pkgname}-${pkgver}/.git
	  pushd ${pkgname}-${pkgver}
	  popd
	  tar -cvJf ${pkgname}-${pkgver}.tar.xz ${pkgname}-${pkgver}/*
	  md5sum ${pkgname}-${pkgver}.tar.xz
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  patch -Np1 -i "${srcdir}/alsa-plugins-1.0.25-ffmpeg-0.11-renamed-CH_LAYOUT-defs-v2.patch"
  patch -Np1 -i "${srcdir}/alsa-plugins-1.0.25-ffmpeg-0.11-obsolete-avcodec_init.patch"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -m644 doc/README* doc/*.txt "$pkgdir/usr/share/doc/$pkgname/"
}
