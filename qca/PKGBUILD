#
# Platform Packages for Chakra, part of chakralinux.org
#

pkgbase=qca
pkgname=(qca qca-qt5)
pkgver=2.1.3
pkgrel=2
pkgdesc="Qt Cryptographic Architecture"
arch=('x86_64')
url="http://delta.affinix.com/qca/"
license=('LGPL')
makedepends=('qt' 'qt5-base' 'cmake' 'git' 'nss')
source=("http://download.kde.org/stable/$pkgbase/$pkgver/src/$pkgbase-$pkgver.tar.xz")
md5sums=('5019cc29efcf828681cd93164238ce26')

prepare(){
  cd ${pkgbase}-${pkgver}
  mkdir $srcdir/build
  mkdir $srcdir/build-qt5
}

build() {
  cd $srcdir/build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DQCA_LIBRARY_INSTALL_DIR=/usr/lib \
    -DQCA_FEATURE_INSTALL_DIR=/usr/share/qt/mkspecs/features/ \
    -DQT4_BUILD=ON
  make

  cd $srcdir/build-qt5
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DQCA_INSTALL_IN_QT_PREFIX=ON \
    -DQCA_FEATURE_INSTALL_DIR=/usr/share/qt5/mkspecs/features/ \
    -DQCA_MAN_INSTALL_DIR=/usr/share/man
  make
}

package_qca() {
  pkgdesc="Qt Cryptographic Architecture"
  depends=('qt' 'ca-certificates' 'nss')

  cd $srcdir/build
  make DESTDIR=${pkgdir} install
}

package_qca-qt5() {
  pkgdesc="Qt5 Cryptographic Architecture"
  depends=('qt5-base' 'ca-certificates' 'nss')

  cd $srcdir/build-qt5
  make DESTDIR=${pkgdir} install
}
