pkgname=qca
pkgver=2.1.3
pkgrel=4
pkgdesc="Qt5 Cryptographic Architecture"
arch=('x86_64')
url="http://delta.affinix.com/qca/"
license=('LGPL')
depends=('qt5-base' 'ca-certificates' 'nss')
makedepends=(cmake doxygen pkcs11-helper botan)
conflicts=(qca-qt5)
provides=(qca-qt5)
replaces=(qca-qt5)
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"
        qca-botan2.patch::"https://cgit.kde.org/qca.git/patch/?id=47163784")
md5sums=('5019cc29efcf828681cd93164238ce26'
         '24242f465841923f447b361120daf802')

prepare(){
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../qca-botan2.patch      # Fix build with botan 2

  mkdir $srcdir/build
}

build() {
  cd $srcdir/build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DQCA_SUFFIX=qt5 \
    -DQCA_INSTALL_IN_QT_PREFIX=ON \
    -DQCA_MAN_INSTALL_DIR=/usr/share/man
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=${pkgdir} install
}
