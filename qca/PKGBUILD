#
# Platform Packages for Chakra, part of chakraos.org
#

pkgbase=qca
pkgname=("qca" "qca-qt5")
pkgver=2.0.3.1
pkgrel=2
arch=('x86_64')
url="http://delta.affinix.com/qca/"
license=('LGPL')
makedepends=('qt' 'qt5-base' 'cmake' 'git' 'nss')
source=("git://anongit.kde.org/qca.git")
md5sums=('SKIP')

prepare(){
  mkdir $srcdir/build
  mkdir $srcdir/build-qt5
}

build() {
  cd $srcdir/build
  cmake ../${pkgbase} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT4_BUILD=true
  make

  cd $srcdir/build-qt5
  cmake ../${pkgbase} \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_qca() {
  pkgdesc="Qt Cryptographic Architecture"
  depends=('qt' 'ca-certificates' 'nss')

  cd $srcdir/build
  make DESTDIR=${pkgdir} install
}

package_qca-qt5() {
  pkgdesc="Qt5 Cryptographic Architecture"
  depends=('qt5-base' 'ca-certificates' 'nss')
  conflicts=('qca')

  cd $srcdir/build-qt5
  make DESTDIR=${pkgdir} install
}
