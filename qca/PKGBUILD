#
# Platform Packages for Chakra, part of chakraos.org
#

pkgbase=qca-qt5
pkgname=("qca" "qca-qt5")
pkgver=2.1.0.3
pkgrel=5
arch=('x86_64')
url="http://delta.affinix.com/qca/"
license=('LGPL')
makedepends=('qt' 'qt5-base' 'cmake' 'git' 'nss')
source=("http://download.kde.org/stable/$pkgbase/$pkgver/src/$pkgbase-$pkgver.tar.xz"
        "qt-5.5.patch::http://quickgit.kde.org/?p=qca.git&a=commitdiff&h=7207e6285e932044cd66d49d0dc484666cfb0092&o=plain")
md5sums=('e29cc1d8f0292eb28e20b216f52d60fc'
         '5ea7bcb072e72b28e37b1a1d897c9362')

prepare(){
  cd ${pkgbase}-${pkgver}
  patch -Np1 -i ../qt-5.5.patch
  mkdir $srcdir/build
  mkdir $srcdir/build-qt5
}

build() {
  cd $srcdir/build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT4_BUILD=true
  make

  cd $srcdir/build-qt5
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQCA_SUFFIX=qt5 \
    -DQCA_INSTALL_IN_QT_PREFIX=ON
  make
}

package_qca() {
  pkgdesc="Qt Cryptographic Architecture"
  depends=('qt' 'ca-certificates' 'nss')

  cd $srcdir/build
  make DESTDIR=${pkgdir} install
}

package_qca-qt5() {
  pkgdesc="Qt5 Cryptographic Architecture"
  depends=('qt5-base' 'ca-certificates' 'nss')

  cd $srcdir/build-qt5
  make DESTDIR=${pkgdir} install
}
