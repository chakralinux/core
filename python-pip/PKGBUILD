pkgname=('python3-pip' 'python2-pip')
pkgver=8.1.0
pkgrel=1
pkgdesc="An easy_install replacement for installing pypi python packages"
url="http://www.pip-installer.org/"
arch=('any')
license=('MIT')
makedepends=('python3' 'python3-setuptools' 'python2' 'python2-setuptools')
source=(http://pypi.python.org/packages/source/p/pip/pip-${pkgver}.tar.gz)
md5sums=('e9c3844db343f47d16040b32ad9072be')

package_python3-pip() {
    depends=('python3' 'python3-setuptools')

    cd "$srcdir/pip-$pkgver"
    python3 setup.py build
    python3 setup.py install \
        --prefix=/usr \
        --root="$pkgdir"

    mv "$pkgdir/usr/bin/pip" "$pkgdir/usr/bin/pip3"
    sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python3|" \
        ${pkgdir}/usr/lib/python3.5/site-packages/pip/__init__.py

    install -D -m644 LICENSE.txt \
        "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pip() {
    depends=('python2' 'python2-setuptools')

    cd "$srcdir/pip-$pkgver"
    python2 setup.py build
    python2 setup.py install \
        --prefix=/usr \
        --root="$pkgdir"

    mv "$pkgdir/usr/bin/pip" "$pkgdir/usr/bin/pip2"
    sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    ${pkgdir}/usr/lib/python2.7/site-packages/pip/__init__.py

    install -D -m644 LICENSE.txt \
        "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
