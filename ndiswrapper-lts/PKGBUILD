#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

# Find the kernel name inside the chroot
_extramodules=extramodules-`pacman -Q linux-lts | cut -c11-13 | sed 's/linux-lts //g'`-CHAKRA-LTS
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=ndiswrapper-lts
pkgver=1.57
pkgrel=3
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors. For linux-testing."
license=('GPL')
arch=(i686 x86_64)
url="http://ndiswrapper.sourceforge.net"
install="ndiswrapper.install"
depends=('linux-lts>=3.0' 'linux-lts<3.1' 'ndiswrapper')
makedepends=('linux-lts-headers')
source=(http://downloads.sourceforge.net/sourceforge/ndiswrapper/ndiswrapper-$pkgver.tar.gz)
md5sums=('7a401dc540938bf07893c67f418b6152')

build() {
  cd "$srcdir/ndiswrapper-$pkgver"

  make KVERS=$_kver
}

package() {
  cd "$srcdir/ndiswrapper-$pkgver"

  make INST_DIR="lib/modules/$_extramodules" \
    KVERS=$_kver DESTDIR="$pkgdir/" install

  # rm ndiswrapper
  rm -R "$pkgdir/usr" "$pkgdir/sbin"

  gzip "$pkgdir/lib/modules/$_extramodules/ndiswrapper.ko"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" ${startdir}/*.install
}