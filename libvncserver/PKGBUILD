# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributors from Arch:
# Andrea Scarpino <andrea@archlinux.org>
# Tobias Powalowski <tpowa@archlinux.org>

pkgname=libvncserver
pkgver=0.9.11
pkgrel=1
pkgdesc="A cross-platform C libraries that allow you to easily implement VNC server"
arch=('i686' 'x86_64')
url="https://libvnc.github.io/"
license=('GPL')
depends=('libpng' 'libjpeg' 'gnutls' 'libgcrypt' 'openssl')
makedepends=('sdl')
source=("https://github.com/LibVNC/libvncserver/archive/LibVNCServer-${pkgver}.tar.gz"
        initialize-libgcrypt.patch)
md5sums=('7f06104d5c009813e95142932c4ddb06'
         'e22024d3a81c45129411e39e7d143835')

prepare() {
  cd ${pkgname}-LibVNCServer-${pkgver}
  # Fix crashing when connecting to Mac hosts
  # Suggested by upstream: https://github.com/LibVNC/libvncserver/pull/51#issuecomment-68884516
  patch -p1 -i ../initialize-libgcrypt.patch
}

build() {
  cd ${pkgname}-LibVNCServer-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr --without-libva
  make
}

package() {
  cd ${pkgname}-LibVNCServer-${pkgver}
  make DESTDIR="${pkgdir}" install
}

