#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=r8168
pkgver=8.036.00
pkgrel=5
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
depends=('glibc' 'linux>=3.12' 'linux<3.13')
makedepends=('linux-headers')
source=("http://r8168.googlecode.com/files/$pkgname-$pkgver.tar.bz2"
	'linux-3.10.patch')
md5sums=('a9a5b238f59cc30eefa5917d7f6b728e'
         '8790fa9f6ec1a4d51aec3dd5750e342b')
install=$pkgname.install

_extramodules=extramodules-3.12.5-CHAKRA
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
	cd "$srcdir/$pkgname-$pkgver"
	
	patch -p0 -i "$srcdir/linux-3.10.patch"
	
	make -C /lib/modules/$_kernver/build \
			SUBDIRS="$srcdir/$pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$srcdir/$pkgname-$pkgver/src"
	install -D -m644 $pkgname.ko "$pkgdir/lib/modules/$_extramodules/$pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
