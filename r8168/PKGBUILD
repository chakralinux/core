source ../linux.conf

pkgname=r8168
pkgver=8.044.02
pkgrel=3
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
depends=('glibc' "linux=${_kernelver}")
makedepends=("linux-headers=${_kernelver}")
install=$pkgname.install
source=("https://github.com/mtorromeo/r8168/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        'linux-4.5.patch'
        'linux-4.11.patch')
sha256sums=('aad7bccc4f625c35abe0455b2a6271e9938b76b46e11b24b4f138eb5c95db6d3'
            'e05a4bccf28beecc97db246064a5fe80d1303476b76086bd262c9c8db82b2e6e'
            '21857077bd5b26a08456f97f170b2772e8cadfb331577ddb2bcb680b31c5c0d3')

prepare(){
	cd "$pkgname-$pkgver"
	patch -p1 -i "$srcdir/linux-4.5.patch"
	patch -p1 -i "$srcdir/linux-4.11.patch"
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	
	make -C /usr/lib/modules/$_kver/build \
			SUBDIRS="$srcdir/$pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$srcdir/$pkgname-$pkgver/src"
	install -D -m644 $pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
        sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/$pkgname.install"
}
