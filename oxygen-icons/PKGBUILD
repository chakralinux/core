# Include global configuration

pkgbase=oxygen-icons
_pkgbase=oxygen-icons5
pkgname=('oxygen-icons' 'oxygen-icons-svg')
pkgver=5.50.0
epoch=1
pkgrel=1
arch=('x86_64')
url='http://www.oxygen-icons.org/'
license=('LGPL')
groups=('kf5')
makedepends=('cmake' 'extra-cmake-modules' 'qt5-tools')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${_pkgbase}-${pkgver}.tar.xz"{,.sig})
sha256sums=('cc0e8b5829eddd4813a9d9ae2ff4e7a31e94143c1298f16b2b257ecc894f5daa'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package_oxygen-icons() {
  pkgdesc="The Oxygen Icon Theme"
  
  cd build
  make DESTDIR="$pkgdir" install
}

package_oxygen-icons-svg() {
  pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"

  cd ${_pkgbase}-${pkgver}
  find scalable -type f ! -name '*.sh' -exec \
    install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
