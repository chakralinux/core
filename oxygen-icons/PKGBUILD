# Include global configuration
source ../frameworks.conf

pkgbase=oxygen-icons
_pkgbase=oxygen-icons5
pkgname=('oxygen-icons' 'oxygen-icons-svg')
pkgver=${KFVersion}
epoch=1
pkgrel=1
arch=('x86_64')
url='http://www.oxygen-icons.org/'
license=('LGPL')
groups=('kf5')
makedepends=('cmake' 'extra-cmake-modules' 'qt5-tools')
source=("${KFServer}/${_pkgbase}-${pkgver}.tar.xz")
sha256sums=( $(getSum ${_pkgbase}) )

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${_pkgbase}-${pkgver} 
  make
}

package_oxygen-icons() {
  pkgdesc="The Oxygen Icon Theme"
  
  cd build
  make DESTDIR="$pkgdir" install
}

package_oxygen-icons-svg() {
  pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"

  cd ${_pkgbase}-${pkgver}
  find scalable -type f ! -name '*.sh' -exec \
    install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
