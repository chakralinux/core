# Platform Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgbase=atkmm
pkgname=(atkmm atkmm-docs)
pkgver=2.22.6
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('atk' 'glibmm' 'glibmm-docs' 'libsigc++2.0')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/2.22/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('0a74295106b9e3ae552b169eb4e098b8b825c4115045b9d5134a6c79a238aeb6')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk' 'glibmm' 'libsigc++2.0')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C doc DESTDIR="${pkgdir}" install
}
