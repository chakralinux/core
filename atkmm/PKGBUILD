# Platform Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=atkmm
pkgname=(atkmm atkmm-docs)
pkgver=2.22.5
pkgrel=2
arch=('i686' 'x86_64')
makedepends=('atk' 'glibmm' 'glibmm-docs' 'libsigc++2.0')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/2.22/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('61e03e5e67952c8fd9919049fae680bd0f2a1547344394f36b98b05b34b7be6c')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk' 'glibmm' 'libsigc++2.0')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C doc DESTDIR="${pkgdir}" install
}
