pkgname=upower
pkgver=0.99.7
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('systemd' 'libusb' 'libgudev')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'python2')
optdepends=('libimobiledevice: make upower in use with Apple iPod/iPad')
backup=('etc/UPower/UPower.conf')
source=(https://upower.freedesktop.org/releases/$pkgname-$pkgver.tar.xz)
sha256sums=('24bcc2f6ab25a2533bac70b587bcb019e591293076920f5b5e04bdedc140a401')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
