# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=ki18n
pkgver=5.47.0
pkgrel=1
pkgdesc='A get-text based translation framework'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/ki18n'
license=('LGPL')
depends=('qt5-script' 'qt5-declarative')
makedepends=("extra-cmake-modules>=5.47.0" 'python3')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('2627faa0186e9b463d978e842bf6df1e7d27e7e9d487793f6709e82a1c0729a8'
            SKIP)
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

# check() {
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # conflicts with kde-l10n since kf5 5.20.0
  rm ${pkgdir}/usr/share/locale/nb/LC_SCRIPTS/ki18n5/ki18n5.js
  rm ${pkgdir}/usr/share/locale/nn/LC_SCRIPTS/ki18n5/ki18n5.js
  rm ${pkgdir}/usr/share/locale/nn/LC_SCRIPTS/ki18n5/skjermelement.pmap
  rm ${pkgdir}/usr/share/locale/nn/LC_SCRIPTS/ki18n5/skjermelement.pmapc
  rm ${pkgdir}/usr/share/locale/ca/LC_SCRIPTS/ki18n5/ki18n5.js
  rm ${pkgdir}/usr/share/locale/ca@valencia/LC_SCRIPTS/ki18n5/ki18n5.js
}
