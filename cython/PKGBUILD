pkgname=('cython3' 'cython2')
pkgbase=cython
pkgver=0.28.2
pkgrel=1
pkgdesc="C-Extensions for Python"
arch=(x86_64)
url="http://cython.org"
license=('APACHE')
makedepends=('python3-setuptools' 'python2-setuptools')
source=($pkgbase-$pkgver.tar.gz::"https://github.com/cython/cython/archive/$pkgver.tar.gz")
sha256sums=('d8fad58f7484466ff64a3d54f76943a3e2aef72a4efa1506eccca6ddc3ee547a')

prepare() {
  cp -r cython-$pkgver cython2-$pkgver
  find cython2-$pkgver -name '*.py' | xargs sed -e 's|/usr/bin/env python|/usr/bin/env python2|' -e 's|/usr/bin/python|/usr/bin/python2|' -i
}

build() {
  cd cython-$pkgver
  python3 setup.py build

  cd ../cython2-$pkgver
  python2 setup.py build
}

package_cython3() {
  depends=(python3 python3-setuptools)
  provides=(python3-cython)
  conflicts=(python3-cython)
  replaces=(python3-cython)

  cd cython-$pkgver
  python3 setup.py install --root="$pkgdir" --skip-build

  for f in cygdb cython cythonize; do
    mv "$pkgdir"/usr/bin/$f "$pkgdir"/usr/bin/${f}3
    ln -s ${f}3 "$pkgdir"/usr/bin/$f
  done
}

package_cython2() {
  depends=(python2 python2-setuptools)
  provides=(python2-cython)
  conflicts=(python2-cython)
  replaces=(python2-cython)

  cd cython2-$pkgver
  python2 setup.py install --root="$pkgdir" --skip-build

  for f in cygdb cython cythonize; do
    mv "$pkgdir"/usr/bin/$f "$pkgdir"/usr/bin/${f}2
  done
}
