pkgbase=cython
pkgname=('cython3' 'cython2')
pkgver=0.27.1
pkgrel=1
pkgdesc="C-Extensions for Python"
arch=(x86_64)
url="http://cython.org"
license=('APACHE')
makedepends=('python3-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/10/32/21873ff231e069f860098b1602bb9e3ae2806d2f73ba661b5d806f200243/Cython-$pkgver.tar.gz")
sha256sums=('e6840a2ba2704f4ffb40e454c36f73aeb440a4005453ee8d7ff6a00d812ba176')

package_cython3() {
  depends=('python3' 'python3-setuptools')

  cd "$srcdir"/Cython-$pkgver
  python3 setup.py install --root="$pkgdir"

  sed -i 's|#!.*python|#!/usr/bin/python3|' "$pkgdir"/usr/bin/*

  for f in cygdb cython cythonize; do
    mv "$pkgdir"/usr/bin/$f "$pkgdir"/usr/bin/${f}3
    #ln -s ${f}3 "$pkgdir"/usr/bin/$f
  done
}

package_cython2() {
  depends=('python2' 'python2-setuptools')

  cd "$srcdir"/Cython-$pkgver
  python2 setup.py install --root="$pkgdir"

  for f in cygdb cython cythonize; do
    mv "$pkgdir"/usr/bin/$f "$pkgdir"/usr/bin/${f}2
  done
} 
