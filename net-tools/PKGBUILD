#maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
#contributions from: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/net-tools

pkgname=net-tools
pkgver=1.60.20160710git
pkgrel=1
pkgdesc="Configuration tools for Linux networking"
groups=("base")
arch=('x86_64')
license=('GPL2')
url="http://net-tools.sourceforge.net/"
depends=('glibc')
makedepends=('git')
# use git snapshot currently prefered over release
source=("net-tools::git://git.code.sf.net/p/net-tools/code#commit=115f1af2")
options=(!makeflags)
sha1sums=('SKIP')

prepare() {
  sed -i "s#/usr##" ${srcdir}/${pkgname}/man/Makefile
}

build() {
  cd ${srcdir}/${pkgname}
  yes "" | make
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR=${pkgdir}/usr update

  # hostname is provided by inetutils
  rm "${pkgdir}"/usr/bin/{hostname,dnsdomainname,domainname}
  rm -rf "${pkgdir}"/usr/share/man/man1
}
