# $Id: $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libusbmuxd
pkgver=1.0.10
pkgrel=1
pkgdesc="USB Multiplex Daemon"
url="http://marcansoft.com/blog/iphonelinux/usbmuxd/"
arch=('x86_64')
license=('LGPL2.1' 'GPL2')
depends=('libusb' 'libplist')
conflicts=('usbmuxd<1.0.9')
source=("http://www.libimobiledevice.org/downloads/libusbmuxd-$pkgver.tar.bz2"
        'CVE-2016-5104.patch')
md5sums=('e5351ff6f6eedcb50701e02d91cc480c'
         '3872227d4b1d04752de32404811705d8')

prepare() {
  # CVE-2016-5104, should be upstreamed with pkgver>1.0.10
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/CVE-2016-5104.patch
}

build() {
  cd libusbmuxd-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd libusbmuxd-${pkgver}
  make DESTDIR="${pkgdir}" install
}
