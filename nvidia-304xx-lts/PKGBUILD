# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

_extramodules=extramodules-3.14-CHAKRA-LTS
_kver="$(cat /lib/modules/${_extramodules}/version)"
_kernelver=3.14.57

pkgname=nvidia-304xx-lts
pkgver=304.128
pkgrel=1
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=("linux-lts=${_kernelver}" "nvidia-304xx-libgl" "nvidia-304xx-utils=${pkgver}")
makedepends=("linux-lts-headers=${_kernelver}")
conflicts=('nvidia-340xx-lts' 'nvidia-lts')
license=('custom')
install=nvidia.install
_pkg="NVIDIA-Linux-${arch}-${pkgver}-no-compat32"
source=("ftp://download.nvidia.com/XFree86/Linux-${arch}/${pkgver}/${_pkg}.run")
md5sums=('6478e40ed87d9177cbfc3d0b6e39a051')

prepare() {
    cd "${srcdir}"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
}

build() {
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
