pkgname=nvidia-304xx-lts
pkgver=304.137
pkgrel=7
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=("linux-lts=4.4.154" "nvidia-304xx-utils=${pkgver}")
makedepends=("linux-lts-headers=4.4.154")
conflicts=('nvidia-340xx-lts' 'nvidia-lts')
license=('custom')
_extramodules=extramodules-CHAKRA-LTS
_pkg="NVIDIA-Linux-${arch}-${pkgver}-no-compat32"
source=("http://us.download.nvidia.com/XFree86/Linux-${arch}/${pkgver}/${_pkg}.run")
md5sums=('485506ee6a7c54780488dacddf1d56b1')

prepare() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
}

build() {
    KERNEL_VERSION=$(</usr/lib/modules/$_extramodules/version)
    msg2 "Kernel = $KERNEL_VERSION"
    
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/$KERNEL_VERSION/build module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    echo "blacklist nouveau" |
      install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"
}
 
