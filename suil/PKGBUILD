pkgname=suil
pkgver=0.6.12
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=('x86_64')
url="http://drobilla.net/software/suil/"
license=('custom:ISC')
depends=('lv2')
makedepends=('python2' 'qt' 'gtk2')
optdepends=('qt: Qt 4.x UI wrapping support'
            'gtk2: GTK+ 2.x UI wrapping support')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('528d4a0a37e6c9d5a4547ac36f7bd9e6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove ldconfig --speps
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
