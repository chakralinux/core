#Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/sddm

pkgname=sddm
pkgver=0.14.0
pkgrel=3
pkgdesc='QML based X11 and Wayland display manager'
arch=('x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'extra-cmake-modules' 'python3-docutils' 'qt5-tools')
install="${pkgname}.install"
options=('emptydirs')
backup=('usr/share/sddm/scripts/Xsetup'
        'etc/sddm.conf'
        'etc/pam.d/sddm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        sddm-fix-avatars.patch::"https://github.com/sddm/sddm/commit/ecb903e4.patch"
        'sddm.conf')
md5sums=('393fbed2f2e9d030e1717949867e96c8'
         'f929c400807144f3bc53d8ed6ed74f21'
         'cff07724e92a3e44fcf1c680d0be425b')

prepare() {
  [[ -e build ]] && rm -rf build
  mkdir -p build

  # Fix displaying avatars
  cd $pkgname-$pkgver
  patch -p1 -i ../sddm-fix-avatars.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DDBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
        -DENABLE_PAM=ON \
        -DBUILD_MAN_PAGES=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  # set the default configuration, with heritage as theme
  install -m 644 -p ${srcdir}/sddm.conf ${pkgdir}/etc/

  # set permissions in attempt to fix autologin
  install -d -g 996 -o 996 ${pkgdir}/var/lib/sddm

  # auto unlock the kwallet when the user password
  # and the kwallet password match (FS#1253)

  echo "
# Chakra custom settings
# auto unlock the kwallet when the user password
# and the kwallet password match (FS#1253)

auth		optional	pam_kwallet.so  kdehome=.kde4
session		optional	pam_kwallet.so  kdehome=.kde4
auth		optional	pam_kwallet5.so
session		optional	pam_kwallet5.so" >> ${pkgdir}/etc/pam.d/sddm

}
