#Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/sddm

pkgname=sddm
pkgver=0.14.0.r26.17ccb31
pkgrel=2
pkgdesc='QML based X11 and Wayland display manager'
arch=('x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'extra-cmake-modules' 'python3-docutils' 'qt5-tools' 'git')
install="${pkgname}.install"
options=('emptydirs')
backup=('usr/share/sddm/scripts/Xsetup'
        'etc/sddm.conf'
        'etc/pam.d/sddm')
source=("${pkgname}::git+https://github.com/sddm/sddm.git#commit=17ccb31"
        'sddm.conf')
md5sums=('SKIP'
         'cff07724e92a3e44fcf1c680d0be425b')

prepare() {
  [[ -e build ]] && rm -rf build
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DDBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
        -DENABLE_PAM=ON \
        -DBUILD_MAN_PAGES=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  # set the default configuration, with heritage as theme
  install -m 644 -p ${srcdir}/sddm.conf ${pkgdir}/etc/

  # set permissions in attempt to fix autologin
  install -d -g 996 -o 996 ${pkgdir}/var/lib/sddm

}
