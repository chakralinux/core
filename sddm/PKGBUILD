#Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/sddm

pkgname=sddm
pkgver=0.13.0
pkgrel=3
pkgdesc='QML based X11 and Wayland display manager'
arch=('x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'python3-docutils' 'qt5-tools')
conflicts=('sddm-qt5-git' 'sddm-git')
provides=('sddm-qt5-git' 'sddm-git')
replaces=('sddm-qt5-git' 'sddm-git')
install="${pkgname}.install"
backup=('usr/share/sddm/scripts/Xsetup'
        'etc/sddm.conf'
        'etc/pam.d/sddm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        'sddm.conf')
md5sums=('ecfd285a463eb6490da1333bb2e304a7'
         '7651ef1f651b0bef23b08edb58039d97')
         
build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DDBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
        -DENABLE_PAM=ON \
        -DBUILD_MAN_PAGES=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  # set the default configuration, with heritage as theme
  install -m 644 -p ${srcdir}/sddm.conf ${pkgdir}/etc/


  # auto unlock the kwallet when the user password
  # and the kwallet password match (FS#1253)

  echo "
# Chakra custom settings
# auto unlock the kwallet when the user password
# and the kwallet password match (FS#1253)

auth		optional	pam_kwallet.so  kdehome=.kde4
session		optional	pam_kwallet.so  kdehome=.kde4
auth		optional	pam_kwallet5.so
session		optional	pam_kwallet5.so" >> ${pkgdir}/etc/pam.d/sddm

}
