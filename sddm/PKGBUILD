#Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/sddm

pkgname=sddm
pkgver=0.16.0+137+cc00330
pkgrel=1
pkgdesc='QML based X11 and Wayland display manager'
arch=('x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative' 'xorg-xauth' 'xorg-server')
makedepends=('extra-cmake-modules' 'python3-docutils' 'qt5-tools')
install="${pkgname}.install"
options=('emptydirs')
backup=('usr/share/sddm/scripts/Xsetup'
        'etc/sddm.conf'
        'etc/pam.d/sddm')
source=("${pkgname}::git+https://github.com/sddm/sddm.git#commit=cc00330"
#source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        'sddm.conf' 'sddm.sysusers' 'sddm.tmpfiles')
md5sums=('SKIP'
         '7375f6c4e0be2336b31eec2bec820048'
         '8e67f619f2e4b141499cbfee564ee097'
         '098f2a934ecf9c048b6a9d4b6c41ff5c')

pkgver() {
  cd $pkgname
  printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/\1/;s/-/+/g;s/^v//')"
}

prepare() {
  [[ -e build ]] && rm -rf build
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DDBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
        -DENABLE_PAM=ON \
        -DBUILD_MAN_PAGES=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  # set the default configuration, with heritage as theme
  install -m 644 -p ${srcdir}/sddm.conf ${pkgdir}/etc/

  # create sddm system user and directory
  install -d -g 996 -o 996 ${pkgdir}/var/lib/sddm
  install -Dm644 "$srcdir"/sddm.sysusers "$pkgdir"/usr/lib/sysusers.d/sddm.conf
  install -Dm644 "$srcdir"/sddm.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/sddm.conf
}
