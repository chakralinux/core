# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=gdbm
pkgver=1.8.3
pkgrel=9
pkgdesc="GNU database library"
url="http://www.gnu.org/software/gdbm/gdbm.html"
license=('GPL')
arch=('i686' 'x86_64')
depends=('glibc' 'sh')
source=(ftp://ftp.gnu.org/gnu/gdbm/$pkgname-$pkgver.tar.gz
        gdbm-1.8.3-zeroheaders.patch
	gdbm-1.8.3-fhs.patch)
options=('!libtool' '!makeflags')
install=gdbm.install
md5sums=('1d1b1d5c0245b1c00aff92da751e9aa1'
         'a2ed344be9258775bd718074cf2e4ec6'
         '66a7c235416c136dc89efc7d03352514')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i ../gdbm-1.8.3-zeroheaders.patch
  patch -Np1 -i $srcdir/gdbm-1.8.3-fhs.patch
  
  libtoolize --force --copy
  aclocal
  autoconf 
  
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info

  # https://bugzilla.redhat.com/show_bug.cgi?id=477300
  echo "/* We use fcntl locking (POSIX) instead of flock (BSD) */" >> autoconf.h
  echo "#undef HAVE_FLOCK" >> autoconf.h

  make prefix=/usr || return 1

  make prefix=$pkgdir/usr \
       manprefix=$pkgdir/usr/share/man \
       man3dir=$pkgdir/usr/share/man/man3 \
       infodir=$pkgdir/usr/share/info \
       install install-compat

  ln -sf gdbm/gdbm.h $pkgdir/usr/include
}
