# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# contributor Michael Haesel <michael DOT haesel AT gmail DOT com>
# contributor Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgbase=mariadb
pkgname=('libmariadbclient' 'mariadb-clients' 'mariadb')
pkgver=5.5.40
pkgrel=2
arch=('x86_64')
license=('GPL')
url="http://mariadb.org/"
makedepends=('cmake' 'openssl' 'zlib' 'libaio' 'libxml2' 'pcre' 'jemalloc')
source=("https://downloads.mariadb.org/interstitial/mariadb-$pkgver/source/mariadb-$pkgver.tar.gz"
        'include_check.patch'
        'mariadb.service'
        'mariadb.tmpfiles')
sha256sums=('cbde17f4a31483143490def6fcce33310ebae49eafe92dc4ada0e7227202415a'
            'bd629b5ffc4e376892f0c0c47353b5276a4655a8d1d59841fe0f20cdbea8af63'
            'ef3524c227ce0219e511fd4f3e17ec869fcde7ebf653d21e53bbf4d543cfcc20'
            '51ac45bce09f408d305dd770d75c7893e9f7612d9cf223a30aa22967dd7738de')

prepare() {
  sed -i 's/_SVID_SOURCE/_DEFAULT_SOURCE/' \
    $pkgbase-$pkgver/storage/tokudb/ft-index/cmake_modules/TokuSetupCompiler.cmake
  cd ${pkgbase}-${pkgver}
  patch -p0 -i ${srcdir}/include_check.patch
}

build() {
  [ -d build ] && rm -r build
  mkdir build
  cd build

  cmake ../${pkgbase}-${pkgver} \
    -DBUILD_CONFIG=mysql_release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc/mysql \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DMYSQL_UNIX_ADDR=/var/run/mysqld/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_INFODIR=share/mysql/docs \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=/usr/lib/mysql/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include/mysql \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_DOCDIR=share/mysql/docs \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=system \
    -DWITH_SSL=system \
    -DWITH_PCRE=system \
    -DWITH_LIBWRAP=OFF \
    -DWITH_JEMALLOC=ON \
    -DCMAKE_EXE_LINKER_FLAGS='-ljemalloc' \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITH_PBXT_STORAGE_ENGINE=1 \
    -DWITH_TOKUDB_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITHOUT_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1\
    -DCMAKE_C_FLAGS="-fPIC $CFLAGS -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-fPIC $CXXFLAGS -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
    -DWITH_MYSQLD_LDFLAGS="$LDFLAGS"

  make
}

package_libmariadbclient(){
  pkgdesc="MariaDB client libraries"
  depends=('openssl' 'libaio' 'zlib' 'pcre')
  conflicts=('libmysqlclient')
  provides=("libmysqlclient=${pkgver}")
  
  cd "${srcdir}"/build
  for dir in include libmysql libmysqld libservices; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done

  install -d "${pkgdir}"/usr/bin
  install -m755 scripts/mysql_config "${pkgdir}"/usr/bin/
  install -d "${pkgdir}"/usr/share/man/man1
  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 "${srcdir}"/${pkgbase}-${pkgver}/man/$man.1 "${pkgdir}"/usr/share/man/man1/$man.1
  done
}

package_mariadb-clients() {
  pkgdesc="MariaDB client tools"
  depends=('libmariadbclient' 'zlib' 'openssl' 'jemalloc')
  conflicts=('mysql-clients')
  provides=("mysql-clients=${pkgver}")
  
  cd "${srcdir}"/build
  make -C client DESTDIR="${pkgdir}" install

  # install man pages
  install -d "${pkgdir}"/usr/share/man/man1
  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 "${srcdir}"/${pkgbase}-${pkgver}/man/$man.1 "${pkgdir}"/usr/share/man/man1/$man.1
  done

  # provided by mariadb
  rm "${pkgdir}"/usr/bin/{mysql_{plugin,upgrade},mysqlbinlog,mysqltest}
}

package_mariadb() {
  pkgdesc="A fast SQL database server, drop-in replacement for MySQL"
  backup=('etc/mysql/my.cnf')
  install=mariadb.install
  depends=('mariadb-clients' 'libaio' 'jemalloc')
  conflicts=('mysql')
  provides=("mysql=${pkgver}" 'akonadi-backend')
  optdepends=('perl-dbi' 'perl-dbd-mysql')
  options=('emptydirs')
  
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${pkgdir}"/usr/share/mysql/my-medium.cnf "${pkgdir}"/etc/mysql/my.cnf

  install -Dm644 "${srcdir}"/mariadb.service "${pkgdir}"/usr/lib/systemd/system/mysqld.service
  install -Dm644 "${srcdir}"/mariadb.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/mysql.conf

  # provided by libmariadbclient
  rm "${pkgdir}"/usr/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}
  rm "${pkgdir}"/usr/lib/libmysql*
  rm -r "${pkgdir}"/usr/include/
  rm "${pkgdir}"/usr/share/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1
  
  # provided by mariadb-clients
  rm "${pkgdir}"/usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
  rm "${pkgdir}"/usr/share/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1

  # not needed
  rm -r "${pkgdir}"/usr/{data,mysql-test,sql-bench}
  rm "${pkgdir}"/usr/share/man/man1/mysql-test-run.pl.1

  install -dm700 "${pkgdir}"/var/lib/mysql
}
