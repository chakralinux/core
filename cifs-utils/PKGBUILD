# Platform Packages for Chakra, part of chakra-project.org
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# Contributor: Phil Miller <philm[at]chakra-project[dog]org>

pkgname=cifs-utils
pkgver=5.7
pkgrel=1
pkgdesc="CIFS filesystem user-space tools"
arch=(x86_64)
url="http://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL')
depends=('libcap' 'keyutils' 'krb5' 'talloc')
source=(ftp://ftp.samba.org/pub/linux-cifs/cifs-utils/$pkgname-$pkgver.tar.bz2)
md5sums=('4215f12be8663f65d2c21dbbbafbc7f8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
# systemd support is broken in mount.cifs: https://bugs.archlinux.org/task/30958
  ./configure --prefix=/usr --disable-systemd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" ROOTSBINDIR=/usr/bin install
  msg 'if you want to enable user mounting and unmounting you need to set the suid flag'
  msg 'chmod +s /sbin/mount.cifs'
  msg 'and put the mount in the fstab. use the nosuid option in fstab for remote execution prevention'
}
