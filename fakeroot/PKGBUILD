# maintainer (x86_64): AlmAck <gluca86[at]google[dot]org>

pkgname=fakeroot
pkgver=1.22
pkgrel=1
pkgdesc="Gives a fake root environment, useful for building packages as a non-privileged user."
arch=('x86_64')
license=('GPL')
url="http://packages.debian.org/fakeroot"
groups=('base-devel')
install="fakeroot.install"
depends=('glibc' 'filesystem' 'util-linux' 'sed' 'sh')
makedepends=('po4a')
source=(http://ftp.debian.org/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.bz2
        silence-dlerror.patch)
md5sums=('fae64c9aeb2c895ead8e1b99bf50c631'
         '5fba0b541b5af39d804265223fda525c')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/silence-dlerror.patch

  ./bootstrap
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --libdir=/usr/lib/libfakeroot \
              --disable-static \
              --with-ipc=sysv
	# --with-ipc=tcp is currently broken and no option on smp systems

  make

  cd doc
  po4a -k 0 --rm-backups --variable "srcdir=../doc/" po4a/po4a.cfg
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -dm755 "${pkgdir}/etc/ld.so.conf.d"
  echo '/usr/lib/libfakeroot' > "${pkgdir}/etc/ld.so.conf.d/fakeroot.conf"
  
  # install README for sysv/tcp usage
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim:set ts=2 sw=2 et:
