pkgname=xerces-c
pkgver=3.1.3
pkgrel=2
pkgdesc="A validating XML parser written in a portable subset of C++"
arch=('x86_64')
url="http://xerces.apache.org/xerces-c/"
license=("APACHE")
depends=('gcc-libs' 'curl')
source=("http://apache.osuosl.org/xerces/c/3/sources/${pkgname}-${pkgver}.tar.gz"
        'xerces-c-cve-2016-2099.patch')
md5sums=('70320ab0e3269e47d978a6ca0c0e1e2d'
         '382aa993dc070be469e2ff2b2a9bad09')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ../xerces-c-cve-2016-2099.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc 
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir}/ install
}
