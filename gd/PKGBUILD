pkgname=gd
pkgver=2.2.5
pkgrel=1
pkgdesc='Library for the dynamic creation of images by programmers'
arch=('x86_64')
url='http://www.libgd.org/'
license=('custom')
depends=('libpng' 'fontconfig' 'libjpeg-turbo' 'libxpm' 'libwebp')
optdepends=('perl: bdftogd script')
checkdepends=('ttf-liberation')
source=("https://github.com/libgd/libgd/releases/download/gd-${pkgver}/libgd-${pkgver}.tar.xz")
sha256sums=('8c302ccbf467faec732f0741a859eef4ecae22fea2d2ab87467be940842bde51')

build() {
    cd ${srcdir}/libgd-${pkgver}
    ./configure \
        --prefix=/usr \
        --disable-rpath
    make
}

check() {
    cd ${srcdir}/libgd-${pkgver}
    make check
}

package() {
    cd ${srcdir}/libgd-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
