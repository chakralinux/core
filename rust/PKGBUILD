# Originally imported from Arch Linux with
# Arch Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Arch Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=rust
pkgver=1.17.0
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=('gcc-libs' 'llvm-libs')
makedepends=('rust' 'cargo' 'libffi' 'perl' 'python2' 'curl' 'jemalloc' 'llvm')
optdepends=('cargo')
source=("http://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz")
sha256sums=('4baba3895b75f2492df6ce5a28a916307ecd1c088dc1fd02dbfa8a8e86174f87')
install="$pkgname.install"
options=('staticlibs' '!strip' '!emptydirs')

build() {
  cd "rustc-$pkgver-src"

  ./configure --prefix=/usr \
              --release-channel=stable \
              --llvm-root=/usr \
              --disable-codegen-tests \
              --jemalloc-root=/usr/lib \
              --enable-local-rust

  export RUSTFLAGS="$RUSTFLAGS -C link-args=-lffi"

  python2 ./x.py build --verbose
}

package() {
  cd "rustc-$pkgver-src"

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  cd "$pkgdir/usr/lib"
  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .
  rm rustlib/uninstall.sh # remove uninstall.sh, pacman manages the files anyway
}
