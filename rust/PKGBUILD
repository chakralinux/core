# Originally imported from Arch Linux with
# Arch Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Arch Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=rust
pkgver=1.11.0
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
makedepends=('libffi' 'perl' 'python2' 'curl' 'cmake')
source=("http://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz")
sha256sums=('3685034a78e70637bdfa3117619f759f2481002fd9abbc78cc0f737c9974de6a')
install="$pkgname.install"
options=('staticlibs' '!strip')


build() {
  cd "rustc-$pkgver"

  ./configure --prefix=/usr --disable-docs --release-channel=stable
  make
}

package() {
  cd "rustc-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  cd "$pkgdir/usr/lib"
  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .
  rm rustlib/uninstall.sh # remove uninstall.sh, pacman manages the files anyway
}
