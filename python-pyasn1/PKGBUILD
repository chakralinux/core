pkgbase=python-pyasn1
pkgname=('python3-pyasn1' 'python2-pyasn1')
pkgver=0.4.2
pkgrel=1
arch=('any')
url="http://sourceforge.net/projects/pyasn1/"
license=('BSD')
makedepends=('python3-setuptools' 'python2-setuptools')
source=(https://pypi.io/packages/source/p/pyasn1/pyasn1-$pkgver.tar.gz)
sha1sums=('6f997e4b9aace55948e45cffe32980bb92985ce2')

check() {
  cd pyasn1-${pkgver}
  python2 setup.py test
  python3 setup.py test
}

package_python3-pyasn1() {
  pkgdesc="ASN.1 library for Python 3"
  depends=('python3')

  cd pyasn1-${pkgver}
  python3 setup.py install --root="${pkgdir}"
  install -D -m 644 LICENSE.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pyasn1() {
  pkgdesc="ASN.1 library for Python 2"
  depends=('python2')

  cd pyasn1-${pkgver}
  python2 setup.py install --root="${pkgdir}"
  install -D -m 644 LICENSE.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
