#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=perl-mail-spf
_realname=Mail-SPF
pkgver=2.007
pkgrel=2
pkgdesc="Perl module that provides SPF support"
arch=(any)
license=('custom')
url="http://search.cpan.org/dist/${_realname}/"
# Used for testing only.
makedepends=('perl-net-dns-resolver-programmable')
depends=('perl-error' 'perl-netaddr-ip' 'perl-uri' 'perl-net-dns'
'perl>=5.10.0')
conflicts=('perl-mail-spf-query')
source=(http://search.cpan.org/CPAN/authors/id/J/JM/JMEHNLE/mail-spf/${_realname}-v${pkgver}.tar.gz)
options=(!emptydirs)
md5sums=('67dccdc91e3264679a0e17d493d3cc30')

build() {
  cd ${srcdir}/${_realname}-v${pkgver}
  # Fix required perl-net-dns-resolver-programmable version
  sed "s/=> 'v0.002.1',/=> 0,/" -i Build.PL
  perl Makefile.PL INSTALLDIRS=vendor
  make
  make test
}

package() {
  cd ${srcdir}/${_realname}-v${pkgver}
  make DESTDIR=${pkgdir} install
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name 'perllocal.pod' -delete
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}