#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer: abveritas@chakra-project.org
# rebuild libtool on any update

pkgname=sed
pkgver=4.2.1
pkgrel=6
pkgdesc="GNU stream editor"
arch=('x86_64')
url="http://www.gnu.org/software/sed"
license=('GPL3')
groups=('base')
depends=('acl' 'sh')
makedepends=('gettext')
install=sed.install
source=("ftp://ftp.gnu.org/pub/gnu/sed/${pkgname}-${pkgver}.tar.gz"{,.sig})
md5sums=('f0fd4d7da574d4707e442285fd2d3b86'
         '91871b227dbf0916e019ba1fb0fbfdf3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --bindir=/bin \
              --without-included-regex

  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
