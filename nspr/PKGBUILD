# Maintainer: Bruce Liu <brli@users.sourceforge.net>
# TODO: Always remember to build lib32-nspr when bumping pkgver

pkgname=nspr
pkgver=4.10.8
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('glibc')
makedepends=('zip')
options=('!emptydirs')
source=("ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz")
sha1sums=('c87c6a10e0e36866006b45c194d70cd8c67d0934')

build() {
  cd $pkgname-$pkgver
  _confflags="--enable-64bit"

  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug ${_confflags}
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  ln -s nspr.pc "$pkgdir/usr/lib/pkgconfig/mozilla-nspr.pc"
  rm -r "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties} \
         "$pkgdir/usr/include/nspr/md"
}
