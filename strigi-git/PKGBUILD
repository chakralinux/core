#
# Chakra Packages for Chakra, part of chakra-project.org
#
# from AUR package: https://aur.archlinux.org/packages.php?ID=45165

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=strigi-git
pkgver=20110925
pkgrel=1
pkgdesc="Fast crawling desktop search engine with Qt4 GUI"
arch=('i686' 'x86_64')
url="http://strigi.sourceforge.net/"
license=('GPL')
depends=('qt' 'bzip2' 'exiv2' 'libxml2' 'boost-libs')
makedepends=('git' 'cmake' 'boost')
replaces=('strigi')
conflicts=('strigi')
provides=('strigi')

_gitroot="git://anongit.kde.org/strigi"
_gitname="strigi"

# create tarball: source PKGBUILD && mksource

mksource() {
  git clone git://anongit.kde.org/strigi
  pushd strigi
  popd
  tar -cvJf strigi-${pkgver}.tar.xz strigi/*
  md5sum strigi-${pkgver}.tar.xz
}

build() {
  cd $srcdir
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $srcdir/$_gitname 
    git pull origin
    git submodule update
    msg "The local files are updated."
  else
    git clone $_gitroot
    cd $srcdir/$_gitname
    git submodule update --init
  fi
  
  msg "GIT checkout done"
  msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build
  fi
  mkdir $srcdir/build
  cd $srcdir/build
  cmake ${srcdir}/$_gitname -DCMAKE_BUILD_TYPE=debugfull \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_INOTIFY=ON \
		-DENABLE_LOG4CXX=OFF \
		-DENABLE_FAM=OFF \
		-DENABLE_CLUCENE=OFF \
		-DENABLE_CLUCENE_NG=OFF \
		-DENABLE_FFMPEG=OFF \
		-DENABLE_XINE=OFF
  make
}

package() {
  cd "${srcdir}/build/libstreams"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/libstreamanalyzer"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/strigiclient/lib/searchclient/qtdbus"
  make DESTDIR="${pkgdir}" install
  
  cd "${srcdir}/build/strigiutils"
  make DESTDIR="${pkgdir}" install
}
