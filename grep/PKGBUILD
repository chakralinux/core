pkgname=grep
pkgver=2.24
pkgrel=1
pkgdesc="A string search utility"
arch=('x86_64')
license=('GPL3')
url="http://www.gnu.org/software/grep/grep.html"
groups=('base')
depends=('glibc' 'pcre')
makedepends=('texinfo')
install="${pkgname}.install"
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('f1c980134c04692a90078e87283074b6'
         'SKIP')

# If pcre get a major upgrade in order to compile grep you must:
# downgrade pcre, compile grep static by adding  LDFLAGS="$LDFLAGS -static"
# install the static version of grep, compile/install the new pcre
# compile normally grep
# this is needed because grep needs grep to compile (is hardcoded in the source)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --without-included-regex
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
