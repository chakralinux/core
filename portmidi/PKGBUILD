#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=portmidi
pkgver=217
pkgrel=2
pkgdesc="Platform independent library for real-time MIDI input/output."
arch=('x86_64')
url='http://portmedia.sourceforge.net/'
license=('GPL')
depends=('alsa-lib')
makedepends=('cmake' 'java-environment')
conflicts=('portmidi-newer')
options=(!makeflags)
source=("http://downloads.sourceforge.net/project/portmedia/${pkgname}/${pkgver}/${pkgname}-src-${pkgver}.zip"
        "portmidi-217-build-fix.patch")
sha256sums=('08e9a892bd80bdb1115213fb72dc29a7bf2ff108b378180586aa65f3cfd42e0f'
            'a429df6d5a6fded376710742c76fa466a70ffa582d19b6062a552ea44cd78b82')

prepare() {
  cd "${pkgname}"
  
  sed -i "s#/usr/local#/usr#" */CMakeLists.txt pm_python/setup.py
  mkdir -p pm_java/Release
  
  # build fix for "pm_java/CMakeLists.txt" (fixes FS#27118)
  patch -p1 -i "${srcdir}/portmidi-217-build-fix.patch"
}

build() {
  cd "${pkgname}"
  
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_ARCHIVE_OUTPUT_DIRECTORY=Release \
        -DCMAKE_LIBRARY_OUTPUT_DIRECTORY=Release \
        -DCMAKE_RUNTIME_OUTPUT_DIRECTORY=Release \
        
  make
}

package() {
  cd "${pkgname}"
  
  make DESTDIR="${pkgdir}" install
  
  # Fix missing symlink for libporttime.so (FS#21528).
  mv "${pkgdir}/usr/lib/libportmidi_s.a" "${pkgdir}/usr/lib/libportmidi.a"
  ln -s "/usr/lib/libportmidi.so" "${pkgdir}/usr/lib/libporttime.so"
  ln -s "/usr/lib/libportmidi.a" "${pkgdir}/usr/lib/libporttime.a"
}
