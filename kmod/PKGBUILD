#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=kmod
pkgver=5
pkgrel=4
pkgdesc="Linux kernel module handling"
arch=('i686' 'x86_64')
url="http://git.profusion.mobi/cgit.cgi/kmod.git"
license=('GPL2')
depends=('glibc' 'zlib')
options=('!libtool')
provides=('module-init-tools=3.16')
conflicts=('module-init-tools')
replaces=('module-init-tools')
source=("http://packages.profusion.mobi/$pkgname/$pkgname-$pkgver.tar.xz"
        '0001-libkmod-module-probe-Fix-ignore-loaded-flag-not-bein.patch'
        '0001-libkmod-module-probe-fix-infinite-loop-with-softdeps.patch'
        "depmod-search.conf")
md5sums=('b271c2ec54aba1c67bda63c8579d8c15'
         '81545a1509b43008f85c03fb980f0e86'
         '662a85dbe420f04c1ef24f9cd4e4c990'
         '4b8cbcbc54b9029c99fd730e257d4436')

build() {
  cd "$pkgname-$pkgver"

  # fix modprobe --show-depends failures on loaded modules
  patch -Np1 <"$srcdir/0001-libkmod-module-probe-Fix-ignore-loaded-flag-not-bein.patch"

  # fix infinite loop with softdeps
  patch -Np1 <"$srcdir/0001-libkmod-module-probe-fix-infinite-loop-with-softdeps.patch"

  ./configure \
    --sysconfdir=/etc \
    --with-rootprefix= \
    --with-zlib

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,lib}/{depmod,modprobe}.d "$pkgdir/sbin"

  # add symlinks to kmod
  ln -s ../usr/bin/kmod "$pkgdir/sbin/modprobe"
  ln -s ../usr/bin/kmod "$pkgdir/sbin/depmod"

  # add compatible symlinks
  ln -s ../usr/bin/insmod "$pkgdir/sbin/insmod"
  ln -s ../usr/bin/lsmod "$pkgdir/sbin/lsmod"
  ln -s ../usr/bin/modinfo "$pkgdir/sbin/modinfo"
  ln -s ../usr/bin/rmmod "$pkgdir/sbin/rmmod"


  for tool in {ins,ls,rm}mod modinfo; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/lib/depmod.d/search.conf"
}