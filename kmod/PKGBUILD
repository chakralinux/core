# Maintainer: <inkane[at]chakra-project[dot]org>

pkgname=kmod
pkgver=18
pkgrel=2
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url='http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary'
license=('GPL2')
depends=('glibc' 'zlib')
options=('!libtool')
provides=('module-init-tools=3.16')
conflicts=('module-init-tools')
replaces=('module-init-tools')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.xz"
        "depmod-search.conf")
sha1sums=('6cadc3b088fa5874f712d3bbe4df75ece59c4b77'
          '489b0ce0efd193aab70421a852526ed04dd026c4')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --sysconfdir=/etc \
    --with-xz \
    --with-zlib

  make
}

check() {
  make -C "$pkgname-$pkgver" check 
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d
  install -dm755 "$pkgdir"/usr/sbin

  ln -s ../bin/kmod "$pkgdir/usr/sbin/modprobe"
  ln -s ../bin/kmod "$pkgdir/usr/sbin/depmod"

  for tool in {ins,ls,rm}mod modinfo; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"
}
