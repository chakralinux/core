# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=pommed
pkgver=1.39
pkgrel=1
pkgdesc="Handles the hotkeys of Apple MacBook (Pro) laptops"
arch=('x86_64')
url="http://packages.debian.org/unstable/utils/pommed"
license=('GPL2')
install=$pkgname.install
makedepends=('pkgconfig')
depends=('alsa-lib' 'audiofile' 'confuse' 'dbus-core' 'pciutils')
optdepends=('eject: disc ejection support')
#backup=('etc/pommed.conf')
source=("http://ftp.de.debian.org/debian/pool/main/p/${pkgname}/${pkgname}_${pkgver}~dfsg.orig.tar.gz"
        'pommed.service'
        '0001-rely-on-sys-inotify.h-for-inotify-support.patch'
        'apple-keyboard-ids.patch'
        'build_flags.patch'
        'MacBookPro91.patch')
sha256sums=('2d052c8eccc08f1dc070cc8d96316734afb7623ae849277857754b0233ded131'
            'f753c7cc2325e84ec4335f04b79d582797e6a5699bf3fad0f3aedfc653f09599'
            '56a46954de22c33d453174cb35a761edab466e7e0f0e685cab0dab36dc45ed9b'
            '1171e1fa6b3550bb0a0954d2fcd0ec6f94696f41a431298b5129e6e0e94f122e'
            '32f5447e278faaad68cd4ee33f6f8a0ecd8af80f0f5f8b3772d88554c12e3681'
            '2053abda2a24a37591cd32ec32390e3e42da7215a6ab368b8bd7c07c914c4e1d')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}~dfsg
  patch -p1 -i "${srcdir}/build_flags.patch"
  patch -p1 -i "${srcdir}/0001-rely-on-sys-inotify.h-for-inotify-support.patch"
  patch -p1 -i "${srcdir}/apple-keyboard-ids.patch"
  patch -p1 -i "${srcdir}/MacBookPro91.patch"
}
            
build() {
  cd ${srcdir}/${pkgname}-${pkgver}~dfsg
  make pommed
}

package() {
  install -Dm755 ${srcdir}/${pkgname}-${pkgver}~dfsg/pommed/pommed ${pkgdir}/usr/sbin/pommed
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}~dfsg/pommed.conf.mactel ${pkgdir}/etc/pommed.conf.mactel
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}~dfsg/pommed.conf.pmac ${pkgdir}/etc/pommed.conf.pmac
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}~dfsg/dbus-policy.conf ${pkgdir}/etc/dbus-1/system.d/pommed.conf

  # Man page
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}~dfsg/pommed.1 ${pkgdir}/usr/share/man/man1/pommed.1

  # Sounds
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}~dfsg/pommed/data/goutte.wav ${pkgdir}/usr/share/pommed/goutte.wav
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}~dfsg/pommed/data/click.wav ${pkgdir}/usr/share/pommed/click.wav

  # Systemd
  install -Dm644 "${srcdir}/pommed.service" "${pkgdir}/usr/lib/systemd/system/pommed.service"
}
