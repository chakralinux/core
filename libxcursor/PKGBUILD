# Part of the X.org group

pkgname=libxcursor
pkgver=1.1.15
pkgrel=1
pkgdesc="X cursor management library"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'libxrender')
makedepends=('xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXcursor-${pkgver}.tar.bz2)
sha1sums=('3e19f991f244b7fa31566adce7ead078424296cf')

build() {
  cd "${srcdir}/libXcursor-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXcursor-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
