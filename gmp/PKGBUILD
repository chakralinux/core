#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=gmp
pkgver=5.0.1
pkgrel=3
# dash the next line if you want to skip the tests
_test=1
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i686' 'x86_64')
url="http://gmplib.org/"
depends=('gcc-libs' 'sh')
license=('LGPL3')
options=('!libtool' 'log')
install=gmp.install
source=(ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.bz2)
md5sums=('6bac6df75c192a13419dfd71d19240a7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "${CARCH}" = "i686" ] && export ABI="32"
  ./configure --build=${CHOST} \
    --prefix=/usr --infodir=/usr/share/info \
    --enable-cxx
  make

  if [ -n "${_test}" ]; then
     echo "<<< STARTING TESTS >>>"
     make check
     echo "<<< ENDING TESTS >>>"
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
