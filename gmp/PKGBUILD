#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=gmp
pkgver=6.0.0
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('x86_64')
url="http://gmplib.org/"
depends=('gcc-libs' 'sh')
license=('LGPL3' 'GPL')
options=('!libtool')
install=gmp.install
source=(ftp://ftp.gmplib.org/pub/gmp-${pkgver}/gmp-${pkgver}a.tar.lz{,.sig})
md5sums=('a8b0090b79291a54390356414264142c'
         'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build=${CHOST} \
    --prefix=/usr --enable-cxx
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
