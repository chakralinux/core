#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=gmp
pkgver=5.1.1
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('x86_64')
url="http://gmplib.org/"
depends=('gcc-libs' 'sh')
license=('LGPL3')
options=('!libtool')
install=gmp.install
source=(ftp://ftp.gmplib.org/pub/gmp-${pkgver}/gmp-${pkgver}.tar.xz{,.sig})
md5sums=('485b1296e6287fa381e6015b19767989'
         '5cec115b99dc498b079430dd62084d9c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build=${CHOST} \
    --prefix=/usr --enable-cxx
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
