#
# Platform packages of Chakra, part of chakraos.org
#
# Contributor: AlmAck

pkgname=libass
pkgver=0.12.0
pkgrel=1
pkgdesc="A portable library for SSA/ASS subtitles rendering"
arch=('x86_64')
url="https://github.com/libass/libass/"
license=('GPL')
depends=('enca' 'fontconfig' 'fribidi' 'harfbuzz')
makedepends=('pkg-config' 'yasm')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('ba806f521248dd08032d433abc6d5ace')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-enca \
              --enable-harfbuzz \
              --enable-fontconfig
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

