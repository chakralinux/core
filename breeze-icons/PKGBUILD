# Include global configuration

pkgname=breeze-icons
pkgver=5.54.0
pkgrel=1
pkgdesc='Breeze icon themes'
arch=('any')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
groups=('kf5')
optdepends=('oxygen-icons: fallback icon theme')
makedepends=('extra-cmake-modules' 'qt5-base')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('5e160b1c2eb82a55a10e65eba14fcd01da6db14471213df268abe028debdadbc'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
    cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBINARY_ICONS_RESOURCE=ON  ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
} 
