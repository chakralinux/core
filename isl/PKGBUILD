#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=isl
pkgver=0.11.1
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('i686' 'x86_64')
url="http://www.kotnet.org/~skimo/isl/"
depends=('gmp')
license=('MIT')
options=('!libtool')
source=(ftp://ftp.linux.student.kuleuven.be/pub/people/skimo/isl/$pkgname-$pkgver.tar.bz2)
#source=(http://www.kotnet.org/~skimo/isl/$pkgname-$pkgver.tar.bz2)
md5sums=('bce1586384d8635a76d2f017fb067cd2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 DESTDIR="$pkgdir/" install

  install -dm755 $pkgdir/usr/share/gdb/auto-load/usr/lib/
  mv $pkgdir/{,/usr/share/gdb/auto-load/}usr/lib/libisl.so.10.1.1-gdb.py
  
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}

