#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=isl
pkgver=0.15
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('x86_64')
url="http://www.kotnet.org/~skimo/isl/"
depends=('gmp')
license=('MIT')
source=("http://isl.gforge.inria.fr/${pkgname}-${pkgver}.tar.bz2")
md5sums=('8428efbbc6f6e2810ce5c1ba73ecf98c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -dm755 $pkgdir/usr/share/gdb/auto-load/usr/lib/
  mv $pkgdir/usr/lib/libisl.so.*-gdb.py $pkgdir/usr/share/gdb/auto-load/usr/lib/
  
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}
