pkgbase=python-pyasn1-modules
pkgname=('python3-pyasn1-modules' 'python2-pyasn1-modules')
pkgver=0.2.1
pkgrel=1
pkgdesc="A collection of ASN.1-based protocols modules"
arch=('any')
license=('BSD')
url="http://pypi.python.org/pypi/pyasn1-modules"
makedepends=('python3-pyasn1' 'python2-pyasn1')
source=("https://pypi.io/packages/source/p/pyasn1-modules/pyasn1-modules-$pkgver.tar.gz")
md5sums=('ff938698590f314ff9b3d5c48737d899')

prepare() {
   cp -a pyasn1-modules-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd pyasn1-modules-${pkgver}
   python3 setup.py build
 
   # Build python 2 module
   cd ../pyasn1-modules-${pkgver}-python2
   python2 setup.py build
}
 
package_python3-pyasn1-modules() {
   depends=('python3' 'python3-pyopenssl' 'python3-pyasn1')
 
   cd pyasn1-modules-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
 
package_python2-pyasn1-modules() {
   depends=('python2' 'python2-pyopenssl' 'python2-pyasn1')
 
   cd pyasn1-modules-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
   
