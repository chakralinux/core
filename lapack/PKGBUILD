# maintainer (x86_64): AlmAck

pkgname=lapack
pkgver=3.6.0
pkgdesc="Linear Algebra PACKage"
pkgrel=5
url="http://www.netlib.org/lapack"
depends=("openblas")
replaces=("atlas-lapack")
makedepends=('gcc-fortran' 'cmake' 'python2')
arch=('x86_64')
license=("custom")
source=(http://www.netlib.org/${pkgname}/${pkgname}-${pkgver}.tgz
        LICENSE.blas)
md5sums=('f2f6c67134e851fe189bb3ca1fbb5101'
         '38b6acb8ed5691d25863319d30a8b365')

prepare() {
  cd "${srcdir}"
  install -d build

}


build() {
  cd "${srcdir}/build"

  cmake ../${pkgname}-${pkgver} \
    -DUSE_OPTIMIZED_BLAS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_Fortran_COMPILER=gfortran \
    -DLAPACKE=ON
  make
}

package() {

  cd ${srcdir}/build
  make DESTDIR=$pkgdir install

  install -m755 -d "${pkgdir}/usr/share/licenses/lapack"
  install -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" \
    "${pkgdir}/usr/share/licenses/lapack/"
}
