#
# Chakra Packages for Chakra, part of chakraos.org
#
# maintainer (x86_64): AlmAck

pkgbase=lapack
pkgname=('lapack' 'blas')
pkgver=3.5.0
pkgrel=1
url="http://www.netlib.org/lapack"
makedepends=('gcc-fortran' 'cmake')
arch=('x86_64')
license=("custom")
source=(http://www.netlib.org/${pkgbase}/${pkgbase}-${pkgver}.tgz
        LICENSE.blas)
md5sums=('b1d3e3e425b2e44a06760ff173104bdf'
         '38b6acb8ed5691d25863319d30a8b365')

build() {
  cd "${srcdir}"

  install -d build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_Fortran_COMPILER=gfortran \
    -DLAPACKE=ON
  make
}

package_lapack() {
  pkgdesc="Linear Algebra PACKage"
  depends=("blas=${pkgver}")

  install -m755 -d ${pkgdir}/usr/{lib,bin,include}
  
  for libname in liblapack libtmglib liblapacke; do
    install -m755 "${srcdir}/build/lib/${libname}.so" \
      "${pkgdir}/usr/lib/"
    ln -sf ${libname}.so "${pkgdir}/usr/lib/${libname}.so.${pkgver}"
    ln -sf ${libname}.so "${pkgdir}/usr/lib/${libname}.so.3"
  done
  
  install -m755 "${srcdir}"/build/bin/* "${pkgdir}/usr/bin"
  install -m644 "${srcdir}"/build/include/* "${pkgdir}/usr/include"
  install -m755 -d "${pkgdir}/usr/share/licenses/lapack"
  install -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" \
    "${pkgdir}/usr/share/licenses/lapack/"
}

package_blas() {
  pkgdesc="Basic Linear Algebra Subprograms"
  depends=('gcc-libs')

  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 "${srcdir}/build/lib/libblas.so" \
      "${pkgdir}/usr/lib/"
  ln -sf libblas.so "${pkgdir}/usr/lib/libblas.so.${pkgver}"
  ln -sf libblas.so "${pkgdir}/usr/lib/libblas.so.3"

  install -m755 -d "${pkgdir}/usr/share/licenses/blas"
  install -m644 "${srcdir}/LICENSE.blas" \
    "${pkgdir}/usr/share/licenses/blas/"
}

