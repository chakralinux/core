pkgname=f2fs-tools
pkgver=1.12.0
pkgrel=1
pkgdesc="Tools for Flash-Friendly File System (F2FS)"
arch=('x86_64')
url='http://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/about/'
depends=('util-linux')
license=('GPL')
source=("https://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/snapshot/$pkgname-$pkgver.tar.gz")
sha256sums=('e2124e4dffaba865d41495d817bcb924d096adaec67ff777b8c7da99aa13f696')

build() {
	cd "${pkgname}-${pkgver}"

	# sg_write_buffer is provided in sg3_utils
	sed -i '/sg_write_buffer/d' tools/Makefile.am

	autoreconf -fi
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}/" sbindir=/usr/bin install
}
