# Maintainer: Jeff Huang <s8321414[at]gmail[dot]com>

pkgname=ttf-liberation
pkgver=2.00.1
pkgrel=2
pkgdesc="Red Hats Liberation fonts"
arch=('any')
license=('custom:OFL')
url="https://www.redhat.com/promo/fonts/"
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
source=("https://fedorahosted.org/releases/l/i/liberation-fonts/liberation-fonts-ttf-${pkgver}.tar.gz"
        30-0-liberation-mono.conf
        30-0-liberation-sans.conf
        30-0-liberation-serif.conf)
sha256sums=('b3830c6611d24462d405e7bc1fcc7a13e3f365045a614f29a848dba1502d76b2'
            '238aa4fab2c0201611a203c938d38296118cae605055768c06955a8d85697b22'
            '2a63fe17a2873c1021076a1fdfc02057a0e7f19ebd19a3fab3a56bf3eddf79cd'
            'dca84cb4b7ec2bf6ee3dc822b73f5a32e0e3d009056e7ba9ba3a12812687330c')

package() {
  cd "$srcdir/liberation-fonts-ttf-$pkgver"

  install -d "$pkgdir/usr/share/fonts/TTF/"
  install -m644 *.ttf "$pkgdir/usr/share/fonts/TTF/"

  # install fontconfig files
  install -Dm0644 ../30-0-liberation-mono.conf "$pkgdir/etc/fonts/conf.avail/30-$pkgname-mono.conf"
  install -Dm0644 ../30-0-liberation-sans.conf "$pkgdir/etc/fonts/conf.avail/30-$pkgname-sans.conf"
  install -Dm0644 ../30-0-liberation-serif.conf \
    "$pkgdir/etc/fonts/conf.avail/30-$pkgname-serif.conf"

  # install license
  install -Dm644 "${srcdir}/liberation-fonts-ttf-${pkgver}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
