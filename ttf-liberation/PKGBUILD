# Maintainer: Jeff Huang <s8321414[at]gmail[dot]com>

pkgname=ttf-liberation
pkgver=2.00.4
pkgrel=1
pkgdesc="Red Hats Liberation fonts"
arch=('any')
license=('custom:OFL')
url="https://github.com/liberationfonts/liberation-fonts"
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
makedepends=('fontforge' 'python3-fonttools')
provides=('ttf-font')
source=("$url/archive/$pkgver/liberation-fonts-$pkgver.tar.gz"
        30-0-liberation-mono.conf
        30-0-liberation-sans.conf
        30-0-liberation-serif.conf)
sha256sums=('5f21146d94d33ddd0ea55f68e06839888d6ed03f1d0901ee371a981b89c9d6d6'
            '238aa4fab2c0201611a203c938d38296118cae605055768c06955a8d85697b22'
            '2a63fe17a2873c1021076a1fdfc02057a0e7f19ebd19a3fab3a56bf3eddf79cd'
            'dca84cb4b7ec2bf6ee3dc822b73f5a32e0e3d009056e7ba9ba3a12812687330c')

build() {
  cd liberation-fonts-$pkgver
  make
}

package() {
  cd liberation-fonts-$pkgver

  install -Dm644 -t "$pkgdir/usr/share/fonts/TTF" \
    liberation-fonts-ttf-$pkgver/*.ttf

  # install fontconfig files
  install -Dm644 ../30-0-liberation-mono.conf \
    "$pkgdir/etc/fonts/conf.avail/30-$pkgname-mono.conf"
  install -Dm644 ../30-0-liberation-sans.conf \
    "$pkgdir/etc/fonts/conf.avail/30-$pkgname-sans.conf"
  install -Dm644 ../30-0-liberation-serif.conf \
    "$pkgdir/etc/fonts/conf.avail/30-$pkgname-serif.conf"

  # install license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
