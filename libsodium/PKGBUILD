#contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/libsodium

pkgname=libsodium
pkgver=1.0.17
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl-based crypto library"
arch=('x86_64')
url="https://libsodium.org/"
license=('custom:ISC')
depends=('glibc')
source=("https://download.libsodium.org/libsodium/releases/$pkgname-$pkgver.tar.gz")
sha256sums=('0cc3dae33e642cc187b5ceb467e0ad0e1b51dcba577de1190e9ffa17766ac2b1')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # install license
  install -d -m 755 $pkgdir/usr/share/licenses/$pkgname
  install -m 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
