#contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/libsodium

pkgname=libsodium
pkgver=1.0.16
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl-based crypto library"
arch=('x86_64')
url="https://libsodium.org/"
license=('custom:ISC')
depends=('glibc')
source=("https://download.libsodium.org/libsodium/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('eab917d599c9c1fe971a6ecf915b9a6476ccec2d46cf23cbfbf06dd3833089b422f192de4d55f17b93362f1251ba8d5ddeb95ced1a422a3a2631b4b82553907f')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # install license
  install -d -m 755 $pkgdir/usr/share/licenses/$pkgname
  install -m 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
