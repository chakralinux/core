#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=tzdata
pkgver=2011f
pkgrel=1
# dash the next line if you want to skip the tests
_test=1
_tzcode=2011e
_tzdata=2011f
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.twinsun.com/tz/tz-link.htm"
license=('GPL')
depends=()
makedepends=()
optdepends=('sh: required by tzselect')
options=('!emptydirs')
source=(ftp://elsie.nci.nih.gov/pub/tzcode${_tzcode}.tar.gz \
        ftp://elsie.nci.nih.gov/pub/${pkgname}${_tzdata}.tar.gz \
        Makefile.patch)
md5sums=('fbfc05dbf9ebcfe7c4bba18549870173'
         'cc8136284ae1e6c720a18e0f080242e0'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz
  tar -xf ${pkgname}${_tzdata}.tar.gz

  patch -Np1 -i "${srcdir}/Makefile.patch"

  make

  if [ -n "${_test}" ]; then
     echo "<<< STARTING TESTS >>>"
     make check
     echo "<<< ENDING TESTS >>>"
  fi
}

package() {
  cd ${srcdir}
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}/usr/share/zoneinfo/localtime"
}
