pkgbase=ttf-noto
# Please do not merge noto-color-emoji and noto-emoji for now. ColorEmoji
# is likely to cause some crashes in application.
pkgname=('ttf-noto' 'ttf-noto-extra' 'otf-noto-cjk' 'ttf-noto-emoji' 'ttf-noto-color-emoji')
pkgver=20171010
_commit=5a89b487c648c34c2b6d1b9ebb73bb3fab1df4b2  # master
_commit_cjk=32a5844539f2e348ed36b44e990f9b06d7fb89fe
_commit_emoji=8a6530b7b26e8c5ee7b177a1388d492bbcaf4319
pkgrel=1
arch=('any')
url="http://www.google.com/get/noto"
license=('custom:SIL')
depends=(fontconfig)
makedepends=()
source=("noto-fonts-$_commit.zip::https://github.com/googlei18n/noto-fonts/archive/$_commit.zip"
        "noto-cjk-$_commit_cjk.zip::https://github.com/googlei18n/noto-cjk/archive/$_commit_cjk.zip"
        "noto-emoji-$_commit_emoji.zip::https://github.com/googlei18n/noto-emoji/archive/$_commit_emoji.zip"
        66-noto-sans.conf 66-noto-serif.conf 66-noto-mono.conf)
md5sums=('b55c7ef00750a0a168696e893f8f9811'
         'd8f4fc212846dc916fa8a6df5dd33a5d'
         '08ad948161383cbc933cf3e5fe3b3ed0'
         'd1c1f1fdd1ee428b9317de9e639d7544'
         'e99b33ffbf57d7a7d277acea0a0ce910'
         '2478ab187743480e77c10e9e8af5d326')
install=${pkgbase}.install

pkgver() {
  date +%Y%m%d
}

package_ttf-noto() {
    pkgdesc="Google Noto TTF fonts"
    provides=('ttf-font')

    cd noto-fonts-${_commit}
    install -Dm644 hinted/Noto*.tt[fc] -t "$pkgdir"/usr/share/fonts/noto

    # Already in NotoSans-*.ttc
    rm "$pkgdir"/usr/share/fonts/noto/NotoSans{,UI}-*.ttf
    # in -extra
    rm -f "$pkgdir"/usr/share/fonts/noto/Noto*{Condensed,SemiBold,Extra}*.tt[fc]

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

    install -Dm644 ../66-noto-{sans,serif,mono}.conf -t "$pkgdir"/etc/fonts/conf.avail/

    install -dm755 "${pkgdir}"/etc/fonts/conf.d
    pushd "${pkgdir}"/etc/fonts/conf.avail
    for config in *; do
        ln -sf ../conf.avail/${config} ../conf.d/${config}
    done
    popd
}

package_ttf-noto-extra() {
    pkgdesc="Google Noto TTF fonts - additional variants"
    depends=(ttf-noto)

    cd noto-fonts-${_commit}
    mkdir -p "$pkgdir"/usr/share/fonts/noto
    cp hinted/Noto*{Condensed,SemiBold,Extra}*.tt[fc] "$pkgdir"/usr/share/fonts/noto
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_otf-noto-cjk() {
    pkgdesc="Google Noto CJK OTF fonts"
    provides=('otf-noto')
    replaces=('otf-noto')
    cd noto-cjk-${_commit_cjk}
    install -Dm644 *.ttc -t "$pkgdir"/usr/share/fonts/noto
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_ttf-noto-emoji() {
    pkgdesc="Google Noto Emoji TTF fonts"
    cd noto-emoji-${_commit_emoji}
    install -Dm644 fonts/NotoEmoji-Regular.ttf -t "$pkgdir"/usr/share/fonts/noto
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_ttf-noto-color-emoji() {
    pkgdesc="Google Noto Color Emoji TTF fonts"
    cd noto-emoji-${_commit_emoji}
    install -Dm644 fonts/NotoColorEmoji.ttf -t "$pkgdir"/usr/share/fonts/noto
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
