# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=perl-alien-sdl
_realname=Alien-SDL
pkgver=1.444
pkgrel=1
pkgdesc="Building, finding and using SDL binaries"
arch=('x86_64')
license=(PerlArtistic GPL)
url="http://search.cpan.org/dist/Alien-SDL/"
depends=(sdl=1.2.15 perl-file-sharedir perl-capture-tiny)
makedepends=(perl-file-which perl-text-patch perl-archive-extract perl-archive-zip perl-module-build)
options=(!emptydirs)
_author=FROGGS
source=(http://search.cpan.org/CPAN/authors/id/${_author::1}/${_author::2}/$_author/$_realname-$pkgver.tar.gz)
md5sums=('e34b27aa2bdc3a29ce617965d80abaf9')

prepare() {
  cd $_realname-$pkgver

  # Workaround bug with --with-sdl-config
  sed -i '/^GetOptions/d' Build.PL
}

build() {
  cd $_realname-$pkgver

  # install module in vendor directories
  perl Build.PL --with-sdl-config
  perl Build
}

package() {
  cd $_realname-$pkgver
  perl Build install installdirs=vendor destdir="$pkgdir"
}

