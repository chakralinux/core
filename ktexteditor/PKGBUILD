# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/ktexteditor
source ../frameworks.conf

pkgname=ktexteditor
pkgver=${KFVersion}
pkgrel=2
pkgdesc='Advanced embeddable text editor'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/ktexteditor'
license=('LGPL')
depends=('kparts' 'libgit2')
makedepends=("extra-cmake-modules>=${KFECMVersion}")
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgname}-${pkgver}.tar.xz"
        "git://anongit.kde.org/ktexteditor#branch=master" # We only need the syntax upadates here
        'pkgbuild-syntax-highlight.patch')
sha256sums=( $(getSum ${pkgname})
        'SKIP'
        '3ac25ae5f7a4956ae7bed6e79607a4ab5021875fd198fe348a1712db71a25b96')

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  rm -rfv src/syntax/data
  cp -rv ${srcdir}/${pkgname}/src/syntax/data src/syntax
  patch -Np1 -i "${srcdir}"/pkgbuild-syntax-highlight.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver} 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
