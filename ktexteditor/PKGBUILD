# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=ktexteditor
pkgver=${KFVersion}
pkgrel=1
pkgdesc='Advanced embeddable text editor'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/ktexteditor'
license=('LGPL')
depends=('kparts' 'libgit2')
makedepends=("extra-cmake-modules>=${KFECMVersion}")
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgname}-${pkgver}.tar.xz"
        'pkgbuild-syntax-highlight.patch')
sha256sums=(" $(getSum ${pkgname})" 
            '5c4254fb21bde173273b47dc36d317c2dd2769a42a21a70962356d38e832d0b7')

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  patch -p0 -i "${srcdir}"/pkgbuild-syntax-highlight.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
