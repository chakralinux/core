pkgname=lilv
pkgver=0.22.0
pkgrel=1
pkgdesc="A library to make the use of LV2 plugins as simple as possible for applications"
arch=('x86_64')
url="http://drobilla.net/software/lilv/"
license=('custom:ISC')
depends=('python2-numpy' 'sratom' 'jack')
makedepends=('swig')
optdepends=('bash-completion: auto-complete words')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('fd3a14fdaeaba4716b4fef526548a747')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # pick up python2 even when python3 exists
  # (the build system has flaky support for python3)
  export PYTHON="/usr/bin/python2"

  # remove ldconfig --speps
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --dyn-manifest \
                        --bindings

  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

