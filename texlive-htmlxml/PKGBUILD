# Contributions from Arch: https://www.archlinux.org/packages/extra/any/texlive-htmlxml/

pkgname=texlive-htmlxml
pkgver=2016.41440
_revnr=${pkgver#*.}
pkgrel=2
pkgdesc="TeX Live - Packages to convert LaTeX to XML/HTML, and typeset XML/SGML"
license=('GPL')
arch=(x86_64)
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
options=('!emptydirs')
install=texlive.install
md5sums=('a55c2a95d2054c09a9a9eb35324fbc77')

build() {
   cd "${srcdir}"
   for p in *.tar.xz; do
      bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source}

   # patch for biblatex 3.1
   sed -i '/abx@version/ s,3\.0,3\.1,' texmf-dist/tex/generic/tex4ht/biblatex.4ht
}

package() {
   install -m755 -d $pkgdir/var/lib/texmf/chakra/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/chakra/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m755 -d $pkgdir/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   install -m755 -d $pkgdir/usr/bin
   ln -s /usr/share/texmf-dist/scripts/tex4ht/ht.sh $pkgdir/usr/bin/ht
   ln -s /usr/share/texmf-dist/scripts/tex4ht/htcontext.sh $pkgdir/usr/bin/htcontext
   ln -s /usr/share/texmf-dist/scripts/tex4ht/htlatex.sh $pkgdir/usr/bin/htlatex
   ln -s /usr/share/texmf-dist/scripts/tex4ht/htmex.sh $pkgdir/usr/bin/htmex
   ln -s /usr/share/texmf-dist/scripts/tex4ht/httex.sh $pkgdir/usr/bin/httex
   ln -s /usr/share/texmf-dist/scripts/tex4ht/httexi.sh $pkgdir/usr/bin/httexi
   ln -s /usr/share/texmf-dist/scripts/tex4ht/htxelatex.sh $pkgdir/usr/bin/htxelatex
   ln -s /usr/share/texmf-dist/scripts/tex4ht/htxetex.sh $pkgdir/usr/bin/htxetex
   ln -s /usr/share/texmf-dist/scripts/tex4ht/mk4ht.pl $pkgdir/usr/bin/mk4ht
}
