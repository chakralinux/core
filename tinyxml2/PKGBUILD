# Arch Contributor: https://projects.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/tinyxml2

pkgname=tinyxml2
pkgver=6.0.0
pkgrel=1
pkgdesc='Simple, small, C++ XML parser that can be easily integrated into other programs'
url='http://www.grinninglizard.com/tinyxml2/'
arch=('x86_64')
license=('zlib')
depends=('gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git://github.com/leethomason/tinyxml2#tag=$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -G Ninja
  ninja
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 readme.md "$pkgdir/usr/share/licenses/$pkgname/README.md"
}
