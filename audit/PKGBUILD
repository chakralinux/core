#
# Apps Packages for Chakra, part of chakra-project.org
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=audit
pkgver=2.3.7
pkgrel=1
pkgdesc='User space utilities for storing and searching the audit records generated by the audit subsystem in the Linux kernel.'
url="http://people.redhat.com/sgrubb/audit"
arch=('x86_64')
depends=('krb5' 'libcap')
makedepends=('libldap' 'linux-headers' 'python2' 'swig')
license=(GPL)
options=(!libtool staticlibs)
backup=(etc/libaudit.conf
	etc/audit/rules.d/audit.rules
	etc/audit/auditd.conf
	etc/audisp/audispd.conf
	etc/audisp/audisp-remote.conf
	etc/audisp/zos-remote.conf
	etc/audisp/plugins.d/af_unix.conf
	etc/audisp/plugins.d/audispd-zos-remote.conf
	etc/audisp/plugins.d/au-remote.conf
	etc/audisp/plugins.d/syslog.conf)
source=("$url/$pkgname-$pkgver.tar.gz")
md5sums=('42813b6c202f5922ac904c059ac17ee6')
	 
build() {
	cd $srcdir/$pkgname-$pkgver
        # we're currently using normal libcap, as soon as we switch to -ng, add following line
        #--with-libcap-ng=no
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/audit \
		--with-python=yes \
		--enable-gssapi-krb5=yes \
		--with-apparmor \
                --enable-gssapi-krb5=yes \
                --enable-systemd
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	cd "$pkgdir"
	install -d var/log/audit
	rm -rf etc/rc.d etc/sysconfig

	sed -ri 's|/sbin|/usr/bin|' \
		etc/audit/*.conf \
		etc/audisp/plugins.d/*.conf \
		usr/lib/systemd/system/auditd.service
}
