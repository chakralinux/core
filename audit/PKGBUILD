#
# Apps Packages for Chakra, part of chakra-project.org
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=audit
pkgver=2.2.1
pkgrel=2
pkgdesc='User space utilities for storing and searching the audit records generated by the audit subsystem in the Linux kernel.'
url="http://people.redhat.com/sgrubb/$pkgname"
arch=('i686' 'x86_64')
depends=('krb5')
makedepends=('libldap' 'linux-headers' 'python2' 'swig')
license=(GPL)
options=(!libtool)
backup=(	etc/libaudit.conf
	etc/audit/audit.rules
	etc/audit/auditd.conf
	etc/audisp/audispd.conf
	etc/audisp/audisp-remote.conf
	etc/audisp/zos-remote.conf
	etc/audisp/plugins.d/af_unix.conf
	etc/audisp/plugins.d/audispd-zos-remote.conf
	etc/audisp/plugins.d/au-remote.conf
	etc/audisp/plugins.d/syslog.conf)
source=(	"$url/$pkgname-$pkgver.tar.gz"
	auditd.rc
	python2.patch
	auditd.service)
md5sums=('dc099fcb2f9242d47ecc35b46d71dfd1'
         '5f86e87354d0c3b9b408a26daed7a906'
	 '45a33c6bef84f8e1701cb0b5e187336b'
	 '67f6c51891120da038cb790693d8eba1')
	 
build() {
	cd $srcdir/$pkgname-$pkgver
	patch -p0 < ../python2.patch
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--with-python=yes \
		--enable-gssapi-krb5=yes \
		--libexecdir=/usr/lib/audit \
		--with-apparmor
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
	
	install -Dm755 "$srcdir/auditd.service" "$pkgdir/usr/lib/systemd/system/auditd.service"

	cd $pkgdir
	install -m744 $srcdir/auditd.rc etc/rc.d/auditd
	install -d var/log/audit
	rm -rf \
		etc/rc.d/init.d \
		etc/sysconfig
	mv usr/sbin sbin
}
