# maintainer abveritas[at]chakra-project[dot]org>

pkgname=libsoup
pkgver=2.44.2
pkgrel=2
pkgdesc="GNOME HTTP Library - base library"
arch=('x86_64')
license=('LGPL')
depends=('glib2' 'libxml2' 'gnutls' 'sqlite3' )
makedepends=('intltool' 'gobject-introspection' 'python2' 'libgnome-keyring')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
options=('!emptydirs')
provides=('libsoup-gnome')
replaces=('libsoup-gnome')
conflicts=('libsoup-gnome')
md5sums=('92aa3667357157e8f3489bcca287f2fa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/env python/python2/' libsoup/tld-parser.py
  
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --disable-tls-check \
      --without-gnome 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install 
}
