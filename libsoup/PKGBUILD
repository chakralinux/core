#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=libsoup
pkgver=2.34.2
pkgrel=1
pkgdesc="GNOME HTTP Library - base library"
arch=('x86_64')
license=('LGPL')
depends=('glib2>=2.24.0' 'libxml2>=2.7.7' 'gnutls>=2.8.6' 'sqlite3' 'libgnome-keyring')
makedepends=('intltool' 'gobject-introspection' 'python2')
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.34/${pkgname}-${pkgver}.tar.bz2")
options=('!libtool' '!emptydirs')
provides=('libsoup-gnome')
replaces=('libsoup-gnome')
conflicts=('libsoup-gnome')
md5sums=('2454b38681a6e082b613a781a501e721')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/env python/python2/' libsoup/tld-parser.py
  
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --disable-tls-check \
      --without-gnome 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install 
}
