pkgname=libsoup
pkgver=2.52.2
pkgrel=1
pkgdesc="GNOME HTTP Library - base library"
arch=('x86_64')
license=('LGPL')
depends=('glib2' 'libxml2' 'sqlite3' )
makedepends=('intltool' 'gobject-introspection' 'python2' 'vala')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
options=('!emptydirs')
md5sums=('e4757d09012ed93822b1ee41435fec24')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/env python/python2/' libsoup/tld-parser.py
  
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --disable-tls-check \
      --without-gnome 
  make 
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install 
}
